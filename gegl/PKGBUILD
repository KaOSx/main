
pkgname=gegl
pkgver=0.3.20
_pkgver=0.3
pkgrel=1
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'ffmpeg' 'openexr' 'librsvg' 'jasper' 'json-glib' 'libwebp' 'libraw' 'gexiv2')
makedepends=('ruby' 'lua' 'exiv2' 'intltool' 'gobject-introspection')
options=('!libtool')
source=("https://download.gimp.org/pub/gegl/${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('821568d17dc92a46f6105644c4f4d497daea2be794006140a016ed34e05eb084')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-sdl \
    --with-openexr \
    --with-librsvg \
    --with-libavformat \
    --with-jasper \
    --disable-docs 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make install DESTDIR=${pkgdir}
}

