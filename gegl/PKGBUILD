
pkgname=gegl
pkgver=0.3.34
_pkgver=0.3
pkgrel=1
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'ffmpeg' 'openexr' 'librsvg' 'jasper' 'json-glib' 'libwebp' 'libraw' 'gexiv2')
makedepends=('ruby' 'lua' 'exiv2' 'intltool' 'gobject-introspection')
options=('!libtool')
source=("https://download.gimp.org/pub/gegl/${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('5ca2227655ebf1ab2e252cee3eede219c758336394288ef301b93264b9411304')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --with-sdl \
    --with-openexr \
    --with-librsvg \
    --with-libavformat \
    --with-jasper \
    --disable-docs 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make install DESTDIR=${pkgdir}
}

