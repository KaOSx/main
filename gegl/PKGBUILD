
pkgname=gegl
pkgver=0.4.24
_pkgver=0.4
pkgrel=1
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'ffmpeg' 'openexr' 'librsvg' 'json-glib' 'libwebp' 'libraw' 'gexiv2' 'lensfun' 'poppler')
makedepends=('ruby' 'lua' 'exiv2' 'intltool' 'gobject-introspection' 'meson' 'ninja' 'python3-gobject3')
options=('!libtool')
source=("https://download.gimp.org/pub/gegl/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.gnome.org/GNOME/gegl/commit/c1c3541caab485384087bbbcf1c83ef2d5bd4d43.diff")
sha256sums=('7765499f27341b0d16032e665319cbc12876483ff6a944fcdf24a9c58e3e254a'
            'd69bb05a4327c2c1bc2bd1b6a4c938eb035ece37f8ea3ce6840d415dea4234e5')


build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddocs=false \
    -Dlibav=enabled \
    -Dlibrsvg=enabled \
    -Djasper=disabled \
    -Dopenexr=enabled \
    -Dsdl2=enabled
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

