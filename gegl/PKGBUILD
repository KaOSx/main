
pkgname=gegl
pkgver=0.4.18
_pkgver=0.4
pkgrel=2
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'ffmpeg' 'openexr' 'librsvg' 'json-glib' 'libwebp' 'libraw' 'gexiv2')
makedepends=('ruby' 'lua' 'exiv2' 'intltool' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://download.gimp.org/pub/gegl/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.gnome.org/GNOME/gegl/commit/c1c3541caab485384087bbbcf1c83ef2d5bd4d43.diff")
sha256sums=('c946dfb45beb7fe0fb95b89a25395b449eda2b205ba3e8a1ffb1ef992d9eca64'
            'd69bb05a4327c2c1bc2bd1b6a4c938eb035ece37f8ea3ce6840d415dea4234e5')


build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddocs=false \
    -Dlibav=enabled \
    -Dlibrsvg=enabled \
    -Djasper=disabled \
    -Dopenexr=enabled \
    -Dsdl2=enabled
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

