
pkgname=gegl
pkgver=0.4.50
_pkgver=0.4
pkgrel=2
pkgdesc="Graph based image processing framework."
arch=('x86_64')
url="https://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'ffmpeg' 'openexr' 'librsvg' 'json-glib' 'libwebp' 'libraw' 'gexiv2' 'lensfun' 'poppler')
makedepends=('ruby' 'lua' 'exiv2' 'intltool' 'gobject-introspection' 'meson' 'ninja' 'python3-gobject3')
options=('!libtool')
source=("https://download.gimp.org/pub/gegl/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.gnome.org/GNOME/gegl/-/commit/499a239d158fadb3a04499255b5b282a8a6023bb.diff")
sha256sums=('6084969b06ee86ca71142133773f27e13f02e5a6a22c2cfce452ecaaddb790c1'
            '371bda564b4806f603ec6d2640fd95f276aa8de9aa71849a3638a793c8ed0749')

prepare () {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|->params.shot_select = o->image_num;|->rawparams.shot_select = o->image_num;|' operations/external/raw-load.c
  #patch -p1 -i ${srcdir}/499a239d158fadb3a04499255b5b282a8a6023bb.diff
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddocs=false \
    -Dlibav=enabled \
    -Dlibrsvg=enabled \
    -Djasper=disabled \
    -Dopenexr=enabled \
    -Dsdl2=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

