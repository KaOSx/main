
pkgname=libfprint
pkgver=1.94.3
pkgrel=1
pkgdesc="Project that aims to add support for consumer fingerprint reader devices, in Linux, as well as other free Unices."
arch=('x86_64')
url="https://fprint.freedesktop.org/"
license=('LGPL')
depends=('libgudev' 'libgusb' 'nss' 'pixman')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!emptydirs' '!libtool')
source=("https://gitlab.freedesktop.org/libfprint/libfprint/-/archive/v${pkgver}/libfprint-v${pkgver}.tar.bz2")
md5sums=('3c0d0a61a0da625ef1a8afcb75a4af34')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-v${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddoc=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


