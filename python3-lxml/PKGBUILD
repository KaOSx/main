
pkgname=python3-lxml
pkgver=5.2.0
pkgrel=1
pkgdesc="Python3 binding for the libxml2 and libxslt libraries"
arch=('x86_64')
license=('BSD' 'GPL' 'custom')
url="https://lxml.de/"
depends=('python3-cython' 'libxslt')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
#source=("https://pypi.io/packages/source/l/lxml/lxml-${pkgver}.tar.gz")
source=("https://github.com/lxml/lxml/releases/download/lxml-${pkgver}/lxml-${pkgver}.tar.gz")
sha256sums=('21dc490cdb33047bc7f7ad76384f3366fa8f5146b86cc04c4af45de901393b90')

build() {
  cd lxml-${pkgver}

  python3 -m build -nw
}

package() {
  cd lxml-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl

  install -D -m644 LICENSES.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
  install -D -m644 doc/licenses/BSD.txt ${pkgdir}/usr/share/licenses/${pkgname}/BSD.txt 
  install -D -m644 doc/licenses/elementtree.txt ${pkgdir}/usr/share/licenses/${pkgname}/elementtree.txt 
}
