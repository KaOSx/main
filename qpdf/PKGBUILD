
# rebuild cups-filters on every update, but point update
pkgname=qpdf
pkgver=10.0.3
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="http://qpdf.sourceforge.net/"
license=('custom:Artistic-2.0')
depends=('openjpeg' 'zlib')
makedepends=('make')
options=('!libtool')
source=("https://github.com/qpdf/qpdf/releases/download/release-${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('51e96e2e5507f52993e0cc4d04726f0614f6d6dad04eaddcb527cd3f4190a8f2')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}


package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir}/ install

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 Artistic-2.0 ${pkgdir}/usr/share/licenses/${pkgname}/
}
