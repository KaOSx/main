
pkgname=harfbuzz
pkgver=2.6.8
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="https://github.com/harfbuzz/harfbuzz"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite')
makedepends=('cairo')
optdepends=('cairo: hb-view program')
options=('!libtool')
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6648a571a27f186e47094121f0095e1b809e918b3037c630c7f38ffad86e3035')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --with-glib \
        --with-freetype \
        --with-cairo \
        --with-icu \
        --with-graphite2
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/harfbuzz/COPYING
}
