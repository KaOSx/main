
pkgname=harfbuzz
pkgver=10.0.0
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="https://harfbuzz.github.io/"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite' 'cairo')
makedepends=('meson' 'ninja' 'gobject-introspection' 'ragel')
options=('!libtool')
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c2dfe016ad833a5043ecc6579043f04e8e6d50064e02ad449bb466e6431e3e04')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgraphite2=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/harfbuzz/COPYING
}

