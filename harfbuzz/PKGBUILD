
pkgname=harfbuzz
pkgver=2.5.0
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite')
makedepends=('cairo')
optdepends=('cairo: hb-view program')
options=('!libtool')
source=("https://www.freedesktop.org/software/harfbuzz/release/${pkgname}-${pkgver}.tar.xz")
sha256sums=('959c73f98db4f84ad5221ea923146a71b5a48c946138d9ca4b42f3f3ffc416ef')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --with-glib \
        --with-freetype \
        --with-cairo \
        --with-icu \
        --with-graphite2
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/harfbuzz/COPYING
}
