
pkgname=harfbuzz
pkgver=3.0.0
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="https://harfbuzz.github.io/"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite' 'cairo')
makedepends=('meson' 'ninja' 'gobject-introspection')
options=('!libtool')
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('036b0ee118451539783ec7864148bb4106be42a2eb964df4e83e6703ec46f3d9')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgraphite=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/harfbuzz/COPYING
}

