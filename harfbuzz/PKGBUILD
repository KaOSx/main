
pkgname=harfbuzz
pkgver=8.3.1
pkgrel=1
pkgdesc="OpenType text shaping engine."
arch=('x86_64')
url="https://harfbuzz.github.io/"
license=('MIT')
depends=('icu' 'glib2' 'freetype2' 'graphite' 'cairo')
makedepends=('meson' 'ninja' 'gobject-introspection')
options=('!libtool')
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f73e1eacd7e2ffae687bc3f056bb0c705b7a05aee86337686e09da8fc1c2030c')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgraphite2=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/harfbuzz/COPYING
}

