
pkgname=xf86-input-libinput
pkgver=1.5.0
pkgrel=2
pkgdesc="X driver based on libinput."
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/driver/xf86-input-libinput"
license=('MIT')
depends=('libinput')
makedepends=('xorg-server' 'xorgproto')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2524c35f196554ea11aef3bba1cf324759454e1d49f98ac026ace2f6003580e6')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
