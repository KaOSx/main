
pkgname=googletest
pkgver=1.12.0
_pkgver=release-1.12.0
pkgrel=1
pkgdesc='Googles C++ test framework'
url='https://github.com/google/googletest/'
arch=('x86_64')
license=('BSD')
depends=('gcc-libs' 'sh')
makedepends=('cmake' 'python3')
options=('!makeflags')
source=("https://github.com/google/googletest/archive/${_pkgver}.tar.gz")
md5sums=('c483d6c24846cea536a9c2b44085bc14')

build() {
  cmake -B build -S ${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_SKIP_RPATH=ON
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm 644 ${pkgname}-${_pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}

