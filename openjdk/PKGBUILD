# fails with gcc 6.4.0, builds with gcc 6.3.1
pkgname=('openjdk' 'openjdk-src')
pkgbase='openjdk'
_icedtea_release="3.19.0"
_icedtea_jdkver="8"
# ${srcdir}/build/ChangeLog for versions
_openjdk_build="b10"
_pkgver="8u292"
pkgver="${_pkgver}_${_openjdk_build}_${_icedtea_release}"
pkgrel=1
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('x86_64')
url="https://icedtea.classpath.org/"
license=('GPL2 with Classpath exception')
makedepends=('java-environment=8' 'apache-ant' 'zip' 'unzip'
             'fastjar' 'cpio' 'cups' 'coreutils'
             'wget' 'gzip' 'alsa-lib' 'pulseaudio' 'zlib' 'libjpeg-turbo'
             'libpng' 'lcms2' 'freetype2' 'rhino' 'nss' 'libxt' 'pkg-config'
             'libxtst' 'libxp' 'libxrender' 'libxinerama' 'libxcomposite' 'ca-certificates-java' 'pcsclite') #'giflib' 

_hg_url="https://icedtea.wildebeest.org/download/drops/icedtea8/${_icedtea_release}"
_jvmdir="/usr/lib/jvm/java-8-openjdk"

source=("https://icedtea.classpath.org/download/source/icedtea-${_icedtea_release}.tar.xz"
        ${_hg_url}/corba.tar.xz 
        ${_hg_url}/jaxp.tar.xz 
        ${_hg_url}/jaxws.tar.xz 
        ${_hg_url}/jdk.tar.xz 
        ${_hg_url}/langtools.tar.xz 
        ${_hg_url}/openjdk.tar.xz 
        ${_hg_url}/nashorn.tar.xz 
        ${_hg_url}/hotspot.tar.xz 
        'fontconfig-paths.diff'
        'corba_path.diff'
        'command_path.diff'
        'openjdk.profile.sh'
        'openjdk.profile.csh')
sha256sums=('eed61e51ba35635b2292a6e67404d5e3f4bf7cc5d69bc1b81f5b69b1d8d1b5b2'
            '19b87e02b27a7312fa0835660159bee55a9e8867fd0013dc36acc057dc331076'
            'b9d78eec0ae71332b61e44f71bcf991d3af60663028fdb10bdb1c497cb186f10'
            '197053d93fc478adf0645577e85feca004b1496a29dc0d98acc256b1c9be80dd'
            '3bc3dcaa1f9d109ed99139e1108a691964c68ad90375214a841fd1522aa3829c'
            '0b94950c68153862c315e1aae62de58a95bc9487ea197723bcb435d6c53d3f22'
            'c9d0f566a2cf9d4150a16b7c68b07ccdeeb317b7d055c56f671a0d3d5afd6bd0'
            '2451e97fe9b4c3d14859170c9621ce1323075e97f6148890ba3644512f3bd695'
            'a2417711339f3c1bb3b32612022e331fe090037b11ef98d989c1fab1acebcab9'
            '9ad943ceb3dbcdf45d72974fc3667886a7ed65c69ab9abc17be5412827551a7f'
            'b23a0e6b6bd11e8db3c3a7040887bca955a9821fc11bf351e11d97b3e5fde05b'
            '0ad6a9c361176a63537d14faf23c1a8100558f33f4aa222c29d7034a055a384c'
            '5b13278d7a63712dbdc4cb4fb44ad9c00b6cfc65c63425fd84f3c7b7a98e81d1'
            '08428358c22b5017623931dac6345f8cce7eb1293e50bc28e2ab9271a69a5fda')
noextract=("openjdk.tar.xz"
           "corba.tar.xz"
           "jaxp.tar.xz"
           "jaxws.tar.xz"
           "jdk.tar.xz"
           "nashorn.tar.xz"
           "langtools.tar.xz"
           "hotspot.tar.xz")
           
prepare() {
  cd icedtea-${_icedtea_release}
  
  #patch -p1 -i ${srcdir}/makefile.diff
  # sed line needed to make it look for automake-1.16
  #sed -i 's|1.15|1.16|' configure
}

build() {
  cd "${srcdir}"

  # Prepare the patches
  #cp *.diff "${srcdir}/icedtea-${_icedtea_release}/patches"
  #export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
  #                             patches/corba_path.diff \
  #                             patches/command_path.diff"
  #                             patches/openjdk7_nonreparenting-wm.diff

  rm -rf build
  cp -r "icedtea-${_icedtea_release}" \
        "${srcdir}/build"

  cd "${srcdir}/build"
  CFLAGS="-O3"

  . /etc/profile.d/apache-ant.sh

  # Start the configuration and build step
  ./configure --prefix=/usr \
              --with-parallel-jobs="${MAKEFLAGS/-j}" \
              --with-jdk-home="${JAVA_HOME}" \
              --with-openjdk-src-zip="${srcdir}/openjdk.tar.xz" \
              --with-hotspot-src-zip="${srcdir}/hotspot.tar.xz" \
              --with-corba-src-zip="${srcdir}/corba.tar.xz" \
              --with-jaxp-src-zip="${srcdir}/jaxp.tar.xz" \
              --with-jaxws-src-zip="${srcdir}/jaxws.tar.xz" \
              --with-jdk-src-zip="${srcdir}/jdk.tar.xz" \
              --with-nashorn-src-zip="${srcdir}/nashorn.tar.xz" \
              --with-langtools-src-zip="${srcdir}/langtools.tar.xz" \
              --with-pkgversion="Linux build ${pkgver}-${pkgrel}-x86_64" \
              --disable-tests \
              --disable-downloading \
              --disable-Werror \
              --disable-bootstrap \
              --enable-nss \
              --disable-system-sctp
  make
}

package_openjdk() {
pkgdesc="An open-source implementation of the Java SE Platform."
conflicts=('openjdk6' 'openjdk7')
provides=("java-environment=${_icedtea_jdkver}" "java-runtime=${_icedtea_jdkver}")
replaces=('openjdk6' 'openjdk7')
depends=('alsa-lib' 'pulseaudio' 'giflib' 'libjpeg-turbo' 'libpng' 'freetype2'
         'ca-certificates-java')
optdepends=('icedtea-web: web browser plugin and Java Web Start support'
            'gtk2: for the GTK+ Look And Feel')

  cd "${srcdir}/build"

  # Copy the built OpenJDK image to the target directory
  install -d -m755 "${pkgdir}/${_jvmdir%/*}"
  cp -r "openjdk.build/images/j2sdk-image" \
        "${pkgdir}/${_jvmdir#/}"
  # permissions are not preserved
  find "${pkgdir}/${_jvmdir#/}" \! -type l \( -perm /111 -exec chmod 755 {} \; -o -exec chmod 644 {} \; \)
  
  # Remove the .zip file which is going to be inside openjdk-src
  rm "${pkgdir}/${_jvmdir#/}/src.zip"

  # Symlink the binaries into /usr/bin
  install -d -m755 "${pkgdir}/usr/bin"
  pushd "${pkgdir}/${_jvmdir#/}/bin" >/dev/null
    for executable in *; do
      ln -s "${_jvmdir}/bin/${executable}" \
            "${pkgdir}/usr/bin"
    done
  popd >/dev/null

  # Move the manual pages in the right place
  install -d -m755 "${pkgdir}"/usr/share/man/{,ja/}man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/man1/* \
     "${pkgdir}"/usr/share/man/man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/ja_JP.UTF-8/* \
     "${pkgdir}"/usr/share/man/ja

  # link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"
  ln -s /etc/ssl/certs/java/cacerts \
        "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"

  # Setup the shell profile dropin configuration
  install -D -m755 "${srcdir}/openjdk.profile.sh" \
                   "${pkgdir}/etc/profile.d/openjdk.sh"
  install -D -m755 "${srcdir}/openjdk.profile.csh" \
                   "${pkgdir}/etc/profile.d/openjdk.csh"

  # Install the license file
  install -D -m644 "openjdk/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_openjdk-src() {
pkgdesc="An open-source implementation of the Java SE Platform. (Source package)"
arch=('x86_64')
depends=("openjdk=${pkgver}")

  cd "${srcdir}/build"

  # Install the source package
  install -D -m644 "openjdk.build/images/j2sdk-image/src.zip" \
                   "${pkgdir}/${_jvmdir#/}/src.zip"
}

