# fails with gcc 6.4.0, builds with gcc 6.3.1
pkgname=('openjdk' 'openjdk-src')
pkgbase='openjdk'
_icedtea_release="3.7.0"
_icedtea_jdkver="8"
# ${srcdir}/build/ChangeLog for versions
_openjdk_build="b16"
_pkgver="8u161"
pkgver="${_pkgver}_${_openjdk_build}_${_icedtea_release}"
pkgrel=1
pkgdesc="An open-source implementation of the seventh edition of the Java SE Platform."
arch=('x86_64')
url="http://icedtea.classpath.org/"
license=('GPL2 with Classpath exception')
makedepends=('java-environment=8' 'apache-ant' 'zip' 'unzip'
             'fastjar' 'cpio' 'cups' 'gtk2' 'coreutils'
             'wget' 'gzip' 'alsa-lib' 'pulseaudio' 'zlib' 'libjpeg-turbo'
             'libpng' 'lcms2' 'freetype2' 'rhino' 'nss' 'libxt' 'pkg-config'
             'libxtst' 'libxp' 'libxrender' 'ca-certificates-java' 'pcsclite') #'giflib' 

_hg_url="http://icedtea.wildebeest.org/download/drops/icedtea8/${_icedtea_release}"
_jvmdir="/usr/lib/jvm/java-8-openjdk"

source=("http://icedtea.classpath.org/download/source/icedtea-${_icedtea_release}.tar.xz"
        ${_hg_url}/corba.tar.xz 
        ${_hg_url}/jaxp.tar.xz 
        ${_hg_url}/jaxws.tar.xz 
        ${_hg_url}/jdk.tar.xz 
        ${_hg_url}/langtools.tar.xz 
        ${_hg_url}/openjdk.tar.xz 
        ${_hg_url}/nashorn.tar.xz 
        ${_hg_url}/hotspot.tar.xz 
        'fontconfig-paths.diff'
        'corba_path.diff'
        'command_path.diff'
        'openjdk.profile.sh'
        'openjdk.profile.csh')
md5sums=('648a52c2f008ee0bbb865a2a1768891b'
         '2ee9aa7e85ed2fb9af1ab87fb87e404b'
         'c3a5eb1567ac3d8cbfa3dd8cd0848b6f'
         '286778356533d5fe3d8ab9c2c747fe3b'
         '84c39ca647e24fe81c08bc10e016d194'
         'c03e84cceca1d9173b409920fad3aa93'
         'c686c8d283bc9f89a8874c5344d4e83a'
         '4895a6a002374c0dea0be055cb75b9a5'
         '85693d6cd04ca1b9a694afa81e7c4dda'
         'ee1afda124d5927345014ab382ef581e'
         '986131efd2636724ae37870a5cca6d43'
         '05afd9c7fedcc05d4ebbb3427e068dba'
         '2113e8e67f7e11b925b366614721a3dc'
         'd703a04c47c961b16154974b46cab12d')
noextract=("openjdk.tar.xz"
           "corba.tar.xz"
           "jaxp.tar.xz"
           "jaxws.tar.xz"
           "jdk.tar.xz"
           "nashorn.tar.xz"
           "langtools.tar.xz"
           "hotspot.tar.xz")
           
#prepare() {
#  cd icedtea-3.6.0
  
#  patch -p1 -i ${srcdir}/makefile.diff
#}

build() {
  cd "${srcdir}"

  # Prepare the patches
  #cp *.diff "${srcdir}/icedtea-${_icedtea_release}/patches"
  #export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
  #                             patches/corba_path.diff \
  #                             patches/command_path.diff"
  #                             patches/openjdk7_nonreparenting-wm.diff

  rm -rf build
  cp -r "icedtea-${_icedtea_release}" \
        "${srcdir}/build"

  cd "${srcdir}/build"
  CFLAGS="-O3"

  . /etc/profile.d/apache-ant.sh

  # Start the configuration and build step
  ./configure --prefix=/usr \
              --with-parallel-jobs="${MAKEFLAGS/-j}" \
              --with-jdk-home="${JAVA_HOME}" \
              --with-openjdk-src-zip="${srcdir}/openjdk.tar.xz" \
              --with-hotspot-src-zip="${srcdir}/hotspot.tar.xz" \
              --with-corba-src-zip="${srcdir}/corba.tar.xz" \
              --with-jaxp-src-zip="${srcdir}/jaxp.tar.xz" \
              --with-jaxws-src-zip="${srcdir}/jaxws.tar.xz" \
              --with-jdk-src-zip="${srcdir}/jdk.tar.xz" \
              --with-nashorn-src-zip="${srcdir}/nashorn.tar.xz" \
              --with-langtools-src-zip="${srcdir}/langtools.tar.xz" \
              --with-pkgversion="Linux build ${pkgver}-${pkgrel}-x86_64" \
              --disable-tests \
              --disable-downloading \
              --disable-Werror \
              --disable-bootstrap \
              --enable-nss \
              --disable-system-sctp
  make
}

package_openjdk() {
pkgdesc="An open-source implementation of the Java SE Platform."
conflicts=('openjdk6' 'openjdk7')
provides=("java-environment=${_icedtea_jdkver}" "java-runtime=${_icedtea_jdkver}")
replaces=('openjdk6' 'openjdk7')
depends=('alsa-lib' 'pulseaudio' 'giflib' 'libjpeg-turbo' 'libpng' 'freetype2'
         'ca-certificates-java')
optdepends=('icedtea-web: web browser plugin and Java Web Start support'
            'gtk2: for the GTK+ Look And Feel')

  cd "${srcdir}/build"

  # Copy the built OpenJDK image to the target directory
  install -d -m755 "${pkgdir}/${_jvmdir%/*}"
  cp -r "openjdk.build/images/j2sdk-image" \
        "${pkgdir}/${_jvmdir#/}"
  # permissions are not preserved
  find "${pkgdir}/${_jvmdir#/}" \! -type l \( -perm /111 -exec chmod 755 {} \; -o -exec chmod 644 {} \; \)
  
  # Remove the .zip file which is going to be inside openjdk-src
  rm "${pkgdir}/${_jvmdir#/}/src.zip"

  # Symlink the binaries into /usr/bin
  install -d -m755 "${pkgdir}/usr/bin"
  pushd "${pkgdir}/${_jvmdir#/}/bin" >/dev/null
    for executable in *; do
      ln -s "${_jvmdir}/bin/${executable}" \
            "${pkgdir}/usr/bin"
    done
  popd >/dev/null

  # Move the manual pages in the right place
  install -d -m755 "${pkgdir}"/usr/share/man/{,ja/}man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/man1/* \
     "${pkgdir}"/usr/share/man/man2/
  mv "${pkgdir}"/${_jvmdir#/}/man/ja_JP.UTF-8/* \
     "${pkgdir}"/usr/share/man/ja

  # link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"
  ln -s /etc/ssl/certs/java/cacerts \
        "${pkgdir}/${_jvmdir#/}/jre/lib/security/cacerts"

  # Setup the shell profile dropin configuration
  install -D -m755 "${srcdir}/openjdk.profile.sh" \
                   "${pkgdir}/etc/profile.d/openjdk.sh"
  install -D -m755 "${srcdir}/openjdk.profile.csh" \
                   "${pkgdir}/etc/profile.d/openjdk.csh"

  # Install the license file
  install -D -m644 "openjdk/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_openjdk-src() {
pkgdesc="An open-source implementation of the Java SE Platform. (Source package)"
arch=('x86_64')
depends=("openjdk=${pkgver}")

  cd "${srcdir}/build"

  # Install the source package
  install -D -m644 "openjdk.build/images/j2sdk-image/src.zip" \
                   "${pkgdir}/${_jvmdir#/}/src.zip"
}

