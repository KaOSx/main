
pkgbase=python-psycopg2
pkgname=('python3-psycopg2' 'python2-psycopg2')
pkgver=2.7.1
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=('x86_64')
url="http://initd.org/psycopg/"
license=('LGPL3')
makedepends=('python2' 'python3' 'postgresql-libs>=8.4.1')
source=("http://initd.org/psycopg/tarballs/PSYCOPG-2-7/psycopg2-${pkgver}.tar.gz")
md5sums=('67848ac33af88336046802f6ef7081f3')

build() {
  cd psycopg2-${pkgver}
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
}

package_python3-psycopg2() {
  depends=('python3' 'postgresql-libs>=8.4.1')

  cd psycopg2-${pkgver}
  python3 setup.py install --root=${pkgdir}
}

package_python2-psycopg2() {
  depends=('python2' 'postgresql-libs>=8.4.1')

  cd psycopg2-${pkgver}
  python2 setup.py install --root=${pkgdir}
}
