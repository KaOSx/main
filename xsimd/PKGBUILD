pkgname=xsimd
pkgver=12.0.0
pkgrel=1
pkgdesc="C++ wrappers for SIMD intrinsics and parallelized, optimized mathematical functions"
url='https://xsimd.readthedocs.io/en/latest/'
arch=('x86_64')
license=('BSD-3')
makedepends=('cmake')
source=("https://github.com/xtensor-stack/xsimd/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('6f156ec9bcc602d2581ef023ef0e927e3759c30cf1405028210e8c103a4f846d')
 
build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

