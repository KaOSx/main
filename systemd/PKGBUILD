#
# Core Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64:) Mateusz Krawczuk <willingmagic[at]gmail[dot]com>

pkgname=systemd
pkgver=44
pkgrel=3
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('acl' 'dbus-core' 'kbd' 'kmod' 'libcap' 'util-linux' 'udev' 'xz' 'pam')
makedepends=('gperf' 'cryptsetup' 'docbook-xsl' 'intltool' 'libxslt' 'linux-api-headers')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts: legacy support for hostname and vconsole setup'
            'initscripts-systemd: native boot and initialization scripts'
            'python2-cairo: systemd-analyze'
            'systemd-units: collection of native unit files for Chakra daemon/init scripts')
options=('!libtool')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/systemd-logind.conf
        etc/systemd/systemd-journald.conf)
install="$pkgname.install"
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz"
        "os-release"
        0001-util-never-follow-symlinks-in-rm_rf_children.patch)
md5sums=('11f44ff74c87850064e4351518bcff17'
         '68cf3e12b0e98a51efde9811e5fc713c'
         '2cf6162256c02f37fe8d38edc96fa4f3')

build() {
  cd "$pkgname-$pkgver"

    #https://bugzilla.redhat.com/show_bug.cgi?id=803358
    patch -p1 <"$srcdir/0001-util-never-follow-symlinks-in-rm_rf_children.patch"
    
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  ./configure --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --with-pamlibdir=/usr/lib/security \
              --localstatedir=/var \
              --with-distro=other \
              --disable-gtk 
          
  make

  # fix .so links in manpages
  sed -i 's|\.so halt\.8|.so man8/systemd.halt.8|' man/{halt,poweroff}.8
  sed -i 's|\.so systemd\.1|.so man1/systemd.1|' man/init.1
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  printf "d /run/console 755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # symlink to /bin/systemd for compat and sanity
  install -dm755 "$pkgdir/bin"
  ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # rename man pages to avoid conflicts with sysvinit and initscripts
  cd "$pkgdir/usr/share/man"

  # not building this with systemd
  rm -f "man1/systemadm.1"

  manpages=(man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8
            man5/{hostname,{vconsole,locale}.conf}.5)

  for manpage in "${manpages[@]}"; do
    IFS='/' read section page <<< "$manpage"
    mv "$manpage" "$section/systemd.$page"
  done

   # get rid of uneeded lock directories
   sed -ri '/\/run\/lock\/(subsys|lockdev)/d' "$pkgdir"/usr/lib/tmpfiles.d/legacy.conf
}
