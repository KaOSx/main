
pkgname=glslang
pkgver=13.1.0
pkgrel=1
pkgdesc='Khronos-reference front end for GLSL/ESSL and partial front end for HLSL'
arch=('x86_64')
url='https://github.com/KhronosGroup/glslang'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake' 'python3' 'googletest')
source=("https://github.com/KhronosGroup/glslang/archive/${pkgver}.tar.gz")
md5sums=('0b5b8257b3ae121663cff65311f14ccb')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DENABLE_OPT=0
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  #ln -s ${pkgdir}/usr/bin/glslang ${pkgdir}/usr/bin/glslangValidator
}

