
pkgname=libjpeg-turbo
pkgver=2.0.5
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2' 'libjpeg6')
replaces=('libjpeg' 'libjpeg6')
conflicts=('libjpeg' 'libjpeg6')
options=('!libtool')
source=("https://sourceforge.net/projects/libjpeg-turbo/files/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('9d4c565d402b2f5661be78d76098073ec7e30f10')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_DEFAULT_LIBDIR=lib \
    -DWITH_JPEG8=TRUE
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir}/ install

  # provide jpegint.h as it is required by various software
  install -m644 ../${pkgname}-${pkgver}/jpegint.h ${pkgdir}/usr/include/

  install -Dm644 ../${pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/libjpeg-turbo/LICENSE.md
  install -Dm644 ../${pkgname}-${pkgver}/README.md ${pkgdir}/usr/share/licenses/libjpeg-turbo/README.md
}
