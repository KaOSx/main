
pkgname=libjpeg-turbo
pkgver=1.5.2
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2' 'libjpeg6')
replaces=('libjpeg' 'libjpeg6')
conflicts=('libjpeg' 'libjpeg6')
options=('!libtool')
source=("https://sourceforge.net/projects/libjpeg-turbo/files/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('e788f6defa58b4393a5e1685c018f3b962971457')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir}/ install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h ${pkgdir}/usr/include/

  install -Dm644 LICENSE.md ${pkgdir}/usr/share/licenses/libjpeg-turbo/LICENSE.md
  install -Dm644 README.md ${pkgdir}/usr/share/licenses/libjpeg-turbo/README.md
}
