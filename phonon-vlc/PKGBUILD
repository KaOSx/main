
pkgname=phonon-vlc
_pkgname=phonon-backend-vlc
pkgver=0.12.0
pkgrel=1
pkgdesc='Phonon VLC backend, Qt6 version'
arch=('x86_64')
url='https://community.kde.org/Phonon'
license=('LGPL')
depends=('vlc')
makedepends=('extra-cmake-modules' 'mesa' 'phonon-qt6' 'qt6-tools')
#groups=('kde-complete' 'kde-uninstall')
provides=('phonon-backend-qt6')
source=("https://download.kde.org/stable/phonon/${_pkgname}/${pkgver}/${_pkgname}-$pkgver.tar.xz")
sha256sums=('338479dc451e4b94b3ca5b578def741dcf82f5c626a2807d36235be2dce7c9a5')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPHONON_BUILD_QT5=OFF \
    -DPHONON_BUILD_QT6=ON \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
