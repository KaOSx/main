
pkgname=gptfdisk
pkgver=1.0.5
pkgrel=1
pkgdesc="Text-mode partitioning tool that works on Globally Unique Identifier (GUID) Partition Table (GPT) disks"
arch=('x86_64')
url="http://www.rodsbooks.com/gdisk/"
license=('GPL2')
depends=('gcc-libs' 'util-linux' 'popt' 'icu')
provides=("gdisk=${pkgver}")
source=("https://downloads.sourceforge.net/project/gptfdisk/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('58dac67c85e46ca87b587231549aefe6')

build() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's|-lncursesw|$(shell $(PKG_CONFIG) --libs ncursesw)|g' Makefile

  make
}

package () {
  cd ${pkgname}-${pkgver}

  install -d $pkgdir/usr/{bin,share/{doc/gdisk,man/man8}}
  install -t $pkgdir/usr/bin {,c,s}gdisk fixparts
  install -m644 -t $pkgdir/usr/share/man/man8 {{,c,s}gdisk,fixparts}.8
  install -m644 -t $pkgdir/usr/share/doc/gdisk README NEWS
}
