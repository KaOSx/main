
pkgname=opensc
pkgver=0.23.0
pkgrel=2
pkgdesc='Tools and libraries for smart cards'
arch=('x86_64')
url='https://github.com/OpenSC/OpenSC/wiki'
license=('LGPL')
backup=('etc/opensc.conf')
depends=('openssl' 'pcsclite' 'libltdl')
makedepends=('docbook-xsl')
options=('!emptydirs')
source=("https://github.com/OpenSC/OpenSC/archive/$pkgver.tar.gz")
md5sums=('5f4ff4664e1364f6e976b2b91f54320e')

build() {
    cd OpenSC-$pkgver
    export LIBS=-lltdl
    ./bootstrap
    _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*)

    ./configure \
          --prefix=/usr \
          --sysconfdir=/etc \
          --enable-man \
          --enable-doc \
          --enable-readline \
          --enable-openssl \
          --enable-pcsc \
          --enable-zlib \
          --enable-sm \
          --with-xsl-stylesheetsdir="$_sheetdir"
    make
}

package(){
    cd OpenSC-$pkgver
    make DESTDIR="$pkgdir" install

    install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
}

