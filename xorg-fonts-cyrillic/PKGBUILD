
pkgname=xorg-fonts-cyrillic
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org cyrillic fonts"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
install=xfonts.install
source=("https://xorg.freedesktop.org/releases/individual/font/font-cronyx-cyrillic-${pkgver}.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-misc-cyrillic-${pkgver}.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-screen-cyrillic-1.0.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-winitzki-cyrillic-${pkgver}.tar.xz")
sha256sums=('dc0781ce0dcbffdbf6aae1a00173a13403f92b0de925bca5a9e117e4e2d6b789'
            '76021a7f53064001914a57fd08efae57f76b68f0a24dca8ab1b245474ee8e993'
            '8f758bb8cd580c7e655487d1d0db69d319acae54d932b295d96d9d9b83fde5c0'
            '3b6d82122dc14776e3afcd877833a7834e1f900c53fc1c7bb2d67c781cfa97a8')

build() {
  cd ${srcdir}

  for dir in *; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      ./configure --prefix=/usr --with-fontdir=/usr/share/fonts/cyrillic
      make
      popd
    fi
  done
}

package() {
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  cd ${srcdir}

  for dir in *; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      make DESTDIR=${pkgdir} install
      install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}
      popd
    fi    
  done

  rm -f ${pkgdir}/usr/share/fonts/*/fonts.*
}
