
pkgname=aspell-pt
_pkgverPT=20220304
_pkgverBR=20131030
pkgver=$_pkgverPT
pkgrel=1
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('x86_64')
url="http://aspell.net/"
license=('LGPL' 'GPL')
depends=('aspell')
source=("https://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-${_pkgverBR}-12-0.tar.bz2"
        "https://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-${_pkgverPT}.tar.bz2"
        "https://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-${_pkgverPT}.tar.bz2")
md5sums=('9ac547609aeaa3891dfa00407b7ffb83'
         '7f35c7071931bcfd91561dd384ccabc8'
         'f99a9973d2e99669b3452f00346f4a21')

build() {
  cd aspell6-pt_BR-${_pkgverBR}-12-0
  ./configure
  make

  cd ../aspell6-pt_PT-${_pkgverPT}-0
  ./configure
  make

  cd ../aspell6-pt_PT-preao-${_pkgverPT}-0
  ./configure
  make
}

package() {
  cd aspell6-pt_BR-${_pkgverBR}-12-0
  make DESTDIR=${pkgdir} install

  cd ../aspell6-pt_PT-${_pkgverPT}-0
  make DESTDIR=${pkgdir} install

  cd ../aspell6-pt_PT-preao-${_pkgverPT}-0
  make DESTDIR=${pkgdir} install

  echo "add pt_BR.multi" > ${pkgdir}/usr/lib/aspell-0.60/brasileiro.alias
}
