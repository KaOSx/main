
pkgname=gtk2
pkgver=2.24.32
_pkgver=2.24
pkgrel=1
arch=('x86_64')
url="https://www.gtk.org/"
pkgdesc="GTK+ is a multi-platform toolkit (v2)"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
makedepends=('gobject-introspection')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
source=("https://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-$pkgver.tar.xz"
        'xid-collision-debug.patch'
        'gtk-query-immodules-2.0.hook')
sha256sums=('b6c8a93ddda5eabe3bfee1eb39636c9a03d2a56c7b62828b359bf197943c582e'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '9656a1efc798da1ac2dae94e921ed0f72719bd52d4d0138f305b993f778f7758')

build() {
    cd gtk+-${pkgver}
    patch -p1 -i ${srcdir}/xid-collision-debug.patch

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes
    make
}

package() {
    cd gtk+-${pkgver}
    make DESTDIR=${pkgdir} install

    echo 'gtk-fallback-icon-theme = "gnome"' > ${pkgdir}/etc/gtk-2.0/gtkrc
    
    install -Dm644 ../gtk-query-immodules-2.0.hook ${pkgdir}/usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook
    
    rm $pkgdir/usr/bin/gtk-update-icon-cache
}

