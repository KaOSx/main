
pkgname=libxvmc
pkgver=1.0.13
pkgrel=1
pkgdesc="X11 Video Motion Compensation extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxv')
makedepends=('pkgconfig')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXvMC-${pkgver}.tar.xz")
sha256sums=('0a9ebe6dea7888a747e5aca1b891d53cd7d3a5f141a9645f77d9b6a12cee657c')

build() {
  cd libXvMC-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXvMC-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
