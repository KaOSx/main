
pkgname=libxvmc
pkgver=1.0.11
pkgrel=1
pkgdesc="X11 Video Motion Compensation extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxv')
makedepends=('pkgconfig')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/libXvMC-${pkgver}.tar.bz2")
sha256sums=('4a2e34d444a683a7c010b01b23cefe2b8043a063ce4dc6a9b855836b5262622d')

build() {
  cd libXvMC-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXvMC-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
