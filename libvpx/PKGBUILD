
pkgname=libvpx
pkgver=1.9.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="https://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=("https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz")
#source=("http://storage.googleapis.com/downloads.webmproject.org/releases/webm/libvpx-${pkgver}.tar.bz2")
md5sums=('e5fab59896984392124d0bfaffc36e14')

build() {
    cd libvpx-${pkgver}

    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs \
        --enable-experimental
        #--enable-spatial-svc
    make
}

package() {
    cd libvpx-${pkgver}
    make DIST_DIR=${pkgdir}/usr install
    
    #chmod -R o+r "${pkgdir}/usr/include/vpx"
    install -D -m 0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
