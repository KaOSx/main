
pkgname=libvpx
pkgver=1.6.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=("https://github.com/webmproject/libvpx/archive/v${pkgver}.tar.gz")
#source=("$pkgname::git+http://code.google.com/p/webm.libvpx#tag=v$pkgver")
md5sums=('ba3e22827b9c017213e3a122fabf677b')

build() {
    cd ${srcdir}/libvpx-${pkgver}

    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd ${srcdir}/libvpx-${pkgver}
    make DIST_DIR="$pkgdir/usr" install
    
    chmod -R o+r "${pkgdir}/usr/include/vpx"
    install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
