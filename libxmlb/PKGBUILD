
pkgname=libxmlb
pkgver=0.3.20
pkgrel=1
pkgdesc="Library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL2.1')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
#source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/hughsie/libxmlb/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4c5b534d645f7328643d6a0d3040ffb9832e13e3530025af55086a06e3c018ed')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dgtkdoc=false \
    -Dtests=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
} 
