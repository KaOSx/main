
pkgname=libxmlb
pkgver=0.3.8
pkgrel=1
pkgdesc="Library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL2.1')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('87d3729069bc74cc269549173a8fd798dae70bb05016a1353f66146725df0801')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dgtkdoc=false \
    -Dtests=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
} 
