
pkgname=libxmlb
pkgver=0.3.14
pkgrel=1
pkgdesc="Library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL2.1')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
#source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/hughsie/libxmlb/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a2f0056eed14ff791aee2b08b1514a0f1b6cf215f0579138a8cae8c45a0d3b0f')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dgtkdoc=false \
    -Dtests=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
} 
