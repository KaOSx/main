
pkgname=groff
pkgver=1.22.4
pkgrel=9
pkgdesc="GNU troff text-formatting system"
arch=('x86_64')
url="https://www.gnu.org/software/groff/groff.html"
license=('GPL')
depends=('perl' 'gcc-libs' 'texinfo')
makedepends=('netpbm' 'psutils' 'ghostscript')
optdepends=('netpbm: to use together with man -H command interaction in browsers'
            'psutils: to use together with man -H command interaction in browsers')
source=("https://ftp.gnu.org/gnu/groff/${pkgname}-${pkgver}.tar.gz"
        'site.tmac'
        'gropdf.patch')
options=('!docs' '!emptydirs')
md5sums=('08fb04335e2f5e73f23ea4c3adbf0c5f'
         'a1fedafd7863b37968d32ad9ae1d8c13'
         '96f261605bd4b31c1f0b56cf590cf459')

build() {
  cd ${pkgname}-${pkgver}
  # https://savannah.gnu.org/bugs/index.php?43581
  #patch -p1 -i ${srcdir}/gropdf.patch
  
  
  ./configure --prefix=/usr --without-x
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # needed for xman
  ln -s eqn ${pkgdir}/usr/bin/geqn
  ln -s tbl ${pkgdir}/usr/bin/gtbl
  ln -s soelim ${pkgdir}/usr/bin/zsoelim
  
  # Fix some issues when encoding to utf8 man pages
  cat ${srcdir}/site.tmac >> \
    ${pkgdir}/usr/share/groff/site-tmac/man.local
  cat ${srcdir}/site.tmac >> \
    ${pkgdir}/usr/share/groff/site-tmac/mdoc.local
}
