
pkgname=groff
pkgver=1.22.4
pkgrel=19
pkgdesc="GNU troff text-formatting system"
arch=('x86_64')
url="https://www.gnu.org/software/groff/groff.html"
license=('GPL')
depends=('perl' 'gcc-libs' 'texinfo')
makedepends=('netpbm' 'psutils' 'ghostscript')
optdepends=('netpbm: to use together with man -H command interaction in browsers'
            'psutils: to use together with man -H command interaction in browsers')
source=("https://ftp.gnu.org/gnu/groff/${pkgname}-${pkgver}.tar.gz")
options=('!docs' '!emptydirs')
md5sums=('08fb04335e2f5e73f23ea4c3adbf0c5f')

build() {
  cd ${pkgname}-${pkgver}
  # https://savannah.gnu.org/bugs/index.php?43581
  #patch -p1 -i ${srcdir}/gropdf.patch

  ./configure --prefix=/usr --without-x
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # FAIL: contrib/gdiffmk/tests/gdiffmk_tests.sh
  make check
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # needed for xman
  ln -s eqn ${pkgdir}/usr/bin/geqn
  ln -s tbl ${pkgdir}/usr/bin/gtbl
  ln -s soelim ${pkgdir}/usr/bin/zsoelim
}
