
pkgname=ttf-liberation
pkgver=2.1.1
_pkgver=4743885
pkgrel=1
pkgdesc="Font family which aims at metric compatibility with Arial, Times New Roman, and Courier New, sponsored by Red Hat"
arch=('x86_64')
license=('custom' 'GPL2')
url="https://pagure.io/liberation-fonts/"
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
makedepends=('fontforge' 'python3-fonttools')
install=${pkgname}.install
source=("https://github.com/liberationfonts/liberation-fonts/files/${_pkgver}/liberation-fonts-${pkgver}.tar.gz"
        '30-0-liberation-mono.conf'
        '30-0-liberation-sans.conf'
        '30-0-liberation-serif.conf')
md5sums=('a0841311f96b44378e5cea3322eda7bd'
         '23ce227d5d72c6df2c69caf367d96341'
         '88e91e718053d46cb1215067ffb0024a'
         '5bc229f0f7ad387f1ba311148fcbff00')

build() {
  cd liberation-fonts-${pkgver}
  
  make 
}
package() {
  cd liberation-fonts-${pkgver}/liberation-fonts-ttf-${pkgver}

  install -d ${pkgdir}/usr/share/fonts/TTF/
  install -m644 *.ttf ${pkgdir}/usr/share/fonts/TTF/
  
  install -Dm0644 ${srcdir}/30-0-liberation-mono.conf ${pkgdir}/etc/fonts/conf.avail/30-${pkgname}-mono.conf
  install -Dm0644 ${srcdir}/30-0-liberation-sans.conf ${pkgdir}/etc/fonts/conf.avail/30-${pkgname}-sans.conf
  install -Dm0644 ${srcdir}/30-0-liberation-serif.conf ${pkgdir}/etc/fonts/conf.avail/30-${pkgname}-serif.conf

  install -Dm0644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

