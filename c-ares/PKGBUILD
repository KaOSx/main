
pkgname=c-ares
pkgver=1.16.0
pkgrel=1
pkgdesc='C library that performs DNS requests and name resolves asynchronously'
arch=('x86_64')
url='https://c-ares.haxx.se/'
license=('MIT')
depends=('glibc')
makedepends=('cmake')
source=("https://c-ares.haxx.se/download/${pkgname}-${pkgver}.tar.gz"
        'LICENSE')
md5sums=('fe28cb779f17ff6a75473225394edbdb'
         'c69f2042941b708ce3e7121424d0b7e6')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCARES_STATIC=OFF \
    -DCARES_SHARED=ON
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
