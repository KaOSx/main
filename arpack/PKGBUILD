
pkgname=arpack
pkgver=3.8.0
pkgrel=2
arch=('x86_64')
pkgdesc="Fortran77 subroutines designed to solve large scale eigenvalue problems"
url="https://github.com/opencollab/arpack-ng"
license=('BSD')
depends=('glibc' 'lapack' 'openmpi')
options=('!libtool')
source=("https://github.com/opencollab/arpack-ng/archive/${pkgver}.tar.gz")
md5sums=('bb4cf448f2480a0ffe5517d579f980c3')

build() {
  cd ${pkgname}-ng-${pkgver}

  sh bootstrap
  ./configure --prefix=/usr --enable-mpi
  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd ${pkgname}-ng-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
