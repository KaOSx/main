
pkgname=libgit2
pkgver=1.0.1
pkgrel=1
pkgdesc='Cross-platform, linkable library implementation of Git that you can use in your application.'
arch=('x86_64')
url="https://libgit2.org/"
depends=('openssl' 'pcre' 'zlib')
makedepends=('cmake' 'libssh2' 'python3')
license=('GPL2')
source=("https://github.com/libgit2/libgit2/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('63d199e1421b3000983fe828f6a26db8')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_HTTP_PARSER=builtin
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
  install -m644 -D ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
