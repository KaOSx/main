
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt6-tools
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="A cross-platform application and UI framework, development tools and help files"
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('qt6-declarative' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
         #'qt6-translations' 'qt5-location')
makedepends=('cmake' 'ninja' 'clang' 'vulkan-headers')
#optdepends=('qt6-doc: offline help documentation')
groups=('qt6')
options=('!libtool')
_pkgfqn="qttools-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        "https://github.com/qt/qttools/commit/96306747f46f6c9f770bbe5b8adf22121063f087.diff")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         'a1529d53e16b196b5fa5b82f5dadceb4')

prepare() {
  cd ${_pkgfqn}

  patch -p1 -i ${srcdir}/96306747f46f6c9f770bbe5b8adf22121063f087.diff
}

build() {
    cmake -B build -S ${_pkgfqn} -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt6/bin); do
        ln -s /usr/lib/qt6/bin/${i} ${pkgdir}/usr/bin/${i}-qt6
    done

    install -D -m644 ${_pkgfqn}/LICENSES/* -t ${pkgdir}/usr/share/licenses/${pkgname}
}

