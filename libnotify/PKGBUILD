
pkgname=libnotify
pkgver=0.7.11
_pkgver=0.7
pkgrel=1
pkgdesc="Desktop notification library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/libnotify"
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('gtk3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/libnotify/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('dd5682ec68220339e11c5159b7e012204a318dd1b3683a09c482ca421ab07375')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgtk_doc=false \
    -Ddocbook_docs=disabled \
    -Dman=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

