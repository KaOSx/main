
pkgname=libnotify
pkgver=0.8.0
_pkgver=0.8
pkgrel=1
pkgdesc="Desktop notification library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/libnotify"
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('gtk3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/libnotify/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('46a26f0db4e64cf24016291eb1579ed9f0ba7611fe6cd9e1afec8f42780d3924')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgtk_doc=false \
    -Ddocbook_docs=disabled \
    -Dman=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

