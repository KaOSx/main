
pkgname=libnotify
pkgver=0.8.1
_pkgver=0.8
pkgrel=1
pkgdesc="Desktop notification library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/libnotify"
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('gtk3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/libnotify/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d033e6d4d6ccbf46a436c31628a4b661b36dca1f5d4174fe0173e274f4e62557')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgtk_doc=false \
    -Ddocbook_docs=disabled \
    -Dman=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

