
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=('qt5-declarative' 'qt5-declarative-docs')
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
arch=('x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
makedepends=('qt5-xmlpatterns' 'mesa' 'python2' 'qt5-tools')
options=('!libtool' 'debug')
_pkgfqn="qtdeclarative-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        "https://github.com/qt/qtdeclarative/commit/98358715930739ca8de172d88c5ce6941c275ff3.diff")
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '6529d25e63640f8f27f47c088490d953')

build() {
    cd $srcdir/$_pkgfqn
    #patch -R -p1 -i $srcdir/98358715930739ca8de172d88c5ce6941c275ff3.diff

    /usr/lib/qt5/bin/qmake
    make
    make docs
}

package_qt5-declarative() {
    pkgdesc="A cross-platform application and UI framework, QtDeclarative files"
    depends=('qt5-xmlpatterns' 'mesa')
    groups=('qt5')
    
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install
    
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

package_qt5-declarative-docs() {
    pkgdesc="Documentation files for QtDeclarative"
    depends=('qt5-base')
    groups=('qt5-docs')
    
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install_docs
}
