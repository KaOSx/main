
pkgname=fcgi
pkgver=2.4.2
pkgrel=1
depends=('gcc-libs')
pkgdesc="FASTCgi (fcgi) is a language independent, high performant extension to CGI"
url="https://fastcgi-archives.github.io/"
arch=('x86_64')
license=('custom')
options=('!makeflags')
#source=("https://sourceforge.net/projects/slackbuildsdirectlinks/files/fcgi/${pkgname}-${pkgver}.tar.gz"
source=("https://github.com/FastCGI-Archives/fcgi2/archive/${pkgver}.tar.gz")
md5sums=('146376d5c5c059b9184755db76505fab')

build() {
  cd ${pkgname}2-${pkgver}
  
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}2-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 LICENSE.TERMS ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
