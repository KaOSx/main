
pkgname=python3-setuptools
pkgver=71.0.4
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python3 packages"
arch=('x86_64')
license=('PSF')
url="https://pypi.org/project/setuptools/"
depends=('python3-appdirs' 'python3-packaging')
makedepends=('asciidoc' 'python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/s/setuptools/setuptools-${pkgver}.tar.gz")
#source=("https://github.com/pypa/setuptools/archive/v${pkgver}.tar.gz")
sha256sums=('48297e5d393a62b7cb2a10b8f76c63a73af933bd809c9e0d0d6352a1a0135dd8')

build() {
  cd setuptools-${pkgver}
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py

  python3 -m build -nw
}

package() {
  cd setuptools-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}


