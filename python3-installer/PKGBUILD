
pkgname=python3-installer
_pkgname=installer
pkgver=0.5.1
_pyver=3.10
pkgrel=1
pkgdesc="Low-level library for installing from a Python wheel distribution."
arch=('x86_64')
license=('MIT')
url="https://github.com/pypa/installer"
depends=('python3')
makedepends=('python3-build' 'python3-flit' 'python3-installer' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/i/installer/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('f970995ec2bb815e2fdaf7977b26b2091e1e386f0f42eafd5ac811953dc5d445')

build() {
    cd ${_pkgname}-${pkgver}

    python3 -m build -nw
}

package() {
    cd ${_pkgname}-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    # windows exe files are useless
    rm ${pkgdir}/usr/lib/python${_pyver}/site-packages/installer/_scripts/*.exe
}
