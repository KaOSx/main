
pkgname=gtk4
pkgver=4.10.2
_pkgver=4.10
pkgrel=1
pkgdesc="The GTK+ Toolkit (v4)"
arch=('x86_64')
url="https://www.gtk.org/"
depends=('cairo' 'ffmpeg' 'gdk-pixbuf2' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
          'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 
          'colord' 'at-spi2-core' 'libepoxy' 'libxkbcommon' 'wayland'
          'wayland-protocols' 'iso-codes' 'vulkan-loader' 'graphene' 'gst-plugins-bad')
makedepends=('gobject-introspection' 'sassc' 'meson' 'ninja')
options=('!libtool' '!docs')
license=('LGPL')
source=("https://ftp.gnome.org/pub/gnome/sources/gtk/${_pkgver}/gtk-${pkgver}.tar.xz")
sha256sums=('ca6c81029e1db6326be6ab5ddf1c734a39d3aab46e6a4e0fcced20d01a7cd585')

build() {
  mkdir -p build
  cd build

  meson setup ../gtk-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -Dgtk_doc=false \
    -Dintrospection=enabled \
    -Ddemos=false \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    -Dvulkan=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

