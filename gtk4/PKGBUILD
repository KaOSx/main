
pkgname=gtk4
pkgver=4.6.0
_pkgver=4.6
pkgrel=1
pkgdesc="The GTK+ Toolkit (v4)"
arch=('x86_64')
url="https://www.gtk.org/"
depends=('cairo' 'ffmpeg' 'gdk-pixbuf2' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
          'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 
          'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'wayland'
          'wayland-protocols' 'iso-codes' 'vulkan-loader' 'graphene' 'gst-plugins-bad')
makedepends=('gobject-introspection' 'sassc' 'meson' 'ninja')
options=('!libtool' '!docs')
license=('LGPL')
source=("https://ftp.gnome.org/pub/gnome/sources/gtk/${_pkgver}/gtk-${pkgver}.tar.xz")
sha256sums=('782d5951fbfd585fc9ec76c09d07e28e6014c72db001fb567fff217fb96e4d8c')

build() {
  mkdir -p build
  cd build

  meson ../gtk-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -Dgtk_doc=false \
    -Dintrospection=enabled \
    -Ddemos=false \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    -Dinstall-tests=false \
    -Dvulkan=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

