
pkgname=wxwidgets
_pkgname=wxWidgets
pkgver=3.2.2.1
pkgrel=1
epoch=1
pkgdesc="Open source cross-platform C++ framework for writing advanced GUI applications using native controls."
arch=('x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk3' 'glu' 'gst-plugins-base' 'libnotify' 'sdl2' 'pcre2') #no build flag available to use system version
makedepends=('cmake')
options=('!emptydirs')
conflicts=('wxgtk')
replaces=('wxgtk')
provides=('wxgtk')
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('d645cacbf2678cfc402ae163f2b0e0b05be5d239')

prepare() {
  cd ${_pkgname}-${pkgver}
  #patch -p1 -i ../wxGTK-2.9.5-collision.patch
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxUSE_REGEX=sys \
    -DwxUSE_ZLIB=sys \
    -DwxUSE_EXPAT=sys \
    -DwxUSE_LIBJPEG=sys \
    -DwxUSE_LIBPNG=sys \
    -DwxUSE_LIBTIFF=sys \
    -DwxUSE_LIBLZMA=sys \
    -DwxUSE_OPENGL=ON \
    -DwxUSE_LIBSDL=ON \
    -DwxUSE_WEBVIEW=OFF \
    -DwxUSE_INTL=ON \
    -DwxBUILD_TOOLKIT=gtk3
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D -m644 ${_pkgname}-${pkgver}/docs/licence.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
