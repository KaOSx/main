#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=upower
pkgver=0.9.17
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd>=188' 'libusbx' 'polkit>=0.98' 'pm-utils>=1.4.1' 'dbus-glib>=0.92')
makedepends=('pkg-config' 'intltool' 'docbook-xsl')
replaces=('devicekit-power')
options=('!libtool')
source=($url/releases/${pkgname}-${pkgver}.tar.xz)
md5sums=('9ef7fc8ec438542f014f3a34552822aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/upower \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}