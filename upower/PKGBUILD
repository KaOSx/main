
pkgname=upower
pkgver=0.99.16
pkgrel=2
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('x86_64')
url="https://upower.freedesktop.org"
license=('GPL')
depends=('systemd' 'libgudev' 'polkit' 'dbus-glib' 'libimobiledevice')
makedepends=('meson' 'ninja' 'pkg-config' 'intltool' 'docbook-xsl' 'gtk-doc')
options=('!libtool')
install=upower.install
source=("https://gitlab.freedesktop.org/upower/upower/-/archive/v${pkgver}/upower-v${pkgver}.tar.bz2"
#source=("https://gitlab.freedesktop.org/upower/upower/uploads/177df5b9f9b76f25a2ad9da41aa0c1fa/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/upower/upower/commit/7192ff481bc46a257f44fb5625cbe97361cac140.diff"
        "https://gitlab.freedesktop.org/upower/upower/commit/72469426b737f1398651efc360f62a86e3b2633b.diff"
        "https://gitlab.freedesktop.org/upower/upower/commit/2f81d690148cd80909083e0390ba50fb1e431b39.diff")
sha1sums=('7ce9e16cb0902df2db52aaa4f0f345c56dbdfcde'
          '54fd2692b0ec4af7e320a3b07c7d6e4a8b06fad0'
          'dd1709eb6c82446cf07edb9366ec6c2d8b5ef792'
          'e0de251ff38ea7981ef5be097ac2fbcfd787e8c3')

prepare() {
  cd ${pkgname}-v${pkgver}

  sed -e "s|PrivateUsers=yes|PrivateUsers=no|" -i src/upower.service.in
  patch -p1 -i ${srcdir}/7192ff481bc46a257f44fb5625cbe97361cac140.diff
  patch -p1 -i ${srcdir}/72469426b737f1398651efc360f62a86e3b2633b.diff
  patch -p1 -i ${srcdir}/2f81d690148cd80909083e0390ba50fb1e431b39.diff
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-v${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dgtk-doc=false \
    -Dsystemdsystemunitdir=/usr/lib/systemd/system \
    -Dudevrulesdir=/usr/lib/udev/rules.d \
    -Didevice=enabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
