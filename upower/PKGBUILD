#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=upower
pkgver=0.9.19
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd>=196' 'libusbx' 'polkit>=0.98' 'pm-utils>=1.4.1' 'dbus-glib>=0.92')
makedepends=('pkg-config' 'intltool' 'docbook-xsl')
replaces=('devicekit-power')
options=('!libtool')
install=upower.install
source=($url/releases/${pkgname}-${pkgver}.tar.xz
       'unifying.patch')
md5sums=('f96955ff1a2e4f006937d6b5ea95afb8'
         '04b2a6141e80e422a9fe2820b5b3a613')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  patch -p1 -i "${srcdir}"/unifying.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/upower \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}