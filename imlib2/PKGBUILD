
pkgname=imlib2
pkgver=1.12.0
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="https://sourceforge.net/projects/enlightenment/"
arch=('x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo' 'libheif' 'jpeg-xl')
options=('!libtool')
source=("https://downloads.sourceforge.net/enlightenment/${pkgname}-${pkgver}.tar.xz")
sha256sums=('95ff5d4cc17dd9f934c2e7ad151c360f308880a0a7849a6ca43b7c7b9a4bb216')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i $srcdir/giflib51.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc/imlib2 \
    --x-libraries=/usr/lib \
    --enable-amd64
  make
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
