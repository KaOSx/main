
pkgname=man-db
pkgver=2.9.3
pkgrel=1
pkgdesc="A utility for reading man pages"
arch=('x86_64')
url="https://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
depends=( 'bash' 'gdbm' 'zlib' 'groff' 'less' 'libpipeline')
optdepends=('gzip')
backup=('etc/man_db.conf')
install=${pkgname}.install
source=("https://download-mirror.savannah.gnu.org/releases/man-db/${pkgname}-${pkgver}.tar.xz"
        'convert-mans'
        'man-db.service'
        'man-db.timer'
        'root_man.patch')
md5sums=('4c8721faa54a4c950c640e5e5c713fb0'
         '2b7662a7d5b33fe91f9f3e034361a2f6'
         '36fb3f616e42a0e23cf7a4d284e640cd'
         'df6d4f432e3e16c255c785fca1447346'
         '86a53b9d348f65aebdba0e65dce8da06')
                        
build() {
  cd ${pkgname}-${pkgver}
  # tmpfile errors, swap man root https://bugzilla.redhat.com/show_bug.cgi?id=1110274
  #patch -p1 -i ${srcdir}/root_man.patch
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-db=gdbm \
        --disable-setuid \
        --disable-cache-owner \
        --enable-mandirs=GNU \
        --with-sections="1 n l 8 3 0 2 5 4 9 6 7"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check || true
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # groff conflict
  rm -f ${pkgdir}/usr/bin/zsoelim

  # http://www.linuxfromscratch.org/lfs/view/6.4/chapter06/man-db.html
  install -D -m755 ${srcdir}/convert-mans  ${pkgdir}/usr/bin/convert-mans 

  # install man-db scheduler
  install -D -m644 ${srcdir}/man-db.timer ${pkgdir}/usr/lib/systemd/system/man-db.timer
  install -D -m644 ${srcdir}/man-db.service ${pkgdir}/usr/lib/systemd/system/man-db.service
  install -d -m755 ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants
  ln -s ../man-db.timer ${pkgdir}/usr/lib/systemd/system/multi-user.target.wants/man-db.timer
}

