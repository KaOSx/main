
pkgname=libtheora
pkgver=1.1.1
pkgrel=15
pkgdesc="An open video codec developed by the Xiph.org"
arch=('x86_64')
url="https://www.xiph.org"
license=('BSD')
depends=('libogg')
makedepends=('libvorbis')
options=('!libtool')
source=("https://downloads.xiph.org/releases/theora/libtheora-${pkgver}.tar.bz2")
sha256sums=('b6ae1ee2fa3d42ac489287d3ec34c5885730b1296f0801ae577a35193d3affbc')

build() {
  cd libtheora-${pkgver}

  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd libtheora-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
