
pkgname=python3-flit
_pkgname=flit
pkgver=3.8.0
pkgrel=1
pkgdesc="Simplified packaging of Python modules."
arch=('x86_64')
url='https://github.com/pypa/pep517'
license=('MIT')
depends=('python3-docutils' 'python3-pip' 'python3-requests')
makedepends=('python3-wheel')
source=("https://files.pythonhosted.org/packages/source/f/flit/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('d0f2a8f4bd45dc794befbf5839ecc0fd3830d65a57bd52b5997542fac5d5e937')

prepare() {
  cd ${_pkgname}-${pkgver}

  cd flit_core
  python3 build_dists.py
}

build() {
  cd ${_pkgname}-${pkgver}

  PYTHONPATH=flit_core python3 -m flit build --format wheel
}

package() {
  cd ${_pkgname}-${pkgver}

  PIP_CONFIG_FILE=/dev/null pip3 install --isolated --root=${pkgdir} --ignore-installed --no-deps dist/*.whl
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd flit_core
  PIP_CONFIG_FILE=/dev/null pip3 install --isolated --root="${pkgdir}" --ignore-installed --no-deps dist/*.whl
}

 
