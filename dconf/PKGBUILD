
pkgname=dconf
pkgver=0.40.0
_pkgver=0.40
pkgrel=10
pkgdesc="A low-level configuration system."
arch=('x86_64')
url="https://wiki.gnome.org/Projects/dconf"
license=('LGPL2.1')
depends=('glib2')
makedepends=('gobject-introspection' 'intltool' 'docbook-xsl' 'python3' 'meson' 'ninja')
source=("https://download.gnome.org/sources/dconf/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('cf7f22a4c9200421d8d3325c5c1b8b93a36843650c9f95d6451e20f0bcb24533')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -e "s|subdir('tests')|#subdir('tests')|" -i meson.build
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/dconf \
    -D vapi=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
