
pkgname=wxgtk
pkgver=3.0.4
pkgrel=4
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'glu' 'gst-plugins-base')
options=('!emptydirs')
conflicts=('wxgtk2.9')
replaces=('wxgtk2.9')
provides=('wxgtk2.9')
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2")
sha1sums=('246561a73ec5b9a5a7aaaaed46b64515dc9039ab')

prepare() {
  cd wxWidgets-${pkgver}
  #patch -p1 -i ../wxGTK-2.9.5-collision.patch
}

build() {
  cd wxWidgets-${pkgver}
  
  ./configure --prefix=/usr \
    --libdir=/usr/lib \
    --with-gtk=2 \
    --with-opengl \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-mediactrl \
    --disable-webview \
    --with-regex=builtin \
    --with-libpng=sys \
    --with-libxpm=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --disable-precomp-headers 
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 docs/licence.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
