
pkgname=zvbi
pkgver=0.2.40
pkgrel=2
pkgdesc="VBI capture and decoding library"
url="https://github.com/zapping-vbi/zvbi"
arch=('x86_64')
depends=('libpng' 'libx11')
license=('GPL')
options=('!libtool')
#source=("https://downloads.sourceforge.net/sourceforge/zapping/${pkgname}-${pkgver}.tar.bz2")
source=("https://github.com/zapping-vbi/zvbi/archive/refs/tags/v${pkgver}.tar.gz")
md5sums=('bc7bfedf197fa99c8545c3946c01324b')

build() {
  cd ${pkgname}-${pkgver}
  # https://github.com/zapping-vbi/zvbi/issues/40
  sed -i -e 's|HEADERS = libzvbi.h|HEADERS = libzvbi.h version.h|' src/Makefile.am

  ./autogen.sh
  ./configure --prefix=/usr \
    --disable-static \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}

