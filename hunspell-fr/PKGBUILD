
pkgname=hunspell-fr
pkgver=7.0
pkgrel=1
pkgdesc="French (modern) hunspell dictionaries"
arch=('x86_64')
url="https://grammalecte.net"
license=('GPL' 'MPL' 'LGPL')
optdepends=('hunspell:	the spell checking libraries and apps')
source=("https://grammalecte.net/download/fr/hunspell-french-dictionaries-v${pkgver}.zip")
md5sums=('d403a9f7b4a69c51b16e5f8fe07bc040')

package() {
  cd ${srcdir}
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 fr-toutesvariantes.dic ${pkgdir}/usr/share/hunspell/fr_FR.dic
  install -m644 fr-toutesvariantes.aff ${pkgdir}/usr/share/hunspell/fr_FR.aff

  pushd ${pkgdir}/usr/share/hunspell/
  fr_FR_aliases="fr_BE fr_CA fr_CH fr_LU"
  for lang in ${fr_FR_aliases}; do
	ln -s fr_FR.aff ${lang}.aff
	ln -s fr_FR.dic ${lang}.dic
  done

  popd

  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd
  
  install -Dm644 README_dict_fr.txt $pkgdir/usr/share/doc/${pkgname}/README_fr.txt
}
