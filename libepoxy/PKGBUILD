
pkgname=libepoxy
pkgver=1.5.0
_pkgver=1.5
pkgrel=1
pkgdesc="Library for handling OpenGL function pointer management for you"
url="https://github.com/anholt/libepoxy"
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('xorg-util-macros' 'python3' 'libgl' 'meson' 'ninja')
options=('!libtool')
source=("https://github.com/anholt/libepoxy/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('63fe3847789258254dcd7e3fdb9e7f5e')

build() {
    mkdir -p build
    cd build

    meson ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release
    ninja
}

check() {
    cd build

    meson test
}

package() {
    cd build
    DESTDIR=${pkgdir} ninja install

    install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
