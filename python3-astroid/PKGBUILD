
pkgname=python3-astroid
pkgver=2.14.1
pkgrel=1
pkgdesc="Module to provide a common base representation of python source code for projects 
         such as pychecker, pyreverse and pylint"
arch=('x86_64')
url="https://github.com/pycqa/astroid"
license=('LGPL')
depends=('python3-lazy-object-proxy' 'python3-logilab-common' 'python3-six' 'python3-wrapt')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
#source=("https://github.com/PyCQA/astroid/archive/astroid-${pkgver}.tar.gz")
source=("https://files.pythonhosted.org/packages/source/a/astroid/astroid-${pkgver}.tar.gz")
sha256sums=('bd1aa4f9915c98e8aaebcd4e71930154d4e8c9aaf05d35ac0a63d1956091ae3f')

build() {
  cd astroid-${pkgver}
  sed -i "s|setuptools~=|setuptools>=|g" pyproject.toml
  sed -i "s|wheel~=|wheel>=|g" pyproject.toml

  python3 -m build -nw
}

package() {
  cd astroid-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
}
