 
pkgname=python3-psutil
pkgver=5.9.6
pkgrel=1
pkgdesc='Cross-platform process and system utilities module for Python'
arch=('x86_64')
url='https://github.com/giampaolo/psutil'
license=('custom: BSD')
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/p/psutil/psutil-${pkgver}.tar.gz")
sha256sums=('e4b92ddcd7dd4cdd3f900180ea1e104932c7bce234fb88976e2a3b296441225a')

build() {
  cd psutil-${pkgver}

  python3 -m build -nw
}

package() {
  cd psutil-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -D -m 644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
