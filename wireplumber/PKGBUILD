
pkgname=wireplumber
pkgver=0.4.10
pkgrel=3
pkgdesc="Modular session/policy manager for PipeWire and a GObject-based library that wraps PipeWire's API."
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
arch=('x86_64')
depends=('pipewire' 'glib2') # 'lua')
makedepends=('meson' 'ninja' 'gobject-introspection')
install=wireplumber.install
source=("https://gitlab.freedesktop.org/pipewire/wireplumber/-/archive/${pkgver}/wireplumber-${pkgver}.tar.bz2"
        '50-alsa-config.lua'
        '50-default-access-config.lua')
md5sums=('a943e28259abd061d1fdacb73be0dfbb'
         'b67360a2276ab10d95712c3ba1976e21'
         '02135ac26307e8b56a0bd78642e8f12f')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddoc=disabled \
    -Dtests=false \
    -Dsystemd-system-service=false \
    -Dsystem-lua=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -D -m0644 ../${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}

  # custom lua settings for Wayland session, issue for user to connect to dbus for these options
  install -Dm644 ../50-alsa-config.lua ${pkgdir}/etc/wireplumber/main.lua.d/50-alsa-config.lua
  install -Dm644 ../50-default-access-config.lua ${pkgdir}/etc/wireplumber/main.lua.d/50-default-access-config.lua
}
