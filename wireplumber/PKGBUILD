
pkgname=wireplumber
pkgver=0.4.6
pkgrel=1
pkgdesc="Modular session/policy manager for PipeWire and a GObject-based library that wraps PipeWire's API."
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
arch=('x86_64')
depends=('pipewire' 'glib2')
makedepends=('meson' 'ninja' 'gobject-introspection')
install=wireplumber.install
source=("https://gitlab.freedesktop.org/pipewire/wireplumber/-/archive/${pkgver}/wireplumber-${pkgver}.tar.bz2")
md5sums=('2ab3ba8816ff5617590cee6e2ae001a9')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddoc=disabled \
    -Dtests=false \
    -Dsystemd-system-service=true
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -D -m0644 ../${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}
