 
pkgname=tpm2-tss
pkgver=4.0.0
pkgrel=1
pkgdesc="OSS implementation of the TCG Trusted Platform Module 2.0 Software Stack (TSS2)"
arch=('x86_64')
url="https://tpm2-software.github.io/"
license=('BSD')
depends=('openssl' 'json-c')
source=("https://github.com/tpm2-software/tpm2-tss/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('7c9997a73910252c736051fb9b1aa5e0')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --with-udevrulesprefix=60- \
        --localstatedir=/var \
        --with-runstatedir=/run \
        --sysconfdir=/etc \
        --with-tmpfilesdir=/etc/tmpfiles.d \
        --disable-static \
        --disable-doxygen-doc \
        --disable-doxygen-man
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install

    install -Dm644 LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
