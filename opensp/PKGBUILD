
pkgname=opensp
pkgver=1.5.2
pkgrel=7
pkgdesc="A library and a set of tools for validating, parsing and manipulating SGML and XML documents"
arch=('x86_64')
url="http://openjade.sourceforge.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('xmlto' 'docbook-xsl')
options=('!libtool')
source=("https://download.sourceforge.net/openjade/OpenSP-${pkgver}.tar.gz")
md5sums=('670b223c5d12cee40c9137be86b6c39b')

build() {
  cd OpenSP-${pkgver}
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-nls \
    --enable-http \
    --enable-default-catalog=/etc/sgml/catalog:/etc/xml/catalog \
    --enable-default-search-path=/usr/share/sgml:/usr/share/xml \
    --enable-xml-messages
  make
}

package() {
  cd OpenSP-$pkgver
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

