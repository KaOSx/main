
# only update if transifex can use newer
pkgname=python3-urllib3
pkgver=1.26.17
pkgrel=1
pkgdesc="Python HTTP library with thread-safe connection pooling, file post support, sanity friendly, and more."
arch=('x86_64')
license=('MIT')
url="https://urllib3.readthedocs.io/en/latest/"
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/u/urllib3/urllib3-${pkgver}.tar.gz")
sha256sums=('24d6a242c28d29af46c3fae832c36db3bbebcc533dd1bb549172cd739c82df21')

build() {
    cd urllib3-${pkgver}

    python3 -m build -nw
}

package() {
    cd urllib3-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
