
pkgname=sord
pkgver=0.16.0
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory"
arch=('x86_64')
url="http://drobilla.net/software/sord/"
license=('custom:ISC')
depends=('serd' 'pcre')
makedepends=('python2')
source=("http://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('010313c3baf5b8a4a639e9ee45310af6')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python2 waf install --destdir=${pkgdir}

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

