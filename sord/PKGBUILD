
pkgname=sord
pkgver=0.16.16
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory"
arch=('x86_64')
url="https://drobilla.net/software/sord.html"
license=('custom:ISC')
depends=('serd' 'pcre2' 'zix')
makedepends=('python3' 'meson' 'ninja')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.xz")
md5sums=('003384f397d832180b0cc1bdf339368d')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgbase}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddocs=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgbase}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

