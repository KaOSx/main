#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=zeitgeist
pkgver=0.9.0
pkgrel=2
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('i686' 'x86_64')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('dbus-python' 'pygobject' 'pyxdg' 'xapian-core')
makedepends=('intltool' 'raptor' 'python2-rdflib')
source=("http://launchpad.net/$pkgname/0.9/$pkgver/+download/$pkgname-$pkgver.tar.bz2")
md5sums=('fc2706611f435a027cd520f0da0982d7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 LIBS+="-lgmodule-2.0" ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" zeitgeistdocdir=/usr/share/doc/zeitgeist install
  install -D -m644 data/zeitgeist-daemon.bash_completion "$pkgdir/usr/share/bash-completion/completions/zeitgeist-daemon"
}
