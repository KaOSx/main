
pkgname=power-profiles-daemon
pkgver=0.13
pkgrel=1
pkgdesc="Makes power profiles handling available over D-Bus."
arch=('x86_64')
license=('LGPL' 'MPL')
url="https://gitlab.freedesktop.org/hadess/power-profiles-daemon"
depends=('polkit' 'upower')
makedepends=('meson' 'ninja')
install=power-profiles-daemon.install
source=("https://gitlab.freedesktop.org/hadess/power-profiles-daemon/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('9fef0a3e1b76e6c0f551841d4a9fef36e6aae047d5279cfc60b833d80fd2a8b7')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -e "s|subdir('tests')|#subdir('tests')|" -i meson.build
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexec=lib \
    -Dgtk_doc=false \
    -Dpylint=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
