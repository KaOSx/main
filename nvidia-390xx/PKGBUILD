
_extramodules=extramodules-4.18
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-390xx' 'khd-nvidia-390xx')
pkgver=390.87
pkgrel=2
arch=('x86_64')
url="https://www.nvidia.com/page/home.html"
makedepends=('linux-headers' 'linux>=4.18' 'linux<4.19' "nvidia-390xx-utils=${pkgver}")
license=('custom')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        'nvidia_supported'
        'linux-4.10.patch')
md5sums=('b787bc161651e5f87620fd1bfa349d4d'
         '282f73f1a7326339a7b0e0716eee4f76'
         '4ab00fe5ab144083caa6c6994feea7e6')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}"
    #patch -p1 -i ${srcdir}/linux-4.10.patch
    
    export IGNORE_CC_MISMATCH=yes

    cd "${srcdir}/${_pkg}/kernel"  
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-390xx() {
    pkgdesc="NVIDIA drivers for linux, 390xx legacy version."
    depends=('linux>=4.18' 'linux<4.19' "nvidia-390xx-utils=${pkgver}")
    conflicts=('nvidia' 'nvidia-340xx')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-drm.ko"
    gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}

package_khd-nvidia-390xx() {
    pkgdesc="KHD ids for nvidia-390xx $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-xxx.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia390xx_hwdb"
}
