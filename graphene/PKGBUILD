
pkgname=graphene
pkgver=1.10.4
pkgrel=1
pkgdesc="Thin layer of types for graphic libraries"
arch=('x86_64')
url="https://ebassi.github.io/graphene/"
license=('MIT')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://github.com/ebassi/graphene/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b2a45f230f332478553bd79666eca8df1d1c6dbf208c344ba9f5120592772414')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dgtk_doc=false \
    -Dinstalled_tests=false \
    -Darm_neon=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


