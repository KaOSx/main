
pkgname=graphene
pkgver=1.10.6
pkgrel=1
pkgdesc="Thin layer of types for graphic libraries"
arch=('x86_64')
url="https://ebassi.github.io/graphene/"
license=('MIT')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://github.com/ebassi/graphene/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('80ae57723e4608e6875626a88aaa6f56dd25df75024bd16e9d77e718c3560b25')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dgtk_doc=false \
    -Dinstalled_tests=false \
    -Darm_neon=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
