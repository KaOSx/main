
pkgname=graphene
pkgver=1.10.8
_pkgver=1.10
pkgrel=1
pkgdesc="Thin layer of types for graphic libraries"
arch=('x86_64')
url="https://ebassi.github.io/graphene/"
license=('MIT')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://download.gnome.org/sources/graphene/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a37bb0e78a419dcbeaa9c7027bcff52f5ec2367c25ec859da31dfde2928f279a')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dgtk_doc=false \
    -Dinstalled_tests=false \
    -Darm_neon=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
