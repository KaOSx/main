
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# check in tools/scripts/version_resolver.py for used Chromium
pkgname=qt6-webengine
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=2
pkgdesc="Provides a web browser engine that makes it easy to embed content from the Web into your Qt application."
arch=('x86_64')
url='https://github.com/qt/qtwebengine'
license=('GPL3' 'LGPL')
depends=('qt6-declarative' 'mesa' 'systemd' 'pango' 'nss' 'speex' 'libevent' 'libcups'
         'libxslt' 'libpng' 'libjpeg-turbo' 'libwebp' 'zlib' 'pciutils' 'jsoncpp' 'pulseaudio' 'libxcomposite'
         'qt6-positioning' 'qt6-webchannel' 'libxss' 'opus' 'libvpx' 'pipewire') #'ffmpeg'
makedepends=('cmake' 'ninja' 'python3' 'gperf' 'yasm' 'openssh' 'icu' 'qt6-tools' 'nodejs'
             'python3-html5lib' 'libepoxy')
groups=('qt6')
options=('!libtool')
_pkgfqn="qtwebengine-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        'icu74.patch'
        'libxml2.12.patch')
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '63faeb3ac781a8c0eb107b956e62118a'
         'f491ca0d283e513a91dea41860306115')

prepare() {
  cd ${_pkgfqn}/

  #patch -p1 -i ${srcdir}/icu74.patch
  #patch -p1 -i ${srcdir}/libxml2.12.patch
}

build() {
    cmake -B build -S ${_pkgfqn} -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DQT_FEATURE_webengine_proprietary_codecs=ON \
        -DQT_FEATURE_webengine_system_ffmpeg=OFF \
        -DQT_FEATURE_webengine_system_opus=ON \
        -DQT_FEATURE_webengine_system_icu=ON \
        -DQT_FEATURE_webengine_kerberos=ON \
        -DQT_FEATURE_webengine_system_libevent=ON \
        -DQT_FEATURE_webengine_webrtc_pipewire=ON \
        -DQT_FEATURE_webengine_system_libxml=ON \
        -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    install -D -m644 ${_pkgfqn}/LICENSES/* -t ${pkgdir}/usr/share/licenses/${pkgname}
}

