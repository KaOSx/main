
pkgname=apache-ant
pkgver=1.10.9
pkgrel=1
pkgdesc="A java-based build tool."
arch=('x86_64')
license=('APACHE')
url="https://ant.apache.org/"
depends=('java-runtime')
optdepends=('junit: to have junit on the classpath in javac tasks')
source=("https://mirror.olnevhost.net/pub/apache//ant/binaries/${pkgname}-${pkgver}-bin.tar.xz"
        "${pkgname}.sh"
        "${pkgname}.csh")
sha1sums=('2fc1c47d57a31da0c59bc16e3d5ca5edfd2e3a48'
          '97e75a4648b6e091e55a3f665d2e82fe080d868e'
          'f47782092043e59996e64e2f0e9e7d98d05d9a03')

package() {
  # profile.d scripts
  install -d -m755 ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/${pkgname}.{csh,sh} ${pkgdir}/etc/profile.d

  # ANT_HOME env var
  source ${srcdir}/${pkgname}.sh

  cd ${pkgname}-${pkgver}
  install -d -m755 ${pkgdir}/${ANT_HOME}/{bin,lib}

  install -m644 ./lib/*.jar ${pkgdir}/${ANT_HOME}/lib
  cp -Rp ./etc ${pkgdir}/${ANT_HOME}

  # Do not copy Windows .bat/.cmd files
  find ./bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 \{\} ${pkgdir}/${ANT_HOME}/bin \;

  install -d -m755 ${pkgdir}/usr/bin
  ln -s ${ANT_HOME}/bin/ant ${pkgdir}/usr/bin/ant

  # symlink to junit 
  ln -sf /usr/share/java/junit.jar ${pkgdir}/usr/share/java/apache-ant/lib


  #cd "${srcdir}/${pkgname}-${pkgver}"
  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/${pkgname}
}

