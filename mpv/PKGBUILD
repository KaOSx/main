
pkgname=mpv
pkgver=0.15.0
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('x86_64')
license=('GPL')
url='http://mpv.io'
depends=('ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libgl' 'enca' 'libxinerama'
         'libxv' 'libxkbcommon' 'libva' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme'
         'xdg-utils' 'libdvdnav' 'youtube-dl' 'libass' 'libbluray' 'lua' 'libxrandr' 'jack'
         'rubberband' 'samba' 'libxss' 'libcaca')
makedepends=('mesa' 'python2-docutils' 'ladspa')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz"
        "https://github.com/mpv-player/mpv/commit/5053f4cc3f48538c3d9a3ba13dc98442f3302052.patch")
#source=("https://github.com/mpv-player/mpv/archive/${_commit}.zip")
md5sums=('9042bd3fbff2bc8ba0b7fadaa4a22101'
         '0e0c367e20d4c821db4456fb01f5bc71')

prepare() {
    cd ${pkgname}-${pkgver}
    # https://github.com/mpv-player/mpv/issues/2729
    patch -p1 -i ${srcdir}/5053f4cc3f48538c3d9a3ba13dc98442f3302052.patch

    ./bootstrap.py
}

build() {
    cd ${pkgname}-${pkgver}

    ./waf configure --prefix=/usr \
	--confdir=/etc/mpv \
	--enable-libmpv-shared \
	--enable-cdda \
	--enable-sdl1 \
	--enable-gpl3
		
    ./waf build
}

package() {
    cd ${pkgname}-${pkgver}
    ./waf install --destdir="${pkgdir}"

    install -m644 DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
}
