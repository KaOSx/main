
pkgname=mpv
pkgver=0.27.2
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('x86_64')
license=('GPL')
url='http://mpv.io'
depends=('ffmpeg' 'lcms2' 'libdvdread' 'libcdio' 'libcdio-paranoia' 'libgl' 'libxinerama'
         'libxv' 'libxkbcommon' 'libva' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme'
         'xdg-utils' 'libdvdnav' 'youtube-dl' 'libass' 'libbluray' 'lua' 'libxrandr' 'jack'
         'rubberband' 'samba' 'libxss' 'libcaca')
makedepends=('mesa' 'python2-docutils' 'ladspa' 'perl')
options=('!emptydirs' '!buildflags')
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz"
        "https://github.com/mpv-player/mpv/commit/2ecf240b1cd20875991a5b18efafbe799864ff7f.patch")
#source=("https://github.com/mpv-player/mpv/archive/${_commit}.zip")
md5sums=('8cfb48e921e58c0d9d181d96d4809beb'
         'ec43562bba26f19c55ab6ba47731ab50')

prepare() {
    cd ${pkgname}-${pkgver}
    # libva 2.0.0 fix
    patch -p1 -i ${srcdir}/2ecf240b1cd20875991a5b18efafbe799864ff7f.patch

    ./bootstrap.py
}

build() {
    cd ${pkgname}-${pkgver}

    ./waf configure --prefix=/usr \
	--confdir=/etc/mpv \
	--enable-libmpv-shared \
	--enable-cdda \
	--enable-sdl2 \
	--enable-zsh-comp \
	--enable-libarchive \
	--enable-dvdnav \
	--enable-dvdread \
	--enable-libbluray \
	--enable-cdda
		
    ./waf build
}

package() {
    cd ${pkgname}-${pkgver}
    ./waf install --destdir=${pkgdir}

    install -m644 DOCS/{encoding.rst,tech-overview.txt} ${pkgdir}/usr/share/doc/mpv
}
