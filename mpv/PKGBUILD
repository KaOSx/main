
pkgname=mpv
pkgver=0.34.1
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('x86_64')
license=('GPL')
url='https://mpv.io'
depends=('ffmpeg' 'lcms2' 'libdvdread' 'libcdio' 'libcdio-paranoia' 'libglvnd' 'libxinerama'
         'libxv' 'libxkbcommon' 'libva' 'wayland' 'desktop-file-utils' 'hicolor-icon-theme'
         'xdg-utils' 'libdvdnav' 'yt-dlp' 'libass' 'libbluray' 'lua' 'libxrandr' 'jack2'
         'rubberband' 'libxss' 'libcaca' 'libarchive' 'openal')
makedepends=('mesa' 'python3-docutils' 'ladspa' 'perl')
options=('!emptydirs' '!buildflags')
source=("https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz"
        "https://github.com/mpv-player/mpv/commit//cc4ada655aae06218b900bb434e3521566394cde.diff")
#source=("https://github.com/mpv-player/mpv/archive/${_commit}.zip")
md5sums=('b5c76f9a7ce3a19a445869ffd9871d12'
         'a91b76f6edcc272f81528d3c8e95bfaa')

prepare() {
    cd ${pkgname}-${pkgver}
    # move to yt-dlp
    #patch -p1 -i ${srcdir}//cc4ada655aae06218b900bb434e3521566394cde.diff

    ./bootstrap.py
}

build() {
    cd ${pkgname}-${pkgver}

    ./waf configure --prefix=/usr \
	--confdir=/etc/mpv \
	--enable-libmpv-shared \
	--enable-cdda \
	--enable-sdl2 \
	--enable-libarchive \
	--enable-dvdnav \
	--enable-libbluray \
	--enable-openal
    ./waf build
}

package() {
    cd ${pkgname}-${pkgver}
    ./waf install --destdir=${pkgdir}

    install -m644 DOCS/{encoding.rst,tech-overview.txt} ${pkgdir}/usr/share/doc/mpv
}
