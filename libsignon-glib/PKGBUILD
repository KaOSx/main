
pkgname=libsignon-glib
pkgver=2.1
pkgrel=4
pkgdesc='Single signon authentication library for GLib applications'
arch=('x86_64')
url='https://gitlab.com/accounts-sso/libsignon-glib'
license=('LGPL2.1')
depends=('signonqt')
makedepends=('python3-gobject3' 'gtk-doc' 'meson' 'ninja' 'vala' 'git')
# check https://groups.google.com/forum/#!forum/accounts-sso-announce
source=("git+https://gitlab.com/accounts-sso/libsignon-glib#tag=VERSION_${pkgver}")
md5sums=('SKIP')

prepare() {
  cd ${pkgname}
  
  git submodule init
  git submodule update
}

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dtests=false 
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
