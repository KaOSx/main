
pkgname=intel-gmmlib
pkgver=21.1.3
pkgrel=1
pkgdesc="Intel Graphics Memory Management Library provides management for the Intel Media Driver for VAAPI."
arch=('x86_64')
url="https://github.com/intel/gmmlib/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/intel/gmmlib/archive/${pkgname}-${pkgver}.tar.gz")
sha1sums=('700d55829696949d2a38daea818534e96868e7b4')

build() {
  mkdir -p build 
  cd build
  
  cmake ../gmmlib-${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DRUN_TEST_SUITE=OFF
  make
}

package() {
  cd build
  make install DESTDIR=${pkgdir}
  
  install -m644 -D ../gmmlib-${pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

