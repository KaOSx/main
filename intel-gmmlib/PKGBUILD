
pkgname=intel-gmmlib
pkgver=22.3.0
pkgrel=1
pkgdesc="Intel Graphics Memory Management Library provides management for the Intel Media Driver for VAAPI."
arch=('x86_64')
url="https://github.com/intel/gmmlib/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/intel/gmmlib/archive/${pkgname}-${pkgver}.tar.gz")
sha1sums=('894709cfa30478040bb9d5871ce39fabd8603328')

build() {
  cmake -B build -S gmmlib-${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DRUN_TEST_SUITE=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -m644 -D gmmlib-${pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

