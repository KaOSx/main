
pkgname=intel-gmmlib
pkgver=22.3.11
pkgrel=1
pkgdesc="Intel Graphics Memory Management Library provides management for the Intel Media Driver for VAAPI."
arch=('x86_64')
url="https://github.com/intel/gmmlib/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/intel/gmmlib/archive/${pkgname}-${pkgver}.tar.gz")
sha1sums=('15ada3eb041014ee8e7ba29961d6c70f5320dca8')

build() {
  cmake -B build -S gmmlib-${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DRUN_TEST_SUITE=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -m644 -D gmmlib-${pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

