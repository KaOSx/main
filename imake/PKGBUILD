
pkgname=imake
pkgver=1.0.8
pkgrel=2
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkg-config' 'xproto' 'perl')
optdepends=('perl:	for mkhtmlindex')
source=("https://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.6.tar.bz2"
        'linuxconfig.patch'
        'LICENSE')
sha256sums=('b8d2e416b3f29cd6482bcffaaf19286d32917a164d07102a0e531ccd41a2a702'
            'b275dcf1f7323ed89e8b36f8fbd5da665d8700005f1779fa5b90a1688bbf2ee4'
            '49f4fab0de8d418db4ce80dad34e9b879a4199f3e554253a8e1ab68f7c7cb65d'
            'd558a52e8017d984ee59596a9582c8d699a1962391b632bec3bb6804bf4d501c'
            '4dcf5a9dbe3c6ecb9d2dd05e629b3d373eae9ba12d13942df87107fdc1b3934d'
            '1aa503f9a78e3d3b87b26aca15798825a7190cdedfcfa3e8ef850487a7bd45d3'
            '507f58781329532b9177ce21f8d4212a3146903eaa3aaf8472453d65f267b1bc')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.6
  patch -p1 -i ${srcdir}/linuxconfig.patch
  
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR=${pkgdir} install
      popd
    fi
  done
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

