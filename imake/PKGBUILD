
pkgname=imake
pkgver=1.0.10
pkgrel=2
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/wiki/"
license=('custom')
depends=('glibc')
makedepends=('pkg-config' 'xorgproto' 'perl')
optdepends=('perl:	for mkhtmlindex')
source=("https://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.9.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.8.tar.xz"
        'linuxconfig.patch'
        'LICENSE')
sha256sums=('75decbcea8d7b354cf36adc9675e53c4790ee3de56a14bd87b42c8e8aad2ecf5'
            'b275dcf1f7323ed89e8b36f8fbd5da665d8700005f1779fa5b90a1688bbf2ee4'
            '49f4fab0de8d418db4ce80dad34e9b879a4199f3e554253a8e1ab68f7c7cb65d'
            '92d0deb659fff6d8ddbc1d27fc4ca8ceb2b6dbe15d73f0a04edc09f1c5782dd4'
            '7408955defcfab0f44d1bedd4ec0c20db61914917ad17bfc1f1c9bf56acc17b9'
            '1aa503f9a78e3d3b87b26aca15798825a7190cdedfcfa3e8ef850487a7bd45d3'
            '507f58781329532b9177ce21f8d4212a3146903eaa3aaf8472453d65f267b1bc')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.8
  patch -p1 -i ${srcdir}/linuxconfig.patch

  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR=${pkgdir} install
      popd
    fi
  done
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

