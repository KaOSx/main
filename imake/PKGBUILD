
pkgname=imake
pkgver=1.0.9
pkgrel=3
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/wiki/"
license=('custom')
depends=('glibc')
makedepends=('pkg-config' 'xorgproto' 'perl')
optdepends=('perl:	for mkhtmlindex')
source=("https://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.8.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.7.tar.bz2"
        'linuxconfig.patch'
        'LICENSE')
sha256sums=('72de9d278f74d95d320ec7b0d745296f582264799eab908260dbea0ce8e08f83'
            'b275dcf1f7323ed89e8b36f8fbd5da665d8700005f1779fa5b90a1688bbf2ee4'
            '49f4fab0de8d418db4ce80dad34e9b879a4199f3e554253a8e1ab68f7c7cb65d'
            'bfb26f8025189b2a01286ce6daacc2af8fe647440b40bb741dd5c397572cba5b'
            '74a771d5bb2189020399998dfa2329c3e038aa2e14dd3d4056144ed9a5976308'
            '1aa503f9a78e3d3b87b26aca15798825a7190cdedfcfa3e8ef850487a7bd45d3'
            '507f58781329532b9177ce21f8d4212a3146903eaa3aaf8472453d65f267b1bc')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.7
  patch -p1 -i ${srcdir}/linuxconfig.patch

  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR=${pkgdir} install
      popd
    fi
  done
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

