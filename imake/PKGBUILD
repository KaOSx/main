
pkgname=imake
pkgver=1.0.10
pkgrel=4
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/wiki/"
license=('custom')
depends=('glibc')
makedepends=('pkg-config' 'xorgproto' 'perl')
optdepends=('perl:	for mkhtmlindex')
source=("https://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.9.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.8.tar.xz"
        'linuxconfig.patch'
        'LICENSE')
sha256sums=('75decbcea8d7b354cf36adc9675e53c4790ee3de56a14bd87b42c8e8aad2ecf5'
            '5088f98769fb63c326e9b9d2cb7c9f4a630a2801dd1da06971d0829176cf25b6'
            '3b65577a5575cce095664f5492164a96941800fe6290a123731d47f3e7104ddb'
            '92d0deb659fff6d8ddbc1d27fc4ca8ceb2b6dbe15d73f0a04edc09f1c5782dd4'
            '7408955defcfab0f44d1bedd4ec0c20db61914917ad17bfc1f1c9bf56acc17b9'
            '1aa503f9a78e3d3b87b26aca15798825a7190cdedfcfa3e8ef850487a7bd45d3'
            '507f58781329532b9177ce21f8d4212a3146903eaa3aaf8472453d65f267b1bc')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.8
  patch -p1 -i ${srcdir}/linuxconfig.patch

  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR=${pkgdir} install
      popd
    fi
  done
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

