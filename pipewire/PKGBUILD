
pkgname=pipewire
pkgver=0.3.50
pkgrel=2
pkgdesc="Project that aims to greatly improve handling of audio and video under Linux"
url="https://pipewire.org"
arch=("x86_64")
license=('MIT')
depends=('gst-plugins-base' 'v4l-utils' 'sbc' 'ffmpeg' 'rtkit' 'bluez' 'jack2' 'vulkan-loader' 'lilv' 'libcanberra')
makedepends=('meson' 'ninja' 'vulkan-headers' 'wireplumber')
#provides=('jack2' 'jack') # 'pulseaudio')
#conflicts=('jack2')
install=pipewire.install
source=("https://gitlab.freedesktop.org/pipewire/pipewire/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://gitlab.freedesktop.org/pipewire/pipewire/-/commit/d3ea3142e1a4de206e616bc18f63a529e6b4986a.diff")
md5sums=('a89cebd5238d048d0e8b446748b50d11'
         '64a53f1b0d95958575ab7de357d1a3cf')

prepare() {
  cd ${pkgname}-${pkgver}
  # sdl2 2.0.22 needed
  patch -p1 -i ${srcdir}/d3ea3142e1a4de206e616bc18f63a529e6b4986a.diff
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dman=disabled \
    -Dudevrulesdir=/usr/lib/udev/rules.d \
    -Dsystemd-system-service=enabled \
    -Dsession-managers=[] \
    -Djack-devel=false \
    -Ddbus=enabled \
    -Dx11=enabled \
    -Dlibcanberra=enabled
    # -Dlibjack-path=/usr/lib \
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
} 
