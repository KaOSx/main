
pkgname=libinput
pkgver=1.12.0
pkgrel=2
pkgdesc="Library to handle input devices in Wayland compositors and to provide a generic X.Org input driver"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=('custom:X11')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
makedepends=('meson' 'ninja')
options=('!libtool')
source=("https://freedesktop.org/software/libinput/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/libinput/libinput/commit/7c51c881dc72827ca50ae9eea3d123eef9cb4a66.diff")
sha256sums=('15ac2b78ec0b502c14400d711dbd6b9164a43a724cedeaf21c7fa29960e701a4'
            '2f99b2068f844dd651d7c868b01340f0d1ebb46637158e08a84236c2a83031ce')

prepare() {
    cd ${pkgname}-${pkgver}
    # https://gitlab.freedesktop.org/libinput/libinput/issues/50
    #patch -p1 -i ${srcdir}/7c51c881dc72827ca50ae9eea3d123eef9cb4a66.diff
}

build() {
    mkdir -p build
    cd build
    
    export LANG=en_US.UTF-8

    meson ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release \
        --libexecdir=/usr/lib \
        -Dtests=false \
        -Ddebug-gui=false \
        -Ddocumentation=false
    ninja    
   
}

package() {
    cd build
    DESTDIR=${pkgdir} ninja install

    install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    }
