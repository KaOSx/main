
pkgname=libinput
pkgver=1.10.1
pkgrel=1
pkgdesc="Library to handle input devices in Wayland compositors and to provide a generic X.Org input driver"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=('custom:X11')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
makedepends=('meson' 'ninja')
options=('!libtool')
source=("https://freedesktop.org/software/libinput/${pkgname}-${pkgver}.tar.xz")
sha256sums=('877522d76575e1945c7b3a9eb92cab90d5ad0379c4ef0dbe13054e489d4c6665')

build() {
    mkdir -p build
    cd build
    
    export LANG=en_US.UTF-8

    meson ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release \
        --libexecdir=/usr/lib \
        -Dtests=false \
        -Ddebug-gui=false \
        -Ddocumentation=false
    ninja    
   
}

package() {
    cd build
    DESTDIR=${pkgdir} ninja install

    install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    }
