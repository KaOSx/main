
pkgname=libinput
pkgver=1.9.4
pkgrel=1
pkgdesc="Library to handle input devices in Wayland compositors and to provide a generic X.Org input driver"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=('custom:X11')
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
makedepends=('meson' 'ninja')
options=('!libtool')
source=("https://freedesktop.org/software/libinput/${pkgname}-${pkgver}.tar.xz")
sha256sums=('0bcdbd4c4e3c2a2db322fbdf2ef3284f2e6d6fb7be3af80e6d8de7783f675190')

build() {
    mkdir -p build
    cd build
    
    export LANG=en_US.UTF-8

    meson ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release \
        --libexecdir=/usr/lib \
        -Dtests=false \
        -Ddebug-gui=false \
        -Ddocumentation=false
    ninja    
   
}

package() {
    cd build
    DESTDIR=${pkgdir} ninja install

    install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    }
