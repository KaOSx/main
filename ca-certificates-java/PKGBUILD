
pkgname=ca-certificates-java
pkgver=20121112+nmu2
pkgrel=3
pkgdesc='Common CA certificates (JKS keystore)'
arch=('x86_64')
url='https://packages.qa.debian.org/c/ca-certificates-java.html'
license=('GPL')
depends=('ca-certificates')
makedepends=('java-runtime')
install='ca-certificates-java.install'
backup=('etc/default/cacerts')
#source=("http://ftp.debian.org/debian/pool/main/c/ca-certificates-java/${pkgname}_${pkgver}.tar.gz"
source=("http://ftp.caixamagica.pt/anvil/pool/main/c/ca-certificates-java/${pkgname}_${pkgver}.tar.gz"
        'init-jks-keystore'
        'jks-keystore.hook')
md5sums=('9562403f0cc9300d29af5b8baa9ddeab'
         '75eeca3f443f4b5a17ed5ac81875f1e9'
         '6dd4f915aee0df51260083e00b7fa012')

build() {
    cd ${pkgname}-${pkgver}

    javac -target 1.6 -source 1.6 UpdateCertificates.java
    jar cfe ca-certificates-java.jar UpdateCertificates *.class
}

package() {
    cd ${pkgname}-${pkgver}

    install -d -m755 ${pkgdir}/etc/ssl/certs/java
    install -D -m600 debian/default ${pkgdir}/etc/default/cacerts
    install -D -m644 ca-certificates-java.jar ${pkgdir}/usr/share/ca-certificates-java/ca-certificates-java.jar
    install -D -m755 ${srcdir}/init-jks-keystore ${pkgdir}/usr/sbin/init-jks-keystore
    install -D -m755 ${srcdir}/jks-keystore.hook ${pkgdir}/etc/ca-certificates/update.d/jks-keystore
}
