
pkgname=python2-pillow
_pkgname=Pillow
pkgver=4.1.0
pkgrel=1
pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
arch=('x86_64')
url="http://python-pillow.github.io/"
license=('BSD')
depends=('python2' 'lcms' 'libwebp' 'sane')
makedepends=('python2-setuptools' 'libwebp' 'tk')
optdepends=('tk: for the ImageTK module'
            'pyqt-python2: for the ImageQt module')
provides=('python2-imaging')
conflicts=('python2-imaging')
replaces=('python2-imaging')
source=("https://github.com/python-pillow/Pillow/archive/${pkgver}.tar.gz")
md5sums=('b08815f87d442b486cbe8f01c4a3193a')

package() { 
  cd ${_pkgname}-${pkgver}

  python2 setup.py install --root=${pkgdir}/ --optimize=0
  install -Dm644 docs/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # avoid python3-pillow conflict
  cd ${pkgdir}/usr/bin
  for f in *.py; do
    mv "$f" "${f%.py}2"
  done
}
