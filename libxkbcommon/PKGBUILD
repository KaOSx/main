
pkgname=libxkbcommon
pkgver=0.8.3
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=('x86_64')
url="https://xkbcommon.org/"
license=('custom')
depends=('xkeyboard-config' 'glibc' 'wayland-protocols')
makedepends=('doxygen' 'meson' 'ninja')
options=('!libtool')
#source=("https://xkbcommon.org/download/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/xkbcommon/libxkbcommon/archive/xkbcommon-${pkgver}.tar.gz")
sha256sums=('aefea423f6f18422f0a80395f36404d9dbcbc45b881d3e70ceea95c623559069')

build() {
  mkdir -p build
  cd build
  
  export LANG=en_US.UTF-8
  
  meson ../${pkgname}-xkbcommon-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib 
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  install -Dm644 ../${pkgname}-xkbcommon-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
