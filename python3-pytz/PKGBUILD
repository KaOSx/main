
pkgname=python3-pytz
pkgver=2024.2
_pkgver=2024.2
pkgrel=1
arch=('x86_64')
pkgdesc="Cross platform time zone library for Python"
url="https://pypi.python.org/pypi/pytz"
license=("MIT")
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://pypi.io/packages/source/p/pytz/pytz-${_pkgver}.tar.gz")
sha256sums=('2aa355083c50a0f93fa581709deac0c9ad65cca8a9e9beac660adcbd493c798a')

build() {
    cd pytz-${_pkgver}

    python3 -m build -nw
}

check() {
    cd pytz-${_pkgver}/pytz/tests

    python3 test_tzinfo.py
}

package() {
    cd pytz-${_pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
