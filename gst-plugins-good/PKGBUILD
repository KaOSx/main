
pkgname=gst-plugins-good
pkgver=1.12.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('pulseaudio' 'jack' 'libsoup' 'gst-plugins-base' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout'
         'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils' 'libgudev')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394' 'python3')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-good/${pkgname}-${pkgver}.tar.xz"
        'equalizer.patch')
md5sums=('9294b22ddab3bec373cbc5e84ff4c084'
         '36a5b58e136f711152b17db3e30f225e')

build() {
  cd ${pkgname}-${pkgver}
  # https://github.com/clementine-player/Clementine/issues/4541
  # https://bugzilla.gnome.org/show_bug.cgi?id=737886
  #patch -p1 -i "${srcdir}/equalizer.patch"
 
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Good (KaOS)" \
    --with-package-origin="http://kaosx.us/"
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
