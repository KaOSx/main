
pkgname=gst-plugins-good
pkgver=1.20.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('pulseaudio' 'jack2' 'gst-plugins-base' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout'
         'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils' 'libgudev'
         'qt5-x11extras' 'qt5-wayland' 'mpg123')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394' 'python3' 'meson' 'ninja' 'qt5-tools')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-good/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/gstreamer/gst-plugins-good/commit/c481cb5f2209087ffc0094490bacab46eda2d06b.diff")
sha256sums=('f8f3c206bf5cdabc00953920b47b3575af0ef15e9f871c0b6966f6d0aa5868b7'
            'b499e9f62c127e7bea2c08bd683e4ac0513552a951e76e2c3219a766de23e61a')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 2 fail 109 elements_rtpvp8 & 109 elements_rtpstorage
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
