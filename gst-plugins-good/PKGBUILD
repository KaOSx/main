
pkgname=gst-plugins-good
pkgver=1.24.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('pulseaudio' 'jack2' 'gst-plugins-base' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout'
         'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils' 'libgudev'
         'qt5-x11extras' 'qt5-wayland' 'qt6-wayland' 'mpg123' 'flac')
makedepends=('gstreamer' 'speex' 'libraw1394' 'python3' 'meson' 'ninja' 'qt5-tools' 'qt6-tools')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-good/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/gstreamer/gst-plugins-good/commit/c481cb5f2209087ffc0094490bacab46eda2d06b.diff")
sha256sums=('c02ba5583863e0bd9e265e5e4a27a03a3c473eb9ec1c10b07238f7176d9368a2'
            'b499e9f62c127e7bea2c08bd683e4ac0513552a951e76e2c3219a766de23e61a')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 117/117 elements_flvmux                    FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
