
pkgname=xorg-fonts-100dpi
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org 100dpi fonts"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
groups=('xorg')
install=xfonts.install
source=("https://xorg.freedesktop.org/releases/individual/font/font-adobe-100dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-adobe-utopia-100dpi-1.0.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-100dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bitstream-100dpi-1.0.4.tar.xz")
sha256sums=('b67aff445e056328d53f9732d39884f55dd8d303fc25af3dbba33a8ba35a9ccf'
            'fb84ec297a906973548ca59b7c6daeaad21244bec5d3fb1e7c93df5ef43b024b'
            'fd8f5efe8491faabdd2744808d3d4eafdae5c83e617017c7fddd2716d049ab1e'
            '76ec09eda4094a29d47b91cf59c3eba229c8f7d1ca6bae2abbb3f925e33de8f2'
            '2d1cc682efe4f7ebdf5fbd88961d8ca32b2729968728633dea20a1627690c1a7')

build() {
  cd ${srcdir}

  for dir in font-*-100dpi*; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}

  for dir in font-*-100dpi*; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      make DESTDIR=${pkgdir} install
      install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-100dpi-*}
      popd
    fi
  done

  rm -f ${pkgdir}/usr/share/fonts/100dpi/fonts.*
}
