
pkgname=xorg-fonts-100dpi
pkgver=1.0.3
pkgrel=4
pkgdesc="X.org 100dpi fonts"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
groups=('xorg')
install=xfonts.install
source=("https://xorg.freedesktop.org/releases/individual/font/font-adobe-100dpi-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/font/font-adobe-utopia-100dpi-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-100dpi-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/font/font-bitstream-100dpi-1.0.3.tar.bz2")
md5sums=('1347c3031b74c9e91dc4dfa53b12f143'
         '66fb6de561648a6dce2755621d6aea17'
         '9f11ade089d689b9d59e0f47d26f39cd'
         'c8b73a53dcefe3e8d3907d3500e484a9'
         '6b223a54b15ecbd5a1bc52312ad790d8')

build() {
  cd ${srcdir}
  
  for dir in font-*-100dpi*; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  
  for dir in font-*-100dpi*; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      make DESTDIR=${pkgdir} install
      install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-100dpi-*}
      popd
    fi
  done
  
  rm -f ${pkgdir}/usr/share/fonts/100dpi/fonts.*
}
