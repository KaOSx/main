
pkgname=libetonyek
pkgver=0.1.9
pkgrel=7
pkgdesc="Library and a set of tools for reading and converting Apple Keynote presentations."
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL2')
options=('!libtool')
depends=('libwpd' 'libxml2' 'gperf' 'zlib' 'librevenge' 'liblangtag')
makedepends=('boost' 'glm' 'cppunit' 'mdds' 'clang')
source=("https://dev-www.libreoffice.org/src/libetonyek/${pkgname}-${pkgver}.tar.xz"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/app-text/libetonyek/files/libetonyek-0.1.9-boost-1.73.patch")
md5sums=('b891c14c5233f93fb7db052b5762a692'
         '35dc01073704667b82631648a2fbf413')

build() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/libetonyek-0.1.9-boost-1.73.patch

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++
  
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --disable-werror \
      --without-docs \
      --enable-static=no \
      --with-mdds=1.5
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
