
pkgname=libetonyek
pkgver=0.1.10
pkgrel=6
pkgdesc="Library and a set of tools for reading and converting Apple Keynote presentations."
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL2')
options=('!libtool')
depends=('libwpd' 'libxml2' 'gperf' 'zlib' 'librevenge' 'liblangtag')
makedepends=('boost' 'glm' 'cppunit' 'mdds' 'clang')
source=("https://dev-www.libreoffice.org/src/libetonyek/${pkgname}-${pkgver}.tar.xz")
md5sums=('c29792ef7d4cb98463e3c3ec3e56ace6')

build() {
  cd ${pkgname}-${pkgver}

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --disable-werror \
      --without-docs \
      --enable-static=no \
      --with-mdds=2.0
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
