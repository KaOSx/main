
pkgname=libetonyek
pkgver=0.1.6
pkgrel=1
pkgdesc="Library and a set of tools for reading and converting Apple Keynote presentations."
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL2')
options=('!libtool')
depends=('libwpd' 'libxml2' 'gperf' 'zlib' 'librevenge' 'liblangtag')
makedepends=('boost' 'glm' 'cppunit' 'mdds')
source=("http://dev-www.libreoffice.org/src/libetonyek/${pkgname}-${pkgver}.tar.xz")
md5sums=('dfe25b0c7739283a5c61a930ea1f9bf8')

build() {
  cd "${pkgname}-${pkgver}"

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --disable-werror \
      --without-docs \
      --enable-static=no 
  make
}

check() {
  cd "$pkgname-$pkgver"
  
  make check
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}