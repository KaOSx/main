 
pkgname=xsettingsd
pkgver=1.0.2
pkgrel=1
pkgdesc="Daemon that implements the XSETTINGS specification, used for GTK+ application configuration changes."
arch=('x86_64')
url="https://github.com/derat/xsettingsd"
license=('custom:BSD')
depends=('libx11')
makedepends=('scons')
source=("https://github.com/derat/xsettingsd/archive/v${pkgver}.tar.gz")
md5sums=('a550949ee88e68a0b3fe6e43c4ccd343')

build() {
  cd ${pkgname}-${pkgver}
    
  scons xsettingsd dump_xsettings
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm755 xsettingsd dump_xsettings -t ${pkgdir}/usr/bin/
  install -Dm644 xsettingsd.1 dump_xsettings.1 -t ${pkgdir}/usr/share/man/man1/
  
  install -Dm644 COPYING -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
