
pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.42.0
_pkgver=2.42
pkgrel=4
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
makedepends=('pango' 'glibmm' 'glibmm-docs' 'cairomm' 'pkgconfig')
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/pangomm/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ca6da067ff93a6445780c0b4b226eb84f484ab104b8391fb744a45cbc7edbf56')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango' 'glibmm' 'cairomm')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
