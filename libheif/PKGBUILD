
pkgname=libheif
pkgver=1.18.0
pkgrel=1
pkgdesc="HEIF and AVIF (AV1 Image File Format) file format decoder and encoder."
arch=('x86_64')
url="https://github.com/strukturag/libheif"
license=('GPL3')
depends=('dav1d' 'ffmpeg' 'libde265' 'openjpeg' 'x265' 'libvpx')
makedepends=('cmake' 'gdk-pixbuf2')
options=('!emptydirs')
source=("https://github.com/strukturag/libheif/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3f25f516d84401d7c22a24ef313ae478781b95f235c250b06152701c401055c3')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_EXAMPLES=OFF \
    -DWITH_LIBDE265=ON \
    -DWITH_X265=ON \
    -DWITH_DAV1D=ON \
    -DWITH_FFMPEG_DECODER=ON \
    -DWITH_OpenJPEG_ENCODER=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
