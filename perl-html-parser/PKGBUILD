
pkgname=perl-html-parser
_pkgname=HTML-Parser
pkgver=3.81
pkgrel=2
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/HTML-Parser"
depends=('perl-html-tagset' 'perl')
checkdepends=('perl-test-pod' 'perl-json' 'perl-uri')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_pkgname}-${pkgver}.tar.gz")
md5sums=('b9f5dd3b14632f7a60541a2b61389c06')

build() {
  cd ${_pkgname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  # 1 fail t/headparser-http.t
  make test
}

package() {
  cd ${_pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}
