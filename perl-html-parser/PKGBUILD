
pkgname=perl-html-parser
_pkgname=HTML-Parser
pkgver=3.79
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/HTML-Parser"
depends=('perl-html-tagset' 'perl')
checkdepends=('perl-test-pod' 'perl-json' 'perl-uri')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_pkgname}-${pkgver}.tar.gz")
md5sums=('e39bbc692fa8955f87eea92f38f973a0')

build() {
  cd ${_pkgname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  # 2 fail t/headparser-http.t t/linkextor-base.t
  make test
}

package() {
  cd ${_pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}
