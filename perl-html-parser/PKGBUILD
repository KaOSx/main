
pkgname=perl-html-parser
_pkgname=HTML-Parser
pkgver=3.77
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/HTML-Parser"
depends=('perl-html-tagset' 'perl')
checkdepends=('perl-test-pod')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_pkgname}-${pkgver}.tar.gz")
md5sums=('866c898bba07fdd079d7a78c4348ecda')

build() {
  cd ${_pkgname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  # 2 fail t/headparser-http.t t/linkextor-base.t
  make test
}

package() {
  cd ${_pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
}
