
pkgname=bumblebee
pkgver=3.2.1
pkgrel=6
pkgdesc="Bumblebee brings Optimus Support for Linux Through VirtualGL."
arch=('x86_64')
url="https://www.Bumblebee-Project.org"
license=("GPL3")
depends=('virtualgl' 'glib2')
makedepends=('help2man')
optdepends=('xf86-video-nouveau: Nouveau driver' 
            'bbswitch: switch on/off discrete card')
install='bumblebee.install'
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia')
source=("https://www.bumblebee-project.org/${pkgname}-${pkgver}.tar.gz"
        'bumblebee.conf')
md5sums=('30974e677bb13e8a3825fd6f3e7d3b24'
         '43c2c5c069ac8628bd86cd5865785639')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --with-udev-rules=/usr/lib/udev/rules.d \
        --without-pidfile \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make install DESTDIR=${pkgdir}
  
    install -D -m644 scripts/systemd/bumblebeed.service ${pkgdir}/usr/lib/systemd/system/bumblebeed.service
    mv -v ${pkgdir}/etc/bash_completion.d/bumblebee ${pkgdir}/etc/bash_completion.d/optirun
    # upstream recommended blacklisting
    install -D -m644 ${srcdir}/bumblebee.conf ${pkgdir}/etc/modprobe.d/bumblebee.conf
}
