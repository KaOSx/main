
pkgname=imap
pkgver=2007f
pkgrel=5
pkgdesc="An IMAP/POP server"
arch=('x86_64')
license=('APACHE')
url="https://www.washington.edu/imap"
depends=('openssl' 'pam')
source=("http://ftp.ntua.gr/pub/net/mail/imap/${pkgname}-${pkgver}.tar.gz"
        'imap'
        'ipop2'
        'ipop3'
        'openssl1.1.patch')
backup=(etc/xinetd.d/{imap,ipop2,ipop3} etc/ssl/certs/{imapd,ipop3d}.pem)
md5sums=('2126fd125ea26b73b20f01fcd5940369'
         'e77dedb80c2fec9bf2ef784ed5f976fb'
         'cc3eba6453242aa0b1bc8881160b2136'
         '0a5e27183a9b7e8ec5f6703125371e0a'
         'cc8cc4df43f73bc144b9a41c55ef5991')

build() {
  # fix Makefile
  sed \
    -e "s:-g -fno-omit-frame-pointer -O6:\${CFLAGS}:" \
    -e "s:SSLDIR=/usr/local/ssl:SSLDIR=/usr:" \
    -e "s:SSLCERTS=\$(SSLDIR)/certs:SSLCERTS=/etc/ssl/certs:" \
    -i ${srcdir}/${pkgname}-${pkgver}/src/osdep/unix/Makefile
	
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/openssl1.1.patch

  make lnp SPECIALAUTHENTICATORS=ssl SSLTYPE=unix EXTRACFLAGS="${CFLAGS} -fPIC"

  # create ssl certs for secure imap
  for i in imapd ipop3d; do
    PEM1=${srcdir}/pem1
    PEM2=${srcdir}/pem2
    /usr/bin/openssl req -newkey rsa:1024 -keyout ${PEM1} \
      -nodes -x509 -days 365 -out  ${PEM2} << EOF
--
SomeState
SomeCity
SomeOrganization
SomeOrganizationalUnit
localhost.localdomain
root@localhost.localdomain
EOF

    cat ${PEM1} >  ${i}.pem
    echo ""    >> ${i}.pem
    cat ${PEM2} >> ${i}.pem
    rm ${PEM1} ${PEM2}
    umask 022
  done
}

package() {
  cd ${pkgname}-${pkgver}

  install -d ${pkgdir}/usr/sbin
  install -D -m0755 imapd/imapd ${pkgdir}/usr/sbin/imapd
  install -D -m0755 ipopd/ipop2d ${pkgdir}/usr/sbin/ipop2d
  install -D -m0755 ipopd/ipop3d ${pkgdir}/usr/sbin/ipop3d
  for i in c-client mail imap4r1 rfc822 linkage misc smtp nntp \
    osdep env_unix env fs ftl nl tcp sslio utf8 utf8aux; do
    install -D -m0644 c-client/${i}.h ${pkgdir}/usr/include/imap/${i}.h
  done
  install -D -m0644 c-client/c-client.a ${pkgdir}/usr/lib/c-client.a
  ln -sf c-client.a ${pkgdir}/usr/lib/libc-client.a

  install -D -m0600 imapd.pem ${pkgdir}/etc/ssl/certs/imapd.pem
  install -D -m0600 ipop3d.pem ${pkgdir}/etc/ssl/certs/ipop3d.pem

  install -D -m0644 ../imap ${pkgdir}/etc/xinetd.d/imap
  install -D -m0644 ../ipop2 ${pkgdir}/etc/xinetd.d/ipop2
  install -D -m0644 ../ipop3 ${pkgdir}/etc/xinetd.d/ipop3
}
