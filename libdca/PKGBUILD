
pkgname=libdca
pkgver=0.0.7
pkgrel=1
pkgdesc="Free library for decoding DTS Coherent Acoustics streams"
arch=('x86_64')
url="https://www.videolan.org/developers/libdca.html"
license=('GPL')
depends=('sh')
options=('!libtool' '!emptydirs')
source=("https://download.videolan.org/pub/videolan/libdca/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('dae15d77d066687c882833d5bed8d65a585c1fc0277b7276563c89ddd5a83b35389ec94cca445f38af28a9b01430b72647e9afd1b08f030959e711de1a08924a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./bootstrap
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/share/man/man1/{extract_dts,dtsdec}.1
  ln -s dcadec.1.gz ${pkgdir}/usr/share/man/man1/dtsdec.1.gz
  ln -s extract_dca.1.gz ${pkgdir}/usr/share/man/man1/extract_dts.1.gz
  rm ${pkgdir}/usr/lib/libdts.a
}
