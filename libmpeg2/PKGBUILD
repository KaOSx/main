
pkgname=libmpeg2
pkgver=0.5.1
pkgrel=6
pkgdesc="libmpeg2 is a library for decoding MPEG-1 and MPEG-2 video streams."
arch=('x86_64')
url="http://libmpeg2.sourceforge.net/"
license=('GPL2')
depends=('glibc' 'sdl' 'libxv')
options=('!libtool')
source=("http://libmpeg2.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz"
        'gcc.patch')
md5sums=('0f92c7454e58379b4a5a378485bbd8ef'
         '062c4913e528c4e83e3ac3d284ff8bcb')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/gcc.patch
  
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf --force --install
  
  ./configure --prefix=/usr --enable-shared --disable-static
  make	OPT_CFLAGS="${CFLAGS}" \
	MPEG2DEC_CFLAGS="${CFLAGS}" \
	LIBMPEG2_CFLAGS=""
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
