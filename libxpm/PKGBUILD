
pkgname=libxpm
pkgver=3.5.12
pkgrel=1
pkgdesc="X11 pixmap library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxt' 'libxext')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/libXpm-${pkgver}.tar.bz2")
md5sums=('20f4627672edb2bd06a749f11aa97302')

build() {
  cd libXpm-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXpm-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
