
pkgname=libxpm
pkgver=3.5.16
pkgrel=1
pkgdesc="X11 pixmap library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxt' 'libxext')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXpm-${pkgver}.tar.xz")
sha256sums=('e6bc5da7a69dbd9bcc67e87c93d4904fe2f5177a0711c56e71fa2f6eff649f51')

build() {
  cd libXpm-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --disable-open-zfile
  make
}

package() {
  cd libXpm-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
