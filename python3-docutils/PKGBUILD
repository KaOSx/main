
pkgname=python3-docutils
_pkgname=docutils
pkgver=0.17
pkgrel=1
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX"
arch=('x86_64')
url="http://docutils.sourceforge.net"
license=('custom')
depends=('python3')
makedepends=('python3-setuptools')
source=("https://files.pythonhosted.org/packages/source/D/Docutils/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e2ffeea817964356ba4470efba7c2f42b6b0de0b04e66378507e3e2504bbff4c')

build() {
  cd ${_pkgname}-${pkgver}
  
  python3 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python3 setup.py install --root=${pkgdir} --optimize=1

  for _f in ${pkgdir}/usr/bin/*.py; do
      ln -s $(basename $_f) ${pkgdir}/usr/bin/$(basename $_f .py)
  done

  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt
  install -D -m644 licenses/python* ${pkgdir}/usr/share/licenses/${pkgname}/
}
