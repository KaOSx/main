
pkgname=python3-docutils
_pkgname=docutils
pkgver=0.19
pkgrel=1
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX"
arch=('x86_64')
url="https://docutils.sourceforge.io/"
license=('custom')
depends=('python3')
makedepends=('python3-setuptools')
source=("https://files.pythonhosted.org/packages/source/D/Docutils/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('33995a6753c30b7f577febfc2c50411fec6aac7f7ffeb7c4cfe5991072dcf9e6')

build() {
  cd ${_pkgname}-${pkgver}

  python3 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python3 setup.py install --root=${pkgdir} --optimize=1

  for _f in ${pkgdir}/usr/bin/*.py; do
      ln -s $(basename $_f) ${pkgdir}/usr/bin/$(basename $_f .py)
  done

  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt
  install -D -m644 licenses/python* ${pkgdir}/usr/share/licenses/${pkgname}/
}
