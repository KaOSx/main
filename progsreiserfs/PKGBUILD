
pkgname=progsreiserfs
pkgver=0.3.0.5
pkgrel=12
pkgdesc="library for accessing and manipulating reiserfs partitions"
arch=('x86_64')
url="https://reiserfs.osdn.org.ua/"
license=('GPL')
depends=('e2fsprogs')
options=('!libtool')
source=("https://www.mirrorservice.org/sites/download.salixos.org/i486/13.37/source/l/progsreiserfs/${pkgname}-${pkgver}.tar.gz"
        'fix-underquoted-definition.patch')
md5sums=('d822579bbd4a29a1be5e4490869d9bee'
         '0ff24db039edbcc574f59075340b003a')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-underquoted-definition.patch

  ./configure --prefix=/usr \
    --mandir=/usr/share/man

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
  
  # progsreiserfs has been proven dangerous in the past, generating bad partitions and destroying
  # data on resize/cpfs operations, ship only libraries
  rm -r ${pkgdir}/usr/sbin
  rm -r ${pkgdir}/usr/share/man
}

