
pkgname=openobex
pkgver=1.7.2
pkgrel=2
pkgdesc="Implementation of the OBject EXchange (OBEX) protocol"
url="http://dev.zuckschwerdt.org/openobex/"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('bluez' 'libusbx')
options=('!libtool')
source=("https://downloads.sourceforge.net/openobex/${pkgname}-${pkgver}-Source.tar.gz")
md5sums=('f6e0b6cb7dcfd731460a7e9a91429a3a')

build() {
  cd ${srcdir}/
  sed -i 's|MODE="660", GROUP="plugdev"|TAG+="uaccess"|' ${pkgname}-${pkgver}-Source/udev/openobex.rules.in
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver}-Source \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib 
  make all openobex-apps
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  cp ../${pkgname}-${pkgver}-Source/apps/lib/*.h  ${pkgdir}/usr/include/openobex/
  install -m644 apps/lib/libopenobex-apps-common.a ${pkgdir}/usr/lib/libopenobex-apps-common.a
}
