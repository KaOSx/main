
pkgname=gsm
pkgver=1.0.22
_pkgver=1.0-pl22
pkgrel=3
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="https://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("https://www.quut.com/gsm/${pkgname}-${pkgver}.tar.gz"
        'gsm.patch')
md5sums=('fcca74c770a341d78ea4604418c1264b'
         '5592b2b0e545ded578ef0746e491b2e4')

build() {
  cd ${pkgname}-${_pkgver}/
  patch -p1 -i ${srcdir}/gsm.patch

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  make CCFLAGS="-c ${CFLAGS}"
}

package() {
  cd ${pkgname}-${_pkgver}/

  install -m755 -d ${pkgdir}/usr/{bin,lib,include/gsm,share/{licenses/${pkgname},man/man{1,3}}}

  make -j1 INSTALL_ROOT=${pkgdir}/usr \
    GSM_INSTALL_INC=${pkgdir}/usr/include/gsm \
    GSM_INSTALL_MAN=${pkgdir}/usr/share/man/man3 \
    TOAST_INSTALL_MAN=${pkgdir}/usr/share/man/man1 \
    install

  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
