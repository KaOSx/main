
pkgname=alsa-utils
pkgver=1.1.4
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('alsa-lib' 'dialog' 'pciutils' 'ncurses' 'psmisc' 'libsamplerate' 'fftw')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=("ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2"
        '90alsa')
md5sums=('01e3934ca5bd22a80c27289d1b0adcdc'
         '529216f6a46e61a546640e08ea7a0879')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --disable-alsaconf \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}

