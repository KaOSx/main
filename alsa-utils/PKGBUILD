
pkgname=alsa-utils
pkgver=1.2.5.1
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('alsa-lib' 'dialog' 'pciutils' 'ncurses' 'psmisc' 'libsamplerate' 'fftw')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=("https://www.alsa-project.org/files/pub/utils/$pkgname-${pkgver}.tar.bz2"
#source=("https://github.com/alsa-project/alsa-utils/archive/v${pkgver}.tar.gz"
        '90alsa')
md5sums=('8f142bebff84ff05438b46a0e896f53a'
         '529216f6a46e61a546640e08ea7a0879')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi
  ./configure --prefix=/usr \
    --disable-alsaconf \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}

