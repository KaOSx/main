
# Find the kernel name inside the chroot
_extramodules=extramodules-4.16
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl
pkgver=6.30.223.271
_pkgver=6_30_223_271
pkgrel=36
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='https://www.broadcom.com/support/802.11/'
license=('custom')
depends=('linux>=4.16' 'linux<4.17')
makedepends=('linux-headers' 'elfutils')

source=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${_pkgver}.tar.gz"
        'modprobe.d'
        'license.patch'
        'linux-recent.patch')
md5sums=('115903050c41d466161784d4c843f4f9'
         '3600df7db49c759c655bc6a7789b28d4'
         '52f9dcef8112f8e325d9236e5290cb88'
         'aa386c73131d51ebacea5efefadb1bda')

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install

build() {
	cd "${srcdir}"

	patch -p1 -i license.patch
	patch -p1 -i linux-recent.patch
	
	sed -i 's|BRCM_WLAN_IFNAME eth|BRCM_WLAN_IFNAME wlan|' src/wl/sys/wl_linux.c

	make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
