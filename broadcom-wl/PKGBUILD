
# Find the kernel name inside the chroot
_extramodules=extramodules-6.2
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=broadcom-wl
pkgver=6.30.223.271
_pkgver=6_30_223_271
_commit=452d163a8bac9b6aaef659ebb6b8fb805334f555
pkgrel=127
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
arch=('x86_64')
url='https://www.broadcom.com/support/802.11/'
license=('custom')
depends=('linux>=6.2.10' 'linux<6.3')
makedepends=('linux-headers' 'elfutils')
options=('!strip')
install=install
#source=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${_pkgver}.tar.gz"
source=("https://github.com/antoineco/broadcom-wl/archive/452d163a8bac9b6aaef659ebb6b8fb805334f555.zip"
        'modprobe.d'
        'license.patch'
        'linux-recent.patch')
md5sums=('6ac72a40fa7528495e3e2a7d68c64139'
         '3600df7db49c759c655bc6a7789b28d4'
         '52f9dcef8112f8e325d9236e5290cb88'
         '2c68b5a86241e8dd0f28381758e13c08')

backup=('etc/modprobe.d/broadcom-wl.conf')

build() {
    cd ${pkgname}-${_commit}

    patch -p1 -i ${srcdir}/license.patch

    make -C /lib/modules/"${_kver}"/build M=`pwd`
}

package() {
    cd ${pkgname}-${_commit}

    install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

    # sign the module
    _file=/usr/src/linux-${_kver}/scripts/sign-file
    _pem=/$HOME/signing_key.pem
    _key=/$HOME/signing_key.x509

    ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/wl.ko

    gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

    install -D -m 644 lib/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    install -D -m 644 ${srcdir}/modprobe.d ${pkgdir}/etc/modprobe.d/broadcom-wl.conf
    #automatically update install file
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" ${startdir}/install
}
