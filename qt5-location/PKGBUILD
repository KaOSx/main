
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt5-location
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
_commit=35d566724c48180c9a372c2ed50a253871a51574
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtLocation, QtPositioning)."
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('qt5-declarative')
groups=('qt5')
options=('!libtool')
_pkgfqn="qtlocation-everywhere-opensource-src-${subver}"
__pkgfqn="qtlocation-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        "https://github.com/qt/qtlocation/commit/3eaed3d6fa15d8a29cb8d8d683aaea9b256a36b1.diff"
        "https://invent.kde.org/qt/qt/qtlocation-mapboxgl/-/archive/${_commit}/qtlocation-mapboxgl-${_commit}.tar.bz2"
        'qt_kde.patch')
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '17f69eb6b8c2185d7e358280e962f6ec'
         '79faa92ba65fc1c76151e115122b2e47'
         '66a2adb2130df8b0c28c756389fb2bad')

prepare() {
    cd ${__pkgfqn}
    rm -rf src/3rdparty/mapbox-gl-native/*
    mv ${srcdir}/qtlocation-mapboxgl-${_commit}/* src/3rdparty/mapbox-gl-native

    #patch -R -p1 -i ${srcdir}/3eaed3d6fa15d8a29cb8d8d683aaea9b256a36b1.diff
    patch -p1 -i ${srcdir}/qt_kde.patch
}

build() {
    cd ${__pkgfqn}

    /usr/lib/qt5/bin/qmake
    make
}

package() {
    cd ${__pkgfqn}
    make INSTALL_ROOT=$pkgdir install

    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done

    install -D -m644 LICENSE.GPL3-EXCEPT \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.GPL3-EXCEPT
}

