
# rebuild libetonyek on 1.x updates
pkgname=mdds
pkgver=2.0.3
pkgrel=1
pkgdesc="Collection of multi-dimensional data structures and indexing algorithm"
arch=('x86_64')
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=("https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver}")
source=("https://kohei.us/files/mdds/src/mdds-${pkgver}.tar.xz")
sha256sums=('6c01cdbd7f662a7adee5a6879d31130815fd31048d940f1b780ed67b719fc8e4')

build() {
  cd ${pkgname}-${pkgver}

  #./autogen.sh
  ./configure --prefix=/usr
  # header files only, so no "make"
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
