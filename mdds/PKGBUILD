
pkgname=mdds
pkgver=1.3.1
pkgrel=1
pkgdesc="Collection of multi-dimensional data structures and indexing algorithm"
arch=('x86_64')
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=("https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver}")
source=("http://kohei.us/files/mdds/src/mdds-${pkgver}.tar.bz2")
sha256sums=('dcb8cd2425567a5a5ec164afea475bce57784bca3e352ad4cbdd3d1a7e08e5a1')

build() {
  cd ${pkgname}-${pkgver}

  #./autogen.sh
  ./configure --prefix=/usr
  # header files only, so no "make"
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
