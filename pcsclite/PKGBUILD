
pkgname=pcsclite
pkgver=1.8.18
_pkgver=4179
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('python2' 'systemd')
makedepends=('pkg-config')
options=('!docs')
source=("https://alioth.debian.org/frs/download.php/file/${_pkgver}/pcsc-lite-${pkgver}.tar.bz2")
md5sums=('03d92b060183b9cb0b559ea38c6c6c6f')

build() {
  cd pcsc-lite-${pkgver}

  ./configure --prefix=/usr \
              --enable-libudev \
              --sysconfdir=/etc \
              --enable-ipcdir=/run/pcscd \
              --enable-usbdropdir=/usr/lib/pcsc/drivers \
              --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd pcsc-lite-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
