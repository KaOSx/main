
pkgname=pcsclite
pkgver=2.2.0
_pkgver=4235
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('x86_64')
url="https://pcsclite.apdu.fr/"
license=('BSD')
depends=('python3' 'systemd' 'polkit')
makedepends=('meson' 'ninja')
options=('!docs')
source=("https://pcsclite.apdu.fr/files/pcsc-lite-${pkgver}.tar.xz")
md5sums=('9e4006e1f3210ffa7dd0fadbc9083dc2')

prepare() {
  cd pcsc-lite-${pkgver}

  sed -i "s|'/' / get_option('sbindir')|get_option('sbindir')|" meson.build
}

build() {
  mkdir -p build
  cd build

  meson setup ../pcsc-lite-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sbindir=/usr/sbin \
    -Dlibudev=true \
    -Dipcdir=/run/pcsd \
    -Dusbdropdir=/usr/lib/pcsc/drivers \
    -Dsystemdunit=system
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -D -m644 ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
