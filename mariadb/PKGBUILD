
pkgbase=mariadb
pkgname=('libmariadbclient' 'mariadb-clients' 'mariadb')
pkgver=10.1.23
_pkgver=10.1.23
pkgrel=1
arch=('x86_64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake' 'openssl' 'zlib' 'lz4' 'pcre')
options=('!libtool')
source=("http://ftp.osuosl.org/pub/mariadb/mariadb-${_pkgver}/source/mariadb-${_pkgver}.tar.gz"
        'mariadb.service'
        'mariadb-post.sh'
        'mariadb-tmpfile.conf')
md5sums=('1a7392cc05c7c249acd4495022719ca8'
         '41fe4303f88b7006743ee1218b332b54'
         'b79e65a5aa536e6b5bc60988eb0b78a2'
         '2fa6e456964d4ff5e6d4f9ff0126aed6')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgbase}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_PCRE=system \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_TOKUDB_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}"


  make
}

package_libmariadbclient() {
  pkgdesc='MariaDB client libraries'
  depends=('openssl' 'pcre')
  conflicts=('libmysqlclient')
  provides=("libmysqlclient=$-pkgver")
  
  cd build
  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR=${pkgdir} install
  done

  install -d ${pkgdir}/usr/bin
  install -m755 scripts/mysql_config ${pkgdir}/usr/bin/
  install -d ${pkgdir}/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 ${srcdir}/${pkgbase}-${_pkgver}/man/${man}.1 ${pkgdir}/usr/share/man/man1/${man}.1
  done
}

package_mariadb-clients() {
  pkgdesc='MariaDB client tools'
  depends=('libmariadbclient')
  conflicts=('mysql-clients')
  provides=("mysql-clients=${_pkgver}")
  
  cd build
  make -C client DESTDIR=${pkgdir} install

  # install man pages
  install -d ${pkgdir}/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 ${srcdir}/${pkgbase}-${_pkgver}/man/${man}.1 ${pkgdir}/usr/share/man/man1/${man}.1
  done

  # provided by mariadb
  rm ${pkgdir}/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mariadb() {
  pkgdesc='Fast SQL database server, drop-in replacement for MySQL'
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients' 'inetutils' 'lz4')
  conflicts=('mysql' 'mytop')
  provides=("mysql=${_pkgver}" 'mytop')
  options=('emptydirs')

  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${pkgdir}/usr/share/mysql/my-medium.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mariadb-post.sh ${pkgdir}/usr/bin/mysqld-post
  install -Dm644 ${srcdir}/mariadb.service ${pkgdir}/usr/lib/systemd/system/mysqld.service
  install -Dm644 ${srcdir}/mariadb-tmpfile.conf ${pkgdir}/usr/lib/tmpfiles.d/mysql.conf

  # provided by libmariadbclient
  rm ${pkgdir}/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm ${pkgdir}/usr/lib/libmysql*
  rm -r ${pkgdir}/usr/include/
  rm ${pkgdir}/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  
  # provided by mariadb-clients
  rm ${pkgdir}/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm ${pkgdir}/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r ${pkgdir}/usr/{data,mysql-test,sql-bench}
  rm ${pkgdir}/usr/share/man/man1/mysql-test-run.pl.1
}
