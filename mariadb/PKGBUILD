
pkgname=mariadb
pkgver=10.1.37
_pkgver=10.1.37
pkgrel=1
arch=('x86_64')
license=('GPL')
url='https://mariadb.org/'
pkgdesc='Open Source database to support todayâ€™s enterprise needs from OLTP to analytics from a single SQL compliant interface.'
depends=('openssl' 'pcre' 'inetutils' 'lz4')
makedepends=('cmake' 'zlib')
options=('!libtool' 'emptydirs')
conflicts=('libmariadbclient' 'mariadb-clients')
provides=('libmariadbclient' 'mariadb-clients')
replaces=('libmariadbclient' 'mariadb-clients')
backup=('etc/mysql/my.cnf')
install=mariadb.install
source=("https://ftp.heanet.ie/mirrors/mariadb/mariadb-${_pkgver}/source/mariadb-${_pkgver}.tar.gz"
        'mariadb.service'
        'mariadb-post.sh'
        'mariadb-tmpfile.conf'
        "https://github.com/MariaDB/server/commit/440157cbbe796b6b9a44a3de46bbb93d0cb5a77c.diff"
        "https://github.com/MariaDB/server/commit/8b18a44fa7e5ddf6c8caee37de4f6112c64dfc87.diff")
sha256sums=('8cd516b0a7f7aa36a7c1d6e687dbbad8c0b08c92d5fd60c6e691b19a6cab4d46'
            '56badfd5a654f212566154cd4c1ecc8c88d4e8c0b7e5935b6ad6853ef745da01'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd'
            '7fb14a4cb8e6e0c5865b8d52940fe9342ad0f186eabf8f12e0c4bafb830a0eed'
            '1368fb10582edb123a23fe9005f53a42f848852cddb738713f32d6c1cc96b6fe')

prepare() {
  cd ${pkgname}-${pkgver}
  
  #sed -i 's|MARIADB_CLIENT_VERSION_PATCH "5"|MARIADB_CLIENT_VERSION_PATCH "6"|' libmariadb/CMakeLists.txt
  #patch -p1 -R -i ${srcdir}/440157cbbe796b6b9a44a3de46bbb93d0cb5a77c.diff
  #patch -p1 -R -i ${srcdir}/8b18a44fa7e5ddf6c8caee37de4f6112c64dfc87.diff
}
         
build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_PCRE=bundled \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_TOKUDB_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}"


  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${pkgdir}/usr/share/mysql/my-medium.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mariadb-post.sh ${pkgdir}/usr/bin/mysqld-post
  install -Dm644 ${srcdir}/mariadb.service ${pkgdir}/usr/lib/systemd/system/mariadb.service
  ln -s mariadb.service ${pkgdir}/usr/lib/systemd/system/mysqld.service
  install -Dm644 ${srcdir}/mariadb-tmpfile.conf ${pkgdir}/usr/lib/tmpfiles.d/mysql.conf

  # not needed
  rm -r ${pkgdir}/usr/{data,mysql-test,sql-bench}
  rm ${pkgdir}/usr/share/man/man1/mysql-test-run.pl.1
}
