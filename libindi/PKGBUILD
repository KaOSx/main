
pkgname=libindi
_pkgname=indi
pkgver=1.9.5.1
_pkgver=1.9.5
pkgrel=2
pkgdesc="Interface control protocol providing backend driver support and
        automation for a wide range of Astronomical devices (telescopes, focusers, CCDs..etc)."
url="https://www.indilib.org/"
license=('GPL2')
arch=('x86_64')
depends=('gcc-libs' 'zlib' 'libnova' 'cfitsio' 'libusb' 'boost-libs' 'libjpeg-turbo' 'gsl'
         'qt5-base' 'libtheora' 'fftw')
makedepends=('boost' 'cmake' 'googletest')
source=("https://github.com/indilib/indi/archive/v${pkgver}.tar.gz")
options=('!libtool')
md5sums=('e891824ba3df0165b58c7791fcdbc7b5')

build() {
    cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINDI_BUILD_QT5_CLIENT=ON
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
