
pkgname=libindi
_pkgname=indi
pkgver=1.5.0
_pkgver=1.5.0
pkgrel=4
pkgdesc="Interface control protocol providing backend driver support and
        automation for a wide range of Astronomical devices (telescopes, focusers, CCDs..etc)."
url="http://indi.sourceforge.net/"
license=('GPL2')
arch=('x86_64')
depends=('gcc-libs' 'zlib' 'libnova' 'cfitsio' 'libusb' 'boost-libs' 'libjpeg-turbo' 'gsl' 'qt5-base')
makedepends=('boost' 'cmake' 'googletest')
source=("https://github.com/indilib/indi/archive/v${pkgver}.tar.gz")
options=('!libtool')
md5sums=('171ba2c4133436dfbf692d25baa0ae29')

build() {
    mkdir -p build
    cd build

    cmake ../${_pkgname}-${pkgver}/${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DCMAKE_INSTALL_LIBDIR=lib
    make
}

package() {
    cd build 
    make DESTDIR=${pkgdir} install
    # systemd errors becuase of non-existing plugdev group
    sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i ${pkgdir}/usr/lib/udev/rules.d/99-vantage.rules
    sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i ${pkgdir}/usr/lib/udev/rules.d/99-perfectstar.rules
}
