
pkgname=libindi
_pkgname=indi
pkgver=2.0.9
_pkgver=2.0.9
pkgrel=1
pkgdesc="Interface control protocol providing backend driver support and
        automation for a wide range of Astronomical devices (telescopes, focusers, CCDs..etc)."
url="https://www.indilib.org/"
license=('GPL2')
arch=('x86_64')
depends=('gcc-libs' 'zlib' 'libnova' 'cfitsio' 'libusb' 'libjpeg-turbo' 'gsl'
         'qt5-base' 'libtheora' 'fftw' 'libev')
makedepends=('cmake' 'googletest')
source=("https://github.com/indilib/indi/archive/v${pkgver}.tar.gz")
options=('!libtool')
md5sums=('12339568c66e860178ba1ae2ee3dd211')

build() {
    cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DINDI_BUILD_QT5_CLIENT=ON
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
