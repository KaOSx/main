 
pkgname=libwacom
pkgver=2.9.0
pkgrel=1
pkgdesc="Library that provides applications with a way to get information about any tablets that are currently connected."
arch=('x86_64')
url="https://github.com/linuxwacom/libwacom/wiki"
license=('GPLv2')
depends=('glib2' 'libgudev')
makedepends=('python3' 'meson' 'ninja')
source=("https://github.com/linuxwacom/libwacom/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6f678156797becc4f1537a19aadcc48ed7a54e1ff3cbf591d1233f8a2d82e242')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dtests=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

