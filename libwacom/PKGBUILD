 
pkgname=libwacom
pkgver=1.3
pkgrel=1
pkgdesc="Library that provides applications with a way to get information about any tablets that are currently connected."
arch=('x86_64')
url="https://github.com/linuxwacom/libwacom/wiki"
license=('GPLv2')
depends=('glib2' 'libgudev')
source=("https://github.com/linuxwacom/libwacom/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://github.com/linuxwacom/libwacom/commit/007c25052bf20adc1f5311c3e5668f6f343ac18b.diff")
sha256sums=('e19ce00fe1e074cc54ffd198dda6c6f1ec389e5c913134adc8795f2c5e63883c'
            'b1588dac7bff6f6988434041c0bf7bded7c09f1bfcd47db2922dca4555c5a4a1')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/007c25052bf20adc1f5311c3e5668f6f343ac18b.diff
  
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  
  # adding rules as per https://github.com/linuxwacom/libwacom/blob/master/README
  install -m755 -d ${pkgdir}/usr/lib/udev/rules.d
  cd tools 
  ./generate-udev-rules > ${pkgdir}/usr/lib/udev/rules.d/65-libwacom.rules
  
}
