# keep at 5 series, unless smokeqt is ready for newer

pkgname=qwt
pkgver=6.1.3
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications"
arch=('x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt5-base' 'qt5-svg')
makedepends=('qt5-tools')
options=('!makeflags')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/sourceforge/qwt/$pkgname-$pkgver.tar.bz2" 
        "qwtconfig.pri")
md5sums=('19d1f5fa5e22054d22ee3accc37c54ba'
         '1091f53d0f5905378be0b10312a642ac')

build() {
  cd ${pkgname}-${pkgver}

  # needed config file 
  install -Dm644 ${srcdir}/qwtconfig.pri ${srcdir}/${pkgname}-${pkgver}/qwtconfig.pri

  /usr/lib/qt5/bin/qmake qwt.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  mv -f ${pkgdir}/usr/share/doc/qwt/man/ ${pkgdir}/usr/share/

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
