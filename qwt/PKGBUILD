
pkgname=qwt
pkgver=6.2.0
pkgrel=5
pkgdesc="Qt Widgets for Technical Applications"
arch=('x86_64')
url="https://qwt.sourceforge.io/"
depends=('qt6-base' 'qt6-svg')
makedepends=('qt6-tools')
options=('!makeflags')
license=("custom:$pkgname")
source=("https://downloads.sourceforge.net/sourceforge/qwt/${pkgname}-${pkgver}.tar.bz2" 
        "qwtconfig.pri")
md5sums=('00c94f0af8b29d4785cec47351127c00'
         '93dad0b9a37e082bbe392f5b62a97e37')

build() {
  cd ${pkgname}-${pkgver}

  # adjusted config file, check with every update
  install -Dm644 ../qwtconfig.pri qwtconfig.pri

  sed -i -e 's|Qt5|Qt6|g' src/src.pro

  /usr/lib/qt6/bin/qmake qwt.pro
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  mv -f ${pkgdir}/usr/share/doc/qwt/man/ ${pkgdir}/usr/share/

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
