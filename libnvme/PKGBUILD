
pkgname=libnvme
pkgver=1.11.1
pkgrel=1
pkgdesc="C Library for NVM Express on Linux"
arch=('x86_64')
url="https://nvmexpress.org/"
license=('GPL')
depends=('json-c' 'openssl')
makedepends=('meson' 'ninja')
source=("https://github.com/linux-nvme/libnvme/archive/v${pkgver}.tar.gz")
md5sums=('b6d2e2277c81d25a213636ac97a311af')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir /etc \
    --buildtype=release \
    -Ddocs-build=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
