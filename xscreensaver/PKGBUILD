
pkgname=xscreensaver
pkgver=5.35
pkgrel=1
pkgdesc="Screen saver and locker for the X Window System"
arch=('x86_64')
url="http://www.jwz.org/xscreensaver/"
license=('BSD')
depends=('mesa' 'pam' 'xorg-res-utils' 'libxmu' 'libjpeg-turbo' 'libglade' 'glu')
makedepends=('bc' 'intltool' 'libxpm' 'mesa')
backup=('etc/pam.d/xscreensaver')
source=("http://www.jwz.org/xscreensaver/${pkgname}-${pkgver}.tar.gz"
        'add-electricsheep.diff'
        'LICENSE')
md5sums=('91ed6e69340e9a1c3d66d717f30d4c7e'
         '259e8bdb61b3a7c6f752c81e72afe4cf'
         '5e7f3f2a63d20a484742f5b4cb5d572c')


build() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/add-electricsheep.diff
  sed -i 's|(HAVE_COCOA)  // Real X11|(HAVE_COCOA)  /* Real X11 */|' utils/grabclient.c
  sed -i 's|(HAVE_COCOA)   // Desktop OSX|(HAVE_COCOA)   /* Desktop OSX */|' utils/grabclient.c
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-x-app-defaults=/usr/share/X11/app-defaults \
    --with-pam \
    --without-motif \
    --with-gtk \
    --with-gl \
    --without-gle \
    --with-xpm \
    --with-jpeg 
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  install -d ${pkgdir}/etc/pam.d
  make install_prefix=${pkgdir} install 
  
  install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
  chmod 755 ${pkgdir}/usr/bin/xscreensaver 
  echo "NotShowIn=KDE;GNOME;" >> ${pkgdir}/usr/share/applications/xscreensaver-properties.desktop 
}
