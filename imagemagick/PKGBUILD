
pkgname=imagemagick
pkgver=6.9.11.45
_pkgver=6.9.11-45
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="https://www.imagemagick.org/"
license=('custom')
depends=('libtool' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
         'libxext' 'fftw' 'liblqr' 'libraw' 'librsvg' 'libxml2' 'perl' 'libwebp'
         'ghostscript' 'openexr')
makedepends=('opencl-headers' 'subversion')
options=('!docs' 'libtool' '!emptydirs')
source=("https://www.imagemagick.org/download/ImageMagick-${_pkgver}.tar.xz"
        'perlmagick.rpath.patch')
md5sums=('cfe376dc943db729fd4048be0d591d07'
         '0faf7158fd3a0904d9a70dd3e3c1fe9c')

build() {
	cd ImageMagick-${_pkgver}
	sed '/AC_PATH_XTRA/d' -i configure.ac
        autoreconf --force --install

	patch -p0 -i ${srcdir}/perlmagick.rpath.patch 

	./configure --prefix=/usr \
        --sysconfdir=/etc \
        --with-modules \
        --disable-static \
        --enable-shared \
	    --enable-hdri \
	    --without-wmf \
	    --with-openexr \
	    --with-xml \
	    --enable-opencl \
	    --with-gslib \
	    --with-gs-font-dir=/usr/share/fonts/Type1 \
	    --with-perl \
	    --with-perl-options="INSTALLDIRS=vendor" \
	    --with-lqr \
	    --with-rsvg \
	    --with-webp \
	    --without-gvc \
	    --without-djvu \
	    --without-autotrace \
	    --without-jbig \
	    --without-openjp2 \
	    --without-fpx \
	    --without-dps \
	    --without-fftw \
	    --with-gcc-arch=x86-64
	make 
}

check() {
        cd ImageMagick-${_pkgver}
        # 2 demos fail without libwmf
        make check
}

package() {
	cd ImageMagick-${_pkgver}

	make -j1 DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	rm -f ${pkgdir}/usr/lib/*.la 
}
