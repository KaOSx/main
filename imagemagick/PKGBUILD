
pkgname=imagemagick
pkgver=6.9.1.2
_pkgver=6.9.1-2
pkgrel=2
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('libltdl' 'lcms' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
	 'libxext' 'fftw' 'liblqr' 'libwmf' 'librsvg' 'libxml2' 'perl' 'libwebp' 'libcl')
makedepends=('ghostscript' 'openexr' 'jasper' 'opencl-headers' 'subversion')
optdepends=('ghostscript: for Ghostscript support' 
            'openexr: for OpenEXR support'  
            'jasper: for JPEG-2000 support')
options=('!docs' 'libtool' '!emptydirs')
source=("http://www.imagemagick.org/download/ImageMagick-${_pkgver}.tar.xz"
	'perlmagick.rpath.patch')
md5sums=('57e8e138b05d8115c1ab7d9f151645bd'
         '0faf7158fd3a0904d9a70dd3e3c1fe9c')

build() {
	cd ${srcdir}/ImageMagick-${_pkgver}
	sed '/AC_PATH_XTRA/d' -i configure.ac
        autoreconf --force --install

	patch -p0 -i "${srcdir}/perlmagick.rpath.patch" 

	./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
	    --enable-hdri --with-wmf --with-openexr --with-xml --enable-opencl \
	    --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
	    --with-perl --with-perl-options="INSTALLDIRS=vendor" \
	    --with-lqr --with-rsvg --with-lcms2 --with-webp \
	    --without-gvc --without-djvu --without-autotrace \
	    --without-jbig --without-fpx --without-dps --without-fftw --with-gcc-arch=x86-64
	make 
}

check() {
  cd ImageMagick-${_pkgver}
  
  make check
}

package() {
	cd ${srcdir}/ImageMagick-${_pkgver}

	make -j1 DESTDIR=${pkgdir} install 

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
	install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

	#Cleaning
	rm -f ${pkgdir}/usr/lib/*.la 
}
