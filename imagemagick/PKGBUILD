
pkgname=imagemagick
pkgver=7.1.1.26
_pkgver=7.1.1-26
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=('x86_64')
url="https://www.imagemagick.org/"
license=('custom')
depends=('libtool' 'lcms2' 'libxt' 'gcc-libs' 'bzip2' 'freetype2' 'fontconfig'
         'libxext' 'fftw' 'liblqr' 'libraw' 'librsvg' 'libxml2' 'perl' 'libwebp'
         'ghostscript' 'openexr' 'djvulibre' 'libheif' 'jpeg-xl')
makedepends=('opencl-headers' 'subversion')
options=('!docs' 'libtool' '!emptydirs')
source=("https://imagemagick.org/archive/ImageMagick-${_pkgver}.tar.xz"
        'perlmagick.rpath.patch')
md5sums=('c27dccea0ba721f42085deb19a13942c'
         '0faf7158fd3a0904d9a70dd3e3c1fe9c')

build() {
    cd ImageMagick-${_pkgver}
    sed '/AC_PATH_XTRA/d' -i configure.ac
    autoreconf --force --install

    patch -p0 -i ${srcdir}/perlmagick.rpath.patch

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --with-modules \
        --disable-static \
        --enable-shared \
        --enable-hdri \
        --without-wmf \
        --with-openexr \
        --with-xml \
        --enable-opencl \
        --with-gslib \
        --with-gs-font-dir=/usr/share/fonts/Type1 \
        --with-jxl \
        --with-perl \
        --with-perl-options="INSTALLDIRS=vendor" \
        --with-lqr \
        --with-rsvg \
        --with-webp \
        --without-gvc \
        --with-djvu \
        --without-autotrace \
        --without-jbig \
        --without-openjp2 \
        --without-fpx \
        --without-dps \
        --with-fftw \
        --with-gcc-arch=x86-64
    make 
    }

    check() {
        cd ImageMagick-${_pkgver}
        # 1 demo fails without libwmf
        make check
    }

    package() {
    cd ImageMagick-${_pkgver}

    make -j8 DESTDIR=${pkgdir} install 

    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
    install -Dm644 NOTICE ${pkgdir}/usr/share/licenses/${pkgname}/NOTICE 

    #Cleaning
    rm -f ${pkgdir}/usr/lib/*.la 
}
