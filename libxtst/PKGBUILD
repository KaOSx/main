
pkgname=libxtst
pkgver=1.2.4
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'libxi' 'xorgproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXtst-${pkgver}.tar.xz")
sha256sums=('84f5f30b9254b4ffee14b5b0940e2622153b0d3aed8286a3c5b7eeb340ca33c8')

build() {
  cd libXtst-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXtst-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
