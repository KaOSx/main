
pkgname=libxfont
pkgver=1.5.4
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans' 'git')
options=('!libtool')
source=("https://xorg.freedesktop.org/archive/individual/lib/libXfont-${pkgver}.tar.bz2")
# git build for http://lists.freedesktop.org/archives/xorg/2014-May/056616.html
#source=("git://anongit.freedesktop.org/xorg/lib/libXfont")
sha256sums=('1a7f7490774c87f2052d146d1e0e64518d32e6848184a18654e8d0bb57883242')

build() {
  cd libXfont-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXfont-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
