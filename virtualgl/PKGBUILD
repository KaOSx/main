
pkgname=virtualgl
pkgver=3.0.1
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo' 'glu' 'opencl-headers' 'xcb-util-keysyms' 'fltk')
makedepends=('cmake' 'libxtst')
source=("https://sourceforge.net/projects/virtualgl/files/${pkgver}/VirtualGL-${pkgver}.tar.gz")
md5sums=('e7ec38c7e69a2d8db10941057b753aa6')

build() {
  mkdir -p build
  cd build

  export CFLAGS+=" -fpermissive "

  cmake ../VirtualGL-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DTJPEG_INCLUDE_DIR=/usr/include \
    -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
    -DVGL_FAKEOPENCL=0 \
    -DVGL_SYSTEMFLTK=1
  make -j1
}

package() {
  cd build
  make install DESTDIR=${pkgdir}

  # conflicts with mesa-demos
  mv ${pkgdir}/usr/bin/glxinfo ${pkgdir}/usr/bin/vglxinfo
}

