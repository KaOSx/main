
pkgname=virtualgl
pkgver=2.6.3
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo' 'glu' 'opencl-headers' 'xcb-util-keysyms')
makedepends=('cmake' 'libxtst')
source=("https://sourceforge.net/projects/virtualgl/files/${pkgver}/VirtualGL-${pkgver}.tar.gz")
md5sums=('d47f257e5b4596262379bd6bb33eed79')

build() {
  mkdir -p build
  cd build

  cmake ../VirtualGL-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DTJPEG_INCLUDE_DIR=/usr/include \
    -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so 
  make
}

package() {
  cd build
  make install DESTDIR=${pkgdir}
  
  # conflicts with mesa-demos
  mv ${pkgdir}/usr/bin/glxinfo ${pkgdir}/usr/bin/vglxinfo
}

