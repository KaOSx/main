
pkgname=virtualgl
pkgver=3.1
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo' 'glu' 'opencl-headers' 'xcb-util-keysyms')
makedepends=('cmake' 'libxtst')
source=("https://sourceforge.net/projects/virtualgl/files/${pkgver}/VirtualGL-${pkgver}.tar.gz"
        "https://github.com/VirtualGL/virtualgl/commit/46d09529563127fef0d42a4fb2681ae8d65a856b.diff")
md5sums=('6ab663e3607ee569c3b5fc054b0e1b94'
         'ef47ea86ff47af3d12ac2d070d758444')

prepare() {
  cd VirtualGL-${pkgver}

  #patch -p1 -i ${srcdir}/46d09529563127fef0d42a4fb2681ae8d65a856b.diff
}

build() {
  mkdir -p build
  cd build

  export CFLAGS+=" -fpermissive "

  cmake ../VirtualGL-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DTJPEG_INCLUDE_DIR=/usr/include \
    -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
    -DVGL_FAKEOPENCL=0 \
    -DVGL_FAKEXCB=1
  make
}

package() {
  cd build
  make install DESTDIR=${pkgdir}

  # conflicts with mesa-demos
  mv ${pkgdir}/usr/bin/glxinfo ${pkgdir}/usr/bin/vglxinfo
}

