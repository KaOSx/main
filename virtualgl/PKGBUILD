
pkgname=virtualgl
pkgver=2.5
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('x86_64')
url="http://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo' 'glu')
makedepends=('cmake')
source=("http://sourceforge.net/projects/virtualgl/files/$pkgver/VirtualGL-$pkgver.tar.gz")
md5sums=('7b0a781be8eb249fbe77a99af89cc758')

build() {
  cd VirtualGL-$pkgver

  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/opt/VirtualGL \
           -DTJPEG_INCLUDE_DIR=/usr/include \
           -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
           -DVGL_LIBDIR=/usr/lib \
           -DVGL_BINDIR=/usr/bin 
  make
}

package() {
  cd VirtualGL-$pkgver/build
  make install DESTDIR=$pkgdir

  mkdir -p $pkgdir/opt/VirtualGL/bin
  mv $pkgdir/usr/bin/glxinfo $pkgdir/opt/VirtualGL/bin
}

