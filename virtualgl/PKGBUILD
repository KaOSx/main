
pkgname=virtualgl
pkgver=2.5.2
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('x86_64')
url="http://virtualgl.org"
license=('LGPL')
depends=('mesa' 'libxv' 'libjpeg-turbo' 'glu')
makedepends=('cmake')
source=("http://sourceforge.net/projects/virtualgl/files/${pkgver}/VirtualGL-${pkgver}.tar.gz")
md5sums=('1a9f404f4a35afa9f56381cb33ed210c')

build() {
  mkdir -p build
  cd build

  cmake ../VirtualGL-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DTJPEG_INCLUDE_DIR=/usr/include \
    -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so 
  make
}

package() {
  cd build
  make install DESTDIR=${pkgdir}
  
  # conflicts with mesa-demos
  mv ${pkgdir}/usr/bin/glxinfo ${pkgdir}/usr/bin/vglxinfo
}

