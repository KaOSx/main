#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=xf86-input-wacom
pkgver=0.19.0
pkgrel=1
pkgdesc="X.Org Wacom tablet driver."
arch=('x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
backup=('etc/X11/xorg.conf.d/50-wacom.conf')
depends=('libxi' 'libxrandr' 'libxinerama')
makedepends=('xorg-server' 'libxext' 'libxi')
conflicts=('xorg-server<1.12.3')
options=(!libtool)
source=("http://downloads.sourceforge.net/project/linuxwacom/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        '70-wacom.rules')
md5sums=('bdc66047bb0afb4ac29882cca16cdfaa'
         '10db4f8272286690255c1bcc18bfdd92')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../70-wacom.rules "$pkgdir/usr/lib/udev/rules.d/70-wacom.rules"
}

# vim:set ts=2 sw=2 et:
