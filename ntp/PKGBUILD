
pkgname=ntp
pkgver=4.2.6.p5
_realver=4.2.6p5
pkgrel=5
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=('x86_64')
license=('custom')
url="http://www.ntp.org/"
depends=('openssl' 'readline' 'libcap')
makedepends=('perl-html-parser' 'libedit')
optdepends=('libedit: ntpdc, ntpq')
backup=('etc/ntp.conf')
install=install
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${_realver}.tar.gz"
        'ntp.conf'
	'ntpd.service'
	'ntpdate.service')
md5sums=('00df80a84ec9528fcfb09498075525bc'
         'ac884ad959850731d8f27667681bbdf8'
         '06ae4a88dd7b9e0bc9679aebf2241167'
         'e57a527fb190e6865ff7b836183b845c')

build() {
  cd "${srcdir}/${pkgname}-${_realver}"
  
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-linuxcaps --enable-ntp-signd
  make
}

package() {
  cd "${srcdir}/${pkgname}-$_realver" 
  make DESTDIR="${pkgdir}" install
  
  rmdir "${pkgdir}"/usr/{lib,sbin}
  install -d -o 87 "${pkgdir}/var/lib/ntp"
  install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
  install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
  install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  cd html
  ../scripts/html2man
  install -d "${pkgdir}"/usr/share/man
  mv man/man* "${pkgdir}"/usr/share/man
  mv "${pkgdir}/usr/share/man/man8/ntpd.8" "${pkgdir}/usr/share/man/man8/ntp-ntpd.8" # we should ditch openntpd
 
  install -dm755 "$pkgdir/usr/lib/systemd/ntp-units.d"
  echo "ntpd.service" > "$pkgdir/usr/lib/systemd/ntp-units.d/$pkgname.list"
}
