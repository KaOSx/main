
pkgname=ntp
pkgver=4.2.8.16
_pkgver=4.2.8p16
pkgrel=1
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=('x86_64')
license=('custom')
url="https://www.ntp.org/"
depends=('openssl' 'readline' 'libcap' 'libedit' 'libevent')
backup=('etc/ntp.conf')
install=install
source=("https://archive.ntp.org/ntp4/ntp-4.2/ntp-${_pkgver}.tar.gz"
        'ntp.conf'
        'ntpd.service'
        'ntpdate.service'
        'glibc2.34.patch')
sha256sums=('5225858bfd843b080fb9daa5b7370519130e5e49ac3eb0371e334bdc06c52dd7'
            '3c089391cb70ee752a71396e7fde1be000acc199de08f02b9453510ef312c8f9'
            'e1f39fe1e60d4b507aa9d298b1f7ac77dcef19005249f42729043e0b8f0bb64a'
            '15f4394fb7efddb5f600fea64a4fc72d0f4835b175461e0fc29d9d5853dd9486'
            'c3fbc0766977bb768299ef32823ac234cdd3729b2af893f565cd353b77c54dcf')

build() {
  cd ${pkgname}-${_pkgver}
  # https://bugs.ntp.org/show_bug.cgi?id=3741
  #patch -p1 -i ${srcdir}/glibc2.34.patch

  ./configure --prefix=/usr \
      --mandir=/usr/share/man \
      --libexecdir=/usr/lib \
      --enable-linuxcaps \
      --enable-ntp-signd 
  make
}

package() {
  cd ${pkgname}-${_pkgver} 
  make DESTDIR=${pkgdir} install
  
  install -d -o 87 ${pkgdir}/var/lib/ntp
  echo > ${pkgdir}/var/lib/ntp/.placeholder

  install -Dm644 ../ntp.conf ${pkgdir}/etc/ntp.conf
  install -Dm644 ../ntpd.service ${pkgdir}/usr/lib/systemd/system/ntpd.service
  install -Dm644 ../ntpdate.service ${pkgdir}/usr/lib/systemd/system/ntpdate.service
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
