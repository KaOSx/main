
pkgname=mutagen
pkgver=1.47.0
pkgrel=2
pkgdesc="Python module for handling audio metadata, supporting ASF, FLAC, MP4, MP3, Ogg and many more audio files."
arch=('x86_64')
url="https://mutagen.readthedocs.org/en/latest/"
license=('GPL2')
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://github.com/quodlibet/mutagen/releases/download/release-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('719fadef0a978c31b4cf3c956261b3c58b6948b32023078a2117b1de09f0fc99')

build() {
    cd ${pkgname}-${pkgver}

    python3 -m build -nw
}

package() {
    cd ${pkgname}-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
