
pkgname=source-highlight
pkgver=3.1.9
pkgrel=10
pkgdesc="Produces a document with syntax highlighting and provides a C++ highlight library"
arch=('x86_64')
url="https://www.gnu.org/software/src-highlite/"
license=('GPL')
depends=('boost-libs')
makedepends=('boost')
source=("ftp://ftp.gnu.org/gnu/src-highlite/${pkgname}-${pkgver}.tar.gz"
        'gcc11.patch')
sha256sums=('3a7fd28378cb5416f8de2c9e77196ec915145d44e30ff4e0ee8beb3fe6211c91'
            '9f68afedb634723fac7fc58f7c03fb4aa8723ce017a4f0015fddd3852770f66c')

prepare() {
  cd ${pkgname}-${pkgver}

  # http://git.savannah.gnu.org/cgit/src-highlite.git/commit/?id=904949c9026cb772dc93fbe0947a252ef47127f4
  patch -p1 -i ${srcdir}/gcc11.patch
}

build() {
  mkdir -p build
  cd build

  #export CC=/usr/bin/clang
  #export CXX=/usr/bin/clang++

  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-static=false \
    --with-bash-completion=/usr/share/bash-completion/completions
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir}  install
} 
