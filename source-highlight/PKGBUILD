
pkgname=source-highlight
pkgver=3.1.9
pkgrel=3
pkgdesc="Produces a document with syntax highlighting and provides a C++ highlight library"
arch=('x86_64')
url="https://www.gnu.org/software/src-highlite/"
license=('GPL')
depends=('boost-libs')
makedepends=('boost')
source=("ftp://ftp.gnu.org/gnu/src-highlite/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3a7fd28378cb5416f8de2c9e77196ec915145d44e30ff4e0ee8beb3fe6211c91')

build() {
  mkdir -p build
  cd build

  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-static=false \
    --with-bash-completion=/usr/share/bash-completion/completions
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir}  install
} 
