
pkgname=vlc
pkgver=3.0.11.1
_pkgver=3.0.11.1
pkgrel=6
epoch=1
pkgdesc="Free and open source cross-platform multimedia player and framework that plays most multimedia files and various streaming protocols."
arch=('x86_64')
url="https://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio' 'libdca' 'libproxy'
         'sdl_image' 'libdvdnav' 'mesa' 'v4l-utils' 'libcddb' 'samba' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 'libshout' 'libmad' 
         'libmpeg2' 'libmodplug' 'opus' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils'
         'desktop-file-utils' 'xcb-util-keysyms' 'pulseaudio' 'chromaprint' 
         'qt5-x11extras' 'qt5-svg' 'x265' 'lua' 'mpg123' 'protobuf' 'vcdimager'
         'avahi' 'libnotify' 'libbluray' 'flac' 'libtheora' 'alsa-lib' 'dav1d'
         'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'live-media' 'microdns' 'libidn') # fails with 'fribidi' 1.0.1
makedepends=('pkgconfig' 'jack' 'systemd' 'gnutls' 'libcaca' 'librsvg' 'qt5-tools')
backup=('usr/share/vlc/http/.hosts'
        'usr/share/vlc/http/dialogs/.hosts'
        'usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
groups=('multimedia')
options=('!libtool' 'debug')
install="vlc.install"
source=("https://download.videolan.org/vlc/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'qt5.15.patch')
#source=("https://nightlies.videolan.org/build/source/${pkgname}-${_pkgver}.tar.xz")
sha256sums=('189311d28aa814f106a7b3645211ac52c0b3e2b9f4b348de2f63bab3218086b8'
            'a8a2267ddab07ea5267cb210a38c8772b2efb5d834f803a37e215aee1f784e95')

prepare() {
  cd ${pkgname}-${_pkgver}
  
  patch -p1 -i ${srcdir}/qt5.15.patch
  #sed -i -e 's|5.5.0|5.5.4|' configure.ac
  #autoreconf -fi
}

build() {
  cd ${pkgname}-${_pkgver}
  
  #CXXFLAGS="-std=c++11"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype/freetype.c
  ./bootstrap

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-faad \
              --enable-nls \
              --enable-upnp \
              --enable-realrtsp \
              --enable-bluray \
              --enable-opus \
              --enable-ncurses \
              --enable-lua \
              --enable-mpg123 \
              --enable-kwallet \
              --enable-chromecast \
              --enable-microdns \
              --disable-fribidi \
              --enable-vpx \
              RCC=/usr/bin/rcc-qt5
  make
}

package() {
  cd ${pkgname}-${_pkgver}

  make DESTDIR=${pkgdir} install

  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/vlc-${_pkgver}/share/icons/${res}x${res}/vlc.png \
        ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done
}

