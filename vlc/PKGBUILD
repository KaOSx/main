
pkgname=vlc
pkgver=3.0.18
_pkgver=3.0
pkgrel=13
epoch=1
pkgdesc="Free and open source cross-platform multimedia player and framework that plays most multimedia files and various streaming protocols."
arch=('x86_64')
url="https://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio' 'libdca' 'libproxy'
         'sdl_image' 'libdvdnav' 'mesa' 'v4l-utils' 'libcddb' 'samba' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 'libshout' 'libmad' 
         'libmpeg2' 'libmodplug' 'opus' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils'
         'desktop-file-utils' 'xcb-util-keysyms' 'pulseaudio' 'chromaprint' 
         'qt5-x11extras' 'qt5-svg' 'x265' 'lua' 'mpg123' 'protobuf' 'vcdimager'
         'avahi' 'libnotify' 'libbluray' 'flac' 'libtheora' 'alsa-lib' 'dav1d'
         'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'microdns' 'libidn' 'live-media') # fails with 'fribidi' 1.0.1
makedepends=('pkgconfig' 'jack' 'systemd' 'gnutls' 'libcaca' 'librsvg' 'qt5-tools')
backup=('usr/share/vlc/http/.hosts'
        'usr/share/vlc/http/dialogs/.hosts'
        'usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
groups=('multimedia')
options=('!libtool' 'debug')
install="vlc.install"
#source=("https://code.videolan.org/videolan/vlc-${_pkgver}/-/archive/${pkgver}/vlc-${_pkgver}-${pkgver}.tar.bz2"
source=("https://get.videolan.org/vlc/${pkgver}/vlc-${pkgver}.tar.xz"
        '0001-Add-lssl-and-lcrypto-to-liblive555_plugin_la_LIBADD.patch')
sha256sums=('57094439c365d8aa8b9b41fa3080cc0eef2befe6025bb5cef722accc625aedec'
            '81c1db61be41bfe31cedcdc83fe6ecf8178708d3ab372a0c4811008a3eded1a1')

prepare() {
  cd ${pkgname}-${pkgver}

  #patch -p1 -i ${srcdir}/0001-Add-lssl-and-lcrypto-to-liblive555_plugin_la_LIBADD.patch
  #sed -i -e 's|5.5.0|5.5.4|' configure.ac
}

build() {
  cd ${pkgname}-${pkgver}
  
  #CXXFLAGS="-std=c++11"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype/freetype.c
  ./bootstrap

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-faad \
              --enable-nls \
              --enable-upnp \
              --enable-realrtsp \
              --enable-bluray \
              --enable-opus \
              --enable-ncurses \
              --enable-lua \
              --enable-mpg123 \
              --enable-kwallet \
              --enable-chromecast \
              --enable-microdns \
              --disable-fribidi \
              --enable-live555 \
              --enable-vpx \
              RCC=/usr/bin/rcc-qt5
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png \
        ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done
}

