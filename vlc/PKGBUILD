#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=vlc
pkgver=2.0.5
pkgrel=2
arch=('x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libxpm' 'libcdio'
         'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa'
         'lua' 'v4l-utils' 'libcddb' 'smbclient' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp'
         'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 'opus'
         'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'desktop-file-utils' 'xcb-util-keysyms')
makedepends=('avahi' 'pkgconfig' 'live-media' 'libnotify' 'libbluray'
             'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'systemd'
             'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'pulseaudio'
             'lirc-utils' 'gnutls' 'libcaca' 'librsvg' 'oss')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'pulseaudio: PulseAudio support'
            'vdpau-video: vdpau back-end for nvidia'
            'libva-driver-intel: back-end for intel cards'
            'libbluray: for Blu-Ray disks'
            'oss: for oss sound support')
conflicts=('vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/http/.hosts'
        'usr/share/vlc/http/dialogs/.hosts'
        'usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
categories=('multimedia')
options=('!libtool')
install="vlc.install"
source=("http://download.videolan.org/pub/videolan/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha1sums=('31bd518f4edd03ea394305176e5df3478e251cc0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-faad \
              --enable-oss \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-upnp \
              --enable-realrtsp \
              --enable-bluray \
              --enable-opus \
              --enable-ncurses
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png" \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
  
}

