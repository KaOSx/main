
# install subversion prior to makepkg, svn in source fails otherwise
# version at https://sourceforge.net/p/netpbm/code/HEAD/tree/advanced/version.mk
pkgname=netpbm
pkgver=10.92.0
pkgrel=1
pkgdesc="A toolkit for manipulation of graphic images"
arch=('x86_64')
license=('custom' 'BSD' 'GPL' 'LGPL')
url="http://netpbm.sourceforge.net/"
depends=('perl' 'libjpeg-turbo' 'libpng' 'libtiff' 'libxml2' 'libx11')
makedepends=('python3' 'subversion')
options=('!makeflags')
source=("svn://svn.code.sf.net/p/netpbm/code/advanced")
md5sums=('SKIP')

build() {
  cd advanced
  
  # use the interactive way
  ./configure
  
  sed -i 's|misc|share/netpbm|' common.mk
  sed -i 's|/link|/lib|' lib/Makefile

  make
}

package() {
  cd advanced
  make package pkgdir=${pkgdir}/usr 
  
  install -D -m644 doc/copyright_summary ${pkgdir}/usr/share/licenses/${pkgname}/copyright_summary.txt
    
  #mv -iv ${pkgdir}/usr/man ${pkgdir}/usr/share/man
  rm ${pkgdir}/usr/conf*
  rm ${pkgdir}/usr/pkg*
  rm ${pkgdir}/usr/README
  rm ${pkgdir}/usr/VERSION
}
