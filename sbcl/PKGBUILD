
# rebuild maxima on updates
pkgname=sbcl
pkgver=2.0.2
pkgrel=1
pkgdesc="Steel Bank Common Lisp"
arch=('x86_64')
url="http://www.sbcl.org/"
license=('custom')
depends=('glibc' 'zlib')
makedepends=('sbcl')
provides=('common-lisp' 'cl-asdf')
source=("https://downloads.sourceforge.net/project/sbcl/sbcl/${pkgver}/${pkgname}-${pkgver}-source.tar.bz2"
        'fixes.lisp')
md5sums=('8c0b0352b898b4e2ecd66f16b6eb8dfd'
         '7ac0c1936547f4278198b8bf7725204d')

build() {
  cd ${pkgname}-${pkgver}

  sh make.sh sbcl --prefix=/usr --fancy 
}

package() {
  cd ${pkgname}-${pkgver}
  SBCL_HOME="" INSTALL_ROOT=$pkgdir/usr sh install.sh

  src/runtime/sbcl --core output/sbcl.core --script ${srcdir}/fixes.lisp
  mv sbcl-new.core ${pkgdir}/usr/lib/sbcl/sbcl.core

  mkdir -p ${pkgdir}/usr/share/sbcl-source
  cp -R -t ${pkgdir}/usr/share/sbcl-source ${srcdir}/${pkgname}-${pkgver}/{src,contrib}

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

  find ${pkgdir} \( -name Makefile -o -name .cvsignore \) -delete
  find ${pkgdir}/usr/share/sbcl-source -type f \
    \( -name \*.fasl -o -name \*.o -o -name \*.log -o -name \*.so -o -name a.out \) -delete

  rm ${pkgdir}/usr/share/sbcl-source/src/runtime/sbcl{,.nm}

}
