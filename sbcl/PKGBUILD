
# rebuild maxima on updates
pkgname=sbcl
pkgver=2.0.0
pkgrel=1
pkgdesc="Steel Bank Common Lisp"
arch=('x86_64')
url="http://www.sbcl.org/"
license=('custom')
depends=('glibc' 'zlib')
makedepends=('sbcl')
provides=('common-lisp' 'cl-asdf')
source=("https://downloads.sourceforge.net/project/sbcl/sbcl/${pkgver}/${pkgname}-${pkgver}-source.tar.bz2"
        'fixes.lisp')
md5sums=('4caef839afb1c98724ffcd5197c70984'
         '7ac0c1936547f4278198b8bf7725204d')

build() {
  cd ${pkgname}-${pkgver}

  export CFLAGS+=" -D_GNU_SOURCE -fno-omit-frame-pointer -DSBCL_HOME=/usr/lib/sbcl"
  export GNUMAKE="make"

  export LINKFLAGS="$LDFLAGS"
  unset LDFLAGS
  unset MAKEFLAGS

  cat >customize-target-features.lisp <<EOF
(lambda (features)
  (flet ((enable (x) (pushnew x features))
         (disable (x) (setf features (remove x features))))
  (enable :sb-thread)
  (enable :sb-core-compression)
  (disable :largefile)))
EOF

  sh make.sh sbcl --prefix=/usr
  make -C doc/manual info
}

package() {
  cd ${pkgname}-${pkgver}
  SBCL_HOME="" INSTALL_ROOT=$pkgdir/usr sh install.sh

  src/runtime/sbcl --core output/sbcl.core --script ${srcdir}/fixes.lisp
  mv sbcl-new.core ${pkgdir}/usr/lib/sbcl/sbcl.core

  mkdir -p ${pkgdir}/usr/share/sbcl-source
  cp -R -t ${pkgdir}/usr/share/sbcl-source ${srcdir}/${pkgname}-${pkgver}/{src,contrib}

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

  find ${pkgdir} \( -name Makefile -o -name .cvsignore \) -delete
  find ${pkgdir}/usr/share/sbcl-source -type f \
    \( -name \*.fasl -o -name \*.o -o -name \*.log -o -name \*.so -o -name a.out \) -delete

  rm ${pkgdir}/usr/share/sbcl-source/src/runtime/sbcl{,.nm}

}
