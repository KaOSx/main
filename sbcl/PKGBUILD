
# rebuild maxima on updates
pkgname=sbcl
pkgver=2.2.7
pkgrel=2
pkgdesc="Steel Bank Common Lisp"
arch=('x86_64')
url="http://www.sbcl.org/"
license=('custom')
depends=('glibc' 'zlib')
makedepends=('sbcl')
provides=('common-lisp' 'cl-asdf')
source=("https://downloads.sourceforge.net/project/sbcl/sbcl/${pkgver}/${pkgname}-${pkgver}-source.tar.bz2"
        'fixes.lisp'
        "https://github.com/sbcl/sbcl/commit/48d686f9718f98122547a9006c871cfcd50439ab.diff")
md5sums=('1e4edd5e0d283b53e7298db57ab65be0'
         '7ac0c1936547f4278198b8bf7725204d'
         '5006310c88ced32ac722c4b91a2dd640')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/48d686f9718f98122547a9006c871cfcd50439ab.diff

  sh make.sh sbcl --prefix=/usr --fancy
}

package() {
  cd ${pkgname}-${pkgver}
  SBCL_HOME="" INSTALL_ROOT=$pkgdir/usr sh install.sh

  src/runtime/sbcl --core output/sbcl.core --script ${srcdir}/fixes.lisp
  mv sbcl-new.core ${pkgdir}/usr/lib/sbcl/sbcl.core

  mkdir -p ${pkgdir}/usr/share/sbcl-source
  cp -R -t ${pkgdir}/usr/share/sbcl-source ${srcdir}/${pkgname}-${pkgver}/{src,contrib}

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

  find ${pkgdir} \( -name Makefile -o -name .cvsignore \) -delete
  find ${pkgdir}/usr/share/sbcl-source -type f \
    \( -name \*.fasl -o -name \*.o -o -name \*.log -o -name \*.so -o -name a.out \) -delete

  #rm ${pkgdir}/usr/share/sbcl-source/src/runtime/sbcl{,.nm}

}
