
pkgbase=nvidia-340xx-utils
pkgname=('nvidia-340xx-utils' 'nvidia-340xx-settings')
# Edit hardware-detection/hooks/hwdetect_7_graphics on version jump from 3xx.xx
pkgver=340.108
pkgrel=2
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom')
makedepends=('xorg-server' 'libxvmc')
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        'nvidia-restore-settings.desktop'
        'nvidia-drm-outputclass.conf')
md5sums=('e783e383bd4344d590ad429eb0883717'
         '9237516792da7b6df32f8c7e8a98f475'
         'cb27b0f4a78af78aa96c5aacae23256c')

create_links() {
  # create soname links
  while read -d '' _lib; do
    _soname="$(dirname "${_lib}")/$(readelf -d "${_lib}" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
    [ -e "${_soname}" ] || ln -s "$(basename "${_lib}")" "${_soname}"
    [ -e "${_soname/.[0-9]*/}" ] || ln -s "$(basename "${_soname}")" "${_soname/.[0-9]*/}"
  done < <(find "${pkgdir}" -type f -name '*.so*' -print0)
}

build() {
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
}

package_nvidia-340xx-utils() {
  pkgdesc="NVIDIA drivers utilities and libraries, 340xx legacy version."
  depends=('xorg-server' 'libxvmc')
  optdepends=('gtk2: nvidia-settings' 'pkg-config: nvidia-xconfig'
              'opencl-nvidia: OpenCL support'
	      'nvidia-settings: Gtk GUI')
  conflicts=('nvidia-utils' 'nvidia-390xx-utils')

  cd "${srcdir}/${_pkg}"

  # X driver
  install -D -m755 nvidia_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/nvidia_drv.so"
  # GLX extension module for X
  #install -D -m755 "libglx.so.${pkgver}" "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so.${pkgver}"
  #ln -s "libglx.so.${pkgver}" "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" # X doesn't find glx otherwise
  # OpenGL library
  #install -D -m755 "libGL.so.${pkgver}" "${pkgdir}/usr/lib/libGL.so.${pkgver}"
  # OpenGL core library
  install -D -m755 "libnvidia-glcore.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-glcore.so.${pkgver}"
  # VDPAU
  install -D -m755 "libvdpau_nvidia.so.${pkgver}" "${pkgdir}/usr/lib/vdpau/libvdpau_nvidia.so.${pkgver}"
  # nvidia-tls library
  install -D -m755 "tls/libnvidia-tls.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-tls.so.${pkgver}"
  install -D -m755 "libnvidia-cfg.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-cfg.so.${pkgver}"

  install -D -m755 "libnvidia-ml.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-ml.so.${pkgver}"
  # CUDA
  install -D -m755 "libcuda.so.${pkgver}" "${pkgdir}/usr/lib/libcuda.so.${pkgver}"
  install -D -m755 "libnvcuvid.so.${pkgver}" "${pkgdir}/usr/lib/libnvcuvid.so.${pkgver}"
  # nvidia-xconfig
  install -D -m755 nvidia-xconfig "${pkgdir}/usr/bin/nvidia-xconfig"
  install -D -m644 nvidia-xconfig.1.gz "${pkgdir}/usr/share/man/man1/nvidia-xconfig.1.gz"
  # nvidia-bug-report
  install -D -m755 nvidia-bug-report.sh "${pkgdir}/usr/bin/nvidia-bug-report.sh"
  # nvidia-smi
  install -D -m755 nvidia-smi "${pkgdir}/usr/bin/nvidia-smi"
  install -D -m644 nvidia-smi.1.gz "${pkgdir}/usr/share/man/man1/nvidia-smi.1.gz"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nvidia/LICENSE"
  ln -s nvidia "${pkgdir}/usr/share/licenses/nvidia-utils"
  install -D -m644 README.txt "${pkgdir}/usr/share/doc/nvidia/README"
  install -D -m644 NVIDIA_Changelog "${pkgdir}/usr/share/doc/nvidia/NVIDIA_Changelog"
  ln -s nvidia "${pkgdir}/usr/share/doc/nvidia-utils"

  # conf
  install -D -m644 ${srcdir}/nvidia-drm-outputclass.conf ${pkgdir}/etc/X11/xorg.conf.d/nvidia-drm-outputclass.conf

  create_links
}

package_nvidia-340xx-settings() {
  pkgdesc="Gtk Graphical User Interface for nVidia, , 340xx legacy version"
  depends=('nvidia-340xx-utils' 'gtk2')
  conflicts=('nvidia-settings' 'nvidia-304xx-settings')
  replaces=('nvidia-304xx-settings')

  cd "${srcdir}/${_pkg}"


  install -D -m755 nvidia-settings "${pkgdir}/usr/bin/nvidia-settings"
  install -D -m644 nvidia-settings.1.gz "${pkgdir}/usr/share/man/man1/nvidia-settings.1.gz"
  install -D -m644 nvidia-settings.desktop "${pkgdir}/usr/share/applications/nvidia-settings.desktop"
  install -D -m644 nvidia-settings.png "${pkgdir}/usr/share/pixmaps/nvidia-settings.png"
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i "${pkgdir}/usr/share/applications/nvidia-settings.desktop"

  # Install the autostart .desktop file, so that parameters saved through nvidia-settings
  # are automatically reloaded at each login
  install -D -m644 "${srcdir}/nvidia-restore-settings.desktop" "${pkgdir}/etc/xdg/autostart/nvidia-restore-settings.desktop"
}

