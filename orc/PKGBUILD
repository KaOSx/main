
pkgname=orc
pkgver=0.4.26
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="http://cgit.freedesktop.org/gstreamer/orc/"
depends=('glibc')
makedepends=('valgrind')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
md5sums=('8e9bef677bae289d3324d81c337a4507')

build () {
    cd ${pkgname}-${pkgver}
    
    ./configure --prefix=/usr --disable-static
    make
}

check() {
    cd ${pkgname}-${pkgver}
    
    make check
}
    
package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    
    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
