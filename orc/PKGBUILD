
pkgname=orc
pkgver=0.4.31
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="https://gitlab.freedesktop.org/gstreamer/orc"
depends=('glibc')
makedepends=('valgrind' 'meson' 'ninja' 'gtk-doc')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a0ab5f10a6a9ae7c3a6b4218246564c3bf00d657cbdf587e6d34ec3ef0616075')

build () {
  mkdir -p build
  cd build
  
  export LANG=en_US.UTF-8
    
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dorc-backend=all
  ninja
}

check() {
  cd build
  
  meson test
}
    
package() {
  cd build
  DESTDIR=${pkgdir} ninja install
    
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
