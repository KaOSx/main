
pkgname=orc
pkgver=0.4.29
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="https://cgit.freedesktop.org/gstreamer/orc/"
depends=('glibc')
makedepends=('valgrind' 'meson' 'ninja' 'gtk-doc')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4f8901f9144b5ec17dffdb33548b5f4c7f8049b0d1023be3462cdd64ec5a3ab2')

build () {
  mkdir -p build
  cd build
  
  export LANG=en_US.UTF-8
    
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Denable-backend=all
  ninja
}

check() {
  cd build
  
  meson test
}
    
package() {
  cd build
  DESTDIR=${pkgdir} ninja install
    
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
