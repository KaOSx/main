
pkgname=orc
pkgver=0.4.38
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="https://gitlab.freedesktop.org/gstreamer/orc"
depends=('glibc')
makedepends=('valgrind' 'meson' 'ninja' 'gtk-doc')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a55a98d4772567aa3faed8fb84d540c3db77eaba16d3e2e10b044fbc9228668d')

build () {
  mkdir -p build
  cd build

  #export LANG=en_US.UTF-8

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dorc-backend=all
  ninja
}

check() {
  cd build

  meson test
}
    
package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
