
pkgname=orc
pkgver=0.4.36
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="https://gitlab.freedesktop.org/gstreamer/orc"
depends=('glibc')
makedepends=('valgrind' 'meson' 'ninja' 'gtk-doc')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
sha256sums=('83b074cb67317d58bef1e8d0cc862f7ae8a77a45bbff056a1f987c6488b2f5fd')

build () {
  mkdir -p build
  cd build

  #export LANG=en_US.UTF-8

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dorc-backend=all
  ninja
}

check() {
  cd build

  meson test
}
    
package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
