
pkgname=orc
pkgver=0.4.30
pkgrel=1
pkgdesc="The Optimized Inner Loops Runtime Compiler"
arch=('x86_64')
license=('custom')
url="https://gitlab.freedesktop.org/gstreamer/orc"
depends=('glibc')
makedepends=('valgrind' 'meson' 'ninja' 'gtk-doc')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/orc/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ba41b92146a5691cd102eb79c026757d39e9d3b81a65810d2946a1786a1c4972')

build () {
  mkdir -p build
  cd build
  
  export LANG=en_US.UTF-8
    
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Denable-backend=all
  ninja
}

check() {
  cd build
  
  meson test
}
    
package() {
  cd build
  DESTDIR=${pkgdir} ninja install
    
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
