
pkgname=tidyhtml
_pkgname=tidy-html5
pkgver=5.6.0
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('x86_64')
url="https://www.html-tidy.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake' 'git')
options=('!libtool')
source=("https://github.com/htacg/tidy-html5/archive/${pkgver}.tar.gz")
md5sums=('85c8a163d9ece6a02fe12bc9bddbc455')

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr 
  make 
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${_pkgname}-${pkgver}/README/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
}
