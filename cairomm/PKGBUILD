
pkgname=cairomm
pkgver=1.14.5
pkgrel=1
pkgdesc="C++ bindings to Cairo vector graphics library"
arch=('x86_64')
url="https://www.cairographics.org/cairomm/"
license=('LGPL' 'MPL')
depends=('cairo' 'libsigc++')
makedepends=('meson' 'mm-common' 'ninja')
source=("https://www.cairographics.org/releases/${pkgname}-${pkgver}.tar.xz")
options=('!libtool' '!emptydirs')
sha1sums=('c33938b021046619d86cd2631f4c79dfd0f5bdac')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D build-examples=false \
    -D build-tests=false \
    -D build-deprecated-api=true \
    -D build-documentation=false # needs 'dot'
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
