
pkgname=cairomm
pkgver=1.14.4
pkgrel=2
pkgdesc="C++ bindings to Cairo vector graphics library"
arch=('x86_64')
url="https://www.cairographics.org/cairomm/"
license=('LGPL' 'MPL')
depends=('cairo' 'libsigc++')
makedepends=('meson' 'mm-common' 'ninja')
source=("https://www.cairographics.org/releases/${pkgname}-${pkgver}.tar.xz")
options=('!libtool' '!emptydirs')
sha1sums=('a58419bb9792a2c998631704ce5671c38e0c82a1')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D build-examples=false \
    -D build-tests=false \
    -D build-deprecated-api=true \
    -D build-documentation=false # needs 'dot'
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
