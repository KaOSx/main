#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org>

_kernver="3.0.62-1-lts"

pkgname=catalyst-lts
pkgver=13.1
pkgrel=2
_amdver=9.01
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only"
arch=('x86_64')
url="http://www.ati.amd.com"
screenshot="http://linuxtation3.files.wordpress.com/2007/03/pantallazo.jpg"
license=('custom')
depends=('linux-lts>=3.0' 'linux-lts<3.3' 'catalyst-utils')
makedepends=('gcc-libs' 'gcc>4.0.0' 'make' 'linux-lts-headers')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd')
install=catalyst.install

source=(http://www2.ati.com/drivers/linux/amd-driver-installer-catalyst-${pkgver}-linux-x86.x86_64.zip
        ati_make.sh
        makefile_compat.patch)

md5sums=('b5d9de9b000448dcc2a9eb74a889116d'
         '660396540b0ceaff71d6155c986734de'
         '3e1b82bd69774ea808da69c983d6a43b')

build() {

    msg "Setting compilation variables ..."
    # include ati_make.sh and use _ati_check function from it
    . ati_make.sh
    _ati_check


    msg "Unpacking archive ..."
    /bin/sh ./amd-driver-installer-catalyst-${pkgver}-linux-x86.x86_64.run --extract archive_files

	BUILDARCH=x86_64
	_archdir=x86_64

    cd "${srcdir}/archive_files/"


    msg "Applying patch(es) ..."
    # makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
    # works only in combination with ati_make.sh script
    patch -p1 -i ../makefile_compat.patch || return 1

    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
    cp "${srcdir}/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" . || return 1
    cp 2.6.x/Makefile . || return 1


    msg "Compiling fglrx module ..."
    # Compile module using _ati_check variables
    make -C /lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
    MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
    PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules || return 1
}

package() {

    # here we are checking kernel's extramodules dir
    for r in /lib/modules/*; do
      s=${r:13}
      if [[ ${s:0:3} = "ext" ]]; then
	if [[ `cat ${r}/version | grep -c ${_kernver}` != 0 ]]; then
	  _destidir=${s}
	  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${s}/" $startdir/catalyst.install
	fi
	elif [[ ${s} = ${_kernver} ]] && [[ ! -e ${r}/extramodules ]]; then
	  _destidir=${_kernver}/video
	  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=null/" $startdir/catalyst.install
      fi
    done


    cd "${srcdir}/archive_files/common/lib/modules/fglrx/build_mod"
    install -m755 -d "${pkgdir}/lib/modules/${_destidir}/" || return 1
    install -m644 fglrx.ko "${pkgdir}/lib/modules/${_destidir}/" || return 1
    find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/catalyst.install

    # License
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

    sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
}
