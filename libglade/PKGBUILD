
pkgname=libglade
pkgver=2.6.4
_pkgver=2.6
pkgrel=9
pkgdesc="Allows you to load glade interface files in a program at runtime"
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
depends=('gtk2' 'libxml2')
makedepends=('python2' 'pkgconfig')
optdepends=('python2: libglade-convert script')
options=('!libtool')
install=glade.install
source=("https://ftp.gnome.org/pub/gnome/sources/libglade/${_pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'libglade-2.0.1-nowarning.patch')
sha256sums=('64361e7647839d36ed8336d992fd210d3e8139882269bed47dc4674980165dec'
            '423c12af1c73442caa851a0b8db33b00fa4b778b1b422a4e8ac33d121d043008')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/libglade-2.0.1-nowarning.patch 
  
  ./configure --sysconfdir=/etc \
    --prefix=/usr \
    --localstatedir=/var
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install 
  install -m755 libglade-convert ${pkgdir}/usr/bin/ 
}
