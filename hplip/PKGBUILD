
pkgname=hplip
pkgver=3.16.7
# incase a or b is used in hplip rls:
_pkgver=3.16.7
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python2' 'ghostscript' 'foomatic-db' 'foomatic-db-engine' 'cups'
         'net-snmp' 'libusb' 'wget' 'python3-gobject3' 'pyqt5-python3')
makedepends=('pkgconfig' 'sane' 'rpcbind' 'dbus-python3')
optdepends=('dbus-python2: for dbus support'
            'sane: for scanner support'
            'rpcbind: for network support')
options=('!libtool')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/hplip/${pkgname}-${_pkgver}.tar.gz")
md5sums=('d4982fd71549f45856b5ad76e1ae8809')

build() {
  cd ${pkgname}-${_pkgver}
  sed -i '1s|#!/usr/bin/env python$|&3|' *.py
  sed -i '1s|#!/usr/bin/env python$|&3|' */*.py
  sed -i '1s|#!/usr/bin/env python$|&3|' */*/*.py

  # from Gentoo
  # Use system foomatic-rip for hpijs driver instead of foomatic-rip-hplip
  # move to cups-filters version asap http://hplipopensource.com/hplip-web/install/manual/distros/lfs.html
  local i
  for i in ppd/hpijs/*.ppd.gz ; do
    rm -f ${i}.temp
    gunzip -c ${i} | sed 's/foomatic-rip-hplip/foomatic-rip/g' | \
    gzip > ${i}.temp || return 1
    mv ${i}.temp ${i}
  done

  export AUTOMAKE='automake --foreign'
  export PYTHON=/usr/bin/python3
  autoreconf --force --install

	./configure --prefix=/usr \
             --disable-qt4 \
             --enable-qt5 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build 
  make 
}

package() {
  cd ${pkgname}-${_pkgver}

  make rulesdir=/usr/lib/udev/rules.d DESTDIR=${pkgdir}/ install

  # remove sane conflicts and obsolete files
  rm -rf ${pkgdir}/etc/{sane.d,xdg}
  rm -vrf ${pkgdir}/usr/share/hal
  rm -vrf $pkgdir/etc/init.d
}
