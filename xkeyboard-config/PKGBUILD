
pkgname=xkeyboard-config
pkgver=2.39
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=('x86_64')
license=('custom')
url="https://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkb-utils')
makedepends=('intltool' 'python3' 'meson' 'ninja')
source=("https://www.x.org/releases/individual/data/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/xkeyboard-config/xkeyboard-config/commit/e957016003d5342bcaec97d43ffbc49600726660.diff")
sha256sums=('5ac5f533eff7b0c116805fe254fd79b2c9882700a4f9f2c070f8c4eae5aaa682'
            '46d050878041c0522d0c19a85f19242bfcba45c57b4304a008b650be19727594')

prepare() {
  cd ${pkgname}-${pkgver}
  # https://github.com/calamares/calamares/issues/2078
  patch -p1 -i ${srcdir}/e957016003d5342bcaec97d43ffbc49600726660.diff
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dxkb-base=/usr/share/X11/xkb \
    -Dxorg-rules-symlinks=true \
    -Dcompat-rules=true
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/var/lib/xkb
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}


