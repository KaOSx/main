
pkgname=sox
pkgver=14.4.2
pkgrel=4
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('x86_64')
url="http://sox.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm' 'pulseaudio' 
         'libao' 'libmad' 'libid3tag' 'wavpack')
options=('!libtool')
source=("https://downloads.sourceforge.net/sox/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ba804bb1ce5c71dd484a102a5b27d0dd')

build() {
  cd ${pkgname}-${pkgver}
  sed -i 's/(sox_.*|lsx_(check_read_params|/(sox_.*|lsx_(error|flush|check_read_params|/' src/Makefile.am
  
  aclocal
  automake
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dyn-default \
    --with-distro="KaOS" 
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
