
pkgname=libsecret
pkgver=0.20.1
_pkgver=0.20
pkgrel=1
pkgdesc='Library for storing and retrieving passwords and other secrets.'
arch=('x86_64')
license=('LGPL')
url="https://wiki.gnome.org/Projects/Libsecret"
depends=('glib2' 'libgcrypt')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'vala' 'meson' 'ninja' 'valgrind')
source=("https://download.gnome.org/sources/libsecret/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('57f73e94ec6263a17a077fb809cf8cf424637a897a7f15b4eec42ce4aef52447')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    -Dgtk_doc=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
