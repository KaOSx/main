
pkgname=cups-pdf
pkgver=3.0.1
pkgrel=2
pkgdesc="PDF printer for cups"
url="https://www.cups-pdf.de/"
arch=('x86_64')
depends=('cups' 'ghostscript')
install=cups-pdf.install
license=('GPL2')
backup=('etc/cups/cups-pdf.conf')
source=("https://www.cups-pdf.de/src/cups-pdf_${pkgver}.tar.gz")
md5sums=('5071bf192b9c6eb5ada4337b6917b939')

build() {
  cd ${pkgname}-${pkgver}/src
  [ -z "$CC" ] && CC=gcc
  $CC $CFLAGS -O9 -s cups-pdf.c -o cups-pdf -lcups
}

package() {
  cd ${pkgname}-${pkgver}/src
  install -D -m700 cups-pdf ${pkgdir}/usr/lib/cups/backend/cups-pdf
  install -D -m 644 ../README ${pkgdir}/usr/share/doc/${pkgname}/README

  cd ../extra
  install -D -m644 CUPS-PDF_opt.ppd ${pkgdir}/usr/share/cups/model/CUPS-PDF_opt.ppd
  install -D -m644 CUPS-PDF_noopt.ppd ${pkgdir}/usr/share/cups/model/CUPS-PDF_noopt.ppd
  install -D -m644 cups-pdf.conf ${pkgdir}/etc/cups/cups-pdf.conf
}
