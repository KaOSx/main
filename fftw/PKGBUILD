
pkgname=fftw
pkgver=3.3.8
_pkgver=3.3.8
pkgrel=2
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('x86_64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('glibc' 'bash')
options=('!libtool')
source=("http://www.fftw.org/${pkgname}-${_pkgver}.tar.gz")
md5sums=('8aac833c943d8e90d51b697b27d4384d')

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision

build() {
  cd ${srcdir}
  
  cp -a ${pkgname}-${_pkgver} ${pkgname}-${pkgver}-double
  cp -a ${pkgname}-${_pkgver} ${pkgname}-${pkgver}-long-double
  mv ${pkgname}-${_pkgver} ${pkgname}-${pkgver}-single
  
  CFLAGS+=" -O3 -fomit-frame-pointer -malign-double -fstrict-aliasing -ffast-math"

  CONFIGURE="./configure F77=gfortran --prefix=/usr --enable-shared --enable-threads"

  # build double precision
  cd ${srcdir}/${pkgname}-${pkgver}-double
  $CONFIGURE --enable-sse2
  make

  # build & install long double precission
  cd ${srcdir}/${pkgname}-${pkgver}-long-double
  $CONFIGURE --enable-long-double
  make

  # build & install single precision
  cd ${srcdir}/${pkgname}-${pkgver}-single
  $CONFIGURE --enable-float --enable-sse
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}-double
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}-long-double
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}-single
  make DESTDIR=${pkgdir} install  
}
