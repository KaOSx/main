
pkgname=media-driver
_pkgname=intel-media
pkgver=23.1.1
pkgrel=1
pkgdesc="Intel Media Driver is a VA-API user mode driver for GEN based graphics hardware."
arch=('x86_64')
url="https://github.com/intel/media-driver/"
license=('MIT' 'BSD-3')
depends=('intel-gmmlib' 'libdrm' 'libpciaccess' 'libva' 'python3')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/intel/media-driver/archive/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('3a3ab4e76afadf137b1498b32e1462c39028044e')

build() {
  cmake -B build -S ${pkgname}-${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DMEDIA_RUN_TEST_SUITE=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -m644 -D ${pkgname}-${_pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

