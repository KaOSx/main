
pkgname=media-driver
_pkgname=intel-media
pkgver=24.3.0
pkgrel=1
pkgdesc="Intel Media Driver is a VA-API user mode driver for GEN based graphics hardware."
arch=('x86_64')
url="https://github.com/intel/media-driver/"
license=('MIT' 'BSD-3')
depends=('intel-gmmlib' 'libdrm' 'libpciaccess' 'libva' 'python3')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/intel/media-driver/archive/${_pkgname}-${pkgver}.tar.gz"
        "https://github.com/intel/media-driver/commit/47bae3c809c9f5b11793b76b5edfd7704adb4080.diff")
sha1sums=('ff748c438131209c2c0eeff0486632d2ba026260'
          'b2095f5f27b20ed98d7bf25c00d23af7e718203f')

prepare() {
  cd ${pkgname}-${_pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/47bae3c809c9f5b11793b76b5edfd7704adb4080.diff
}

build() {
  cmake -B build -S ${pkgname}-${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DMEDIA_RUN_TEST_SUITE=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -m644 -D ${pkgname}-${_pkgname}-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

