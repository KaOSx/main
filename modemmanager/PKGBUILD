
pkgname=modemmanager
_pkgname=ModemManager
pkgver=1.20.2
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
depends=('dbus-glib' 'systemd' 'libgudev' 'ppp' 'polkit' 'hicolor-icon-theme' 'libqmi' 'libmbim')
makedepends=('meson' 'ninja' 'python3')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
source=("https://gitlab.freedesktop.org/mobile-broadband/${_pkgname}/-/archive/${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('a9657cda4eb6d04214ba2894b1fcbf5c03d43a769dfbd38e213f814c56301efa')

build() {
  mkdir -p build
  cd build

  meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dudevdir=/usr/lib/udev \
    -Dpolkit=strict \
    -Dintrospection=false \
    -Dgtk_doc=false \
    -Dqrtr=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
