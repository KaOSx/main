
pkgname=modemmanager
_pkgname=ModemManager
pkgver=1.14.2
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
depends=('dbus-glib' 'systemd' 'libgudev' 'ppp' 'polkit' 'hicolor-icon-theme' 'libqmi' 'libmbim')
makedepends=('python3')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
source=("https://www.freedesktop.org/software/ModemManager/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('5fb5553aecd6eb9d6d8ecd130a24f3461e5f93c5f91a0e4ae0508b5228e8b0be')

build() {
  cd ${_pkgname}-${pkgver}
  
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
	--disable-gtk-doc \
	--with-polkit=strict \
	--with-systemd-journal \
	--enable-introspection=no \
	--enable-all-plugins
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
