
pkgname=modemmanager
_pkgname=ModemManager
pkgver=1.6.6
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
depends=('dbus-glib' 'systemd' 'libgudev' 'ppp' 'polkit' 'hicolor-icon-theme' 'libqmi' 'libmbim')
makedepends=('intltool' 'python3')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
source=("https://www.freedesktop.org/software/ModemManager/${_pkgname}-${pkgver}.tar.xz")
md5sums=('99a46bd34ce65f8c18bd48994982922e')

build() {
  cd ${_pkgname}-${pkgver}
  
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
	--disable-gtk-doc \
	--with-polkit=strict \
	--with-suspend-resume=systemd
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
