
pkgname=modemmanager
_pkgname=ModemManager
pkgver=1.4.14
pkgrel=2
pkgdesc="Mobile broadband modem management service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=('GPL2' 'LGPL2.1')
depends=('dbus-glib' 'systemd' 'libgudev' 'ppp' 'polkit' 'hicolor-icon-theme' 'libqmi' 'libmbim')
makedepends=('intltool' 'python2')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
source=("http://www.freedesktop.org/software/ModemManager/${_pkgname}-${pkgver}.tar.xz")
md5sums=('cabb72e7c2ddf6af96eca2c9f3d168a3')

build() {
  cd ${_pkgname}-$pkgver
  
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static \
	--disable-gtk-doc \
	--with-polkit=strict \
	--with-newest-qmi-commands  #not clear what the needed deps are to enable
  make
}

package() {
  cd ${_pkgname}-$pkgver
  
  make DESTDIR="$pkgdir" install
}
