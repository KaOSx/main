
pkgname=pyside2-tools
_pkgname=pyside-setup-opensource-src
pkgver=5.15.2
pkgrel=1
pkgdesc="Plugin (front-end) for Generator Runner. It generates bindings for C++ libraries using CPython source code"
arch=('x86_64')
license=('LGPL')
url="https://wiki.qt.io/Qt_for_Python"
depends=('pyside2')
makedepends=('cmake')
#source=("git://code.qt.io/pyside/pyside-setup.git")
source=("https://download.qt.io/official_releases/QtForPython/${pkgname}/PySide2-${pkgver}-src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('e9bb6b57d39eb6cf1720cd3589a8b76a')

prepare() {
  cd ${_pkgname}-${pkgver}/sources/pyside2-tools

  sed -i -e 's|uic${EXE_EXT}|uic-qt5${EXE_EXT}|' CMakeLists.txt
  sed -i -e 's|rcc${EXE_EXT}|rcc-qt5${EXE_EXT}|' CMakeLists.txt
}

build() {
  cmake -B build -S ${_pkgname}-${pkgver}/sources/pyside2-tools \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  # part of qt5-base
  rm ${pkgdir}/usr/bin/rcc-qt5
  rm ${pkgdir}/usr/bin/uic-qt5
}

