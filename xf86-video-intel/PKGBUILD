
pkgname=xf86-video-intel
pkgver=2.99.917.34
pkgrel=1
_commit=f66d39544bb8339130c96d282a80f87ca1606caf
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/driver/xf86-video-intel"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd'
         'libxrandr' 'libxinerama' 'libxcursor' 'libxtst' 'pixman' 'libxss')
makedepends=('xorg-server' 'libx11' 'libxrender' 'xorgproto')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
#source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
# check at https://cgit.freedesktop.org/xorg/driver/xf86-video-intel/
source=("git://anongit.freedesktop.org/git/xorg/driver/xf86-video-intel.git#commit=${_commit}")
md5sums=('SKIP')

build() {
  cd ${pkgname}

  ./autogen.sh
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --with-default-dri=3 \
              --enable-sna \
              --enable-uxa \
              --with-default-accel=sna
  make
}

package() {
  cd ${pkgname}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
