
pkgname=xf86-video-intel
pkgver=2.99.917
pkgrel=6
_commit=57725564179b8ddb48d5c9437fde91a6c02c0740
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd'
         'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
makedepends=('xorg-server>=1.17.2' 'libx11' 'xf86driproto' 'glproto' 'libxrender'
             'resourceproto' 'scrnsaverproto')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
#source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
source=("git://anongit.freedesktop.org/git/xorg/driver/xf86-video-intel.git#commit=${_commit}")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"

  ./autogen.sh
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --enable-dri \
              --enable-sna \
              --enable-uxa \
              --with-default-accel=sna
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
