
pkgname=xf86-video-intel
pkgver=2.99.917.37
pkgrel=1
_commit=ad5540f6ecaec287c70259f0181e613561b716f6
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/driver/xf86-video-intel"
license=('custom')
depends=('mesa' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'systemd'
         'libxrandr' 'libxinerama' 'libxcursor' 'libxtst' 'pixman' 'libxss')
makedepends=('xorg-server' 'libx11' 'libxrender' 'xorgproto')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
install=xf86-video-intel.install
#source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
# check at https://cgit.freedesktop.org/xorg/driver/xf86-video-intel/
source=("https://gitlab.freedesktop.org/xorg/driver/xf86-video-intel/-/archive/${_commit}/xf86-video-intel-${_commit}.tar.bz2")
md5sums=('df6fba8783279460a8b4f5a27f3c60fc')

build() {
  cd ${pkgname}-${_commit}

  ./autogen.sh
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-default-dri=3 \
    --enable-sna \
    --enable-uxa \
    --with-default-accel=sna
  make
}

package() {
  cd ${pkgname}-${_commit}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
