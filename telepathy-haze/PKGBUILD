
pkgname=telepathy-haze
pkgver=0.8.0
pkgrel=3
pkgdesc="A telepathy-backend to use libpurple (Pidgin) protocols."
arch=('x86_64')
url="http://telepathy.freedesktop.org"
license=('GPL')
depends=('telepathy-glib' 'libpurple')
makedepends=('libxslt' 'python2')
install=telepathy-haze.install
groups=('kde-telepathy')
conflicts=('papyon' 'telepathy-butterfly')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'contact-list.patch')
md5sums=('b9ee3638833fb50db6276d1b771820b0'
         '41c0c7b49ba3afd0e948513e2bcc677b')

build() {
  cd ${pkgname}-${pkgver}
  # https://bugs.freedesktop.org/show_bug.cgi?id=47005
  patch -p1 -i ${srcdir}/contact-list.patch
  # fix compatibility with libpurple 2.10.12 & up
  sed -i 's|PURPLE_MEDIA_NETWORK_PROTOCOL_TCP;|PURPLE_MEDIA_NETWORK_PROTOCOL_TCP_PASSIVE;|' src/media-stream.c
  
  ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
