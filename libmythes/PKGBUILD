
pkgname=libmythes
_pkgname=mythes
pkgver=1.2.4
pkgrel=6
pkgdesc="A simple thesaurus"
arch=('x86_64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('glibc' 'perl')
makedepends=('hunspell')
options=('!libtool')
source=("https://downloads.sourceforge.net/hunspell/${_pkgname}-${pkgver}.tar.gz")
md5sums=('a8c2c5b8f09e7ede322d5c602ff6a4b6')

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make

  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR=$pkgdir install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
