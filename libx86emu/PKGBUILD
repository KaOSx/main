
pkgname=libx86emu
pkgver=3.3
_pkgver=3
pkgrel=1
pkgdesc="x86 emulation library"
arch=('x86_64')
url="https://github.com/wfeldt/libx86emu"
license=('BSD')
depends=('glibc')
options=('!emptydirs')
#source=("https://download.opensuse.org/source/tumbleweed/repo/oss/src/${pkgname}-${_pkgver}.src.rpm")
source=("https://github.com/wfeldt/libx86emu/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('fc043d1229b2fc8215e7fce972640baba86b42a0560069a737a10d013f2d8f02')

build() {
  cd ${pkgname}-${pkgver}

  make VERSION=${pkgver} MAJOR_VERSION=${_pkgver} GIT2LOG= LIBDIR=/usr/lib
}

package() {
  cd ${pkgname}-${pkgver}
  make VERSION=${pkgver} MAJOR_VERSION=${_pkgver} GIT2LOG= LIBDIR=/usr/lib DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

