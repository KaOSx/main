
pkgname=ufw
pkgver=0.36
pkgrel=5
pkgdesc="Uncomplicated and easy to use CLI tool for managing a netfilter firewall"
arch=('x86_64')
url="https://launchpad.net/ufw"
license=('GPL3')
depends=('iptables' 'python3')
backup=('etc/ufw/before.rules'
        'etc/ufw/before6.rules'
        'etc/ufw/after.rules'
        'etc/ufw/after6.rules'
        'etc/ufw/ufw.conf'
        'etc/ufw/user.rules'
        'etc/ufw/sysctl.conf'
        'etc/default/ufw'
        'usr/lib/ufw/user.rules'
        'usr/lib/ufw/user6.rules')
#install=ufw.install
categories=('network')
source=("https://launchpad.net/ufw/${pkgver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.service")
md5sums=('6d8ab1506da21ae003f4628f93d05781'
         '2c85da635f53c6817be8c1941a9a35fb')

package() {
  cd ${pkgname}-${pkgver}

  sed -e 's|/lib|/usr/lib|' -i setup.py
  python3 setup.py install --root=${pkgdir}

  install -D -m644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service

  # Fix permissions
  chmod 644 ${pkgdir}/etc/ufw/*.rules

  # Enable by default
  #sed -e 's|ENABLED=no|ENABLED=yes|' -i ${pkgdir}/etc/ufw/ufw.conf
}
