
pkgname=mlt6
_pkgname=mlt
pkgver=7.18.0
pkgrel=1
pkgdesc="An open source multimedia framework, Qt6 version"
arch=('x86_64')
url="https://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'python3' 'jack2' 'ladspa' 'qt6-svg'
         'frei0r-plugins' 'libexif' 'opencv' 'rubberband' 'gdk-pixbuf2' 'qt6-5compat')
makedepends=('cmake' 'swig')
source=("https://github.com/mltframework/mlt/releases/download/v${pkgver}/mlt-${pkgver}.tar.gz")
md5sums=('89daf7bbce3f10fa6dfc1db5ae1d314c')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGPL=ON \
    -DGPL3=ON \
    -DBUILD_TESTING=OFF \
    -DMOD_GLAXNIMATE=ON \
    -DMOD_OPENCV=ON \
    -DSWIG_PYTHON=ON \
    -DMOD_QT=OFF \
    -DMOD_QT6=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
