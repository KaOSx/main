
pkgname=rarian
pkgver=0.8.1
pkgrel=4
pkgdesc="Documentation meta-data library, designed as a replacement for Scrollkeeper."
arch=('x86_64')
url="http://rarian.freedesktop.org/"
license=('GPL')
depends=('gcc-libs')
makedepends=('libxslt')
options=('!libtool')
source=("http://rarian.freedesktop.org/Releases/${pkgname}-${pkgver}.tar.bz2"
        'user-segfault.patch')
md5sums=('75091185e13da67a0ff4279de1757b94'
         '457e9827197009cfa360911fe49a5ad7')

build() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/user-segfault.patch
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
