
pkgname=rarian
pkgver=0.8.1
_pkgver=0.8
pkgrel=5
pkgdesc="Documentation meta-data library, designed as a replacement for Scrollkeeper."
arch=('x86_64')
url="https://rarian.freedesktop.org/"
license=('GPL')
depends=('gcc-libs')
makedepends=('libxslt')
options=('!libtool')
source=("https://download.gnome.org/sources/rarian/${_pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'user-segfault.patch')
sha256sums=('aafe886d46e467eb3414e91fa9e42955bd4b618c3e19c42c773026b205a84577'
            '518b1834d4f44e92161f0cf2f72a6474f0ec687ea53562c64109df90d343fe71')

build() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/user-segfault.patch
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
