
pkgname=mpg123
pkgver=1.27.2
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('x86_64')
url="https://www.mpg123.org/"
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'libtool' 'alsa-lib')
makedepends=('sdl' 'jack' 'pulseaudio')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support')
options=('libtool')
groups=('multimedia')
source=("https://www.mpg123.org/download/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('058acbcb4b20609ef52a843cc95f45a0cdd7286a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --enable-int-quality \
              --with-audio="alsa oss sdl jack pulse" \
              --with-module-suffix=.so
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
