
pkgname=mpg123
pkgver=1.25.8
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('x86_64')
url="http://www.mpg123.org/"
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'libtool' 'alsa-lib')
makedepends=('sdl' 'jack' 'pulseaudio')
optdepends=('sdl: for sdl audio support'
            'jack: for jack audio support')
options=('libtool')
groups=('multimedia')
source=("https://downloads.sourceforge.net/sourceforge/mpg123/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('6b6fd24ebfd09d2d378af3f656816981ae3cc15a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --enable-int-quality \
              --with-audio="alsa oss sdl jack pulse" \
              --with-module-suffix=.so
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
