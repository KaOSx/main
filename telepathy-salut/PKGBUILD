
pkgname=telepathy-salut
pkgver=0.8.1
pkgrel=3
pkgdesc="A link-local XMPP connection manager."
arch=('x86_64')
url="http://telepathy.freedesktop.org/"
license=('GPL')
depends=('telepathy-glib' 'avahi' 'libsoup' 'sqlite3')
makedepends=('libxslt' 'python2' 'xmldiff')
install=telepathy-salut.install
source=("http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('7516e6f6fa56a61054413a03642b938d')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy \
        --disable-plugins --disable-Werror \
        --with-tls=openssl \
        --enable-avahi-tests=no \
        --enable-static=no

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    make DESTDIR="${pkgdir}" install
}

