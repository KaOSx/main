
pkgname=avahi
pkgver=0.7
_pkgver=0.7
pkgrel=1
pkgdesc="A multicast/unicast DNS-SD framework"
arch=('x86_64')
url="http://www.avahi.org/"
license=('LGPL')
depends=('dbus' 'libcap' 'libdaemon' 'gdbm' 'glib2' 'expat')
makedepends=('dbus-python2' 'gobject-introspection' 'intltool')
backup=(etc/avahi/avahi-daemon.conf etc/avahi/services/{sftp-,}ssh.service)
install=avahi.install
options=('!libtool')
#source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz")
source=("https://github.com/lathiat/avahi/releases/download/v${_pkgver}/${pkgname}-${_pkgver}.tar.gz")
md5sums=('d76c59d0882ac6c256d70a2a585362a6')

prepare() {
  cd ${pkgname}-${_pkgver}
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}-${_pkgver}

    ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-qt3 \
    --disable-qt4 \
    --disable-mono \
    --disable-monodoc \
    --disable-doxygen-doc \
    --disable-xmltoman \
    --disable-manpages \
    --disable-gtk \
    --disable-gtk3 \
    --disable-pygobject \
    --enable-compat-libdns_sd \
    --enable-compat-howl \
    --with-distro=none \
    --with-avahi-priv-access-group=network \
    --with-autoipd-user=avahi \
    --with-autoipd-group=avahi \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make 
}

package() {
	cd ${pkgname}-${_pkgver}
	make DESTDIR=${pkgdir} install
	
	rm ${pkgdir}/var -rfv
	rm -fr ${pkgdir}/etc/rc.d

	cd ${pkgdir}
	sed -i '1c #!/usr/bin/python2' usr/bin/avahi-bookmarks
	# howl and mdnsresponder compatability
	(cd usr/include; ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h; ln -s avahi-compat-howl howl)
	(cd usr/lib/pkgconfig; ln -s avahi-compat-howl.pc howl.pc)
}

