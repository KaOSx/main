
pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.15
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="https://github.com/libofx/libofx"
license=('GPL')
depends=('opensp' 'curl' 'libxml++')
checkdepends=('gnupg')
#source=("https://downloads.sourceforge.net/libofx/${pkgbase}-${pkgver}.tar.gz")
source=("https://github.com/libofx/libofx/archive/${pkgver}.tar.gz")
md5sums=('ee4567bf8ceb1db81f81d38ff446d833' )

build() {
  cd ${pkgbase}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr \
  --disable-gengetopt
  make CXXFLAGS="-std=c++11"
}

check() {
  cd ${pkgbase}-${pkgver}
  
  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install-docDATA
  make -C doc DESTDIR=$pkgdir install
}
