
pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.10.0
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="https://github.com/libofx/libofx"
license=('GPL')
depends=('opensp' 'curl') # not ready for 2.6 > 'libxml++', ofxconnect not build)
checkdepends=('gnupg')
source=("https://downloads.sourceforge.net/libofx/${pkgbase}-${pkgver}.tar.gz")
#source=("https://github.com/libofx/libofx/archive/${pkgver}.tar.gz")
md5sums=('0e7a2f45ffb91222014f6e89d90f4a9a' )

build() {
  cd ${pkgbase}-${pkgver}

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  
  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install-docDATA
  make -C doc DESTDIR=$pkgdir install
}
