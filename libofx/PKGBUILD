
pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.10.9
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="https://github.com/libofx/libofx"
license=('GPL')
depends=('opensp' 'curl') # not ready for 2.6 > 'libxml++', ofxconnect not build)
checkdepends=('gnupg' 'doxygen')
#source=("https://downloads.sourceforge.net/libofx/${pkgbase}-${pkgver}.tar.gz")
source=("https://github.com/libofx/libofx/releases/download/${pkgver}/${pkgbase}-${pkgver}.tar.gz"
        "https://github.com/libofx/libofx/commit/aef85c5e11bfd5c5d82c4c73a44f79dd8aa8f55e.diff")
md5sums=('ce12e085838cf8ddce5f5c7e9fed872f'
         'ce6831e4c9c1204e4a4a21cdd899b878' )

build() {
  cd ${pkgbase}-${pkgver}
  #patch -p1 -i ${srcdir}/aef85c5e11bfd5c5d82c4c73a44f79dd8aa8f55e.diff

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgbase}-${pkgver}

  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd ${pkgbase}-${pkgver}
  make DESTDIR=$pkgdir install-docDATA
  make -C doc DESTDIR=$pkgdir install
}
