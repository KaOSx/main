
pkgname=cantarell-fonts
pkgver=0.303.1
_pkgver=0.303
pkgrel=2
pkgdesc="The Cantarell typeface family is a contemporary Humanist sans serif"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
license=('GPL2')
depends=('fontconfig' 'python3')
makedepends=('meson' 'ninja')
install=cantarell-fonts.install
source=("https://download.gnome.org/sources/cantarell-fonts/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
# use tar from CI/CD > Jobs > build-variable
#source=("https://gitlab.gnome.org/GNOME/cantarell-fonts/-/jobs/1515398/artifacts/raw/build/meson-dist/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f9463a0659c63e57e381fdd753cf1929225395c5b49135989424761830530411')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dbuildappstream=false \
    -Duseprebuilt=true
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
