
_pkgbase=pidgin
pkgname=libpurple
pkgver=2.14.7
pkgrel=1
pkgdesc="IM library extracted from Pidgin"
arch=('x86_64')
url="https://pidgin.im/"
license=('GPL')
depends=('libsasl' 'dbus-glib' 'nss' 'libidn' 'farstream')
makedepends=('startup-notification' 'libxss' 'python3' 'avahi'
             'ca-certificates' 'intltool')
options=('!libtool')
source=("https://downloads.sourceforge.net/pidgin/${_pkgbase}-${pkgver}.tar.bz2")
sha256sums=('fea6ab4f0572fe24646049c2b3fecbdca27abca6d06e95bd655e44db99bd69fe')

build() {
    cd ${_pkgbase}-${pkgver}

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-schemas-install \
        --disable-meanwhile \
        --disable-gnutls \
        --disable-gtkui \
        --disable-screensaver \
        --disable-sm \
        --disable-nm \
        --disable-gtkspell \
        --enable-cyrus-sasl \
        --disable-doxygen \
        --disable-tcl \
        --with-python=/usr/bin/python3 \
        --with-system-ssl-certs=/etc/ssl/certs
    make
}

package() {
    cd ${_pkgbase}-${pkgver}
    
    for dir in libpurple share/sounds share/ca-certs m4macros po
    do  make -C "${dir}" DESTDIR="${pkgdir}" install
    done
}
