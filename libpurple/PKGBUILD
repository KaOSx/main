
_pkgbase=pidgin
pkgname=libpurple
pkgver=2.14.8
pkgrel=4
pkgdesc="IM library extracted from Pidgin"
arch=('x86_64')
url="https://pidgin.im/"
license=('GPL')
depends=('libsasl' 'dbus-glib' 'nss' 'libidn' 'farstream' 'libxcrypt')
makedepends=('startup-notification' 'libxss' 'python3' 'avahi'
             'ca-certificates' 'intltool')
options=('!libtool')
source=("https://downloads.sourceforge.net/pidgin/${_pkgbase}-${pkgver}.tar.bz2")
sha256sums=('3f8085c0211c4ca1ba9f8a03889b3d60738432c1673b57b0086070ef6e094cca')

build() {
    cd ${_pkgbase}-${pkgver}

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-schemas-install \
        --disable-meanwhile \
        --disable-gnutls \
        --disable-gtkui \
        --disable-screensaver \
        --disable-sm \
        --disable-nm \
        --disable-gtkspell \
        --enable-cyrus-sasl \
        --disable-doxygen \
        --disable-tcl \
        --with-python=/usr/bin/python3 \
        --with-system-ssl-certs=/etc/ssl/certs
    make
}

package() {
    cd ${_pkgbase}-${pkgver}

    for dir in libpurple share/sounds share/ca-certs m4macros po
        do  make -C ${dir} DESTDIR=${pkgdir} install
    done
}
