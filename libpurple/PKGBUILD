
_pkgbase='pidgin'
pkgname='libpurple'
pkgver=2.10.8
pkgrel=1
pkgdesc="IM library extracted from Pidgin"
arch=('x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=('farstream' 'libsasl' 'dbus-glib' 'nss' 'cyrus-sasl-plugins' 'libidn')
makedepends=('startup-notification' 'libxss' 'python2' 'gstreamer0.10' 'avahi' 'tk'
             'ca-certificates' 'intltool')
optdepends=('dbus-python2: for purple-remote and purple-url-handler')
options=('!libtool')
source=("http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('384be5689e62a9d4d42320e4ca90433d')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    sed -i "s#env python#env python2#" */plugins/*.py
    sed -i "s#env python#env python2#" libpurple/purple-{remote,notifications-example,url-handler}
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-gnutls \
                --disable-gtkui \
                --disable-screensaver \
                --disable-sm \
                --disable-nm \
                --disable-gtkspell \
                --enable-cyrus-sasl \
                --disable-doxygen \
                --with-python=/usr/bin/python2 \
                --with-system-ssl-certs=/etc/ssl/certs
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    
    for dir in libpurple share/sounds share/ca-certs m4macros po
    do  make -C "${dir}" DESTDIR="${pkgdir}" install
    done
}