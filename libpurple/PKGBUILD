
_pkgbase=pidgin
pkgname=libpurple
pkgver=2.11.0
pkgrel=2
pkgdesc="IM library extracted from Pidgin"
arch=('x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=('libsasl' 'dbus-glib' 'nss' 'cyrus-sasl-plugins' 'libidn' 'farstream')
makedepends=('startup-notification' 'libxss' 'python2' 'avahi' 'tk'
             'ca-certificates' 'intltool')
optdepends=('dbus-python2: for purple-remote and purple-url-handler')
options=('!libtool')
source=("http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('7b167474db669aab2f71fa46835fb83f')

build() {
    cd ${_pkgbase}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --disable-schemas-install \
                --disable-meanwhile \
                --disable-gnutls \
                --disable-gtkui \
                --disable-screensaver \
                --disable-sm \
                --disable-nm \
                --disable-gtkspell \
                --enable-cyrus-sasl \
                --disable-doxygen \
                --with-python=/usr/bin/python2 \
                --with-system-ssl-certs=/etc/ssl/certs
    make
}

package() {
    cd ${_pkgbase}-${pkgver}
    
    for dir in libpurple share/sounds share/ca-certs m4macros po
    do  make -C "${dir}" DESTDIR="${pkgdir}" install
    done
}
