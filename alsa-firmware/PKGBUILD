
pkgname=alsa-firmware
pkgver=1.0.29
pkgrel=3
pkgdesc="ALSA firmware package"
arch=('x86_64')
url="https://alsa-project.org/"
license=('GPL')
options=('!strip')
source=("ftp://ftp.alsa-project.org/pub/firmware/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9a1182f8a6ac44cb9af5774cc045565f')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --with-hotplug-dir=/usr/lib/firmware
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # conflicts with linux-firmware
  rm -rf ${pkgdir}/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
  # remove broken symlinks (broken upstream)
  rm -rf ${pkgdir}/usr/lib/firmware/turtlebeach
  # remove empty dir
  rm -rf ${pkgdir}/usr/bin
}
