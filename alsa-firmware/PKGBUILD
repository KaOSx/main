
pkgname=alsa-firmware
pkgver=1.2.4
pkgrel=2
pkgdesc="ALSA firmware package"
arch=('x86_64')
url="https://alsa-project.org/"
license=('GPL')
depends=('alsa-topology-conf' 'alsa-ucm-conf')
options=('!strip')
source=("https://www.alsa-project.org/files/pub/firmware/${pkgname}-${pkgver}.tar.bz2")
#source=("https://github.com/alsa-project/alsa-firmware/archive/v${pkgver}.tar.gz")
md5sums=('ee6c1d24a1a4ac1d86992b408ed710a2')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi
  ./configure --prefix=/usr --with-hotplug-dir=/usr/lib/firmware
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # conflicts with linux-firmware
  rm -rf ${pkgdir}/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
  # remove broken symlinks (broken upstream)
  rm -rf ${pkgdir}/usr/lib/firmware/turtlebeach
  # remove empty dir
  rm -rf ${pkgdir}/usr/bin
}
