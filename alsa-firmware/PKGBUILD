
pkgname=alsa-firmware
pkgver=1.2.1
pkgrel=2
pkgdesc="ALSA firmware package"
arch=('x86_64')
url="https://alsa-project.org/"
license=('GPL')
depends=('alsa-topology-conf' 'alsa-ucm-conf')
options=('!strip')
source=("ftp://ftp.alsa-project.org/pub/firmware/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f8458efd25e6d6600dbc7aedf98f83a3')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --with-hotplug-dir=/usr/lib/firmware
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # conflicts with linux-firmware
  rm -rf ${pkgdir}/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
  # remove broken symlinks (broken upstream)
  rm -rf ${pkgdir}/usr/lib/firmware/turtlebeach
  # remove empty dir
  rm -rf ${pkgdir}/usr/bin
}
