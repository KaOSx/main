
pkgname=gstreamer-vaapi
pkgver=1.18.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Vaapi Plugin"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-bad' 'libva' 'libxrandr')
makedepends=('python3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer-vaapi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4a460fb95559f41444eb24864ad2d9e37922b6eea941510310319fc3e0ba727b')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 1 fail: elements_vaapipostproc FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
