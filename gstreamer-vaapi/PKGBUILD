
pkgname=gstreamer-vaapi
pkgver=1.22.9
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Vaapi Plugin"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-bad' 'libva' 'libxrandr')
makedepends=('python3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer-vaapi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8ba20da8c4cbf5b2953dba904672c4275d0053e1528f97fdf8e59942c7883ca8')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 1 fail: elements_vaapipostproc FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
