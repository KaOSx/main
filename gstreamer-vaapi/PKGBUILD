
pkgname=gstreamer-vaapi
pkgver=1.18.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Vaapi Plugin"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-bad' 'libva' 'libxrandr')
makedepends=('python3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer-vaapi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('400d3c42810b50b4566df03f37319a6bdd758f969560c40147e7d9a3b0e8a6ea')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 1 fail: elements_vaapipostproc FAIL
  ninja test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
