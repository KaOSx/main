
pkgname=gstreamer-vaapi
pkgver=1.24.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Vaapi Plugin"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-bad' 'libva' 'libxrandr')
makedepends=('python3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer-vaapi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('cc5ab8c88443e0f5bf23d6110b4aac33df59e4ae626f536d968b1605cc7b962f')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 1 fail: elements_vaapipostproc FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
