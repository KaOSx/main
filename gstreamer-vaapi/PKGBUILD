
pkgname=gstreamer-vaapi
pkgver=1.24.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Vaapi Plugin"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-bad' 'libva' 'libxrandr')
makedepends=('python3' 'gobject-introspection' 'meson' 'ninja')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer-vaapi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('fdb2da26b23895c56b20c6bad9edcf0faec3a3887765bc62c88f0955ed5b7c72')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 1 fail: elements_vaapipostproc FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
