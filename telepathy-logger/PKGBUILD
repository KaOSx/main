#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe cal√† <jiveaxe@gmail.com>

pkgname=telepathy-logger
pkgver=0.4.0
pkgrel=4
pkgdesc="Telepathy framework logging daemon"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/Logger"
license=('LGPL2.1')
depends=('telepathy-glib' 'sqlite3' 'libxml2' 'dconf')
makedepends=('intltool' 'gobject-introspection' 'libxslt')
install=${pkgname}.install
options=('!libtool')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0b891b860c7f3a01926f5cc22fd26120')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/telepathy \
    --disable-static \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -r $pkgdir/usr/share/gtk-doc
}

