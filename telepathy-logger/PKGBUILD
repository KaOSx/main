
pkgname=telepathy-logger
pkgver=0.8.2
pkgrel=2
pkgdesc="Telepathy framework logging daemon"
arch=('x86_64')
url="https://telepathy.freedesktop.org/wiki/Logger"
license=('LGPL2.1')
depends=('telepathy-glib' 'sqlite3' 'libxml2' 'dconf')
makedepends=('intltool' 'gobject-introspection' 'libxslt')
options=('!libtool')
source=("https://telepathy.freedesktop.org/releases/telepathy-logger/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c2f6a31c76cbdf8790500cf4f872ba71')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/telepathy \
    --disable-static \
    --disable-schemas-compile
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir}/ install
  rm -r ${pkgdir}/usr/share/gtk-doc
}

