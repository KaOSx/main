
pkgname=libplacebo
pkgver=7.349.0
pkgrel=2
pkgdesc='Core rendering algorithms and ideas of mpv rewritten as an independent library. '
arch=('x86_64')
license=('LGPL-2.1')
url='https://libplacebo.org/'
depends=('glslang' 'lcms2' 'vulkan-loader')
makedepends=('python3-jinja' 'python3-markupsafe' 'meson' 'ninja' 'vulkan-headers')
options=('!emptydirs' '!buildflags')
source=("https://github.com/haasn/libplacebo/archive/refs/tags/v${pkgver}.tar.gz"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/media-libs/libplacebo/files/libplacebo-7.349.0-glslang-1.3.296.patch")
md5sums=('8dd72edf2ec5f1918770a317ef8107a5'
         'a148ca856823001c0c45baa38d2d8e89')

prepare() {
    cd ${pkgname}-${pkgver}

    patch -p1 -i ${srcdir}/libplacebo-7.349.0-glslang-1.3.296.patch
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dshaderc=disabled \
    -Ddemos=false \
    -Dxxhash=enabled \
    -D d3d11=disabled \
    -D libdovi=disabled \
    -Dopengl=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
