
pkgname=libplacebo
pkgver=6.338.2
pkgrel=1
pkgdesc='Core rendering algorithms and ideas of mpv rewritten as an independent library. '
arch=('x86_64')
license=('LGPL-2.1')
url='https://libplacebo.org/'
depends=('glslang' 'lcms2' 'vulkan-loader')
makedepends=('python3-jinja' 'python3-markupsafe' 'meson' 'ninja' 'vulkan-headers')
options=('!emptydirs' '!buildflags')
source=("https://github.com/haasn/libplacebo/archive/refs/tags/v${pkgver}.tar.gz")
md5sums=('4f5b743ec43c684e6da0d56efed3891c')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgbase}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dshaderc=disabled \
    -Ddemos=false \
    -Dxxhash=enabled \
    -D d3d11=disabled \
    -D libdovi=disabled \
    -Dopengl=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
