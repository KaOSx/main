
pkgname=libsm
pkgver=1.2.3
pkgrel=1
pkgdesc="X11 Session Management library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libice' 'util-linux')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libSM-${pkgver}.tar.bz2")
sha256sums=('2d264499dcb05f56438dee12a1b4b71d76736ce7ba7aa6efbf15ebb113769cbb')

build() {
  cd libSM-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libSM-${pkgver}
  make DESTDIR=${pkgdir} install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
