
pkgname=python3-markupsafe
pkgver=3.0.1
pkgrel=1
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python"
arch=('x86_64')
url="https://pypi.python.org/pypi/MarkupSafe"
license=('custom')
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://pypi.io/packages/source/m/markupsafe/markupsafe-${pkgver}.tar.gz")
sha256sums=('3e683ee4f5d0fa2dde4db77ed8dd8a876686e3fc417655c2ece9a90576905344')

build() {
  cd markupsafe-${pkgver}

  python3 -m build -nw
}

package() {
  cd markupsafe-${pkgver}
  python3 -m installer --destdir=${pkgdir} dist/*.whl

  install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
