
pkgname=libical
pkgver=3.0.8
pkgrel=4
epoch=1
pkgdesc="An Open Source implementation of the iCalendar protocols and protocol data units."
arch=('x86_64')
url='https://github.com/libical/libical'
license=('LGPL' 'MPL')
depends=('glibc' 'icu')
makedepends=('cmake' 'pkg-config')
options=('!libtool')
source=("https://github.com/libical/libical/archive/v${pkgver}.tar.gz"
        "https://github.com/libical/libical/pull/448/commits/a90657f5bbc5e2eb45fa419e7f43889aeccd4c39.diff")
md5sums=('41bd1f1fcdcb4779cea478bb55cf07bf'
         'f5cea66c98b89e1d8cb0ccb230c91977')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/a90657f5bbc5e2eb45fa419e7f43889aeccd4c39.diff
}

build() {
  cd ${pkgname}-${pkgver}

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_GTK_DOC=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  
  make install DESTDIR=${pkgdir}
}
