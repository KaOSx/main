
pkgname=libquotient
pkgver=0.8.2
_commit=27ad32abbac3a6979f49d44bea57829aefca7eb4
pkgrel=1
pkgdesc="Qt6 library to write cross-platform clients for Matrix"
arch=('x86_64')
url="https://matrix.org/ecosystem/sdks/"
license=('LGPL2')
depends=('qt6-multimedia' 'olm' 'qtkeychain-qt6')
makedepends=('cmake')
conflicts=('libquotient-qt6')
replaces=('libquotient-qt6')
provides=('libquotient-qt6')
source=("https://github.com/quotient-im/libQuotient/archive/${pkgver}.tar.gz")
#source=("https://github.com/quotient-im/libQuotient/archive/${_commit}.zip")
sha256sums=('12ff2fa8b80a934b9dd88fa3416a4b88e94bc0e18a8df0dcebfc90614dd2f5c9')

build() {
    cmake -B build -S libQuotient-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON \
        -DQuotient_ENABLE_E2EE=ON \
        -DBUILD_WITH_QT6=ON
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
