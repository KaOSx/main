 
pkgname=libquotient
pkgver=0.7.0
pkgrel=1
pkgdesc="Qt5 library to write cross-platform clients for Matrix"
arch=('x86_64')
url="https://matrix.org/docs/projects/sdk/quotient"
license=('LGPL2')
depends=('qt5-multimedia' 'libqtolm' 'qtkeychain')
makedepends=('cmake')
source=("https://github.com/quotient-im/libQuotient/archive/${pkgver}.tar.gz")
sha256sums=('2e46a9889c2ce86b780f04b0b831896179af6621177f805c1a650a980e041525')

build() {
    cmake -B build -S libQuotient-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON \
        -DQuotient_ENABLE_E2EE=ON
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
