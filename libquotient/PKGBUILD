 
pkgname=libquotient
pkgver=0.6.11
pkgrel=1
pkgdesc="Qt5 library to write cross-platform clients for Matrix"
arch=('x86_64')
url="https://matrix.org/docs/projects/sdk/quotient"
license=('LGPL2')
depends=('qt5-multimedia' 'libqtolm')
makedepends=('cmake')
source=("https://github.com/quotient-im/libQuotient/archive/${pkgver}.tar.gz")
sha256sums=('12b15d1296e630477d5e8f4d32c821dc724b3c5b99d15d383417ba7d88f03c46')

build() {
    cmake -B build -S libQuotient-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}
