 
pkgname=libquotient
pkgver=0.6.4
pkgrel=1
pkgdesc="Qt5 library to write cross-platform clients for Matrix"
arch=('x86_64')
url="https://matrix.org/docs/projects/sdk/quotient"
license=('LGPL2')
depends=('qt5-multimedia' 'libqtolm')
makedepends=('cmake')
source=("https://github.com/quotient-im/libQuotient/archive/${pkgver}.tar.gz")
sha256sums=('4c43a68c144d41e3826f2e1ce006760129694b79d8d7f302dea81328c6a663ce')

build() {
    mkdir -p build
    cd build

    cmake ../libQuotient-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON

    make
}

package() {
    cd build
    
    make DESTDIR=${pkgdir} install
}
