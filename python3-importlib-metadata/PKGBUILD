
pkgname=python3-importlib-metadata
_pkgname=importlib_metadata
pkgver=7.2.1
pkgrel=1
pkgdesc="Library to access the metadata for a Python package."
arch=('x86_64')
url='https://github.com/python/importlib_metadata'
license=('BSD')
depends=('python3-zipp')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools-scm' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/i/importlib_metadata/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('509ecb2ab77071db5137c655e24ceb3eee66e7bbc6574165d0d114d9fc4bbe68')

build() {
  cd ${_pkgname}-${pkgver}

  python3 -m build -nw
}

package() {
  cd ${_pkgname}-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
