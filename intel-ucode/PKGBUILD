
pkgname=intel-ucode
pkgver=20140913
pkgrel=1
pkgdesc="Microcode update files for Intel CPUs"
arch=('x86_64')
url="http://downloadcenter.intel.com/SearchResult.aspx?lang=eng&keyword=%22microcode%22"
replaces=('microcode_ctl')
license=('custom')
source=("http://downloadmirror.intel.com/24290/eng/microcode-${pkgver}.tgz"
        'LICENSE'
        'intel-microcode2ucode.c')
md5sums=('d589dbdbc1b0498f95a6702e46569998'
         'b0f489ae4b3e36dc8827dc53a76047aa'
         '0efc5f6c74a4d7e61ca22683c93c98cf')

build() {
  cd "$srcdir"
  gcc -Wall ${CFLAGS} -o intel-microcode2ucode intel-microcode2ucode.c
  ./intel-microcode2ucode ./microcode.dat
}

package() {
  cd "$srcdir"
  install -d -m755 "${pkgdir}"/usr/lib/firmware/intel-ucode/
  
  cp intel-ucode/* "${pkgdir}"/usr/lib/firmware/intel-ucode/
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
