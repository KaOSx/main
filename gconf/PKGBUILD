
pkgname=gconf
pkgver=3.2.6
_pkgver=3.2
pkgrel=14
pkgdesc="A configuration database system"
arch=('x86_64')
url="https://www.gnome.org"
license=('LGPL')
depends=('libxml2' 'polkit' 'libldap' 'dbus-glib')
makedepends=('pkgconfig' 'intltool' 'gobject-introspection')
options=('!libtool')
source=("https://ftp.gnome.org/pub/gnome/sources/GConf/${_pkgver}/GConf-${pkgver}.tar.xz"
        'gconf-merge-schema'
        'gconfpkg'
        'gconf-reload.patch'
        '01_xml-gettext-domain.patch'
        'gconf-install.hook'
        'gconf-remove.hook')
sha256sums=('1912b91803ab09a5eed34d364bf09fe3a2a9c96751fde03a4e0cfa51a04d784c'
            'ee6b6e6f4975dad13a8c45f1c1f0547a99373bdecdcd6604bfc12965c328a028'
            'bf1928718caa5df2b9e54a13cfd0f15a8fe0e09e86b84385ce023616a114e898'
            '567b78d8b4b4bbcb77c5f134d57bc503c34867fcc6341c0b01716bcaa4a21694'
            'c883dec2b96978874a53700cfe7f26f24f8296767203e970bc6402b4b9945eb8'
            '2732b2a6b187c5620105a036bde12edee99669605f70cbde56fe5f39619c3dc0'
            '436a65ff290095bc3d35d7d6297cf4d647f61e9f9922cea7ef9f1e251b447ff7')

build() {
  cd GConf-${pkgver}
  patch -p1 -i ${srcdir}/gconf-reload.patch
  patch -p1 -i ${srcdir}/01_xml-gettext-domain.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/GConf \
    --disable-static \
    --enable-defaults-service \
    --disable-orbit

  make pkglibdir=/usr/lib/GConf
}

package() {
  cd GConf-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/etc/gconf/gconf.xml.system
  install -m755 ${srcdir}/gconf-merge-schema ${pkgdir}/usr/bin/
  install -Dm755 ${srcdir}/gconfpkg ${pkgdir}/usr/sbin/gconfpkg
  
  install -dm755 ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ${srcdir}/gconf-install.hook ${pkgdir}/usr/share/libalpm/hooks/
  install -m644 ${srcdir}/gconf-remove.hook ${pkgdir}/usr/share/libalpm/hooks/
}
