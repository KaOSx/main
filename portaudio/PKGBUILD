
pkgname=portaudio
pkgver=19.6
_pkgver=190600_20161030
pkgrel=1
pkgdesc="PortAudio is a free, cross platform, open-source, audio I/O library."
arch=('x86_64')
url="http://www.portaudio.com"
license=('MIT')
depends=('jack')
source=("http://www.portaudio.com/archives/pa_stable_v${_pkgver}.tgz")
md5sums=('4df8224e047529ca9ad42f0521bf81a8')

build() {
  cd ${pkgname}

  ./configure --prefix=/usr \
    --with-alsa \
    --with-jack \
    --enable-cxx \
    --enable-static=no
  make -j1
}

package() {
  cd ${pkgname}
  make DESTDIR=$pkgdir install
  
  install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
