
pkgname=udisks
pkgver=1.0.4
pkgrel=9
pkgdesc="Disk Management Service"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL')
depends=('systemd' 'sg3_utils' 'glib2' 'dbus-glib' 'polkit' 'parted' 'device-mapper'
         'libatasmart' 'lsof' 'lvm2' 'util-linux')
makedepends=('intltool' 'docbook-xsl')
options=(!libtool)
install=udisks.install
replaces=('devicekit-disks')
source=("http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz"
	'udisks.service'
	'drop-pci-db.patch')
md5sums=('86c63b2b5484f2060499a052b5b6256b'
         '5d80832b4d4da72beeb45c15b63a1481'
         '2746c65d0d12fcc6a550b5a8d9085ab2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # fix fail pci-db message, patch thanks to Tom Gundersen <teg@jklm.no>
  patch -p1 -i ${srcdir}/drop-pci-db.patch
  
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/udisks \
      --disable-static

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make profiledir=/usr/share/bash-completion/completions DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/share/bash-completion/completions/udisks-bash-completion.sh"
  # move udev helpers and rules to /usr/lib
  mv "${pkgdir}"/lib/udev "${pkgdir}"/usr/lib/
  rm -r "${pkgdir}"/lib
  
  #fix ntfs mounts
  sed -i -e 's|<allow_active>auth_admin_keep</allow_active>|<allow_active>yes</allow_active>|' ${pkgdir}/usr/share/polkit-1/actions/org.freedesktop.udisks.policy

  # install systemd units
  install -Dm644 "${srcdir}/udisks.service" "${pkgdir}/usr/lib/systemd/system/udisks.service"
  echo "SystemdService=udisks.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.UDisks.service"
}
