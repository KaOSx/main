
pkgname=phonon-qt6
_pkgname=phonon
pkgver=4.12.0
pkgrel=2
arch=('x86_64')
url="https://community.kde.org/Phonon"
license=('LGPL')
pkgdesc="Multi-platform sound framework for application developers"
depends=('phonon-backend-qt6' 'pulseaudio' 'qt6-base' 'qt6-5compat')
makedepends=('extra-cmake-modules' 'qt6-tools')
source=("https://download.kde.org/stable/phonon/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('3287ffe0fbcc2d4aa1363f9e15747302d0b080090fe76e5f211d809ecb43f39a')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DPHONON_BUILD_SETTINGS=OFF \
    -DQT_MAJOR_VERSION=6 \
    -DPHONON_BUILD_QT5=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  # remove phonon-qt5 conflict
  rm -r ${pkgdir}/usr/share/locale
}
