
pkgname=xorg-xkb-utils
pkgver=7.7
pkgrel=8
pkgdesc="X.org keyboard utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw>=1.0.12' 'libxkbfile')
makedepends=('pkgconfig' 'inputproto')
source=("https://xorg.freedesktop.org/releases/individual/app/xkbutils-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbcomp-1.4.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbevd-1.1.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/setxkbmap-1.3.1.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbprint-1.0.4.tar.bz2"
        'LICENSE.setxkbmap' 'LICENSE.xkbevd' 'LICENSE.xkbutils')
sha256sums=('d2a18ab90275e8bca028773c44264d2266dab70853db4321bdbc18da75148130'
            '6dd8bcb9be7e85bd7294abe261b8c7b0539d2fc93e41b80fb8bd013767ce8424'
            '2430a2e5302a4cb4a5530c1df8cb3721a149bbf8eb377a2898921a145197f96a'
            'a9ddb3963f263ba13f0ea105d8c45a531832140530217cc559587bb94f02d3e1'
            '0b3faab8fefad03dfb7e866f634cf859822801de6b5fc6cf5e0a62857ed93e12'
            '48ab2e98bdd2dfbb226e2e595c0bae0286fc5ae0a07feb6d90af76f93b60a43f'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
  # Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
