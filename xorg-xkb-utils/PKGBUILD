
pkgname=xorg-xkb-utils
pkgver=7.12
pkgrel=2
pkgdesc="X.org keyboard utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxkbfile' 'xorg-server-utils')
makedepends=('pkgconfig' 'xorgproto')
source=("https://xorg.freedesktop.org/releases/individual/app/xkbutils-1.0.6.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xkbcomp-1.4.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xkbevd-1.1.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/setxkbmap-1.3.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xkbprint-1.0.6.tar.xz"
        'LICENSE.setxkbmap' 'LICENSE.xkbevd' 'LICENSE.xkbutils')
sha256sums=('31a2bbee1e09ccba01de92897b8f540b545de812f318d31de07bd3a5a75ee25e'
            '0a288114e5f44e31987042c79aecff1ffad53a8154b8ec971c24a69a80f81f77'
            '38357b702de9d3457c4ff75053390f457b84c4accc7f088101255c37c684926b'
            'be8d8554d40e981d1b93b5ff82497c9ad2259f59f675b38f1b5e84624c07fade'
            '99cc9404f7b90289ae04944c0d98a208cc8b158492ad6481386e31d4d09aa7b0'
            '48ab2e98bdd2dfbb226e2e595c0bae0286fc5ae0a07feb6d90af76f93b60a43f'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
  # Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
