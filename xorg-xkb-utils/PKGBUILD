
pkgname=xorg-xkb-utils
pkgver=7.7
pkgrel=4
pkgdesc="X.org keyboard utilities"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw>=1.0.12' 'libxkbfile')
makedepends=('pkgconfig' 'inputproto')
source=("http://xorg.freedesktop.org/releases/individual/app/xkbutils-1.0.4.tar.bz2"
        "http://xorg.freedesktop.org/releases/individual/app/xkbcomp-1.3.1.tar.bz2"
        "http://xorg.freedesktop.org/releases/individual/app/xkbevd-1.1.4.tar.bz2"
        "http://xorg.freedesktop.org/releases/individual/app/setxkbmap-1.3.1.tar.bz2"
        "http://xorg.freedesktop.org/releases/individual/app/xkbprint-1.0.4.tar.bz2"
        'LICENSE.setxkbmap' 'LICENSE.xkbevd' 'LICENSE.xkbutils')
md5sums=('502b14843f610af977dffc6cbf2102d5'
         'a4d8353daf6cb0a9c47379b7413c42c6'
         'c747faf1f78f5a5962419f8bdd066501'
         '2c47a1b8e268df73963c4eb2316b1a89'
         '3c25b40de530112c08bf7d229c5c6a28'
         '7c20baa6a26c32a04b1b1d6cbaa626a7'
         '7b1fc39455375a25afe6f9594ff979ac'
         '7b1fc39455375a25afe6f9594ff979ac')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
  # Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
