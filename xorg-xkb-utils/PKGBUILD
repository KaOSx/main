
pkgname=xorg-xkb-utils
pkgver=7.9
pkgrel=1
pkgdesc="X.org keyboard utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw>=1.0.12' 'libxkbfile')
makedepends=('pkgconfig' 'inputproto')
source=("https://xorg.freedesktop.org/releases/individual/app/xkbutils-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbcomp-1.4.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbevd-1.1.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/setxkbmap-1.3.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkbprint-1.0.5.tar.bz2"
        'LICENSE.setxkbmap' 'LICENSE.xkbevd' 'LICENSE.xkbutils')
sha256sums=('d2a18ab90275e8bca028773c44264d2266dab70853db4321bdbc18da75148130'
            '59cce603a607a17722a0a1cf99010f4894e7812beb5d695abbc08474d59af27e'
            '2430a2e5302a4cb4a5530c1df8cb3721a149bbf8eb377a2898921a145197f96a'
            '8ff27486442725e50b02d7049152f51d125ecad71b7ce503cfa09d5d8ceeb9f5'
            '5b58fe834f0822f06d63d283fac404098c6d3f6acce61888b81016f1c41023fa'
            '48ab2e98bdd2dfbb226e2e595c0bae0286fc5ae0a07feb6d90af76f93b60a43f'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e'
            '4cbfa83543b15bb578c8ff4e43bd102bc4aeb7eea3cadc829dd4c93d84233c9e')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done
  # Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/" 
}
