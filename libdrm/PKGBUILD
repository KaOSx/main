#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libdrm
pkgver=2.4.42
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
replaces=('libdrm-nouveau' 'libdrm-nouveau-lts')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=("http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'no-pthread-stubs.patch'
        'COPYING')
md5sums=('a6e2e26951bcc920b2049b65e86a659f'
         '74010ca2d1743a9a102bb89cc983efca'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/no-pthread-stubs.patch"
  
#  libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-omap-experimental-api \
      --enable-exynos-experimental-api 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
