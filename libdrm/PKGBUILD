
pkgname=libdrm
pkgver=2.4.102
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
url="https://dri.freedesktop.org/"
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind' 'xorg-util-macros' 'pkg-config' 'meson' 'ninja')
options=('!libtool')
source=("https://dri.freedesktop.org/libdrm/${pkgname}-${pkgver}.tar.xz"
        'COPYING')
sha256sums=('8bcbf9336c28e393d76c1f16d7e79e394a7fce8a2e929d52d3ad7ad8525ba05b'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Dudev=true \
    -Dexynos=true \
    -Domap=true
  ninja
}

check() {
  cd build
  
  meson test
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
