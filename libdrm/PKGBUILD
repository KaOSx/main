
pkgname=libdrm
pkgver=2.4.114
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
url="https://dri.freedesktop.org/"
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'xorg-util-macros' 'pkg-config' 'meson' 'ninja')
options=('!libtool')
source=("https://dri.freedesktop.org/libdrm/${pkgname}-${pkgver}.tar.xz"
        'COPYING')
sha256sums=('3049cf843a47d12e5eeefbc3be3496d782fa09f42346bf0b7defe3d1e598d026'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Dudev=true \
    -Dexynos=enabled \
    -Domap=enabled \
    -Dvalgrind=disabled
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
