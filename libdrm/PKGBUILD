
pkgname=libdrm
pkgver=2.4.111
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
url="https://dri.freedesktop.org/"
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'xorg-util-macros' 'pkg-config' 'meson' 'ninja')
options=('!libtool')
source=("https://dri.freedesktop.org/libdrm/${pkgname}-${pkgver}.tar.xz"
        'COPYING')
sha256sums=('1ad7164f77424de6f4ecba7c262bde196a214c6e19a6fbf497f0815f4d7ab2a9'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Dudev=true \
    -Dexynos=true \
    -Domap=true \
    -Dvalgrind=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
