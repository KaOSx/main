
pkgname=libdrm
pkgver=2.4.81
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
url="https://dri.freedesktop.org/"
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind' 'xorg-util-macros')
options=('!libtool')
source=("https://dri.freedesktop.org/libdrm/${pkgname}-${pkgver}.tar.bz2"
        'COPYING')
md5sums=('d04f3567f0a812188d06f860fded5ab7'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-omap-experimental-api \
      --enable-exynos-experimental-api 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
