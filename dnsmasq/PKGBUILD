
pkgname=dnsmasq
pkgver=2.82
pkgrel=1
pkgdesc="Network infrastructure for small networks: DNS, DHCP, router advertisement, network boot, tethering on smartphones and portable hotspots"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('x86_64')
license=('GPL')
depends=('glibc' 'dbus' 'gmp' 'nettle' 'libidn2')
install=dnsmasq.install
backup=('etc/dnsmasq.conf')
source=("http://www.thekelleys.org.uk/dnsmasq/${pkgname}-${pkgver}.tar.xz"
        'dnsmasq.service'
        "https://github.com/themiron/dnsmasq/commit/6fd9aba7abe1e084123bc5002959350897774ace.diff")
md5sums=('0003fe90b69463c1f687f4ae8560f13a'
         '596c88fc2492f99edf71e398b85a69f4'
         'ce755e41c27f867d861572fe63602ca7')

build() {
  cd ${pkgname}-${pkgver}
  
  #patch -p1 -i ${srcdir}/6fd9aba7abe1e084123bc5002959350897774ace.diff
  #sed -e "s|nettle_secp_256r1|nettle_get_secp_256r1|" -i src/crypto.c

  make \
    CFLAGS="$CPPFLAGS $CFLAGS" \
    LDFLAGS="$LDFLAGS" \
    COPTS="-DHAVE_DNSSEC -DHAVE_DBUS -DHAVE_LIBIDN2"
}

package() {
  cd ${pkgname}-${pkgver}

  make \
    COPTS="-DHAVE_DNSSEC -DHAVE_DBUS -DHAVE_LIBIDN2" \
    BINDIR=/usr/bin PREFIX=/usr DESTDIR=${pkgdir} install

  install -Dm644 dbus/dnsmasq.conf ${pkgdir}/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 dnsmasq.conf.example ${pkgdir}/etc/dnsmasq.conf
  install -Dm644 ${srcdir}/dnsmasq.service ${pkgdir}/usr/lib/systemd/system/dnsmasq.service

  sed -i 's|%%PREFIX%%|/usr|' $pkgdir/etc/dnsmasq.conf
  install -Dm644 trust-anchors.conf $pkgdir/usr/share/dnsmasq/trust-anchors.conf
}

