
pkgname=hunspell-de
pkgver=20161207
pkgrel=2
pkgdesc="German hunspell dictionaries"
arch=('x86_64')
url="https://www.j3e.de/ispell/igerman98/"
license=('GPL')
depends=('hunspell')
makedepends=('ispell')
source=("https://www.j3e.de/ispell/igerman98/dict/igerman98-${pkgver}.tar.bz2")
md5sums=('eb170f5d8992653b47ac4a15d127b58a')

build() {
  cd igerman98-${pkgver}
  make hunspell/de_AT.dic hunspell/de_AT.aff \
       hunspell/de_CH.dic hunspell/de_CH.aff \
       hunspell/de_DE.dic hunspell/de_DE.aff
}

package() {
  cd igerman98-${pkgver}/hunspell
  install -dm755 ${pkgdir}/usr/share/hunspell
  cp -p de_??.dic de_??.aff ${pkgdir}/usr/share/hunspell

  pushd ${pkgdir}/usr/share/hunspell/
  de_DE_aliases="de_BE de_LU"
  for lang in $de_DE_aliases; do
    ln -s de_DE.aff $lang.aff
    ln -s de_DE.dic $lang.dic
  done
  de_CH_aliases="de_LI"
  for lang in $de_CH_aliases; do
    ln -s de_CH.aff $lang.aff
    ln -s de_CH.dic $lang.dic
  done
  popd

  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  cp -p README_de_??.txt ${pkgdir}/usr/share/doc/${pkgname}
}
