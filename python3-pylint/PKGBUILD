
pkgname=python3-pylint
_pkgname=pylint
pkgver=2.15.9
pkgrel=1
pkgdesc="Tool that checks for errors in Python code and tries to enforce a coding standard"
arch=('x86_64')
url="https://pylint.pycqa.org/en/latest/"
license=('GPL')
depends=('python3' 'python3-astroid')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
#source=("https://github.com/PyCQA/pylint/archive/v${pkgver}.tar.gz")
source=("https://pypi.python.org/packages/source/p/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('18783cca3cfee5b83c6c5d10b3cdb66c6594520ffae61890858fe8d932e1c6b4')

build() {
    cd ${_pkgname}-${pkgver}
    sed -i -e 's|"setuptools~=62.6", "wheel~=0.37.1"|"setuptools", "wheel"|' pyproject.toml

    python3 -m build -nw
}

package() {
    cd ${_pkgname}-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
