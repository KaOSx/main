
pkgname=vulkan-tools
pkgver=1.3.233
pkgrel=1
pkgdesc="Project provides the Khronos official Vulkan Tools and Utilities "
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
depends=('vulkan-loader' 'mesa' 'wayland' 'libx11' 'libxkbcommon' 'libxrandr')
makedepends=('cmake' 'vulkan-headers' 'vulkan-validationlayers' 'python3-lxml')
source=("https://github.com/KhronosGroup/Vulkan-Tools/archive/v${pkgver}.tar.gz")
md5sums=('66148a8fab9be206abd6ec03a12d815f')

build() {
  cmake -B build -S Vulkan-Tools-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=ON \
    -DBUILD_WSI_XLIB_SUPPORT=ON \
    -DBUILD_WSI_WAYLAND_SUPPORT=ON \
    -DBUILD_ICD=ON \
    -DINSTALL_ICD=OFF \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr/include \
    -DVulkanHeaders_INCLUDE_DIR=/usr/include \
    -DVulkanRegistry_DIR=/usr/share/vulkan/registry
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 Vulkan-Tools-${pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
} 
