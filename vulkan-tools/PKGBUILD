
pkgname=vulkan-tools
pkgver=1.3.275
pkgrel=1
pkgdesc="Project provides the Khronos official Vulkan Tools and Utilities "
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
depends=('vulkan-loader' 'mesa' 'wayland' 'libx11' 'libxkbcommon' 'libxrandr')
makedepends=('cmake' 'vulkan-headers' 'vulkan-validationlayers' 'python3-lxml' 'glslang')
source=("https://github.com/KhronosGroup/Vulkan-Tools/archive/v${pkgver}.tar.gz")
md5sums=('fda5927562600a33009b85d5d9aef06c')

build() {
  cmake -B build -S Vulkan-Tools-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=ON \
    -DBUILD_WSI_XLIB_SUPPORT=ON \
    -DBUILD_WSI_WAYLAND_SUPPORT=ON \
    -DBUILD_ICD=OFF \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr/include \
    -DUPDATE_DEPS=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 Vulkan-Tools-${pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
} 
