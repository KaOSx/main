
pkgname=vim
pkgver=8.2.1961
pkgrel=1
arch=('x86_64')
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor.'
license=('custom:vim')
url="https://www.vim.org"
depends=('gpm' 'perl')
makedepends=('gettext' 'pkgconfig' 'python3')
source=("https://github.com/vim/vim/archive/v${pkgver}.tar.gz")
md5sums=('f66e09f033170e0f7682900dca542e0b')

build() {
  cd ${srcdir}

  # vimrc to /etc/vimrc
  sed -e 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${pkgver}/src/feature.h
  sed -e 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${pkgver}/src/feature.h
  
  (cd vim-${pkgver}/src && autoconf)

  cd vim-${pkgver}

  ./configure --prefix=/usr \
    --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man \
    --with-compiledby=KaOS \
    --with-features=huge \
    --enable-gpm \
    --enable-acl \
    --with-x=no \
    --disable-gui \
    --enable-multibyte \
    --enable-cscope \
    --disable-netbeans \
    --enable-perlinterp=yes \
    --enable-pythoninterp=yes \
    --enable-python3interp=dynamic \
    --enable-rubyinterp=no
  make
}

package() {
  cd vim-${pkgver}
  make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by vi
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by vi
    rm -f evim.1    # no GUI
  done

  install -d -m755 ${pkgdir}/usr/share/licenses/vim
  ln -s /usr/share/vim/vim74/doc/uganda.txt ${pkgdir}/usr/share/licenses/vim/license.txt
}
