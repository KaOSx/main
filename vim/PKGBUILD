
pkgname=vim
pkgver=7.4.2242
pkgrel=1
arch=('x86_64')
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor.'
license=('custom:vim')
url="http://www.vim.org"
depends=('gpm' 'perl')
makedepends=('python2' 'ruby' 'libxt' 'libxext' 'libxpm' 'libxmu' 'desktop-file-utils'
             'gettext' 'pkgconfig')
provides=('vim-runtime')
conflicts=('vim-runtime')
source=("https://github.com/vim/vim/archive/v${pkgver}.tar.gz")
md5sums=('7c57dcc61f8742c032e4c2629395709e')

build() {
  cd ${srcdir}

  # define the place for the global vimrc file (set to /etc/vimrc)
  sed -e 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${pkgver}/src/feature.h
  sed -e 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
      -i vim-${pkgver}/src/feature.h
  
  (cd vim-${pkgver}/src && autoconf)

  cd ${srcdir}/vim-${pkgver}

  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --mandir=/usr/share/man --with-compiledby=KaOS \
    --with-features=huge --enable-gpm --enable-acl --with-x=no \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --disable-pythoninterp \
    --disable-rubyinterp
  make
}

package() {
  cd ${srcdir}/vim-${pkgver}
  make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by vi
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by vi
    rm -f evim.1    # no GUI
  done

  # license
  install -d -m755 ${pkgdir}/usr/share/licenses/vim
  ln -s /usr/share/vim/vim74/doc/uganda.txt ${pkgdir}/usr/share/licenses/vim/license.txt
}
