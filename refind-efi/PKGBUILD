
pkgname=refind-efi
pkgver=0.13.3.1
pkgrel=1
pkgdesc="Rod Smith's fork of rEFIt UEFI Boot Manager"
url="http://www.rodsbooks.com/refind/index.html"
arch=('x86_64')
license=('GPL3' 'custom')
depends=('bash' 'dosfstools' 'efibootmgr' 'refind-theme-midna')
makedepends=('gnu-efi-libs')
optdepends=('imagemagick: For refind-mkfont script')
options=('!strip' 'docs' '!makeflags')
install="${pkgname}.install"
source=("https://downloads.sourceforge.net/refind/refind-src-${pkgver}.tar.gz"
        'refind_linux.conf'
        'os_kaos.png')
md5sums=('0463c04675bc8f312d7fa83c41959a42'
         '12ce3e22a216e7b25c253478a34713b4'
         '0239a75d2d5393a11af30baa5aeb97ea')

prepare() {
    cd refind-${pkgver}
    # Point refind dir paths to /usr/share/refind/ in refind-install script"
    sed 's|RefindDir=\"\$ThisDir/refind\"|RefindDir="/usr/share/refind/"|g' -i refind-install
    # set to refind midna theme
    echo "include themes/midna/theme.conf" >> refind.conf-sample
}

build() {
    cd refind-${pkgver}

    make gptsync_gnuefi
    make all_gnuefi
}

package() {
    cd refind-${pkgver}

    # Install the rEFInd UEFI application
    install -D -m0644 refind/refind_x64.efi -t ${pkgdir}/usr/share/refind

    # Install UEFI drivers built from rEFInd
    install -D -m0644 drivers_x64/*.efi -t ${pkgdir}/usr/share/refind/drivers_x64/

    # Install UEFI applications built from rEFInd
    install -D -m0644 gptsync/gptsync_x64.efi -t ${pkgdir}/usr/share/refind/tools_x64
    # Install rEFInd helper scripts
    install -D -m0755 refind-install -t ${pkgdir}/usr/bin
    install -D -m0755 mkrlconf -t ${pkgdir}/usr/bin
    install -D -m0755 mvrefind -t ${pkgdir}/usr/bin
    install -D -m0755 fonts/mkfont.sh -t ${pkgdir}/usr/bin

    # Install the rEFInd sample config files
    install -D -m0644 refind.conf-sample -t ${pkgdir}/usr/share/refind
    install -D -m0644 ../refind_linux.conf -t ${pkgdir}/usr/share/refind

    # Install the rEFInd docs
    install -D -m0644 docs/refind/* -t ${pkgdir}/usr/share/refind/docs/html/
    install -D -m0644 docs/Styles/* -t ${pkgdir}/usr/share/refind/docs/Styles/
    install -D -m0644 README.txt -t ${pkgdir}/usr/share/refind/docs

    rm -f ${pkgdir}/usr/share/refind/docs/html/.DS_Store

    # Install the rEFInd fonts
    install -D -m0644 fonts/*.png -t ${pkgdir}/usr/share/refind/fonts/

    # Install the rEFInd icons
    install -D -m0644 icons/*.png -t ${pkgdir}/usr/share/refind/icons/
    install -D -m0644 ../os_kaos.png -t ${pkgdir}/usr/share/refind/icons/

    # Install the rEFInd images
    install -D -m0644 images/*.{png,bmp} -t ${pkgdir}/usr/share/refind/images/

    # Install the rEFInd keys
    install -D -m0644 keys/* -t ${pkgdir}/usr/share/refind/keys/

    # Install the rEFIt license file, since rEFInd is a fork of rEFIt
    install -D -m0644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/refind
}
