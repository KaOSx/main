
pkgname=mono
pkgver=5.4.1
_pkgver=5.4.1.6
pkgrel=1
epoch=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('x86_64')
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="https://www.mono-project.com/"
depends=('zlib' 'libgdiplus' 'sh' 'python3')
makedepends=('pkgconfig')
options=('!libtool' '!makeflags')
source=("https://download.mono-project.com/sources/mono/${pkgname}-${_pkgver}.tar.bz2"
        'mono.binfmt.d')
md5sums=('05114ed0a2cff26565e4d03de4966034'
         '568c72e1b42f089a9fec20dfe4bcc3b4')

build() {
  cd ${pkgname}-${_pkgver}

  ./configure --prefix=/usr \
         --sysconfdir=/etc \
         --bindir=/usr/bin \
         --disable-quiet-build \
         --disable-system-aot \
         --with-mcs-docs=no
  make 

  cd mcs/jay
  make 
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install 

  cd mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr INSTALL=../../install-sh install
  cd ../..

  install -D -m644 ${srcdir}/mono.binfmt.d ${pkgdir}/usr/lib/binfmt.d/mono.conf

  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  
  sed -i -e "s|#Requires|Requires|" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}
