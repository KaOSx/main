# pin at <45, see https://github.com/pypa/setuptools/issues/1458
pkgname=python2-setuptools
pkgver=44.1.1
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('x86_64')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
depends=('python2-appdirs' 'python2-packaging')
makedepends=('asciidoc' 'python2-pip')
provides=('python2-distribute' 'setuptools')
replaces=('python2-distribute' 'setuptools')
conflicts=('python2-distribute')
source=("https://github.com/pypa/setuptools/archive/v${pkgver}.tar.gz")
md5sums=('2922ead67497b1fba832569d9b5bae5b')

build() {
   cd setuptools-${pkgver}
   sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
   sed -i '/pip.main(args)/d' bootstrap.py
   
   python2 bootstrap.py
   python2 setup.py build
}
 
check() {
   cd setuptools-${pkgver}
   # test fail in chroot LC_CTYPE=en_US.utf8 fixes tests on installed system
   #python2 setup.py test
}
 
package() {
   cd setuptools-${pkgver}
   
   python2 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1 --skip-build
}
 

