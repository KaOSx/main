
pkgname=gst-plugins-bad
pkgver=1.20.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'libdca' 'soundtouch' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth' 'openjpeg'
         'libwebp' 'libsrtp' 'gnutls' 'librsvg' 'libpng' 'libgudev' 'sbc' 'glu'
         'qt5-x11extras' 'x265' 'libass' 'schroedinger' 'nettle' 'libdvdread'
         'vulkan-validationlayers' 'microdns' 'libnice' 'flac')
makedepends=('libexif' 'libvdpau' 'libmpeg2' 'python3' 'meson' 'ninja'
             'gobject-introspection' 'bluez' 'ladspa' 'openal' 'qt5-wayland')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-bad/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a1a3f53b3604d9a04fdd0bf9a1a616c3d2dab5320489e9ecee1178e81e33a16a')

prepare() {
  cd ${pkgname}-${pkgver}
  # needed for neon 0.31 
  # https://gitlab.freedesktop.org/gstreamer/gst-plugins-bad/-/commit/f10b424418e448211e3427a76fcd046e157ef0b7
  #sed -e "s|<= 0.30.99|<= 0.31.99|" -i ext/neon/meson.build
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -D package-name="GStreamer Bad Plugins (KaOS)" \
    -D package-origin="https://kaosx.us"
  ninja
}

check() {
  cd build
  # 4 fail
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
