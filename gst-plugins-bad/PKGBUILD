
pkgname=gst-plugins-bad
pkgver=1.10.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'celt' 'libdca' 'soundtouch' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth' 'openjpeg'
         'libwebp' 'libsrtp' 'gnutls' 'librsvg' 'libpng' 'libgudev' 'sbc' 'glu'
         'qt5-x11extras' 'x265')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 'python3' 
             'gobject-introspection' 'bluez' 'ladspa' 'openal' 'qt5-wayland')
options=('!libtool' '!emptydirs')
source=("http://gstreamer.freedesktop.org/src/gst-plugins-bad/${pkgname}-${pkgver}.tar.xz")
md5sums=('823f4c33fe27c61332c0122273217988')

build() {
  cd ${pkgname}-${pkgver}
  
  CXXFLAGS="-std=c++11"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="http://kaosx.us"
    
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool 
  
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
