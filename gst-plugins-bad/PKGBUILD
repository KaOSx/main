
pkgname=gst-plugins-bad
pkgver=1.22.10
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'libdca' 'soundtouch' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth' 'openjpeg'
         'libwebp' 'libsrtp' 'gnutls' 'librsvg' 'libpng' 'libgudev' 'sbc' 'glu'
         'x265' 'libass' 'schroedinger' 'nettle' 'libdvdread'
         'vulkan-validationlayers' 'microdns' 'libnice' 'flac' 'libde265' 'webrtc-audio-processing')
makedepends=('libexif' 'libvdpau' 'libmpeg2' 'python3' 'meson' 'ninja'
             'gobject-introspection' 'bluez' 'ladspa' 'openal')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-bad/${pkgname}-${pkgver}.tar.xz")
sha256sums=('dabcd60c762165bb043eba753d599212514c94684e4db9a2e25484cb6508ebbf')

prepare() {
  cd ${pkgname}-${pkgver}
  # needed for neon 0.31 
  # https://gitlab.freedesktop.org/gstreamer/gst-plugins-bad/-/commit/f10b424418e448211e3427a76fcd046e157ef0b7
  #sed -e "s|<= 0.30.99|<= 0.31.99|" -i ext/neon/meson.build
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -D package-name="GStreamer Bad Plugins (KaOS)" \
    -D package-origin="https://kaosx.us" \
    -D gpl=enabled
  ninja
}

check() {
  cd build
  # 8 fail
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
