
pkgname=gst-plugins-bad
pkgver=1.14.2
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base' 'curl' 'chromaprint' 'libmms' 'faad2'
         'mpg123' 'faac' 'celt' 'libdca' 'soundtouch' 'libdvdnav'
         'libmodplug' 'libgme' 'opus' 'wayland' 'neon' 'libofa' 'fluidsynth' 'openjpeg'
         'libwebp' 'libsrtp' 'gnutls' 'librsvg' 'libpng' 'libgudev' 'sbc' 'glu'
         'qt5-x11extras' 'x265' 'libass' 'schroedinger')
makedepends=('libexif' 'libdvdread' 'libvdpau' 'libmpeg2' 'python3' 
             'gobject-introspection' 'bluez' 'ladspa' 'openal' 'qt5-wayland')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-bad/${pkgname}-${pkgver}.tar.xz")
sha256sums=('34fab7da70994465a64468330b2168a4a0ed90a7de7e4c499b6d127c6c1b1eaf')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="https://kaosx.us"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check || :
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
