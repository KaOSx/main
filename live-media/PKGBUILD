
# rebuild vlc on any update, move to shared libs might change ABI
pkgname=live-media
pkgver=2020.12.04
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz"
        'config.kaos')
md5sums=('a72408926510d31774664123fbab3087'
         '6ff1eab03314198badb96032a5299e2b')

prepare() {
  cd live
  
  cp ${srcdir}/config.kaos config.kaos
}
         
build() {
  cd live
      
  ./genMakefiles kaos
  make
}

package() {
  cd live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.so "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
