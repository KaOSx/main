
pkgname=live-media
pkgver=2020.03.06
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
md5sums=('cfca528bda964d2167260b3ab6ee294e')

build() {
  cd live

  sed \
      -e 's/$(INCLUDES) -I. -O2 -DSOCKLEN_T/$(INCLUDES) -I. -O2 -I. -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1 -DSOCKLEN_T/g' \
      -i config.linux
      
  # https://sourceware.org/glibc/wiki/Release/2.26#Removal_of_.27xlocale.h.27
  sed -i 's/#include <xlocale.h>.*/#include <locale.h>/' liveMedia/include/Locale.hh
      
  ./genMakefiles linux
  make
}

package() {
  cd live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
