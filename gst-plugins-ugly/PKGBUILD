
pkgname=gst-plugins-ugly
pkgver=1.16.1
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'x264' 'opencore-amr' 'libcdio')
makedepends=('python3')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-ugly/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4bf913b2ca5195ac3b53b5e3ade2dc7c45d2258507552ddc850c5fa425968a1d')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="https://kaosx.us" 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
