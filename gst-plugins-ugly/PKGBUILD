
pkgname=gst-plugins-ugly
pkgver=1.24.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'x264' 'opencore-amr' 'libcdio')
makedepends=('python3' 'meson' 'ninja')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-ugly/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c5d1cbdf71ab0c675bca236f70edfa1feb3f813fd4bfff563308f466d8805ca5')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build

  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
