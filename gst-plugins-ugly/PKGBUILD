
pkgname=gst-plugins-ugly
pkgver=1.18.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'x264' 'opencore-amr' 'libcdio')
makedepends=('python3' 'meson' 'ninja')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-ugly/${pkgname}-${pkgver}.tar.xz")
sha256sums=('18cd6cb829eb9611ca63cbcbf46aca0f0de1dd28b2df18caa2834326a75ff725')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build

  ninja test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
