
pkgname=gst-plugins-ugly
pkgver=1.4.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
makedepends=('python3')
options=('!libtool' '!emptydirs')
source=("http://gstreamer.freedesktop.org/src/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('316974af949ca4654efee704a0164076')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="http://kaosx.us"
  make
}

check() {
  cd $pkgname-$pkgver
  
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
}
