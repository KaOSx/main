
pkgname=gst-plugins-ugly
pkgver=1.12.3
pkgrel=3
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'libdvdread' 'lame' 'libmpeg2' 'a52dec'
         'libmad' 'libsidplay' 'x264' 'opencore-amr' 'libcdio')
makedepends=('python3')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-ugly/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e88ca584c94ea78eeecbf3af00ef7f134b66bdee7408aa4aa6c547235e060052')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="https://kaosx.us"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
