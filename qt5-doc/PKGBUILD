
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt5-doc
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="A cross-platform application and UI framework, offline documentation files"
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL')
depends=('qt5-tools')
makedepends=('clang' 'gperf' 'nss')
options=('!libtool')
conflicts=('qt5-base-docs' 'qt5-declarative-docs')
provides=('qt5-base-docs' 'qt5-declarative-docs')
replaces=('qt5-base-docs' 'qt5-declarative-docs')
_pkgfqn="qt-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/single/${_pkgfqn}.tar.xz")
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('e1447db4f06c841d8947f0a6ce83a7b5')

build() {
    cd ${_pkgfqn}
    
    ln -sf /usr/lib/qt5/bin qttools/
    ln -sf /usr/lib/qt5/bin/moc qtbase/bin/
    ln -sf /usr/lib/qt5/bin/rcc qtbase/bin/
    ln -sf /usr/lib/qt5/bin/uic qtbase/bin/
    
    ln -sf /usr/lib/libQt5Help.so qttools/lib/
    ln -sf /usr/lib/libQt5Widgets.so qtbase/lib/
    ln -sf /usr/lib/libQt5Gui.so qtbase/lib/
    ln -sf /usr/lib/libQt5Sql.so qtbase/lib/
    ln -sf /usr/lib/libQt5Core.so qtbase/lib/
        
    #unset QTDIR
    #export PATH="$srcdir/bin:$PATH"

    ./configure -confirm-license -opensource \
                -prefix /usr \
                -bindir /usr/lib/qt5/bin \
                -headerdir /usr/include/qt5 \
                -archdatadir /usr/lib/qt5 \
                -docdir /usr/share/doc/qt5 \
                -plugindir /usr/lib/qt5/plugins \
                -importdir /usr/lib/qt5/imports \
                -qmldir /usr/lib/qt5/qml \
                -datadir /usr/share/qt5 \
                -testsdir /usr/share/qt5/tests \
                -translationdir /usr/share/qt5/translations \
                -sysconfdir /etc/xdg/qt5 \
                -examplesdir /usr/share/qt5/examples
     make docs
}

package() {
    cd ${_pkgfqn}
    
    make -j1 INSTALL_ROOT=${pkgdir} install_docs
    
    install -D -m644 LICENSE.LGPLv3 ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.LGPL3
}

