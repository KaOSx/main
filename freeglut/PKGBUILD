
pkgname=freeglut
pkgver=3.2.1
pkgrel=5
pkgdesc="Provides functionality for small OpenGL programs"
arch=('x86_64')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('libxi' 'libxrandr' 'libglvnd' 'libxxf86vm')
makedepends=('cmake' 'mesa' 'glu')
options=('!libtool')
source=("https://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz")
md5sums=('cd5c670c1086358598a6d4a9d166949d')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib 
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
