
pkgname=libtiff
pkgver=4.3.0
pkgrel=1
pkgdesc="Library for manipulation of TIFF images"
arch=('x86_64')
url="https://libtiff.gitlab.io/libtiff/"
license=('custom')
depends=('libjpeg-turbo' 'zlib' 'xz')
makedepends=('freeglut' 'glu' 'mesa')
optdepends=('freeglut: for using tiffgt')
options=('!libtool')
source=("https://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz")
md5sums=('0a2e4744d1426a8fc8211c0cdbc3a1b3')

build() {
  cd tiff-${pkgver}
  
  ./configure --prefix=/usr
  make
}

check() {
  cd tiff-${pkgver}
  
  make check
}

package() {
  cd tiff-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
