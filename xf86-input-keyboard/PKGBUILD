
pkgname=xf86-input-keyboard
pkgver=2.0.0
pkgrel=1
pkgdesc="X.Org keyboard input driver"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server' 'xorgproto')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3266fdf1a2175fd4fe999527ae8527cc59f3f01a1d6271ad474bff0b9451feb9')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
