
## rebuild groff on every update
pkgname=ghostscript
pkgver=9.19
_pkgver=919
pkgrel=1
epoch=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
license=('GPL3' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'jasper' 'zlib' 'libpng' 'libjpeg-turbo'
         'libtiff' 'lcms' 'libpaper')
makedepends=('gnutls')
optdepends=('texlive-core:      needed for dvipdf')
url="http://www.ghostscript.com/"
#source=("http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2")
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${_pkgver}/ghostscript-${pkgver}.tar.bz2")
options=('!libtool' '!makeflags')
md5sums=('e97afb7468c6decb44f18a0719d4ef18')

build() {
  cd ${srcdir}/ghostscript-${pkgver}

  # force it to use system-libs
  rm -rf jpeg libpng jasper expat tiff freetype lcms

  ./configure --prefix=/usr \
	--enable-dynamic \
	--with-ijs \
	--with-jbig2dec \
	--with-omni \
	--with-x \
	--with-drivers=ALL\
	--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
	--enable-fontconfig \
	--enable-freetype \
	--without-luratech \
	--disable-compile-inits \
	--with-system-libtiff \
	--disable-gtk \
	--without-omni \
	--with-libpaper 

  make
  
  # Build IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  make DESTDIR=${pkgdir} \
	cups_serverroot=${pkgdir}/etc/cups \
	cups_serverbin=${pkgdir}/usr/lib/cups install soinstall
  
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/

  # remove unwanted localized man-pages
  rm -rf $pkgdir/usr/share/man/[^man1]*

  # install IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  make DESTDIR=${pkgdir} install
  
  # cups-filters 1.36 conflict
  rm -rf $pkgdir/usr/lib/cups/filter/{gstopxl,gstoraster}
}
