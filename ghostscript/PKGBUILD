
## rebuild groff on every update
pkgname=ghostscript
pkgver=9.23
_pkgver=923
pkgrel=2
epoch=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
license=('GPL3' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'zlib' 'libpng' 'libjpeg-turbo'
         'libtiff' 'lcms2' 'libpaper' 'libxext' 'libidn')
makedepends=('gnutls')
optdepends=('texlive-core:      needed for dvipdf')
url="http://www.ghostscript.com/"
#source=("http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2")
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${_pkgver}/ghostscript-${pkgver}.tar.xz")
options=('!libtool' '!makeflags')
sha256sums=('1fcedc27d4d6081105cdf35606cb3f809523423a6cf9e3c23cead3525d6ae8d9')

build() {
  cd ${srcdir}/ghostscript-${pkgver}

  # force it to use system-libs
  rm -rf jpeg tiff freetype lcms2 libpng expat

  ./configure --prefix=/usr \
	--enable-dynamic \
	--with-jbig2dec \
	--with-x \
	--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
	--without-luratech \
	--disable-compile-inits \
	--with-system-libtiff \
	--disable-gtk 

  make
  
  # Build IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  make DESTDIR=${pkgdir} \
	cups_serverroot=${pkgdir}/etc/cups \
	cups_serverbin=${pkgdir}/usr/lib/cups install soinstall
  
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/

  # remove unwanted localized man-pages
  rm -rf $pkgdir/usr/share/man/[^man1]*

  # install IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  make DESTDIR=${pkgdir} install
  
  # cups-filters 1.36 conflict
  rm -rf $pkgdir/usr/lib/cups/filter/{gstopxl,gstoraster}
}
