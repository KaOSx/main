
## rebuild groff on every update, needs /usr/share/ghostscript/${pkgver}/
pkgname=ghostscript
pkgver=9.53.3
_pkgver=9533
pkgrel=1
epoch=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://www.ghostscript.com/"
license=('GPL3' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'zlib' 'libpng' 'libjpeg-turbo'
         'libtiff' 'lcms2' 'libpaper' 'libxext' 'libidn')
makedepends=('gnutls')
optdepends=('texlive-core:      needed for dvipdf')
options=('!libtool' '!makeflags')
#source=("https://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2")
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${_pkgver}/ghostscript-${pkgver}.tar.xz")
sha512sums=('eb832c27eecd30f15e346408c592d7096fd23ef0a6fa59bd50ca327578915434530a4868e69249c2594def0910c527302e99d54f0877f726a8ca8bea6f0f17b7')

build() {
  cd ${srcdir}/ghostscript-${pkgver}

  # force it to use system-libs
  rm -rf jpeg tiff freetype lcms2 libpng expat

  ./configure --prefix=/usr \
	--enable-dynamic \
	--with-jbig2dec \
	--with-x \
	--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
	--without-luratech \
	--disable-compile-inits \
	--with-system-libtiff \
	--disable-gtk 

  make
  
  # Build IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  
  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ${srcdir}/ghostscript-${pkgver}
  make DESTDIR=${pkgdir} \
	cups_serverroot=${pkgdir}/etc/cups \
	cups_serverbin=${pkgdir}/usr/lib/cups install soinstall
  
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/

  # remove unwanted localized man-pages
  rm -rf $pkgdir/usr/share/man/[^man1]*

  # install IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  make DESTDIR=${pkgdir} install
  
  # cups-filters 1.36 conflict
  rm -rf $pkgdir/usr/lib/cups/filter/{gstopxl,gstoraster}
}
