
## rebuild groff on every update, needs /usr/share/ghostscript/${pkgver}/
pkgname=ghostscript
pkgver=10.03.0
_pkgver=10030
pkgrel=1
epoch=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
url="https://www.ghostscript.com/"
license=('GPL3' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'zlib' 'libpng' 'libjpeg-turbo'
         'libtiff' 'lcms2' 'libpaper' 'libxext' 'libidn')
makedepends=('gnutls')
optdepends=('texlive-core:      needed for dvipdf')
options=('!libtool' '!makeflags')
#source=("https://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2")
source=("https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${_pkgver}/ghostscript-${pkgver}.tar.xz")
sha512sums=('74d7da586eefbf3fb3c085bb4d2b73ee667fd0ae9cff73ece613c77c443148b847086f91802124baf91ac35cdc83be07199799378def92c8fbd96ced7d0ee9a8')

build() {
  cd ghostscript-${pkgver}

  # force it to use system-libs
  rm -rf jpeg tiff freetype lcms2 libpng expat

  ./configure --prefix=/usr \
	--with-jbig2dec \
	--with-x \
	--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
	--disable-compile-inits \
	--with-system-libtiff \
	--disable-gtk \
	--without-tesseract

  make

  # Build IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ghostscript-${pkgver}
  make DESTDIR=${pkgdir} \
	cups_serverroot=${pkgdir}/etc/cups \
	cups_serverbin=${pkgdir}/usr/lib/cups install soinstall

  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/

  # remove unwanted localized man-pages
  rm -rf $pkgdir/usr/share/man/[^man1]*

  # install IJS
  cd ${srcdir}/ghostscript-${pkgver}/ijs
  make DESTDIR=${pkgdir} install

  # cups-filters 1.36 conflict
  rm -rf $pkgdir/usr/lib/cups/filter/{gstopxl,gstoraster}
}
