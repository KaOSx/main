
pkgname=aalib
pkgver=1.4rc5
_pkgver=1.4.0
pkgrel=15
pkgdesc="AAlib is a portable ASCII art GFX library"
arch=('x86_64')
url="https://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('glibc' 'ncurses' 'gpm' 'libx11')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/aa-project/${pkgname}-${pkgver}.tar.gz"
        'aclocal-fixes.patch'
        'aalib-c99.patch'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/media-libs/aalib/files/aalib-1.4_rc5-key-down-OOB.patch"
        'aalib-aalinuxkbd-return.patch'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/media-libs/aalib/files/aalib-1.4_rc5-fix-aarender.patch"
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/media-libs/aalib/files/aalib-1.4_rc5-ncurses-opaque.patch")
md5sums=('9801095c42bba12edebd1902bcf0a990'
         '863a96a6689aa7ee073ca448bc2f133d'
         '726fba4b9be81497d43873166933bb5f'
         '7e0fc75c11054163be8b42c8c82a42d1'
         '517b2577b1062769769434c4fb97e245'
         'dc468370c9be266cfcd68b427f193f7c'
         '95b7e0d13e111f7a7946998af727167e')

build() {
  cd ${pkgname}-${_pkgver}
  patch -p0 -i ${srcdir}/aclocal-fixes.patch
  # https://sourceforge.net/p/aa-project/bugs/10/
  patch -p1 -i ${srcdir}/aalib-c99.patch
  patch -p1 -i ${srcdir}/aalib-1.4_rc5-key-down-OOB.patch
  patch -p1 -i ${srcdir}/aalib-aalinuxkbd-return.patch
  patch -p1 -i ${srcdir}/aalib-1.4_rc5-fix-aarender.patch
  patch -p1 -i ${srcdir}/aalib-1.4_rc5-ncurses-opaque.patch

  autoreconf -fi
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  
  make DESTDIR=${pkgdir} install
}
