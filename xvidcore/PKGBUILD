
pkgname=xvidcore
pkgver=1.3.4
pkgrel=2
pkgdesc="XviD is an open source MPEG-4 video codec"
arch=('x86_64')
license=('GPL')
url="https://www.xvid.org/"
depends=('glibc')
makedepends=('nasm')
source=("http://downloads.xvid.org/downloads/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5e68b84e3286b7bbaa95ddc34f0fcace')

build() {
  cd ${pkgname}/build/generic
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}/build/generic
  make DESTDIR=${pkgdir} install

  #Fix dynamic libraries
  cd ${pkgdir}/usr/lib
  _mylib=$(basename libxvidcore.so.*)
  ln -sf ${_mylib} libxvidcore.so.4
  ln -sf ${_mylib} libxvidcore.so
}
