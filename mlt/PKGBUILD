
pkgname=mlt
pkgver=7.2.0
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="https://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'python3' 'jack' 'ladspa' 'qt5-svg'
         'frei0r-plugins' 'libexif' 'opencv' 'rubberband' 'gdk-pixbuf2')
makedepends=('cmake' 'swig')
source=("https://github.com/mltframework/mlt/releases/download/v${pkgver}/mlt-${pkgver}.tar.gz"
        "https://github.com/mltframework/mlt/commit/f58b44d73442986eeffec7431e59b7d19d214c1b.diff")
md5sums=('fbcdac9bbda30dbf92739bbf40e3120a'
         'b4605b9c281a054e16a7f42d3c69203f')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGPL=ON \
    -DGPL3=ON \
    -DBUILD_TESTING=OFF \
    -DMOD_OPENCV=ON \
    -DSWIG_PYTHON=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
