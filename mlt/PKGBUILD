
# NOTE: Disabled the gtk2 module so no gtk-preview

pkgname=mlt
pkgver=6.16.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'python2' 'jack' 'ladspa' 'qt5-svg'
         'frei0r-plugins' 'libexif' 'opencv')
makedepends=('swig')
source=("https://github.com/mltframework/mlt/releases/download/v${pkgver}/mlt-${pkgver}.tar.gz")
md5sums=('e3872267232aae89f5182fd567be2596')

build() {
  cd mlt-${pkgver}
  
  #CXXFLAGS="-std=c++11"

  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qimage-libdir=/usr/lib/ \
              --qt-includedir=/usr/include/qt \
              --disable-gtk2 \
              --avformat-swscale \
              --enable-opencv
  make

  # mlt python bindings
  cd src/swig/python
  ./build
}

package () {
  cd mlt-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install python bindings
  cd src/swig/python
  mkdir -p ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt.py ${pkgdir}/usr/lib/python2.7/
  install -m0755 _mlt.so ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt_wrap.o ${pkgdir}/usr/lib/python2.7/
}
