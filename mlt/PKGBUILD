#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

# NOTE: Disabled the gtk2 module so no gtk-preview

pkgbase=mlt
pkgname=('mlt' 'mlt-python-bindings')
pkgver=0.8.8
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'qt' 'sox' 'libxml2' 'ffmpeg>=0.11'
         'frei0r-plugins' 'swig' 'python2')
optdepends=('jack')
makedepends=('subversion')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('06de4052cffd6760c468d7da9557edae')

build() {
  # mlt
  cd "${srcdir}/mlt-${pkgver}"

  msg "SSE2=$SSE2"
  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qimage-libdir=/usr/lib/ \
              --qimage-includedir=/usr/include/Qt \
              --avformat-vdpau \
              --disable-gtk2 \
              --avformat-swscale $SSE2 
  make

  # mlt python bindings
  cd "${srcdir}/mlt-${pkgver}/src/swig/python"
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg>=0.11' 'frei0r-plugins')
  optdepends=('jack' 'libexif' 'qt' 'ladspa')

  cd "${srcdir}/mlt-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt>=0.8')

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt.py "${pkgdir}/usr/lib/python2.7/"
  install -m0755 _mlt.so "${pkgdir}/usr/lib/python2.7/"
  install -m0755 mlt_wrap.o "${pkgdir}/usr/lib/python2.7/"
}
