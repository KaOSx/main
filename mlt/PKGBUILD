
# NOTE: Disabled the gtk2 module so no gtk-preview

pkgname=mlt
pkgver=6.10.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'python2' 'jack' 'ladspa' 'qt5-svg'
         'frei0r-plugins' 'libexif' 'opencv')
makedepends=('swig')
conflicts=('mlt-python-bindings' 'mlt-qt5')
provides=('mlt-python-bindings' 'mlt-qt5')
replaces=('mlt-python-bindings' 'mlt-qt5')
#source=("http://downloads.sourceforge.net/sourceforge/mlt/$pkgname-$pkgver.tar.gz")
source=("https://github.com/mltframework/mlt/releases/download/v${pkgver}/mlt-${pkgver}.tar.gz")
md5sums=('28a6f40ea76c167b6bb9d8baf985c4c2')

build() {
  cd mlt-${pkgver}
  
  #CXXFLAGS="-std=c++11"

  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qimage-libdir=/usr/lib/ \
              --qt-includedir=/usr/include/qt \
              --disable-gtk2 \
              --avformat-swscale \
              --disable-opencv
  make

  # mlt python bindings
  cd src/swig/python
  ./build
}

package () {
  cd mlt-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install python bindings
  cd src/swig/python
  mkdir -p ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt.py ${pkgdir}/usr/lib/python2.7/
  install -m0755 _mlt.so ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt_wrap.o ${pkgdir}/usr/lib/python2.7/
}
