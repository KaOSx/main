
# NOTE: Disabled the gtk2 module so no gtk-preview

pkgname=mlt
pkgver=6.2.0
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('x86_64')
url="http://www.mltframework.org"
license=('GPL')
depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'python2' 'jack' 'ladspa' 'qt5-svg'
         'frei0r-plugins' 'libexif')
makedepends=('swig')
conflicts=('mlt-python-bindings' 'mlt-qt5')
provides=('mlt-python-bindings' 'mlt-qt5')
replaces=('mlt-python-bindings' 'mlt-qt5')
#source=("http://downloads.sourceforge.net/sourceforge/mlt/$pkgname-$pkgver.tar.gz")
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
md5sums=('a1c70e535c61dd9242ac9f6ae58899d0')

build() {
  cd mlt-${pkgver}

  ./configure --prefix=/usr \
              --enable-gpl \
              --enable-gpl3 \
              --qimage-libdir=/usr/lib/ \
              --qimage-includedir=/usr/include/Qt \
              --avformat-vdpau \
              --disable-gtk2 \
              --avformat-swscale  
  make

  # mlt python bindings
  cd src/swig/python
  ./build
}

package () {
  cd mlt-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install python bindings
  cd src/swig/python
  mkdir -p ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt.py ${pkgdir}/usr/lib/python2.7/
  install -m0755 _mlt.so ${pkgdir}/usr/lib/python2.7/
  install -m0755 mlt_wrap.o ${pkgdir}/usr/lib/python2.7/
}
