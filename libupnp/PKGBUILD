
pkgname=libupnp
_pkgname=pupnp
pkgver=1.14.20
pkgrel=1
pkgdesc="Portable UPnP development kit."
arch=('x86_64')
url="https://pupnp.github.io/pupnp/"
license=('BSD')
depends=('glibc')
makedepends=('cmake')
source=("https://github.com/pupnp/pupnp/releases/download/release-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
#source=("https://github.com/pupnp/pupnp/archive/refs/tags/release-${pkgver}.tar.gz")
sha1sums=('304898b1478f75192bb9c6ba8d240f91ec1d354d')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUPNP_BUILD_STATIC=OFF \
    -DUPNP_BUILD_SAMPLES=OFF
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
