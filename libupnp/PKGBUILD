
pkgname=libupnp
_pkgname=pupnp
pkgver=1.14.19
pkgrel=1
pkgdesc="Portable UPnP development kit."
arch=('x86_64')
url="https://pupnp.github.io/pupnp/"
license=('BSD')
depends=('glibc')
makedepends=('cmake')
source=("https://github.com/pupnp/pupnp/releases/download/release-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
#source=("https://github.com/pupnp/pupnp/archive/refs/tags/release-${pkgver}.tar.gz")
sha1sums=('ff6022d9b9525f0d9d0182c95c3f91bef9046181')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUPNP_BUILD_STATIC=OFF \
    -DUPNP_BUILD_SAMPLES=OFF \
    -DPUPNP_VERSION_STRING=${pkgver}
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
