
pkgname=pyside6
_pkgname=pyside-setup-everywhere-src
pkgver=6.6.2
_pkgver=6.6.2
pkgrel=4
pkgdesc="Plugin (front-end) for Generator Runner. It generates bindings for C++ libraries using CPython source code"
arch=('x86_64')
license=('LGPL')
url="https://wiki.qt.io/Qt_for_Python"
depends=('phonon-qt6' 'qt6-3d' 'qt6-connectivity' 'qt6-datavis3d' 'qt6-declarative' 'qt6-location'
         'qt6-quick3d' 'qt6-svg' 'qt6-tools' 'qt6-websockets' 'qt6-multimedia' 'qt6-scxml' 'qt6-sensors'
         'qt6-serialport' 'qt6-speech' 'qt6-webengine' 'shiboken6') # 'qt6-remoteobjects' 'qt6-charts'
makedepends=('cmake' 'python3-sphinx' 'python3-setuptools' 'clang')
#source=("git://code.qt.io/pyside/pyside-setup.git")
source=("https://download.qt.io/official_releases/QtForPython/${pkgname}/PySide6-${pkgver}-src/${_pkgname}-${pkgver}.tar.xz"
        'qt6.7.patch')
sha256sums=('14620b694d7af4c978443016292d3d2108ba5dc105f4170e3b71eadcaf04c9f0'
            'fb0b1a4b7f96747d77e5f1c5a02ec070dd2fabd082406dde471f01bf60ccc39e')

prepare() {
  cd ${_pkgname}-${_pkgver}

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  #sed -i -e 's|import importlib|import importlib.machinery|' sources/shiboken2/libshiboken/embed/signature_bootstrap.py
  #sed -i -e 's|#ifndef Py_LIMITED_API|#if PY_VERSION_HEX < 0x03010000|' sources/shiboken2/libshiboken/pep384impl.cpp
  patch -p1 -i ${srcdir}/qt6.7.patch
}

build() {
  cmake -B build -S ${_pkgname}-${_pkgver}/sources/pyside6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

