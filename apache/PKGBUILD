
pkgname=apache
pkgver=2.4.27
pkgrel=1
pkgdesc='A high performance Unix-based HTTP server'
arch=('x86_64')
options=('!libtool')
url='https://www.apache.org/dist/httpd'
license=('Apache')
backup=(etc/httpd/conf/httpd.conf
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/logrotate.d/httpd)
depends=('openssl' 'zlib' 'apr-util' 'pcre' 'systemd')
makedepends=('apr')
install=${pkgname}.install
source=("https://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2"
        'httpd.logrotate'
        'kaos.layout'
        'httpd.service'
        'httpd-tmpfile.conf')
md5sums=('97b6bbfa83c866dbe20ef317e3afd108'
         '6f0c3acecf1160fa6fb6970abce2d93b'
         '7511248625ccaaf3229a320167cdc5d9'
         '06abba73227c1cae7367207e3dacc5e2'
         '110a056408252de4356be7f53872b11d')

build() {
  cd httpd-${pkgver}

  # set default user
  sed -e 's#User daemon#User http#' \
      -e 's#Group daemon#Group http#' \
      -i docs/conf/httpd.conf.in 

  cat ${srcdir}/kaos.layout >> config.layout

    ${srcdir}/httpd-${pkgver}/configure \
        --enable-layout=KaOS \
        --enable-mpms-shared=all \
        --enable-modules=all \
        --enable-mods-shared=all \
        --enable-so \
        --enable-suexec \
        --with-suexec-caller=http \
        --with-suexec-docroot=/srv/http \
        --with-suexec-logfile=/var/log/httpd/suexec.log \
        --with-suexec-bin=/usr/sbin/suexec \
        --with-suexec-uidmin=99 --with-suexec-gidmin=99 \
        --enable-ldap --enable-authnz-ldap \
        --enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
        --enable-ssl --with-ssl \
        --enable-deflate --enable-cgid \
        --enable-proxy --enable-proxy-connect \
        --enable-proxy-http --enable-proxy-ftp \
        --enable-dbd \
        --with-apr=/usr/bin/apr-1-config \
        --with-apr-util=/usr/bin/apu-1-config \
        --with-pcre=/usr 
        #--with-mpm="${mpm}"

    make
}

package() {
  cd httpd-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/httpd.logrotate ${pkgdir}/etc/logrotate.d/httpd

  install -D -m644 ${srcdir}/httpd-tmpfile.conf ${pkgdir}/usr/lib/tmpfiles.d/httpd.conf
  install -d ${pkgdir}/usr/lib/systemd/system
  install -D -m644 ${srcdir}/httpd.service ${pkgdir}/usr/lib/systemd/system/
  
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  ln -fs /usr/lib/httpd/modules ${pkgdir}/etc/httpd/modules

  # set defaults
  sed -e 's#/usr/lib/httpd/modules/#modules/#' \
	    -e 's|#\(LoadModule negotiation_module \)|\1|' \
	    -e 's|#\(LoadModule include_module \)|\1|' \
	    -e 's|#\(LoadModule userdir_module \)|\1|' \
	    -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
	    -i ${pkgdir}/etc/httpd/conf/httpd.conf

  # cleanup
  rm -rf ${pkgdir}/usr/share/httpd/manual
  rm -rf ${pkgdir}/etc/httpd/conf/original
  rm -rf ${pkgdir}/srv/
  rm -rf ${pkgdir}/run
}
