
pkgname=apache
pkgver=2.4.17
pkgrel=1
pkgdesc='A high performance Unix-based HTTP server'
arch=('x86_64')
options=('!libtool')
url='http://www.apache.org/dist/httpd'
license=('Apache')
backup=(etc/httpd/conf/httpd.conf
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/logrotate.d/httpd)
depends=('openssl' 'zlib' 'apr-util' 'pcre' 'systemd')
makedepends=('apr')
install=${pkgname}.install
source=("http://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2"
        'httpd.logrotate'
        'kaos.layout'
        'httpd.service'
        'httpd-tmpfile.conf')
md5sums=('cf4dfee11132cde836022f196611a8b7'
         '13dbaaf949c5bc36cfcf5718b95cb020'
         '7511248625ccaaf3229a320167cdc5d9'
         'ed219c8228d4d5ae9b7a55107cf79706'
         '110a056408252de4356be7f53872b11d')

build() {
  cd "${srcdir}/httpd-${pkgver}"

  # apply the monolithic ITK patch
  # see http://mpm-itk.sesse.net/
  #patch -p1 -i "${srcdir}/apache2.4-mpm-itk-2.4.2-01.patch"

  # set default user
  sed -e 's#User daemon#User http#' \
      -e 's#Group daemon#Group http#' \
      -i docs/conf/httpd.conf.in 

  cat "${srcdir}/kaos.layout" >> config.layout

  #for mpm in prefork worker itk; do
   # if [ "${mpm}" = 'itk' ]; then
    #  mkdir -p server/mpm/experimental/itk
     # cp -r server/mpm/prefork/* server/mpm/experimental/itk/ 
      #mv server/mpm/experimental/itk/prefork.c server/mpm/experimental/itk/itk.c 
      #autoconf 
    #fi

    #mkdir -p "build-${mpm}"
    #pushd "build-${mpm}"

    "${srcdir}/httpd-${pkgver}/configure" \
        --enable-layout=KaOS \
        --enable-mpms-shared=all \
        --enable-modules=all \
        --enable-mods-shared=all \
        --enable-so \
        --enable-suexec \
        --with-suexec-caller=http \
        --with-suexec-docroot=/srv/http \
        --with-suexec-logfile=/var/log/httpd/suexec.log \
        --with-suexec-bin=/usr/sbin/suexec \
        --with-suexec-uidmin=99 --with-suexec-gidmin=99 \
        --enable-ldap --enable-authnz-ldap \
        --enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
        --enable-ssl --with-ssl \
        --enable-deflate --enable-cgid \
        --enable-proxy --enable-proxy-connect \
        --enable-proxy-http --enable-proxy-ftp \
        --enable-dbd \
        --with-apr=/usr/bin/apr-1-config \
        --with-apr-util=/usr/bin/apu-1-config \
        --with-pcre=/usr 
        #--with-mpm="${mpm}"

    make

    #popd >/dev/null
 # done
}

package() {
  cd "${srcdir}/httpd-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/httpd.logrotate" "${pkgdir}/etc/logrotate.d/httpd"  

  # systemd
  install -D -m644 "${srcdir}/httpd-tmpfile.conf" "${pkgdir}/usr/lib/tmpfiles.d/httpd.conf"

  install -d "${pkgdir}"/usr/lib/systemd/system
  install -D -m644 "${srcdir}/httpd.service" "${pkgdir}/usr/lib/systemd/system/"
  
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # symlinks for /etc/httpd
  #ln -fs /var/log/httpd "${pkgdir}/etc/httpd/logs"
  #ln -fs /var/run/httpd "${pkgdir}/etc/httpd/run"
  ln -fs /usr/lib/httpd/modules "${pkgdir}/etc/httpd/modules"
  #ln -fs /usr/lib/httpd/build "${pkgdir}/etc/httpd/build"

  # set sane defaults
  sed -e 's#/usr/lib/httpd/modules/#modules/#' \
	    -e 's|#\(LoadModule negotiation_module \)|\1|' \
	    -e 's|#\(LoadModule include_module \)|\1|' \
	    -e 's|#\(LoadModule userdir_module \)|\1|' \
	    -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
	    -i "${pkgdir}/etc/httpd/conf/httpd.conf"

  # cleanup
  rm -rf "${pkgdir}/usr/share/httpd/manual"
  rm -rf "${pkgdir}/etc/httpd/conf/original"
  rm -rf "${pkgdir}/srv/"
  rm -rf "${pkgdir}/run"
}