
pkgname=apache
pkgver=2.4.46
pkgrel=1
pkgdesc='A high performance Unix-based HTTP server'
arch=('x86_64')
options=('!libtool')
url='https://www.apache.org/dist/httpd'
license=('Apache')
backup=(etc/httpd/conf/httpd.conf
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/logrotate.d/httpd)
depends=('openssl' 'zlib' 'apr-util' 'pcre' 'systemd')
makedepends=('apr')
install=${pkgname}.install
source=("https://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2"
        'httpd.logrotate'
        'kaos.layout'
        'httpd.service'
        'httpd-tmpfile.conf')
sha256sums=('740eddf6e1c641992b22359cabc66e6325868c3c5e2e3f98faf349b61ecf41ea'
            '0bbbfae23a917b2fce0bf8f900f60319b50769224a96314e9301a75ccd078e16'
            '39db659371f1058aad51e1ed9f1969169574ec5febc3fb917b959ebd087f90b8'
            'bbdc4be98cd9968b5cfd278b4b46ad58c8fe4a9e1eff80469828ca3f030fe511'
            '24c62bb791e79b626e81aea8b7995dc63de1fbc51761b7e6dd06b8c9024c391f')

build() {
  cd httpd-${pkgver}

  # set default user
  sed -e 's#User daemon#User http#' \
      -e 's#Group daemon#Group http#' \
      -i docs/conf/httpd.conf.in 
  
  # CVE-2017-9798
  #patch -p1 -i ${srcdir}/optionsbleed.patch

  cat ${srcdir}/kaos.layout >> config.layout

    ${srcdir}/httpd-${pkgver}/configure \
        --enable-layout=KaOS \
        --enable-mpms-shared=all \
        --enable-modules=all \
        --enable-mods-shared=all \
        --enable-so \
        --enable-suexec \
        --with-suexec-caller=http \
        --with-suexec-docroot=/srv/http \
        --with-suexec-logfile=/var/log/httpd/suexec.log \
        --with-suexec-bin=/usr/sbin/suexec \
        --enable-suexec-capabilites \
        --enable-ldap --enable-authnz-ldap \
        --enable-cache --enable-disk-cache --enable-mem-cache --enable-file-cache \
        --enable-ssl --with-ssl \
        --enable-deflate --enable-cgid \
        --enable-proxy --enable-proxy-connect \
        --enable-proxy-http --enable-proxy-ftp \
        --enable-dbd \
        --with-apr=/usr/bin/apr-1-config \
        --with-apr-util=/usr/bin/apu-1-config \
        --with-pcre=/usr 
        #--with-mpm="${mpm}"
        #--with-suexec-uidmin=99 --with-suexec-gidmin=99 \

    make
}

package() {
  cd httpd-${pkgver}
  make -j1 DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/httpd.logrotate ${pkgdir}/etc/logrotate.d/httpd

  install -D -m644 ${srcdir}/httpd-tmpfile.conf ${pkgdir}/usr/lib/tmpfiles.d/httpd.conf
  install -d ${pkgdir}/usr/lib/systemd/system
  install -D -m644 ${srcdir}/httpd.service ${pkgdir}/usr/lib/systemd/system/
  
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  ln -fs /usr/lib/httpd/modules ${pkgdir}/etc/httpd/modules

  # set defaults
  sed -e 's#/usr/lib/httpd/modules/#modules/#' \
	    -e 's|#\(LoadModule negotiation_module \)|\1|' \
	    -e 's|#\(LoadModule include_module \)|\1|' \
	    -e 's|#\(LoadModule userdir_module \)|\1|' \
	    -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
	    -i ${pkgdir}/etc/httpd/conf/httpd.conf

  # cleanup
  rm -rf ${pkgdir}/usr/share/httpd/manual
  rm -rf ${pkgdir}/etc/httpd/conf/original
  rm -rf ${pkgdir}/srv/
  rm -rf ${pkgdir}/run
}
