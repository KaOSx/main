
pkgbase=mesa
pkgname=('mesa' 'libgl')
pkgver=13.0.2
_pkgver=13.0.2
pkgrel=1
arch=('x86_64')
makedepends=('glproto' 'libdrm' 'libxxf86vm' 'libxdamage' 'libva' 'expat' 'llvm' 'libx11' 'libxt' 'gcc-libs'
             'dri2proto' 'python2' 'libxml2' 'imake' 'systemd' 'libvdpau' 'wayland' 'dri3proto'
             'presentproto' 'libxshmfence' 'elfutils' 'libomxil-bellagio' 'clang' 'python2-mako')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(${source[@]} "ftp://ftp.freedesktop.org/pub/mesa/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("mesa::git://anongit.freedesktop.org/mesa/mesa#branch=master"
       'LICENSE'
       "https://github.com/imirkin/mesa/commit/2e6b0e24cfb0f467e8b6d6f394730442a72dcdaf.diff"
       "https://github.com/imirkin/mesa/commit/2733e5483e1c2b80e4b0ae21187ec5e3e1579397.diff"
       "https://github.com/imirkin/mesa/commit/940b3a773f264f3f52574160f0d06c48f8e8aeb2.diff")
md5sums=('9442c2dee914cde3d1f090371ab04113'
         '5c65a0fe315dd347e09b1f2826a1df5a'
         'cc6590fe2678e6ba8e8e762d59634a8b'
         '1fd18456d593bb7ac55dd838a6202def'
         '663c505d69473c2563996812769c9ffd')
  
build() {
    cd ${pkgname}-${pkgver}
    # http://www.linuxfromscratch.org/blfs/view/svn/x/mesa.html 
    # libpthread-stubs package is useless on Linux
    sed -i -e 's|pthread_stubs_possible="yes"|pthread_stubs_possible="no"|' configure.ac
    
    # work in progress for nouveau fixes in qtwebengine
    patch -p1 -i ${srcdir}/2e6b0e24cfb0f467e8b6d6f394730442a72dcdaf.diff
    patch -p1 -i ${srcdir}/2733e5483e1c2b80e4b0ae21187ec5e3e1579397.diff
    patch -p1 -i ${srcdir}/940b3a773f264f3f52574160f0d06c48f8e8aeb2.diff
    
    ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,nouveau,radeonsi,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-vulkan-drivers=intel \
    --enable-gallium-llvm \
    --with-egl-platforms=x11,drm,wayland \
    --enable-llvm-shared-libs \
    --with-clang-libdir=/usr/lib \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-nine \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-egl \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-dri3 \
    --enable-omx 
    #--enable-openvg currently not ported yet with gallium-egl removal
    
  make
}

package_libgl() {
  depends=("mesa=${pkgver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/extensions

  ln -s libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
  
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d ${pkgdir}/usr/share/licenses/libgl
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/libgl/
}

package_mesa() {
  depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'dri2proto' 'glproto' 'wayland' 'elfutils'
           'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'libomxil-bellagio' 'llvm') 
  pkgdesc="Mesa 3-D graphics libraries and include files"
  provides=('libglapi''libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  conflicts=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  replaces=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')

  cd ${pkgname}-${pkgver}   
  #cd "${srcdir}/mesa"
  make DESTDIR=${pkgdir} install

  # rename libgl.so to make it possible to package separately 
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 	${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}
