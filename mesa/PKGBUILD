
pkgbase=mesa
pkgname=('mesa' 'libgl')
pkgver=17.1.8
_pkgver=17.1.8
pkgrel=1
arch=('x86_64')
makedepends=('glproto' 'libdrm' 'libxxf86vm' 'libxdamage' 'libva' 'expat' 'llvm' 'libx11' 'libxt' 'gcc-libs'
             'dri2proto' 'python2' 'libxml2' 'imake' 'systemd' 'libvdpau' 'wayland' 'dri3proto'
             'presentproto' 'libxshmfence' 'elfutils' 'libomxil-bellagio' 'clang' 'python2-mako'
             'lm_sensors' 'libunwind')
url="https://www.mesa3d.org/"
license=('custom')
source=("https://mesa.freedesktop.org/archive/${pkgname}-${pkgver}.tar.xz"
#source=("mesa::git://anongit.freedesktop.org/mesa/mesa#branch=master"
       'LICENSE'
       'nouveau_locking_1.patch'
       'nouveau_locking_2.patch'
       'nouveau_locking_3.patch')
sha256sums=('75ed2eaeae26ddd536150f294386468ae2e1a7717948c41cd14b7875be5269db'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2'
            '3ed2b455e71666862609035f8111e4dd710fe2d87040d3f5b526bd774706e178'
            '5532d795b32ebaf875d75a2d76059fef4f88da0efab14855dc797d9b2a2df6cf'
            '752a293974781bb7c57f2631d5f5282be30c75a1c625dfa90cf4ae6537d8b54d')
  
build() {
    cd ${pkgname}-${pkgver}
    # http://www.linuxfromscratch.org/blfs/view/svn/x/mesa.html 
    # libpthread-stubs package is useless on Linux
    sed -i -e 's|pthread_stubs_possible="yes"|pthread_stubs_possible="no"|' configure.ac
    
    # work in progress for nouveau fixes in qtwebengine
    patch -p1 -i ${srcdir}/nouveau_locking_1.patch
    patch -p1 -i ${srcdir}/nouveau_locking_2.patch
    patch -p1 -i ${srcdir}/nouveau_locking_3.patch
    
    ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,nouveau,radeonsi,svga,swrast,virgl,swr \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-vulkan-drivers=intel,radeon \
    --enable-llvm \
    --with-platforms=x11,drm,wayland \
    --enable-llvm-shared-libs \
    --with-clang-libdir=/usr/lib \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-nine \
    --enable-gallium-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-egl \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-dri3 \
    --enable-omx \
    --enable-lmsensors
    #--enable-openvg currently not ported yet with gallium-egl removal
    
  make
}

package_libgl() {
  depends=("mesa=${pkgver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/extensions

  ln -s libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
  
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d ${pkgdir}/usr/share/licenses/libgl
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/libgl/
}

package_mesa() {
  depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'dri2proto' 'glproto' 'wayland' 'elfutils'
           'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'libomxil-bellagio' 'llvm'
           'lm_sensors' 'libunwind') 
  pkgdesc="Mesa 3-D graphics libraries and include files"
  provides=('libglapi''libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  conflicts=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  replaces=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')

  cd ${pkgname}-${pkgver}   
  #cd "${srcdir}/mesa"
  make DESTDIR=${pkgdir} install

  # rename libgl.so to make it possible to package separately 
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 	${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}
