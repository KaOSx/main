
pkgbase=mesa
pkgname=('mesa' 'libgl')
pkgver=19.2.5
_pkgver=19.2.5
pkgrel=1
arch=('x86_64')
makedepends=('xorgproto' 'libdrm' 'libxxf86vm' 'libxdamage' 'libva' 'expat' 'llvm' 'libx11' 'libxt' 'gcc-libs'
             'libxml2' 'imake' 'systemd' 'libvdpau' 'wayland' 
             'libxshmfence' 'elfutils' 'libomxil-bellagio' 'libxvmc' 'clang' 'python3-mako'
             'lm_sensors' 'libunwind' 'wayland-protocols' 'meson' 'ninja' 'libxrandr')
url="https://www.mesa3d.org/"
license=('custom')
source=("https://mesa.freedesktop.org/archive/${pkgname}-${pkgver}.tar.xz"
#source=("mesa::git://anongit.freedesktop.org/mesa/mesa#branch=master"
       'LICENSE')
sha256sums=('3d010a366b28d10bdd71e32091d8684baf1522e6466c5c5703667091b2108c8b'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')
  
build() {
    mkdir -p build
    cd build
  
  # use order in meson_options.txt to list build options, fill options from meson.build
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Db_ndebug=true \
    -Dplatforms=x11,wayland,drm,surfaceless \
    -Ddri3=true \
    -Ddri-drivers=i915,i965,r200,nouveau \
    -Dgallium-drivers=r300,r600,nouveau,radeonsi,svga,swr,swrast,virgl \
    -Dgallium-extra-hud=true \
    -Dgallium-vdpau=true \
    -Dgallium-xvmc=true \
    -Dgallium-omx=bellagio \
    -Dgallium-va=true \
    -Dgallium-xa=true \
    -Dgallium-nine=true \
    -Dgallium-opencl=disabled \
    -Dvulkan-drivers=amd,intel \
    -Dvulkan-overlay-layer=false \
    -Dshared-glapi=true \
    -Dgles1=true \
    -Dgles2=true \
    -Dopengl=true \
    -Dgbm=true \
    -Dglx=dri \
    -Degl=true \
    -Dglvnd=false \
    -Dllvm=true \
    -Dvalgrind=false \
    -Dlibunwind=true \
    -Dlmsensors=true \
    -Dbuild-tests=false \
    -Dosmesa=gallium \
    -Dswr-arches=avx,avx2 
    ninja
}

package_libgl() {
  depends=("mesa=${pkgver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/extensions

  ln -s libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
  
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d ${pkgdir}/usr/share/licenses/libgl
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/libgl/
}

package_mesa() {
  depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
           'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'libomxil-bellagio' 'llvm'
           'lm_sensors' 'libunwind' 'libxvmc' 'libxrandr') 
  pkgdesc="Mesa 3-D graphics libraries and include files"
  provides=('libglapi''libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  conflicts=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')
  replaces=('libglapi' 'libgles' 'libegl' 'khrplatform-devel' 'ati-dri' 'intel-dri' 'svga-dri' 'nouveau-dri')

  cd build  
  DESTDIR=${pkgdir} ninja install

  # rename libgl.so to make it possible to package separately 
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 	${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}
