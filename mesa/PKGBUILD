
pkgbase=mesa
pkgname=('mesa' 'opencl-clover')
pkgver=24.2.5
_pkgver=24.2.5
pkgrel=1
arch=('x86_64')
pkgdesc="Mesa 3-D graphics libraries and include files"
makedepends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
             'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'llvm'
             'lm_sensors' 'libunwind' 'libxvmc' 'libxrandr' 'libglvnd' 'zstd'
             'libva' 'llvm' 'libxml2' 'imake' 'clang' 'python3-mako' 'glslang'
             'wayland-protocols' 'meson' 'ninja' 'valgrind' 'vulkan-loader'
             'python3-ply' 'libclc' 'spirv-llvm-translator' 'pyyaml')
url="https://www.mesa3d.org/"
license=('custom')
source=("https://mesa.freedesktop.org/archive/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/pzanoni/mesa/-/commit/68e89401140d1b3a17052899c54577de3f008b67.diff"
        'LICENSE')
sha256sums=('733d0bea242ed6a5bb5c806fe836792ce7f092d45a2f115b7b7e15897c9dd96f'
            '52ffabfb10d822a14dd73490209e8917332da431a41fde051d022e3ef4b363e7'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

prepare() {
    cd ${pkgname}-${pkgver}

    #patch -R -p1 -i ${srcdir}/68e89401140d1b3a17052899c54577de3f008b67.diff
}

build() {
    mkdir -p build
    cd build

  # use order in meson_options.txt to list build options, fill options from meson.build
  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Db_ndebug=true \
    -Dplatforms=x11,wayland \
    -Ddri3=enabled \
    -Dgallium-drivers=iris,r300,r600,crocus,d3d12,nouveau,radeonsi,svga,llvmpipe,softpipe,virgl,zink \
    -Dgallium-extra-hud=true \
    -Dgallium-vdpau=enabled \
    -Dgallium-omx=disabled \
    -Dgallium-va=enabled \
    -Dgallium-xa=enabled \
    -Dgallium-nine=true \
    -Dgallium-opencl=icd \
    -Dvulkan-drivers=amd,intel,intel_hasvk,swrast,virtio \
    -Dvulkan-layers=device-select,intel-nullhw,overlay \
    -Dshared-glapi=enabled \
    -Dgles1=enabled \
    -Dgles2=enabled \
    -Dopengl=true \
    -Dgbm=enabled \
    -Dglx=dri \
    -Degl=enabled \
    -Dglvnd=enabled \
    -Dllvm=enabled \
    -Dvalgrind=disabled \
    -Dlibunwind=enabled \
    -Dlmsensors=enabled \
    -Dbuild-tests=false \
    -Dosmesa=true \
    -Dvideo-codecs=all \
    -Dintel-clc=enabled \
    -Dmicrosoft-clc=disabled \
    -Dplatforms=x11,wayland
    ninja
}

package_mesa() {
  pkgdesc="Mesa 3-D graphics libraries and include files"
  depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
           'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'llvm'
           'lm_sensors' 'libunwind' 'libxvmc' 'libxrandr' 'libglvnd' 'zstd')

  cd build
  DESTDIR=${pkgdir} ninja install

  # package in opencl-mesa
  rm -r ${pkgdir}/etc
  rm -r ${pkgdir}/usr/lib/libMesaOpenCL.so*
  rm -r ${pkgdir}/usr/lib/gallium-pipe

  install -dm755 ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}

package_opencl-clover() {
  pkgdesc="Mesa OpenCL implementation for AMD GPUs"
  depends=('clang' 'mesa' 'libclc' 'spirv-llvm-translator')

  cd build/src/gallium/targets/opencl

  install -dm755 ${pkgdir}/etc/OpenCL/vendors
  install -m644 mesa.icd ${pkgdir}/etc/OpenCL/vendors/mesa.icd

  install -dm755 ${pkgdir}/usr/lib
  # exclude directory
  rm -r libMesaOpenCL.so.1.0.0.p
  cp -a libMesaOpenCL.so* ${pkgdir}/usr/lib/

  cd ../pipe-loader

  install -dm755 ${pkgdir}/usr/lib/gallium-pipe
  install -m644 pipe*.so ${pkgdir}/usr/lib/gallium-pipe/

  install -dm755 ${pkgdir}/usr/share/licenses/opencl-mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/opencl-mesa/
}
