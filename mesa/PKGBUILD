
pkgname=mesa
pkgver=20.2.3
_pkgver=20.2.3
pkgrel=1
arch=('x86_64')
pkgdesc="Mesa 3-D graphics libraries and include files"
depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
           'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'libomxil-bellagio' 'llvm'
           'lm_sensors' 'libunwind' 'libxvmc' 'libxrandr' 'libglvnd' 'zstd') 
makedepends=('libva' 'llvm' 'libxml2' 'imake' 'clang' 'python3-mako'
             'wayland-protocols' 'meson' 'ninja')
url="https://www.mesa3d.org/"
license=('custom')
source=("https://mesa.freedesktop.org/archive/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/mesa/mesa/commit/689817c9dfde9a0852f2b2489cb0fa93ffbcb215.diff"
        'LICENSE')
sha256sums=('ae1b240e11531df528d14dc214d2dc4d2b4f2e835c6230ba0b492b171eceb82b'
            'fe619c39cdcbff3e4d79ec673e35f6eceb8bcab56113868277ff8295a2623702'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

prepare() {
    cd ${pkgname}-${pkgver}
    
    #patch -p1 -i ${srcdir}/689817c9dfde9a0852f2b2489cb0fa93ffbcb215.diff
}

build() {
    mkdir -p build
    cd build
  
  # use order in meson_options.txt to list build options, fill options from meson.build
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Db_ndebug=true \
    -Dplatforms=x11,wayland \
    -Ddri3=true \
    -Ddri-drivers=i915,i965,r200,nouveau \
    -Dgallium-drivers=iris,r300,r600,nouveau,radeonsi,svga,swr,swrast,virgl \
    -Dgallium-extra-hud=true \
    -Dgallium-vdpau=enabled \
    -Dgallium-xvmc=enabled \
    -Dgallium-omx=bellagio \
    -Dgallium-va=enabled \
    -Dgallium-xa=enabled \
    -Dgallium-nine=true \
    -Dgallium-opencl=disabled \
    -Dvulkan-drivers=amd,intel \
    -Dvulkan-overlay-layer=false \
    -Dvulkan-device-select-layer=false \
    -Dshared-glapi=enabled \
    -Dgles1=enabled \
    -Dgles2=enabled \
    -Dopengl=true \
    -Dgbm=enabled \
    -Dglx=dri \
    -Degl=enabled \
    -Dglvnd=true \
    -Dllvm=enabled \
    -Dvalgrind=disabled \
    -Dlibunwind=enabled \
    -Dlmsensors=enabled \
    -Dbuild-tests=false \
    -Dosmesa=gallium \
    -Dswr-arches=avx,avx2 
    ninja
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}
