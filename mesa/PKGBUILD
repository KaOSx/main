
pkgname=mesa
pkgver=23.3.4
_pkgver=23.3.4
pkgrel=1
arch=('x86_64')
pkgdesc="Mesa 3-D graphics libraries and include files"
depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
         'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'llvm'
         'lm_sensors' 'libunwind' 'libxvmc' 'libxrandr' 'libglvnd' 'zstd') 
makedepends=('libva' 'llvm' 'libxml2' 'imake' 'clang' 'python3-mako' 'glslang'
             'wayland-protocols' 'meson' 'ninja' 'valgrind' 'vulkan-loader')
url="https://www.mesa3d.org/"
license=('custom')
source=("https://mesa.freedesktop.org/archive/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/pzanoni/mesa/-/commit/68e89401140d1b3a17052899c54577de3f008b67.diff"
        'LICENSE')
sha256sums=('df12d765be4650fe532860b18aa18e6da1d0b07d1a21dfdfe04660e6b7bac39a'
            '52ffabfb10d822a14dd73490209e8917332da431a41fde051d022e3ef4b363e7'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

prepare() {
    cd ${pkgname}-${pkgver}

    #patch -R -p1 -i ${srcdir}/68e89401140d1b3a17052899c54577de3f008b67.diff
}

build() {
    mkdir -p build
    cd build

  # use order in meson_options.txt to list build options, fill options from meson.build
  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Db_ndebug=true \
    -Dplatforms=x11,wayland \
    -Ddri3=enabled \
    -Dgallium-drivers=iris,r300,r600,crocus,d3d12,nouveau,radeonsi,svga,swrast,virgl,zink \
    -Dgallium-extra-hud=true \
    -Dgallium-vdpau=enabled \
    -Dgallium-omx=disabled \
    -Dgallium-va=enabled \
    -Dgallium-xa=enabled \
    -Dgallium-nine=true \
    -Dgallium-opencl=disabled \
    -Dvulkan-drivers=amd,intel,intel_hasvk,swrast,virtio \
    -Dvulkan-layers=device-select,intel-nullhw,overlay \
    -Dshared-glapi=enabled \
    -Dgles1=enabled \
    -Dgles2=enabled \
    -Dopengl=true \
    -Dgbm=enabled \
    -Dglx=dri \
    -Degl=enabled \
    -Dglvnd=true \
    -Dllvm=enabled \
    -Dvalgrind=disabled \
    -Dlibunwind=enabled \
    -Dlmsensors=enabled \
    -Dbuild-tests=false \
    -Dosmesa=true \
    -Dvideo-codecs=vc1dec,h264dec,h264enc,h265dec,h265enc
    ninja
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa/
}
