
pkgname=xorg-mkfontscale
pkgver=1.2.1
pkgrel=1
pkgdesc="Create an index of scalable font files for X"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc')
makedepends=('xorg-util-macros' 'xorgproto')
provides=('xorg-mkfontdir')
replaces=('xorg-mkfontdir')
conflicts=('xorg-mkfontdir')
groups=('xorg-apps' 'xorg')
source=("https://xorg.freedesktop.org/archive/individual/app/mkfontscale-${pkgver}.tar.bz2")
sha256sums=('ca0495eb974a179dd742bfa6199d561bda1c8da4a0c5a667f21fd82aaab6bac7')

build() {
  cd mkfontscale-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package() {
  cd mkfontscale-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
