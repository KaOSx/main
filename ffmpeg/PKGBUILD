
pkgname=ffmpeg
pkgver=3.2
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('x86_64')
url="http://ffmpeg.org/"
license=('GPL')
depends=('bzip2' 'lame' 'sdl' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'celt' 'fribidi'
         'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'schroedinger' 'libvpx>=1.1.0' 'libwebp'
         'libva' 'openjpeg' 'rtmpdump' 'speex' 'gsm' 'pulseaudio' 'v4l-utils' 'gnutls' 'x265'
         'libass' 'dcadec')
makedepends=('yasm')
source=("http://ffmpeg.org/releases/${pkgname}-${pkgver}.tar.xz")
md5sums=('3c065fb5baae1aeb1494a09ac984b2de')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --enable-avisynth \
              --enable-avresample \
              --enable-libmp3lame \
              --enable-libvorbis \
              --enable-libxvid \
              --enable-libx264 \
              --enable-libx265 \
              --enable-libvpx \
              --enable-libtheora \
              --enable-libgsm \
              --enable-postproc \
              --enable-shared \
              --enable-x11grab \
              --enable-libopencore_amrnb \
              --enable-libopencore_amrwb \
              --enable-libschroedinger \
              --enable-libopenjpeg \
              --enable-librtmp \
              --enable-libpulse \
              --enable-gpl \
              --enable-gnutls \
              --enable-vdpau \
              --enable-version3 \
              --enable-runtime-cpudetect \
              --enable-swresample \
              --enable-libspeex \
              --enable-libv4l2 \
              --enable-libcelt \
              --enable-libfribidi \
              --enable-libass \
              --disable-debug \
              --disable-static

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install install-man
  install -D -m755 tools/qt-faststart ${pkgdir}/usr/bin/qt-faststart
}
