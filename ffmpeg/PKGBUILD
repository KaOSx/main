
# rebuild mpv on any update
pkgname=ffmpeg
pkgver=4.4.1
pkgrel=4
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('x86_64')
url="https://ffmpeg.org/"
license=('GPL')
depends=('bzip2' 'lame' 'sdl2' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'fribidi'
         'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'libvpx' 'libwebp'
         'libva' 'openjpeg' 'rtmpdump' 'speex' 'gsm' 'pulseaudio' 'v4l-utils' 'gnutls' 'x265'
         'libass' 'dcadec' 'opus' 'dav1d' 'avisynthplus' 'samba' 'librsvg' 'vulkan-loader')
makedepends=('yasm' 'vulkan-headers')
source=("https://ffmpeg.org/releases/${pkgname}-${pkgver}.tar.xz"
        'openjpeg2.2.patch')
md5sums=('41a1ca8693acc56088e8eaf3899b91ea'
         'e70179f9572035a655c297a2892fae08')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/openjpeg2.2.patch

  ./configure --prefix=/usr \
              --enable-avisynth \
              --enable-libmp3lame \
              --enable-libvorbis \
              --enable-libxvid \
              --enable-libx264 \
              --enable-libx265 \
              --enable-libvpx \
              --enable-libtheora \
              --enable-libgsm \
              --enable-postproc \
              --enable-shared \
              --enable-libopencore_amrnb \
              --enable-libopencore_amrwb \
              --enable-libopus \
              --enable-libopenjpeg \
              --enable-librtmp \
              --enable-libpulse \
              --enable-gpl \
              --enable-gnutls \
              --enable-vdpau \
              --enable-version3 \
              --enable-runtime-cpudetect \
              --enable-swresample \
              --enable-libspeex \
              --enable-libv4l2 \
              --enable-libfribidi \
              --enable-libass \
              --enable-libdav1d \
              --enable-librsvg \
              --enable-libsmbclient \
              --enable-vulkan \
              --disable-debug \
              --disable-static

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play}.1
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install install-man
  install -D -m755 tools/qt-faststart ${pkgdir}/usr/bin/qt-faststart
}
