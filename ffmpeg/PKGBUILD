
# rebuild mpv on any update
pkgname=ffmpeg
pkgver=3.4.2
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('x86_64')
url="https://ffmpeg.org/"
license=('GPL')
depends=('bzip2' 'lame' 'sdl2' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'celt' 'fribidi'
         'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'libvpx>=1.7.0' 'libwebp'
         'libva' 'openjpeg' 'rtmpdump' 'speex' 'gsm' 'pulseaudio' 'v4l-utils' 'gnutls' 'x265'
         'libass' 'dcadec' 'opus')
makedepends=('yasm')
source=("https://ffmpeg.org/releases/${pkgname}-${pkgver}.tar.xz"
        'openjpeg2.2.patch')
md5sums=('cbf4ead227fcedddf54c86013705a988'
         'e70179f9572035a655c297a2892fae08')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/openjpeg2.2.patch

  ./configure --prefix=/usr \
              --enable-avisynth \
              --enable-avresample \
              --enable-libmp3lame \
              --enable-libvorbis \
              --enable-libxvid \
              --enable-libx264 \
              --enable-libx265 \
              --enable-libvpx \
              --enable-libtheora \
              --enable-libgsm \
              --enable-postproc \
              --enable-shared \
              --enable-libopencore_amrnb \
              --enable-libopencore_amrwb \
              --enable-libopus \
              --enable-libopenjpeg \
              --enable-librtmp \
              --enable-libpulse \
              --enable-gpl \
              --enable-gnutls \
              --enable-vdpau \
              --enable-version3 \
              --enable-runtime-cpudetect \
              --enable-swresample \
              --enable-libspeex \
              --enable-libv4l2 \
              --enable-libcelt \
              --enable-libfribidi \
              --enable-libass \
              --disable-debug \
              --disable-static

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install install-man
  install -D -m755 tools/qt-faststart ${pkgdir}/usr/bin/qt-faststart
}
