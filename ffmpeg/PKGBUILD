
pkgname=ffmpeg
pkgver=1.2.5
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('x86_64')
url="http://ffmpeg.org/"
license=('GPL')
depends=('bzip2' 'lame' 'sdl' 'libvorbis' 'xvidcore' 'zlib' 'x264' 'libtheora' 'celt'
         'opencore-amr' 'alsa-lib' 'libvdpau' 'libxfixes' 'schroedinger' 'libvpx>=1.1.0'
         'libva' 'openjpeg' 'rtmpdump' 'speex' 'gsm' 'pulseaudio' 'v4l-utils')
makedepends=('yasm')
source=("http://${pkgname}.org/releases/${pkgname}-${pkgver}.tar.bz2")
md5sums=('57e04df3c2da3e1d42341313ad5cb642')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libmp3lame \
              --enable-libvorbis \
              --enable-libxvid \
              --enable-libx264 \
              --enable-libvpx \
              --enable-libtheora \
              --enable-libgsm \
              --enable-postproc \
              --enable-shared \
              --enable-x11grab \
              --enable-libopencore_amrnb \
              --enable-libopencore_amrwb \
              --enable-libschroedinger \
              --enable-libopenjpeg \
              --enable-librtmp \
              --enable-libpulse \
              --enable-gpl \
              --enable-vdpau \
              --enable-version3 \
              --enable-runtime-cpudetect \
              --enable-libspeex \
              --enable-libv4l2 \
              --enable-libcelt \
              --disable-debug \
              --disable-static

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-man
  install -D -m755 tools/qt-faststart "${pkgdir}/usr/bin/qt-faststart"
}
