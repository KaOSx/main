
# odd numbers rls skip....
pkgname=pixman
pkgver=0.43.0
pkgrel=1
pkgdesc="Image processing and manipulation library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pixman/pixman"
license=('custom')
depends=('glibc')
makedepends=('meson' 'ninja' 'libpng')
options=('!libtool')
source=("https://www.x.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5ba00c2879932c32d2ef878b6e802e3f898dda14c07e881dcfcc6a8f670e0cdc')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgtk=disabled
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

