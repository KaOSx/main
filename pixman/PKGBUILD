
# odd numbers rls skip....
pkgname=pixman
pkgver=0.44.0
pkgrel=1
pkgdesc="Image processing and manipulation library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pixman/pixman"
license=('custom')
depends=('glibc')
makedepends=('meson' 'ninja' 'libpng')
options=('!libtool')
source=("https://www.x.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ea55473db5ec9f068bbf4d14ec3186b742804bf74fdc782fc89aa87d2656fc46')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgtk=disabled
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

