
pkgname=libmicrohttpd
pkgver=0.9.33
pkgrel=1
pkgdesc="Small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
depends=('gnutls')
install=libmicrohttpd.install
options=('!libtool')
source=("ftp://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz")
md5sums=('013b10f9de1cda5448b29c81305354a3')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --disable-dependency-tracking \
              --enable-largefile \
	      --enable-curl \
	      --enable-messages \
	      --with-pic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/include/platform.h \
	${pkgdir}/usr/include/$pkgname/platform.h
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/include/plibc/plibc.h \
	${pkgdir}/usr/include/$pkgname/plibc.h
  sed -i 's#Cflags: -I${includedir}#Cflags: -I${includedir} -I{includedir}/libmicrohttpd#' $pkgdir/usr/lib/pkgconfig/libmicrohttpd.pc
}
