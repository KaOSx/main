
# rebuild kodi on every update
pkgname=libmicrohttpd
pkgver=0.9.71
pkgrel=1
pkgdesc="Small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
url="https://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
depends=('gnutls' 'nettle')
options=('!libtool')
source=("https://ftp.gnu.org/gnu/libmicrohttpd/${pkgname}-${pkgver}.tar.gz")
md5sums=('c7e230e9b899282b43d9a9faa41e893d')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
              --disable-dependency-tracking \
              --enable-largefile \
              --enable-curl \
              --enable-messages \
              --with-pic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 src/include/platform.h ${pkgdir}/usr/include/$pkgname/platform.h
  
  sed -i 's|Cflags: -I${includedir}|Cflags: -I${includedir} -I{includedir}/libmicrohttpd|' ${pkgdir}/usr/lib/pkgconfig/libmicrohttpd.pc
}
