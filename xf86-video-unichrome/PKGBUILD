#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=xf86-video-unichrome
pkgver=0.2.7
pkgrel=5
_gitversion=cd12cce88ff886031c23c743569fba97eccace4e
pkgdesc="Unichrome video drivers for X.Org"
arch=('i686' 'x86_64')
url="http://unichrome.sf.net/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server' 'libx11' 'libdrm' 'xf86driproto' 'mesa' 'libxvmc')
options=('!libtool')
conflicts=('xf86-video-via' 'openchrome' 'xf86-video-openchrome' 'xorg-server<1.10.1')
source=("http://cgit.freedesktop.org/~libv/${pkgname}/snapshot/${pkgname}-${_gitversion}.tar.bz2")
md5sums=('c64332e6c386ed9a580116e131f288f4')

build() {
  cd "${srcdir}/${pkgname}-${_gitversion}"

  ./autogen.sh --prefix=/usr --disable-dri
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_gitversion}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
