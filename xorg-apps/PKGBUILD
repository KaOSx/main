
pkgname=xorg-apps
pkgver=7.11
pkgrel=1
pkgdesc="Various X.Org applications"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcursor' 'libxkbfile' 'libpng' 'libxft' 'libfontenc' 'libxaw')
makedepends=('pkgconfig' 'xbitmaps' 'xorg-util-macros')
source=("https://xorg.freedesktop.org/releases/individual/app/oclock-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/luit-1.1.1.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xclock-1.0.7.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xpr-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xwd-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xwud-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/x11perf-1.6.0.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xbiff-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xclipboard-1.1.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xconsole-1.0.7.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xcursorgen-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xeyes-1.1.1.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xkill-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xload-1.1.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xlogo-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xmag-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xmessage-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xcalc-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xman-1.1.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xedit-1.2.2.tar.bz2"
        'LICENSE'
        'luit.patch')
sha256sums=('cc20cfc1c0d5128d89e1eecd9ba52e58f111075be163e0aeda5690dbaafbaefe'
            '30b0e787cb07a0f504b70f1d6123930522111ce9d4276f6683a69b322b49c636'
            '23ceeca94e3e20a6c26a703ac7f789066d4517f8d2cb717ae7cb28a617d97dd0'
            'fed98df31eb93d3dca4688cb535aabad06be572e70ace3b1685679c18dd86cb5'
            '3bb396a2268d78de4b1c3e5237a85f7849d3434e87b3cd1f4d57eef614227d79'
            'd6b3a09ccfe750868e26bd2384900ab5ff0d434f7f40cd272a50eda8aaa1f8bd'
            'e87098dec1947572d70c62697a7b70bde1ab5668237d4660080eade6bc096751'
            'aa1db4f31f7afcfe131bda7ff6e380ff7de41cf1a7009464783043c817c47ce8'
            '9c0e0a1f4f1d49f73c6cf3ccd27b5ca0e18afc63679f1a1ee466b1d82446ebb5'
            'd1144882df207378381bc83927906559defbf7b83a01d52c50ad44234c5851e0'
            '31c8910f54eb175a8a74a60e7662697467e21a8bf948220a6048a93924b3f66c'
            '975e98680cd59e1f9439016386609546ed08c284d0f05a95276f96aca6e8a521'
            '88ef2a304f32f24b255e879f03c1dcd3a2be3e71d5562205414f267d919f812e'
            '83f8e6260435f1df2e2e5036bb3325688b79f0b33069ef445eff5058d127e078'
            '17af6f4fb73e7310e087fbfe5d773bab2b0d6f37a894ae5ee32de4693af6ba92'
            '87a2bc23b251e2d8f8370d3283a4d6c8dac98a30cb5749a04336cdb55c14e161'
            'bcdf4b461c439bb3ade6e1e41c47d6218b912da8e9396b7ad70856db2f95ab68'
            '2f73c7160c09dc32586ea07daa408ac897c0a16eaa98cad9f9e4ee98cd9057d8'
            'b3fa9d2e3f3745c529eb0b6ee4f0e00d936457d11e5221b2efad06ff7c84df29'
            '69aa42885dfc06332ca22eb01cc7187e49206e6d65b74113a8ee4cc345fc2927'
            '0b874779b97891979c7b6ffc24bc6e23a5518e5fec7229c71e7d8e2c63e38a81'
            '633f8cb6a9c273b51e0dee1f747d1681a4faba7c43d16a28efe544ec940e6eb5')

build() {
  cd "${srcdir}"
  patch -p0 -i $srcdir/luit.patch
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      # remove disable error on luit fix upstream
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-selective-werror
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"    
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

