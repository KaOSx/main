
pkgname=hugin-tools
_pkgname=hugin
pkgver=2018.0
_pkgver=2018.0.0
pkgrel=1
pkgdesc="Hugin libs and tools to be used by Kipi-plugins"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('boost-libs' 'enblend-enfuse' 'exiv2' 'openexr' 'libpano13>=2.9.19'
         'lapack' 'desktop-file-utils' 'icu' 'libxt' 'lensfun' 'glew' 'image-exiftool')
makedepends=('zip' 'cmake' 'boost' 'mesa' 'tclap')
conflict=('hugin')
provides=('hugin')
source=("https://downloads.sourceforge.net/hugin/${_pkgname}-${_pkgver}.tar.bz2")
       # "http://downloads.sourceforge.net/boost/boost_1_55_0.tar.bz2")
md5sums=('91cb2c31409c9ef02fc86e17ec2fa068')
        # 'd6eef4b4cacb2183f2bf265a5a03a354')

build() {
  mkdir -p build 
  cd build

  # make it possible to compile all the hugin tools without GUis
  sed -i -e "s~FIND_PACKAGE(wxWidgets REQUIRED)~~g" ${srcdir}/${_pkgname}-${_pkgver}/CMakeLists.txt
  
  cmake ../${_pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LAPACK=yes \
    -DBUILD_HSI=no
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  rm -f ${pkgdir}/usr/share/applications/pto_gen.desktop 
}
