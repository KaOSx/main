
pkgname=usbmuxd
pkgver=1.1.0
pkgrel=6
pkgdesc="A USB multiplexing daemon."
url="http://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'GPL3')
depends=('libimobiledevice')
install='usbmuxd.install'
source=("http://cgit.sukimashita.com/usbmuxd.git/snapshot/usbmuxd-${pkgver}.tar.bz2"
        'usbmuxd.service'
        '39-usbmuxd.rules')
md5sums=('ffda2df9688384f780931b5c3ad183c2'
         'c846093c80108262d2fa442ee47433bf'
         'dc4b2893651149eccbf7b2c8467cd288')

build() {
    cd ${pkgname}-${pkgver}
    sed -i -e 's|udevrulesdir = /lib/udev/rules.d|udevrulesdir = /usr/lib/udev/rules.d|' Makefile.am

    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install  
	
    install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service
    install -m644 ${srcdir}/39-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/
}
