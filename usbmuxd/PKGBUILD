
pkgname=usbmuxd
pkgver=1.1.0
_commit=5a9601b831ad459e587f5812c3314b3598bdcdc0
pkgrel=10
pkgdesc="A USB multiplexing daemon."
url="http://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'GPL3')
depends=('libimobiledevice')
install='usbmuxd.install'
source=("https://github.com/libimobiledevice/usbmuxd/archive/${_commit}.zip"
        'usbmuxd.service'
        '39-usbmuxd.rules')
md5sums=('471041691a6410153e9310874fc6247f'
         'c846093c80108262d2fa442ee47433bf'
         'dc4b2893651149eccbf7b2c8467cd288')

build() {
    cd ${pkgname}-${_commit}
    sed -i -e 's|udevrulesdir = /lib/udev/rules.d|udevrulesdir = /usr/lib/udev/rules.d|' Makefile.am

    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd ${pkgname}-${_commit}
    make DESTDIR=${pkgdir} install  
	
    install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service
    install -m644 ${srcdir}/39-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/
}
