
pkgname=xf86-video-amdgpu
pkgver=18.0.1
pkgrel=2
pkgdesc="Video driver forked from the xf86-video-ati radeon driver and modified to support the amdgpu kernel driver via libdrm_amdgpu"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm' 'systemd' 'mesa') 
makedepends=('xorg-server' 'xorgproto')
#groups=('xorg-video-drivers')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('7484682ccb403b3ca9e26d1c980572f08cdfa3469e2b2c9a9affc3d51b52691b')

build() { 
    cd ${pkgname}-${pkgver}
    
    ./configure \
        --prefix=/usr \
        --enable-dependency-tracking \
        --enable-glamor
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 
 
    install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
