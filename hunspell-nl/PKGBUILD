pkgname=hunspell-nl
_pkgname=opentaal-hunspell
pkgver=2.20.19
pkgrel=12
pkgdesc="Dutch hunspell dictionaries"
arch=('x86_64')
url="https://www.opentaal.org/"
license=('BSD' 'custom:Creative Commons, Attribution 3.0 Unported')
depends=('hunspell')
makedepends=('unzip')
source=("https://github.com/OpenTaal/opentaal-hunspell/archive/${pkgver}.tar.gz")
#source=('https://sourceforge.net/projects/kaosx/files/sources/hunspell/OpenTaal-210G-LO.oxt')
md5sums=('719e4f4267e2a3a85444608dc11bc326')

package() {
  cd ${_pkgname}-${pkgver}

  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 nl.dic ${pkgdir}/usr/share/hunspell/nl_NL.dic
  install -m644 nl.aff ${pkgdir}/usr/share/hunspell/nl_NL.aff

  pushd ${pkgdir}/usr/share/hunspell/
  nl_NL_aliases="nl_AW nl_BE"
  for lang in $nl_NL_aliases; do
      ln -s nl_NL.aff $lang.aff
      ln -s nl_NL.dic $lang.dic
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README.md ${pkgdir}/usr/share/doc/${pkgname}
  install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
