#
# Platform Packages for Chakra, part of chakra-project.org
#
# Maintainer: Adrian Chaves Fernandez (Gallaecio) <adriyetichaves@gmail.com>

pkgname=python2-imaging
_pkgname=Imaging
pkgver=1.1.7
pkgrel=4
pkgdesc="PIL. Provides image processing capabilities for python"
arch=('x86_64')
url="http://www.pythonware.com/products/pil/index.htm"
license=('custom:"pil"')
depends=('python2' 'libjpeg' 'freetype2') 
makedepends=('tk')
optdepends=('tk')
provides=('python-imaging')
conflicts=('python-imaging')
replaces=('python-imaging')
source=("http://effbot.org/downloads/${_pkgname}-$pkgver.tar.gz")
md5sums=('fc14a54e1ce02a0225be8854bfba478e')

build() {
  cd $srcdir/${_pkgname}-$pkgver
  python2 setup.py build_ext
}

package() {
  cd $srcdir/${_pkgname}-$pkgver
  python2 setup.py install --root=$pkgdir
  install -dm755 $pkgdir/usr/include/python2.7/
  install -m644 -t $pkgdir/usr/include/python2.7/ libImaging/*.h 

  # do not have files ending in .py in /usr/bin
  for f in pildriver pilprint pilconvert pilfile pilfont; do
    mv $pkgdir/usr/bin/$f{.py,}
  done

  # Install license
  install -Dm644 $srcdir/${_pkgname}-$pkgver/README \
                 $pkgdir/usr/share/licenses/$pkgname/README
}
