
#http://news.php.net/group.php?group=php.announce
pkgname=php
pkgver=7.3.13
pkgrel=1
arch=('x86_64')
pkgdesc='An HTML-embedded scripting language'
license=('PHP')
url='https://secure.php.net/'
depends=('postgresql-libs' 'libldap' 'sqlite' 'unixodbc' 'net-snmp' 'enchant' 
         'freetds' 'libmcrypt' 'tidyhtml' 'aspell' 'libtool' 'libpng' 'icu' 'curl' 'libxslt' 'bzip2'
         'freetype2' 'harfbuzz' 'systemd' 'pcre2' 'libxml2' 'libxpm' 'libzip' 'gdbm' 'argon2' 'libsodium') #'libvpx'
makedepends=('imap' 'mariadb' 'postfix' 'file' 'libjpeg-turbo' 'openssl' 'db' 'gmp')
backup=('etc/php/php.ini'
        'etc/php/php-fpm.conf')
conflicts=('php-cgi'
         'php-fpm'
         'php-embed'
         'php-pear'
         'php-enchant'
         'php-gd'
         'php-intl'
         'php-ldap'
         'php-mcrypt'
         'php-mssql'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
replaces=('php-cgi'
         'php-fpm'
         'php-embed'
         'php-pear'
         'php-enchant'
         'php-gd'
         'php-intl'
         'php-ldap'
         'php-mcrypt'
         'php-mssql'
         'php-odbc'
         'php-pgsql'
         'php-pspell'
         'php-snmp'
         'php-sqlite'
         'php-tidy'
         'php-xsl')
source=("http://www.php.net/distributions/${pkgname}-${pkgver}.tar.xz"
        'php.ini.patch'
        'php-fpm.conf.in.patch'
        'php-fpm.service'
        'php-fpm.tmpfiles'
        'enchant2.patch')
sha256sums=('57ac55fe442d2da650abeb9e6fa161bd3a98ba6528c029f076f8bba43dd5c228'
            '07627f57bbc5559a63bfc77e21edd4e2a5d3aa15fed4d9dfe00e877dd31b7c9f'
            '0f60fe0bc69fb06663faeb7ca710e573cdfd32cbb686bf6ccf49af76c53883d0'
            '65af4ebac5a1912292c701cca0313c76bd30450ef00d39f449ff5e8cbcfb82ba'
            '640dba0d960bfeaae9ad38d2826d3f6b5d6c175a4d3e16664eefff29141faad5'
            '190998e5980c9a105c5bde4dd197f3aa0a97466640b71d184c6705894f754012')

prepare() {
	cd ${pkgname}-${pkgver}

	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch
	patch -p1 -i ${srcdir}/enchant2.patch
	
	sed -i -e 's|#include "buffio.h"|#include "tidybuffio.h"|' ext/tidy/tidy.c
}

build() {
    EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR
	#PEAR_INSTALLDIR=/usr/share/pear
	#export PEAR_INSTALLDIR

	cd ${pkgname}-${pkgver}
	
	./configure --config-cache \
		--prefix=/usr \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		--without-pear \
		--enable-zend-signals \
		--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-password-argon2 \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-fpm-systemd \
		--with-freetype-dir=/usr \
		--with-xpm-dir=/usr \
		--with-gd=shared \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-kerberos=/usr \
		--with-jpeg-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-libzip \
		--with-mhash \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysqli=shared,mysqlnd \
		--with-openssl \
		--with-pcre-regex=/usr \
		--with-pdo-dblib=shared,/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sodium=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		--enable-cli \
		--enable-cgi \
		--enable-fpm \
		--with-readline \
		--enable-pcntl \
		--with-fpm-user=http \
		--with-fpm-group=http \
		--enable-embed=shared 

	make
}
	
package() {
	cd ${pkgname}-${pkgver}
	make -j1 INSTALL_ROOT=${pkgdir} install
	#install -d -m755 ${pkgdir}/usr/share/pear
	# install php.ini
	install -D -m644 php.ini-production ${pkgdir}/etc/php/php.ini
	install -d -m755 ${pkgdir}/etc/php/conf.d/
	
	install -d -m755 ${pkgdir}/etc/php/fpm.d
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service

	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	rm -r ${pkgdir}/var/run
}
