
pkgname=opencv
pkgver=4.1.2
_pkgver=4.1.2
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="https://opencv.org/"
depends=('gst-plugins-base' 'openexr' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt5-base'
         'libgphoto2' 'libwebp') # 'protobuf')
makedepends=('pkgconfig' 'cmake' 'python3-numpy' 'python3-setuptools' 'eigen3' 'python3-sphinx' 'mesa' 'glew')
optdepends=('python3-numpy: Python 3.x interface' 'eigen3')
options=('!libtool')
source=("https://github.com/Itseez/opencv/archive/${_pkgver}.tar.gz"
        "opencv_contrib-${_pkgver}::https://github.com/Itseez/opencv_contrib/archive/${_pkgver}.tar.gz"
        "https://github.com/alalek/opencv/commit/24dbb43c096691d0333cacf231e22f1369f8c826.patch"
        "https://github.com/opencv/opencv_contrib/commit/c4419e4e65a8d9e0b5a15e9a5242453f261bee46.diff")
md5sums=('631f23b142665072969debbe39e1c958'
         'ad545ffe9ea32b0a8a718207e4c72438'
         'fea946fad4f25b47e8c8db37d5843863'
         'de1f1ad27a3da356706ba6a6fa74622b')

prepare () {
  cd opencv_contrib-${_pkgver}
  
  #patch -p1 -i ${srcdir}/c4419e4e65a8d9e0b5a15e9a5242453f261bee46.diff
}
         
build() {
  cd ${pkgname}-${_pkgver}
  # known Eigen3 issue upstream https://github.com/opencv/opencv/pull/15247
  #sed -e "s|include <Eigen/Core>|include <eigen3/Eigen/Core>|" -i modules/core/include/opencv2/core/private.hpp
 
  mkdir -p build 
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_SKIP_RPATH=ON \
        -DBUILD_TESTS=OFF \
        -DBUILD_EXAMPLES=ON \
        -DINSTALL_C_EXAMPLES=ON \
        -DINSTALL_PYTHON_EXAMPLES=ON \
        -DBUILD_opencv_python3=ON \
        -DWITH_XINE=OFF \
        -DWITH_QT=ON \
        -DWITH_OPENGL=ON \
        -DWITH_TBB=OFF \
        -DWITH_CUDA=OFF \
        -DWITH_GTK=OFF \
        -DENABLE_FAST_MATH=ON \
        -DOPENCV_EXTRA_MODULES_PATH=${srcdir}/opencv_contrib-${_pkgver}/modules \
        -DOPENCV_GENERATE_PKGCONFIG=ON \
        -DOPENCV_ENABLE_NONFREE=ON
  make
}

package() {
  cd ${pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  cd ..
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}


