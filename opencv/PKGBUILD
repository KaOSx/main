
pkgname=opencv
pkgver=4.5.4
_pkgver=4.5.4
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('Apache2')
url="https://opencv.org/"
depends=('gst-plugins-base' 'openexr' 'libdc1394' 'v4l-utils' 'ffmpeg' 'qt5-base'
         'libgphoto2' 'libwebp') # 'protobuf')
makedepends=('pkgconfig' 'cmake' 'python3-numpy' 'python3-setuptools' 'eigen3' 'python3-sphinx' 'mesa' 'glew')
optdepends=('python3-numpy: Python 3.x interface' 'eigen3')
options=('!libtool')
source=("https://github.com/Itseez/opencv/archive/${_pkgver}.tar.gz"
        "opencv_contrib-${_pkgver}::https://github.com/Itseez/opencv_contrib/archive/${_pkgver}.tar.gz"
        "https://github.com/opencv/opencv_contrib/commit/c4419e4e65a8d9e0b5a15e9a5242453f261bee46.diff")
md5sums=('83ea8262a74f205309514e44e589112d'
         '4d42f88e3be326bf140528b5ace64642'
         'ca884d27cca217f84e3b86dcd658bc69')

prepare () {
  cd opencv_contrib-${_pkgver}
  # https://github.com/opencv/opencv_contrib/issues/3063
  sed -e 's|HAVE_QT5|HAVE_QT|' -i modules/cvv/CMakeLists.txt
  #patch -p1 -i ${srcdir}/c4419e4e65a8d9e0b5a15e9a5242453f261bee46.diff
}
         
build() {
  cmake -B build -S ${pkgname}-${_pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_SKIP_RPATH=ON \
        -DBUILD_TESTS=OFF \
        -DBUILD_EXAMPLES=ON \
        -DINSTALL_C_EXAMPLES=ON \
        -DINSTALL_PYTHON_EXAMPLES=ON \
        -DBUILD_opencv_python3=ON \
        -DWITH_XINE=OFF \
        -DWITH_QT=ON \
        -DWITH_OPENGL=ON \
        -DWITH_TBB=OFF \
        -DWITH_CUDA=OFF \
        -DWITH_GTK=OFF \
        -DENABLE_FAST_MATH=ON \
        -DOPENCV_EXTRA_MODULES_PATH=${srcdir}/opencv_contrib-${_pkgver}/modules \
        -DOPENCV_GENERATE_PKGCONFIG=ON \
        -DOPENCV_ENABLE_NONFREE=ON
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${_pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}


