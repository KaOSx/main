
pkgname=speech-dispatcher
pkgver=0.11.4
pkgrel=2
arch=('x86_64')
url="https://devel.freebsoft.org/speechd"
pkgdesc="High-level device independent layer for speech synthesis interface"
license=('GPL2' 'FDL')
depends=('glib2' 'libtool' 'dotconf' 'pyxdg' 'pulseaudio' )
makedepends=('intltool' 'espeak')
optdepends=('espeak: Text-To-Speak (TTS) engine')
backup=(etc/speech-dispatcher/clients/emacs.conf
        etc/speech-dispatcher/modules/ibmtts.conf
        etc/speech-dispatcher/modules/espeak-generic.conf
        etc/speech-dispatcher/modules/espeak.conf
        etc/speech-dispatcher/modules/swift-generic.conf
        etc/speech-dispatcher/modules/festival.conf
        etc/speech-dispatcher/modules/cicero.conf
        etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
        etc/speech-dispatcher/modules/dtk-generic.conf
        etc/speech-dispatcher/modules/llia_phon-generic.conf
        etc/speech-dispatcher/modules/ivona.conf
        etc/speech-dispatcher/modules/epos-generic.conf
        etc/speech-dispatcher/modules/flite.conf
        etc/speech-dispatcher/speechd.conf)
options=('!libtool')
install=speech-dispatcher.install
#source=("https://devel.freebsoft.org/pub/projects/speechd/${pkgname}-${pkgver}.tar.gz"
source=("https://github.com/brailcom/speechd/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'speech-dispatcherd.service')
md5sums=('4d812bef4488b35f324db010bf5cac4b'
         '0f09abe1af4eb79885ba88a1c2bcde8b')


build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-flite \
    --libexecdir=/usr/lib/speech-dispatcher/
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/speech-dispatcherd.service ${pkgdir}/usr/lib/systemd/system/speech-dispatcherd.service
  install -d ${pkgdir}/var/log/speech-dispatcher

  #sed -i -e 's|# DisableAutoSpawn|DisableAutoSpawn|' ${pkgdir}/etc/speech-dispatcher/speechd.conf
}
