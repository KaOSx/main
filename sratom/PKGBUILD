
pkgname=sratom
pkgver=0.4.6
pkgrel=2
pkgdesc="An LV2 Atom RDF serialisation library"
arch=('x86_64')
url="http://drobilla.net/software/sratom/"
license=('custom:ISC')
depends=('lv2' 'sord')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('5cf28520418779bf41fb14a40fbb20ad')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/ldconfig/d' wscript

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
