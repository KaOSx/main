
pkgname=sratom
pkgver=0.6.10
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=('x86_64')
url="https://drobilla.net/software/sratom.html"
license=('custom:ISC')
depends=('lv2' 'sord')
makedepends=('python3')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('bb883224e73ed937cd58b25436dae260')

build() {
  cd ${pkgname}-${pkgver}
  sed -i '/ldconfig/d' wscript

  python3 waf configure --prefix=/usr
  python3 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python3 waf install --destdir=${pkgdir}

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
