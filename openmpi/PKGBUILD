
pkgname=openmpi
pkgver=3.0.1
_pkgver=3.0
pkgrel=1
pkgdesc="High performance message passing library (MPI)"
arch=('x86_64')
url="https://www.open-mpi.org"
license=('custom')
depends=('gcc' 'openssh' 'valgrind' 'libltdl' 'hwloc')
makedepends=('inetutils')
options=('!libtool')
source=("https://www.open-mpi.org/software/ompi/v${_pkgver}/downloads/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('7f5f22e0002f8f6952154ad9e1da0bc0d0bc4304')

build() {
   cd ${pkgname}-${pkgver}

   ./configure --prefix=/usr \
        --sysconfdir=/etc/${pkgname} \
        --mandir=/usr/share/man \
        --enable-mpi-fortran=all \
        --libdir=/usr/lib/${pkgname} \
        --enable-mpi-cxx \
        --enable-builtin-atomics \
        --enable-mpi-thread-multiple \
        --with-valgrind \
        --enable-memchecker \
        --enable-debug \
        --enable-pretty-print-stacktrace \
        --without-slurm \
        --with-hwloc=/usr \
        --with-libltdl=/usr \
        FC=/usr/bin/gfortran \
        LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR=${pkgdir} install

   install -d -m 755 ${pkgdir}/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/$i ${pkgdir}/usr/lib/pkgconfig/
   done

   install -d -m 755 ${pkgdir}/etc/ld.so.conf.d
   echo /usr/lib/${pkgname} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf

   install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
