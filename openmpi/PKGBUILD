
pkgname=openmpi
pkgver=4.0.4
_pkgver=4.0
pkgrel=1
pkgdesc="High performance message passing library (MPI)"
arch=('x86_64')
url="https://www.open-mpi.org"
license=('custom')
depends=('gcc' 'openssh' 'valgrind' 'libtool' 'hwloc')
makedepends=('inetutils')
options=('!libtool')
source=("https://www.open-mpi.org/software/ompi/v${_pkgver}/downloads/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('47e24eb2223fe5d24438658958a313b6b7a55bb281563542e1afc9dec4a31ac4')

build() {
   cd ${pkgname}-${pkgver}

   ./configure --prefix=/usr \
        --sysconfdir=/etc/${pkgname} \
        --mandir=/usr/share/man \
        --enable-mpi-fortran=all \
        --libdir=/usr/lib/${pkgname} \
        --enable-mpi-cxx \
        --enable-builtin-atomics \
        --with-valgrind \
        --enable-memchecker \
        --enable-debug \
        --enable-pretty-print-stacktrace \
        --without-slurm \
        --with-hwloc=/usr \
        --with-libltdl=/usr \
        FC=/usr/bin/gfortran \
        LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR=${pkgdir} install

   install -d -m 755 ${pkgdir}/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/$i ${pkgdir}/usr/lib/pkgconfig/
   done

   install -d -m 755 ${pkgdir}/etc/ld.so.conf.d
   echo /usr/lib/${pkgname} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf

   install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
