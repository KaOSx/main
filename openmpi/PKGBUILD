
pkgname=openmpi
pkgver=5.0.4
_pkgver=5.0
pkgrel=1
pkgdesc="High performance message passing library (MPI)"
arch=('x86_64')
url="https://www.open-mpi.org"
license=('custom')
depends=('gcc' 'openssh' 'valgrind' 'libtool' 'hwloc' 'libevent')
makedepends=('inetutils' 'python3')
options=('!libtool')
source=("https://www.open-mpi.org/software/ompi/v${_pkgver}/downloads/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('64526852cdd88b2d30e022087c16ab3e03806c451b10cd691d5c1ac887d8ef9d')

build() {
   cd ${pkgname}-${pkgver}
   # https://github.com/open-mpi/ompi/issues/12257
   sed -i 's|WRAPPER__FCFLAGS|WRAPPER_FCFLAGS|g' configure
   sed -i 's|WRAPPER_EXTRA_FCFLAGS|WRAPPER_FCFLAGS|g' configure
   sed -i 's|"-I/usr/include",||' opal/tools/wrappers/opal_wrapper.c


   ./configure --prefix=/usr \
         --sysconfdir=/etc/${pkgname} \
         --mandir=/usr/share/man \
         --enable-mpi-fortran=all \
         --libdir=/usr/lib/${pkgname} \
         --enable-builtin-atomics \
         --with-valgrind \
         --enable-memchecker \
         --enable-debug \
         --enable-pretty-print-stacktrace \
         --without-slurm \
         --with-hwloc=/usr \
         FC=/usr/bin/gfortran \
         LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR=${pkgdir} install

   install -d -m 755 ${pkgdir}/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi-fort.pc ompi.pc orte.pc pmix.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/$i ${pkgdir}/usr/lib/pkgconfig/
   done

   install -d -m 755 ${pkgdir}/etc/ld.so.conf.d
   echo /usr/lib/${pkgname} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf

   install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
