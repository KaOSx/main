
pkgname=python3-pillow
_pkgname=Pillow
pkgver=9.4.0
pkgrel=1
arch=('x86_64')
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
url="https://python-pillow.org/"
license=('BSD')
depends=('python3' 'lcms2' 'libwebp')
makedepends=('python3-build' 'python3-installer' 'python3-wheel' 'python3-setuptools' 'tk')
source=("https://files.pythonhosted.org/packages/source/P/Pillow/${_pkgname}-${pkgver}.tar.gz")
#source=("https://github.com/python-pillow/Pillow/archive/${pkgver}.tar.gz")
sha256sums=('a1c2d7780448eb93fbcc3789bf3916aa5720d942e37945f4056680317f1cd23e')

build() { 
  cd ${_pkgname}-${pkgver}

  python3 -m build -nw
}

package() {
  cd ${_pkgname}-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
