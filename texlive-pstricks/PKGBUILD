
pkgname=texlive-pstricks
pkgver=2012.26865
_pkgver=${pkgver#2012.}
pkgrel=2
pkgdesc="TeX Live - Additional PSTricks packages."
license=('GPL3')
arch=('x86_64')
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
install=texlive.install
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
md5sums=('18c1f8e54233a31f292c4705148afcf7')

build() {
  cd $srcdir
  for p in *.tar.xz; do
    bsdtar -xf $p
  done
  rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_$_pkgver.pkgs
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/pst2pdf/pst2pdf $pkgdir/usr/bin/pst2pdf
   ln -s /usr/share/texmf-dist/scripts/pedigree-perl/pedigree.pl $pkgdir/usr/bin/pedigree
}
