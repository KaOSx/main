
pkgname=perl-file-next
_pkgname=File-Next
pkgver=1.18
pkgrel=1
pkgdesc='File::Next - File-finding iterator'
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/${_pkgname}"
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/P/PE/PETDANCE/${_pkgname}-${pkgver}.tar.gz")
md5sums=('8ad70cbaf936c3867fcc017d50ee6c25')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps            \
      PERL_MM_OPT="INSTALLDIRS=vendor"       \
      PERL_MB_OPT="--installdirs vendor"     \
      MODULEBUILDRC=/dev/null
      
    cd ${_pkgname}-${pkgver}

    /usr/bin/perl Makefile.PL 
    make
  )
}

check() {
  cd ${_pkgname}-${pkgver}
  
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
