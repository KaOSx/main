
pkgname=pkgfile
pkgver=17
pkgrel=1
pkgdesc="An alpm .files metadata explorer"
arch=('x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
install=pkgfile.install
source=("https://pkgbuild.com/~dreisner/sources/pkgfile/${pkgname}-${pkgver}.tar.xz")
md5sums=('c0ced1f0972891b41c9d84abcbf6bc1f')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --with-systemdsystemunitdir=/usr/lib/systemd/system
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

