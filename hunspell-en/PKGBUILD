
pkgname=hunspell-en
pkgver=2020.12.07
pkgrel=2
pkgdesc="English hunspell dictionaries"
arch=('x86_64')
url="http://wordlist.aspell.net/"
license=('LGPL' 'custom:scowl')
optdepends=('hunspell:	the spell checking libraries and apps')
source=("https://downloads.sourceforge.net/wordlist/hunspell-en_US-${pkgver}.zip"
        "https://downloads.sourceforge.net/wordlist/hunspell-en_CA-${pkgver}.zip"
        "https://downloads.sourceforge.net/wordlist/hunspell-en_GB-large-${pkgver}.zip")
md5sums=('38bfa805038da266c37d3a2d2db23972'
         'f28b8cb3e6dad9d4f53e849b5df80184'
         '2da50f83ba274284e5ad63ed6b788c3d')

build() {
 /bin/true
}

package() {
  cd ${srcdir}
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 en_*.dic en_*.aff ${pkgdir}/usr/share/hunspell

  pushd ${pkgdir}/usr/share/hunspell/
  en_GB_aliases="en_AG en_AU en_BS en_BW en_BZ en_DK en_GH en_HK en_IE en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"
  for lang in $en_GB_aliases; do
	ln -s en_GB-large.aff $lang.aff
	ln -s en_GB-large.dic $lang.dic
  done
  en_US_aliases="en_PH"
  for lang in $en_US_aliases; do
	ln -s en_US.aff $lang.aff
	ln -s en_US.dic $lang.dic
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

  # docs
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README_en_??.txt ${pkgdir}/usr/share/doc/${pkgname}

  # licenses
  install -D -m644 README_en_US.txt ${pkgdir}/usr/share/licenses/${pkgname}/Copyright_en_US
  install -D -m644 README_en_CA.txt ${pkgdir}/usr/share/licenses/${pkgname}/Copyright_en_CA
  install -D -m644 README_en_GB-large.txt ${pkgdir}/usr/share/licenses/${pkgname}/Copyright_en_GB
}
