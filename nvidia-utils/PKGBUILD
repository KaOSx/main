
pkgbase=nvidia-utils
pkgname=('nvidia-utils' 'opencl-nvidia' 'nvidia-settings')
# Edit hardware-detection/hooks/hwdetect_7_graphics and Installer job 11 on version jump from 43x.xx
pkgver=455.45.01
pkgrel=1
epoch=1
arch=('x86_64')
url="https://www.nvidia.com/page/home.html"
license=('custom')
makedepends=('xorg-server' 'libxvmc')
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
#source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}.run"
        'nvidia-restore-settings.desktop'
        'nvidia-drm-outputclass.conf')
md5sums=('33bd727e99952db58ccea866cae9cdf1'
         '9237516792da7b6df32f8c7e8a98f475'
         'cb27b0f4a78af78aa96c5aacae23256c')

create_links() {
  while read -d '' _lib; do
        _soname="$(dirname "${_lib}")/$(LC_ALL=C readelf -d "${_lib}" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        [[ -e "${_soname}" ]] || ln -s "$(basename "${_lib}")" "${_soname}"
        [[ -e "${_soname/.[0-9]*/}" ]] || ln -s "$(basename "${_soname}")" "${_soname/.[0-9]*/}"
    done < <(find "${pkgdir}" -type f -name '*.so*' -print0)
}

build() {
  cd ${srcdir}
  sh ${_pkg}.run --extract-only
  # https://download.nvidia.com/XFree86/Linux-x86_64/396.51/README/nvidia-persistenced.html
  cd ${_pkg}
  bsdtar -xf nvidia-persistenced-init.tar.bz2
  
}

package_opencl-nvidia() {
  pkgdesc="OpenCL implemention for NVIDIA"
  depends=('opencl-headers' 'zlib' 'nvidia-utils')

  cd "${srcdir}/${_pkg}"

  # OpenCL
  install -D -m644 nvidia.icd "${pkgdir}/etc/OpenCL/vendors/nvidia.icd"
  install -D -m755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-compiler.so.${pkgver}"
  install -D -m755 "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-opencl.so.${pkgver}"

  create_links
}

package_nvidia-utils() {
  pkgdesc="NVIDIA drivers utilities and libraries."
  depends=('xorg-server' 'libxvmc')
  optdepends=('pkg-config: nvidia-xconfig'
              'opencl-nvidia: OpenCL support'
              'nvidia-settings: Gtk GUI')
  conflicts=('nvidia-utils-bumblebee')
  provides=('nvidia-utils-bumblebee')
	      
  cd ${srcdir}/${_pkg}
  # X driver
  install -D -m755 nvidia_drv.so ${pkgdir}/usr/lib/xorg/modules/drivers/nvidia_drv.so
  
  # GLX extension module for X
  install -D -m755 libglxserver_nvidia.so.${pkgver} ${pkgdir}/usr/lib/xorg/modules/extensions/libglxserver_nvidia.so.${pkgver}
  install -D -m755 libglxserver_nvidia.so.${pkgver} ${pkgdir}/usr/lib/nvidia/xorg/libglxserver_nvidia.so.${pkgver}
  # X doesn't find glx otherwise:
  ln -s libglxserver_nvidia.so.${pkgver} ${pkgdir}/usr/lib/xorg/modules/extensions/libglxserver_nvidia.so 
  ln -s libglxserver_nvidia.so.${pkgver} ${pkgdir}/usr/lib/nvidia/xorg/libglx.so
  
  # GLVND vendor implementation libraries for GLX
  install -D -m755 libGLX_nvidia.so.${pkgver} ${pkgdir}/usr/lib/libGLX_nvidia.so.${pkgver}
  #install -D -m755 libGLX.so.0 ${pkgdir}/lib/xorg/modules/extensions/libgGLX.so
  #install -D -m755 libGLX.so.0 ${pkgdir}/lib/libgGLX.so
  
  # OpenGL library
  #install -D -m755 "libGL.so.1.7.0" "${pkgdir}/usr/lib/libGL.so.1.7.0"
  #install -D -m755 "libGL.la" "${pkgdir}/usr/lib/libGL.la"
  #install -D -m755 "libOpenGL.so.0" "${pkgdir}/usr/lib/libOpenGL.so.0"
  #install -D -m755 "libGLdispatch.so.0" "${pkgdir}/usr/lib/libGLdispatch.so.0"
  
  # OpenGL core library
  install -D -m755 "libnvidia-glcore.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-glcore.so.${pkgver}"
  install -D -m755 "libnvidia-eglcore.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-eglcore.so.${pkgver}"
  install -D -m755 "libnvidia-glsi.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-glsi.so.${pkgver}"
  install -D -m755 "libnvidia-egl-wayland.so.1.1.5" "${pkgdir}/usr/lib/libnvidia-egl-wayland.so.1.1.5"
  install -D -m644 "10_nvidia_wayland.json" "${pkgdir}/usr/share/egl/egl_external_platform.d/10_nvidia_wayland.json"
  install -D -m755 "libEGL_nvidia.so.${pkgver}" "${pkgdir}/usr/lib/libEGL_nvidia.so.${pkgver}"
  install -D -m644 "10_nvidia.json" "${pkgdir}/usr/share/glvnd/egl_vendor.d/10_nvidia.json"
  
  # XvMC
  #install -D -m644 libXvMCNVIDIA.a "${pkgdir}/usr/lib/libXvMCNVIDIA.a"
  #install -D -m755 "libXvMCNVIDIA.so.${pkgver}" "${pkgdir}/usr/lib/libXvMCNVIDIA.so.${pkgver}"
 
  # VDPAU
  install -D -m755 "libvdpau_nvidia.so.${pkgver}" "${pkgdir}/usr/lib/vdpau/libvdpau_nvidia.so.${pkgver}"

  # nvidia-tls library
  install -D -m755 "libnvidia-tls.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-tls.so.${pkgver}"
  install -D -m755 "libnvidia-cfg.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-cfg.so.${pkgver}"

  install -D -m755 "libnvidia-ml.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-ml.so.${pkgver}"
  install -D -m755 "libnvidia-ngx.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-ngx.so.${pkgver}"

  # CUDA
  install -D -m755 "libcuda.so.${pkgver}" "${pkgdir}/usr/lib/libcuda.so.${pkgver}"
  install -D -m755 "libnvcuvid.so.${pkgver}" "${pkgdir}/usr/lib/libnvcuvid.so.${pkgver}"
  #install -D -m755 "libnvidia-fatbinaryloader.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-fatbinaryloader.so.${pkgver}"
  
  # nvidia-xconfig
  install -D -m755 nvidia-xconfig "${pkgdir}/usr/bin/nvidia-xconfig"
  install -D -m644 nvidia-xconfig.1.gz "${pkgdir}/usr/share/man/man1/nvidia-xconfig.1.gz"
 
  # nvidia-bug-report
  install -D -m755 nvidia-bug-report.sh "${pkgdir}/usr/bin/nvidia-bug-report.sh"

  # nvidia-smi
  install -D -m755 nvidia-smi "${pkgdir}/usr/bin/nvidia-smi"
  install -D -m644 nvidia-smi.1.gz "${pkgdir}/usr/share/man/man1/nvidia-smi.1.gz"

  # license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nvidia/LICENSE"
  ln -s nvidia "${pkgdir}/usr/share/licenses/nvidia-utils"
   
  # docs
  install -D -m644 README.txt "${pkgdir}/usr/share/doc/nvidia/README"
  install -D -m644 NVIDIA_Changelog "${pkgdir}/usr/share/doc/nvidia/NVIDIA_Changelog"
  ln -s nvidia "${pkgdir}/usr/share/doc/nvidia-utils"
  
  # OptiX ray tracing
  install -D -m755 libnvoptix.so.${pkgver} ${pkgdir}/usr/lib/libnvoptix.so.${pkgver}
  
  # Vulkan real-time ray tracing
  install -D -m755 libnvidia-rtcore.so.${pkgver} ${pkgdir}/usr/lib/libnvidia-rtcore.so.${pkgver}
  install -D -m755 libnvidia-cbl.so.${pkgver} ${pkgdir}/usr/lib/libnvidia-cbl.so.${pkgver}
  
  # Systemd suspend/hibernate/resume service files
  # http://us.download.nvidia.com/XFree86/Linux-x86_64/430.40/README/powermanagement.html
  install -D -m644 nvidia-suspend.service "${pkgdir}/usr/lib/systemd/system/nvidia-suspend.service"
  install -D -m644 nvidia-hibernate.service "${pkgdir}/usr/lib/systemd/system/nvidia-hibernate.service"
  install -D -m644 nvidia-resume.service "${pkgdir}/usr/lib/systemd/system/nvidia-resume.service"
  install -D -m755 nvidia "${pkgdir}/usr/lib/systemd/system-sleep/nvidia"
  install -D -m755 nvidia-sleep.sh "${pkgdir}/usr/bin/nvidia-sleep.sh"
  
  # nvidia-persistenced
  install -D -m755 nvidia-persistenced ${pkgdir}/usr/bin/nvidia-persistenced
  install -D -m644 nvidia-persistenced.1.gz ${pkgdir}/usr/share/man/man1/nvidia-persistenced.1.gz
  install -D -m644 nvidia-persistenced-init/systemd/nvidia-persistenced.service.template ${pkgdir}/usr/lib/systemd/system/nvidia-persistenced.service
  sed -i 's/__USER__/nvidia-persistenced/' ${pkgdir}/usr/lib/systemd/system/nvidia-persistenced.service
  
  # conf
  install -D -m644 ${srcdir}/nvidia-drm-outputclass.conf ${pkgdir}/etc/X11/xorg.conf.d/nvidia-drm-outputclass.conf

  create_links
}

package_nvidia-settings() {
  pkgdesc="Gtk Graphical User Interface for nVidia"
  depends=('nvidia-utils' 'gtk2')

  cd "${srcdir}/${_pkg}"


  install -D -m755 nvidia-settings "${pkgdir}/usr/bin/nvidia-settings"
  install -D -m644 nvidia-settings.1.gz "${pkgdir}/usr/share/man/man1/nvidia-settings.1.gz"
  install -D -m644 nvidia-settings.desktop "${pkgdir}/usr/share/applications/nvidia-settings.desktop"
  install -D -m644 nvidia-settings.png "${pkgdir}/usr/share/pixmaps/nvidia-settings.png"
  install -D -m755 "libnvidia-gtk2.so.$pkgver" "$pkgdir/usr/lib/libnvidia-gtk2.so.$pkgver"
  install -D -m755 "libnvidia-gtk3.so.$pkgver" "$pkgdir/usr/lib/libnvidia-gtk3.so.$pkgver"
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i "${pkgdir}/usr/share/applications/nvidia-settings.desktop"

  # Install the autostart .desktop file, so that parameters saved through nvidia-settings
  # are automatically reloaded at each login
  install -D -m644 "${srcdir}/nvidia-restore-settings.desktop" "${pkgdir}/etc/xdg/autostart/nvidia-restore-settings.desktop"
  # key documentation and application profile
  install -D -m644 nvidia-application-profiles-${pkgver}-key-documentation "${pkgdir}/usr/share/nvidia/nvidia-application-profiles-${pkgver}-key-documentation"
  install -D -m644 nvidia-application-profiles-${pkgver}-rc "${pkgdir}/usr/share/nvidia/nvidia-application-profiles-${pkgver}-rc"
}
