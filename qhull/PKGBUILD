
pkgname=qhull
pkgver=2020.2
_pkgver=2020-src-8.0.2
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
options=('!libtool')
source=("http://www.qhull.org/download/${pkgname}-${_pkgver}.tgz")
md5sums=('d74284d803e6ef6624e4a413d3ec05ab')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() { 
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

