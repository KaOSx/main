
pkgname=qhull
pkgver=2020.2
_pkgver=2020-src-8.0.2
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
options=('!libtool')
source=("http://www.qhull.org/download/${pkgname}-${_pkgver}.tgz")
md5sums=('295f7332269a38279478f555cc185296')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLINK_APPS_SHARED=ON
  make
}

package() { 
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

