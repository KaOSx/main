
pkgname=qhull
pkgver=2019.1
_pkgver=2019-src-7.3.2
pkgrel=1
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('glibc')
makedepends=('cmake')
options=('!libtool')
source=("http://www.qhull.org/download/${pkgname}-${_pkgver}.tgz")
md5sums=('53c04f9c89b491b17b10c7ea1eaa4cc8')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS=-fPIC \
    -DCMAKE_C_FLAGS=-fPIC \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() { 
  cd build
  make DESTDIR=${pkgdir} install

  install -Dm644 ../${pkgname}-${pkgver}/COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

