
pkgname=xorg-server-utils
pkgver=7.16
pkgrel=5
pkgdesc="X.Org utilities required by xorg-server"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfontcache' 'libxi' 'libxaw' 'libxrandr' 'libxxf86vm' 'mcpp'
         'xcb-util' 'libxinerama' 'libxcursor')
makedepends=('pkgconfig' 'xbitmaps' 'xorgproto' 'xtrans' 'xorg-util-macros')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/iceauth-1.0.9.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/rgb-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/sessreg-1.1.3.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xcmsdb-1.0.6.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xbacklight-1.2.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xgamma-1.0.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xhost-1.0.9.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xinput-1.6.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xmodmap-1.0.11.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xrandr-1.5.2.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xrdb-1.2.2.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xrefresh-1.0.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xset-1.2.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xsetmode-1.0.0.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xsetroot-1.1.3.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xvidtune-1.0.4.tar.xz"
        'LICENSE.iceauth'
        'LICENSE.xcmsdb'
        'LICENSE.xvidtune')
sha256sums=('2cb9dfcb545683af77fb1029bea3fc52dcc8a0666f7b8b2d7373b6ed4c408c05'
            'bbca7c6aa59939b9f6a0fb9fff15dfd62176420ffd4ae30c8d92a6a125fbe6b0'
            '022acd5de8077dddc4f919961f79e102ecd5f3228a333681af5cd0e7344facc2'
            '3c77eec4537d5942bb0966973b787bfdaf7121f3125ffa81bb1c9708d4cf4f55'
            '3a27f324777ae99fee476cfb2f064576fb8cba4eb77f97cda37adda1c1d39ade'
            '1c79dae85a8953a15f4fe5c2895a033307b43b8613456c87ec47b374b113bc8f'
            'ea86b531462035b19a2e5e01ef3d9a35cca7d984086645e2fc844d8f0e346645'
            'ad04d00d656884d133110eeddc34e9c69e626ebebbbab04dc95791c2907057c8'
            '9a2f8168f7b0bc382828847403902cb6bf175e17658b36189eac87edda877e81'
            'c8bee4790d9058bacc4b6246456c58021db58a87ddda1a9d0139bf5f18f1f240'
            '31f5fcab231b38f255b00b066cf7ea3b496df712c9eb2d0d50c670b63e5033f4'
            'a9f1d635f2f42283d0174e94d09ab69190c227faa5ab542bfe15ed9607771b1e'
            '9f692d55635b3862cd63633b1222a87680ec283c7a8e8ed6dd698a3147f75e2f'
            '988b47cd922991c6e6adbce15dc386ac75690b61744b526c3af5a4eaa9afa0aa'
            '6081b45a9eb4426e045d259d1e144b32417fb635e5b96aa90647365ac96638d1'
            '0d4eecd54e440cc11f1bdaaa23180fcf890f003444343f533f639086b05b2cc5'
            '4eb502e7400920e38b7680fb6b5a46d2b5a098e91caadc5f61ffe05d3d0ee681'
            '786776b1cc89ea0494589f9aa83ac653b7cddb8eeccb94b34b084ad4e54efe3b'
            'a8753d4a476a3fcdfe0aed574604588c24565a991cb7f2be1f1106a17f56339b')

build() {
  cd ${srcdir}

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
          ./configure --prefix=/usr --mandir=/usr/share/man --with-cpp=/usr/bin/mcpp 
          ;;
        *)
          ./configure --prefix=/usr --mandir=/usr/share/man
          ;;
      esac
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR=${pkgdir} install
      install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
      install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done

  # Replacing stub license files
  install -m644 LICENSE.* ${pkgdir}/usr/share/licenses/${pkgname}/
}
