
pkgname=xorg-server-utils
pkgver=7.14
pkgrel=5
pkgdesc="X.Org utilities required by xorg-server"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfontcache' 'libxi' 'libxaw' 'libxrandr' 'libxxf86vm' 'mcpp'
         'xcb-util' 'libxinerama' 'libxcursor')
makedepends=('pkgconfig' 'xbitmaps' 'xorgproto' 'xtrans' 'xorg-util-macros')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/iceauth-1.0.8.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/rgb-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/sessreg-1.1.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xcmsdb-1.0.5.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xbacklight-1.2.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xgamma-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xhost-1.0.8.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xinput-1.6.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xmodmap-1.0.10.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xrandr-1.5.1.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xrdb-1.2.0.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xrefresh-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xset-1.2.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xsetmode-1.0.0.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xsetroot-1.1.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xvidtune-1.0.3.tar.bz2"
        'LICENSE.iceauth'
        'LICENSE.xcmsdb'
        'LICENSE.xvidtune')
sha256sums=('e6ee213a217265cc76050e4293ea70b98c32dce6505c6421227efbda62ab60c6'
            'bbca7c6aa59939b9f6a0fb9fff15dfd62176420ffd4ae30c8d92a6a125fbe6b0'
            'dfcb9e954273ace63879aec053c7880466f8755752a35aa9ced3fa5f04fd2c33'
            'e5585361bb8b6a05bb814a8d0e444ee93e0f00180881d3070aff4571e97f67c6'
            '3a27f324777ae99fee476cfb2f064576fb8cba4eb77f97cda37adda1c1d39ade'
            '0ef1c35b5c18b1b22317f455c8df13c0a471a8efad63c89c98ae3ce8c2b222d3'
            'a2dc3c579e13674947395ef8ccc1b3763f89012a216c2cc6277096489aadc396'
            '35a281dd3b9b22ea85e39869bb7670ba78955d5fec17c6ef7165d61e5aeb66ed'
            '473f0941d7439d501bb895ff358832b936ec34c749b9704c37a15e11c318487c'
            '7bc76daf9d72f8aff885efad04ce06b90488a1a169d118dea8a2b661832e8762'
            'f23a65cfa1f7126040d68b6cf1e4567523edac10f8dc06f23d840d330c7c6946'
            '287dfb9bb7e8d780d07e672e3252150850869cb550958ed5f8401f0835cd6353'
            'e4fd95280df52a88e9b0abc1fee11dcf0f34fc24041b9f45a247e52df941c957'
            '988b47cd922991c6e6adbce15dc386ac75690b61744b526c3af5a4eaa9afa0aa'
            '10c442ba23591fb5470cea477a0aa5f679371f4f879c8387a1d9d05637ae417c'
            '24e5e103933a04bfb2e1d18562619ed74031ab515e7343eb546bacdbcbeecf01'
            '4eb502e7400920e38b7680fb6b5a46d2b5a098e91caadc5f61ffe05d3d0ee681'
            '786776b1cc89ea0494589f9aa83ac653b7cddb8eeccb94b34b084ad4e54efe3b'
            'a8753d4a476a3fcdfe0aed574604588c24565a991cb7f2be1f1106a17f56339b')

build() {
  cd "${srcdir}"
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
          ./configure --prefix=/usr --mandir=/usr/share/man --with-cpp=/usr/bin/mcpp 
          ;;
        *)
          ./configure --prefix=/usr --mandir=/usr/share/man
          ;;
      esac
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
      install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done

  # Replacing stub license files
  install -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"
}
