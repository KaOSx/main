
pkgname=autofs
pkgver=5.1.5
pkgrel=1
pkgdesc='A kernel-based automounter for Linux.'
arch=('x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libldap' 'libxml2' 'krb5')
makedepends=('kmod' 'clang')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
options=('!makeflags')
source=("https://www.kernel.org/pub/linux/daemons/autofs/v5/${pkgname}-${pkgver}.tar.xz")
sha256sums=('23afdad9e2fd6d41b1900be8cfecfd5aae0ad174c7708ff685ec27e280f9c0da')

build() {
  cd ${pkgname}-${pkgver}
  
  export CC="clang"
  export CXX="clang++"

  sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
         -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master

  ./configure --prefix=/usr \
        --sysconfdir=/etc/autofs \
        --with-mapdir=/etc/autofs \
        --without-hesiod \
        --disable-ext-env \
        --enable-ignore-busy \
        --with-systemd
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/autofs/auto.master.d
}
