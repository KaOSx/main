
pkgname=autofs
pkgver=5.1.6
pkgrel=2
pkgdesc='A kernel-based automounter for Linux.'
arch=('x86_64')
url='https://git.kernel.org/pub/scm/linux/storage/autofs/autofs.git'
license=('GPL2')
depends=('libldap' 'libxml2' 'krb5')
makedepends=('kmod') # 'clang')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
options=('!makeflags')
source=("https://www.kernel.org/pub/linux/daemons/autofs/v5/${pkgname}-${pkgver}.tar.xz")
sha256sums=('dddee3d9b7388ce6cb7432832dfade25b07ef68ad48dcce01cf247b26a10caef')

build() {
  cd ${pkgname}-${pkgver}
  
  #export CC="clang"
  #export CXX="clang++"

  sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
         -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master
  sed -i -e "/^SUBDIRS/s/$/ samples/g" Makefile.rules

  ./configure --prefix=/usr \
        --sysconfdir=/etc/autofs \
        --with-mapdir=/etc/autofs \
        --without-hesiod \
        --disable-ext-env \
        --enable-ignore-busy \
        --with-systemd
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/autofs/auto.master.d
}
