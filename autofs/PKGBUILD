
pkgname=autofs
pkgver=5.1.3
pkgrel=1
pkgdesc='A kernel-based automounter for Linux.'
arch=('x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libldap' 'libxml2' 'krb5' 'kmod')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
options=('!makeflags')
source=("https://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.xz")
md5sums=('57a6791e596b8b8ee2c4c50420881180')

build() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
         -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master

  ./configure --prefix=/usr \
        --sysconfdir=/etc/autofs \
        --with-mapdir=/etc/autofs \
        --without-hesiod \
        --enable-ignore-busy \
        --with-systemd
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT=${pkgdir} install
  
  rm -r $pkgdir/run
  install -dm755 ${pkgdir}/etc/autofs/auto.master.d
}
