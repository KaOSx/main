
pkgname=libxcursor
pkgver=1.2.2
pkgrel=1
pkgdesc="X cursor management library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXcursor-${pkgver}.tar.xz")
sha256sums=('53d071bd2cc56e517a30998d5e685c8a74556ddada43c6985d14da9a023a88ee')

build() {
  cd libXcursor-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXcursor-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
