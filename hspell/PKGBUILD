
pkgbase=hspell
pkgname=('hspell' 'hunspell-he')
pkgver=1.4
pkgrel=2
arch=('x86_64')
license=('GPL')
url="http://www.ivrix.org.il/projects/spell-checker/"
makedepends=('glibc' 'zlib' 'perl' 'hunspell' 'gawk')
options=('!libtool' '!makeflags')
source=("http://hspell.ivrix.org.il/${pkgname}-${pkgver}.tar.gz")
md5sums=('55d9cdc4fe576db8515945e663ef4791')

build() {
  cd ${pkgname}-${pkgver}
  
  export PERLLIB="$PWD"
  ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-linginfo \
        --enable-fatverb \
        --enable-shared
  make 
  make hunspell
}

package_hspell() {
  pkgdesc="Hebrew spell-checker"
  depends=('glibc' 'zlib' 'perl')

  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/libhspell.a
}

package_hunspell-he() {
  pkgdesc="Hebrew hunspell dictionary"
  
  cd ${pkgbase}-${pkgver}

  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 he.dic ${pkgdir}/usr/share/hunspell/he_IL.dic
  install -m644 he.aff ${pkgdir}/usr/share/hunspell/he_IL.aff

  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
}
