
pkgname=lldb
pkgver=16.0.2
pkgrel=1
pkgdesc="Next generation, high-performance debugger, built as a set of reusable components in the LLVM Project."
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=("llvm=${pkgver}" 'libedit' 'libxml2' 'ncurses' 'python3-six')
makedepends=('groff' 'swig' 'cmake' 'clang')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/lldb-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/cmake-${pkgver}.src.tar.xz")
md5sums=('88a49e48263d19c62cb20e4fa7f1858d'
         '822112a01953bb278fbefa8164e18e34')

build() {
    cmake -B build -S ${pkgname}-${pkgver}.src \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DLLVM_LINK_LLVM_DYLIB:BOOL=ON \
        -DLLDB_INCLUDE_TESTS=OFF \
        -DLLDB_USE_SYSTEM_SIX=1 \
        -DLLVM_COMMON_CMAKE_UTILS="${srcdir}/cmake-${pkgver}.src"
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    # Python scripts
    python3 -m compileall ${pkgdir}/usr/lib/python3.8/site-packages/lldb
    python3 -O -m compileall ${pkgdir}/usr/lib/python3.8/site-packages/lldb

    install -D -m644 ${pkgname}-${pkgver}.src/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE    
}

