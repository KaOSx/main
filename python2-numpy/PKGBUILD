
pkgname=python2-numpy
pkgver=1.14.5
_pkgver=1.14.5
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="https://www.numpy.org/"
depends=('lapack' 'python2-nose')
provides=('python-numeric')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('1b4a02758fb68a65ea986d808867f1d6383219c234aef553a8741818e795b529')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python2-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python2-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python2.7
  ln -sf /usr/lib/python2.7/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python2.7/numpy
}
