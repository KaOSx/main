
pkgname=python2-numpy
pkgver=1.14.0
_pkgver=1.14.0
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://www.numpy.org/"
depends=('lapack' 'python2-nose')
provides=('python-numeric')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('c45d99134bb07600e916537c51c22e87f9e0c44b05d71018ab4907e195f007ce')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python2-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python2-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python2.7
  ln -sf /usr/lib/python2.7/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python2.7/numpy
}
