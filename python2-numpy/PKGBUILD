
pkgname=python2-numpy
pkgver=1.16.2
_pkgver=1.16.2
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="https://www.numpy.org/"
depends=('lapack' 'python2-nose')
provides=('python-numeric')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('8088221e6e27da8d5907729f0bfe798f526836f22cc59ae83a0f867e67416a3e')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python2-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python2-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python2.7
  ln -sf /usr/lib/python2.7/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python2.7/numpy
  
  # remove conflicts, keep files in python3-numpy
  #rm ${pkgdir}/usr/bin/conv-template
  rm ${pkgdir}/usr/bin/f2py
  #rm ${pkgdir}/usr/bin/from-template
}
