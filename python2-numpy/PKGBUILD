
pkgname=python2-numpy
pkgver=1.13.1
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://www.numpy.org/"
depends=('lapack' 'python2-nose')
provides=('python-numeric')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${pkgver}.tar.gz")
md5sums=('6d459e4a24f5035f720dda3c57716a92')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python2 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python2-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python2-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python2.7
  ln -sf /usr/lib/python2.7/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python2.7/numpy
}
