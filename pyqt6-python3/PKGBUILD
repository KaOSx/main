
pkgname=pyqt6-python3
pkgver=6.4.1
_pkgver=6.4.1
pkgrel=1
pkgdesc="A set of Python3 bindings for the Qt6 toolkit"
arch=('x86_64')
url="https://www.riverbankcomputing.com/software/pyqt/intro"
license=('GPL')
depends=('dbus-python3' 'qt6-connectivity' 'qt6-svg' 'qt6-declarative' 'qt6-multimedia' 'qt6-quick3d'
         'qt6-location' 'qt6-sensors' 'qt6-serialport' 'qt6-webchannel' 'qt6-websockets' 'pyqt6-sip')
makedepends=('pyqt-builder' 'qt6-tools' 'python3-setuptools')
source=("https://pypi.python.org/packages/source/P/PyQt6/PyQt6-${pkgver}.tar.gz")
#source=("https://www.riverbankcomputing.com/static/Downloads/PyQt6/PyQt6-${_pkgver}.tar.gz")
sha256sums=('0c1dcadf161331cfdbde0906c05f7f8048dc4907d717647c33bbc4404146f59f')

build() {
  cd PyQt6-${_pkgver}

  sip-build \
    --confirm-license \
    --api-dir /usr/share/qt6/qsci/api/python/ \
    --qmake /usr/lib/qt6/bin/qmake \
    --no-dbus-python \
    --no-make
  cd build
  make
}

package(){
  cd PyQt6-${_pkgver}/build

  make DESTDIR=${pkgdir} INSTALL_ROOT=${pkgdir} install
}
