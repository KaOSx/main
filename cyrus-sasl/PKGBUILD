
pkgname=cyrus-sasl
pkgver=2.1.26
pkgrel=3
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library and daemon"
arch=('x86_64')
license=('custom')
url="http://cyrusimap.org/"
depends=('pam' 'krb5' 'libldap' 'db' 'postgresql-libs' 'mariadb')
conflicts=('cyrus-sasl-plugins')
replaces=('cyrus-sasl-plugins')
provides=('cyrus-sasl-plugins')
backup=(etc/conf.d/saslauthd)
source=("ftp://ftp.cyrusimap.org/cyrus-sasl/${pkgname}-${pkgver}.tar.gz"
        'saslauthd.service'
        'saslauthd.conf.d'
        'tmpfiles.conf') 
md5sums=('a7f4e5e559a0e37b3ffc438c9456e425'
         '3499dcd610ad1ad58e0faffde2aa7a23'
         '96d8a2f6189501f8044838e04d5cae7f'
         '45bb0192d2f188066240b9a66ee6365f')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-ldap=/usr \
    --with-saslauthd=/var/run/saslauthd \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-login \
    --disable-plain \
    --enable-sql \
    --disable-sqlite \
    --enable-gssapi=/usr/include/gssapi \
    --with-mysql=/usr \
    --with-pgsql=/usr \
    --enable-postgresql \
    --enable-ldapdb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  install -Dm644 ${srcdir}/saslauthd.conf.d ${pkgdir}/etc/conf.d/saslauthd
  install -Dm644 ${srcdir}/saslauthd.service ${pkgdir}/usr/lib/systemd/system/saslauthd.service
  install -Dm644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/saslauthd.conf

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/cyrus-sasl/COPYING
  # part of libsasl
  rm -rf ${pkgdir}/usr/include
  rm -f ${pkgdir}/usr/lib/sasl2/libsasldb.*
  rm -f ${pkgdir}/usr/lib/libsasl2.*
  rm -f ${pkgdir}/usr/lib/sasl2/libscram.*
  rm -r ${pkgdir}/usr/sbin/pluginv*
  rm -r ${pkgdir}/usr/sbin/sasldblis*
  rm -r ${pkgdir}/usr/sbin/saslpass*
  rm -r ${pkgdir}/usr/share/man/man8/plugin*
  rm -r ${pkgdir}/usr/share/man/man8/sasldb*
  rm -r ${pkgdir}/usr/share/man/man8/saslpass*
}
