
pkgname=gstreamer
pkgver=1.20.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2' 'libunwind' 'bash-completion')
makedepends=('meson' 'ninja' 'gobject-introspection' 'python3' 'gsl')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('607daf64bbbd5fb18af9d17e21c0d22c4d702fffe83b23cb22d1b1af2ca23a2a')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled \
    -Ddbghelp=disabled
  ninja
}

check() {
  cd build
  # no fail
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

