
pkgname=gstreamer
pkgver=1.14.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('28d82b0d261544a9bf85b429399929e4986eb00efcf1ce16cc71d269a4c3186c')

build() {
  cd gstreamer-${pkgver}
  
    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --with-package-name="GStreamer (KaOS)" \
      --with-package-origin="https://kaosx.us/" \
      --disable-static
  make
}

check() {
  cd gstreamer-${pkgver}
  
  make check
}

package() {
  cd gstreamer-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
