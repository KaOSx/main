
pkgname=gstreamer
pkgver=1.22.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2' 'libunwind' 'bash-completion')
makedepends=('meson' 'ninja' 'gobject-introspection' 'python3' 'gsl')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('b2afe73603921c608ba48969dbb7d743776744bfe5d8059ece241137b7f88e21')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled \
    -Ddbghelp=disabled
  ninja
}

check() {
  cd build
  # no fail
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

