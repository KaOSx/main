
pkgname=gstreamer
pkgver=1.22.9
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2' 'libunwind' 'bash-completion')
makedepends=('meson' 'ninja' 'gobject-introspection' 'python3' 'gsl')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('1e7124d347e8cdc80f08ec1d370c201be513002af1102bb20e83c5279cb48ebd')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled \
    -Ddbghelp=disabled
  ninja
}

check() {
  cd build
  # no FAIL
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

