
pkgname=gstreamer
pkgver=1.24.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2' 'libunwind' 'bash-completion')
makedepends=('meson' 'ninja' 'gobject-introspection' 'python3' 'gsl')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('81c38617798d331269e389d56fb1388073e1dc9d489fe9bf2113f86b48b59138')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled \
    -Ddbghelp=disabled
  ninja
}

check() {
  cd build
  # no FAIL
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

