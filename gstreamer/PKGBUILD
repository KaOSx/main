
pkgname=gstreamer
pkgver=1.18.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="https://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2' 'libunwind' 'bash-completion')
makedepends=('meson' 'ninja' 'gobject-introspection' 'python3' 'gsl')
options=('!libtool')
source=("https://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('79df8de21f284a105a5c1568527f8c559c583c85c0f2bd7bdb5b0372b8beecba')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled \
    -Ddbghelp=disabled
  ninja
}

check() {
  cd build
  
  meson test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

