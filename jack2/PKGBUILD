
pkgname=jack2
pkgver=1.9.16
pkgrel=1
_commit=ce825c6
pkgdesc="The next-generation JACK with SMP support"
arch=('x86_64')
url="https://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
license=('GPL')
depends=('libsamplerate' 'opus' 'libffado' 'dbus' 'opus')
makedepends=('python3')
provides=('jack')
source=("https://github.com/jackaudio/jack2/archive/v${pkgver}.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules')
md5sums=('bdc547d3d56c4ab3bf7b1a32df6ca270'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

build() {
  cd ${pkgname}-${pkgver}
  
  LDFLAGS="$LDFLAGS,--no-as-needed -lm" python3 ./waf configure \
    --prefix=/usr \
    --alsa \
    --classic \
    --dbus \
    --firewire \
    --systemd-unit

  python3 ./waf build


}

package() {
  cd ${pkgname}-${pkgver}

  python3 waf install --destdir=${pkgdir}

  # configure realtime access/scheduling
  install -Dm644 ${srcdir}/99-audio.conf ${pkgdir}/etc/security/limits.d/99-audio.conf
  install -Dm644 ${srcdir}/40-hpet-permissions.rules ${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules
}

