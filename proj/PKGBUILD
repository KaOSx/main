
pkgname=proj
pkgver=8.0.1
_dataver=1.5
pkgrel=1
pkgdesc="Cartographic Projections library"
arch=('x86_64')
url="https://proj.org/"
license=('MIT')
depends=('sqlite' 'libtiff')
makedepends=('cmake')
options=('!libtool')
source=("https://download.osgeo.org/proj/${pkgname}-${pkgver}.tar.gz"
        "https://download.osgeo.org/proj/${pkgname}-data-${_dataver}.tar.gz")
md5sums=('c2511f070f0e4854b1b8bf8baccf294c'
         '1845455f8e416f532edb98462ef84ac8')

prepare() {

  bsdtar -xzvf ${srcdir}/${pkgname}-data-${_dataver}.tar.gz -C ${srcdir}/proj-${pkgver}/data
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

