
pkgname=proj
pkgver=7.2.1
_dataver=1.4
pkgrel=1
pkgdesc="Cartographic Projections library"
arch=('x86_64')
url="https://proj4.org/"
license=('MIT')
depends=('sqlite' 'libtiff')
options=('!libtool')
source=("https://download.osgeo.org/proj/${pkgname}-${pkgver}.tar.gz"
        "https://download.osgeo.org/proj/${pkgname}-data-${_dataver}.tar.gz")
md5sums=('befbafd25e65ddf0c26a8321d8bcdcd2'
         '61a8ab165f3dba0c71319d20955b0b48')

build() {
  bsdtar -xzvf ${srcdir}/${pkgname}-data-${_dataver}.tar.gz -C ${srcdir}/proj-${pkgver}/data
  cd ${srcdir}/proj-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
