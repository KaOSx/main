
pkgname=proj
pkgver=8.1.1
_dataver=1.7
pkgrel=1
pkgdesc="Cartographic Projections library"
arch=('x86_64')
url="https://proj.org/"
license=('MIT')
depends=('sqlite' 'libtiff')
makedepends=('cmake')
options=('!libtool')
source=("https://download.osgeo.org/proj/${pkgname}-${pkgver}.tar.gz"
        "https://download.osgeo.org/proj/${pkgname}-data-${_dataver}.tar.gz")
md5sums=('f017fd7d35311b0d65b2cf0503844690'
         'a7f85578335509c99029b72c139fc0c6')

prepare() {

  bsdtar -xzvf ${srcdir}/${pkgname}-data-${_dataver}.tar.gz -C ${srcdir}/proj-${pkgver}/data
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTING=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

