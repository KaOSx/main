
pkgname=gpgme-qt6
_pkgname=gpgme
pkgver=1.23.0
pkgrel=1
pkgdesc="A C wrapper library for GnuPG, Qt6 version"
arch=('x86_64')
url="https://www.gnupg.org/related_software/gpgme/"
license=('GPL')
depends=('gpgme' 'qt6-base')
options=('!libtool' '!emptydirs')
source=("https://www.gnupg.org/ftp/gcrypt/gpgme/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('043e2efe18b4ad22b96d434dde763fbed32cf8d6c220dc69df0d0ffb9dc66fc6')

build() {
  mkdir -p build
  cd build

  ../${_pkgname}-${pkgver}/configure --prefix=/usr \
       --disable-static \
       --disable-gpgsm-test \
       --disable-fd-passing \
       --enable-languages=qt6,cpp
  make
}

package() {
  cd build/lang/qt

  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/usr/include
}
