
pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.10.3
pkgrel=1
arch=('x86_64')
url="https://libsigcplusplus.github.io/libsigcplusplus/"
license=('LGPL')
makedepends=('gcc-libs')
options=('!libtool' '!emptydirs')
source=("http://download.gnome.org/sources/libsigc++/2.10/libsigc++-${pkgver}.tar.xz")
sha256sums=('0b68dfc6313c6cc90ac989c6d722a1bf0585ad13846e79746aa87cb265904786')

build() {
  cd libsigc++-${pkgver}
  
  ./configure --prefix=/usr 
  make 
}

package_libsigc++() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
  depends=('gcc-libs')

  cd libsigc++-${pkgver}
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile 
  make DESTDIR=${pkgdir} install 
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"

  cd libsigc++-${pkgver}/docs
  make DESTDIR=${pkgdir} install 
}

