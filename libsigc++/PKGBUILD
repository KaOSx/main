
pkgname=libsigc++
pkgver=2.10.6
_pkgver=2.10
pkgrel=2
pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
arch=('x86_64')
url="https://libsigcplusplus.github.io/libsigcplusplus/"
license=('LGPL')
depends=('gcc-libs')
makedepends=('meson' 'ninja' 'mm-common')
options=('!libtool' '!emptydirs')
source=("https://download.gnome.org/sources/libsigc++/${_pkgver}/libsigc++-${pkgver}.tar.xz")
sha256sums=('dda176dc4681bda9d5a2ac1bc55273bdd381662b7a6d49e918267d13e8774e1b')

build() {
  mkdir -p bld
  cd bld
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libdir=lib \
    -Dbuild-documentation=false
  ninja
}

package() {
  cd bld
  
  DESTDIR=${pkgdir} ninja install
}




