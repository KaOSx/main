
pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.10.2
pkgrel=3
arch=('x86_64')
license=('LGPL')
url="http://libsigc.sourceforge.net/"
makedepends=('gcc-libs')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.10/libsigc++-${pkgver}.tar.xz")
sha256sums=('b1ca0253379596f9c19f070c83d362b12dfd39c0a3ea1dd813e8e21c1a097a98')

build() {
  cd libsigc++-${pkgver}
  
  ./configure --prefix=/usr 
  make 
}

package_libsigc++() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
  depends=('gcc-libs')

  cd libsigc++-${pkgver}
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile 
  make DESTDIR=${pkgdir} install 
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"

  cd libsigc++-${pkgver}/docs
  make DESTDIR=${pkgdir} install 
}

