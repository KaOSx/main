
pkgname=libsigc++
pkgver=2.12.0
_pkgver=2.12
pkgrel=2
pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2."
arch=('x86_64')
url="https://libsigcplusplus.github.io/libsigcplusplus/"
license=('LGPL')
depends=('gcc-libs')
makedepends=('meson' 'ninja' 'mm-common')
options=('!libtool' '!emptydirs')
source=("https://download.gnome.org/sources/libsigc++/${_pkgver}/libsigc++-${pkgver}.tar.xz")
sha256sums=('1c466d2e64b34f9b118976eb21b138c37ed124d0f61497df2a90ce6c3d9fa3b5')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libdir=lib \
    -Dbuild-documentation=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}




