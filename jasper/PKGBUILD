
pkgname=jasper
pkgver=2.0.0
pkgrel=1
pkgdesc="Software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard"
arch=('x86_64')
url="http://www.ece.uvic.ca/~mdadams/jasper/"
license=('custom:JasPer2.0')
depends=('libjpeg-turbo' 'freeglut' 'libxi' 'libxmu' 'glu')
makedepends=('cmake')
options=('!libtool')
source=("http://www.ece.uvic.ca/~frodo/jasper/software/${pkgname}-${pkgver}.tar.gz"
        'glu.patch')
md5sums=('ae52fd9d6419159c06fb2401dfec2a54'
         '83146318cfff0d8e656d3f8385af7be0')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i $srcdir/glu.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DJAS_ENABLE_SHARED=true \
    -DJAS_ENABLE_LIBJPEG=true \
    -DJAS_ENABLE_OPENGL=true 
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
