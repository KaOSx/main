#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=pommed
pkgver=1.38
pkgrel=3
pkgdesc="Handles the hotkeys of Apple MacBook (Pro) laptops"
arch=('i686' 'x86_64')
url="http://technologeek.org/projects/pommed/"
license=('GPL2')
install=$pkgname.install
makedepends=('pkgconfig')
depends=('alsa-lib' 'audiofile' 'confuse' 'dbus-core' 'pciutils')
optdepends=('eject: disc ejection support')
#backup=('etc/pommed.conf')
source=(https://alioth.debian.org/frs/download.php/3529/${pkgname}-${pkgver}.tar.gz 
        pommed.rc
        pommed.service)
md5sums=('121b361f6a18cee7dc86f8ffed38ae5a'
         'c024728673d20aef3a9d8dff3fc5b388'
         '834609d433c21d6184bca8848348d3af')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make pommed
}

package() {
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}/pommed/pommed ${pkgdir}/usr/sbin/pommed
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/pommed.conf.mactel ${pkgdir}/etc/pommed.conf.mactel
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/pommed.conf.pmac ${pkgdir}/etc/pommed.conf.pmac
  install -Dm755 ${srcdir}/pommed.rc ${pkgdir}/etc/rc.d/pommed
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/dbus-policy.conf ${pkgdir}/etc/dbus-1/system.d/pommed.conf

  # Man page
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/pommed.1 ${pkgdir}/usr/share/man/man1/pommed.1

  # Sounds
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/pommed/data/goutte.wav ${pkgdir}/usr/share/pommed/goutte.wav
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/pommed/data/click.wav ${pkgdir}/usr/share/pommed/click.wav

  # systemd
  install -Dm644 "$srcdir/pommed.service" "$pkgdir/usr/lib/systemd/system/pommed.service"
}

# vim:set ts=2 sw=2 et:
