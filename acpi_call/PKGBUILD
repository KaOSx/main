
_extramodules=extramodules-5.5
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=acpi_call
pkgver=3.17
pkgrel=29
pkgdesc="A kernel module that enables you to call parameterless ACPI methods by writing the method name to /proc/acpi/call, e.g. to turn off discrete graphics card in a dual graphics environment (like NVIDIA Optimus)."
arch=('x86_64')
url="https://github.com/teleshoes/acpi_call"
license=('GPL')
depends=('linux>=5.5.5' 'linux<5.6' 'linux-headers')
makedepends=('git' 'elfutils')
install=acpi_call.install
options=('!strip')
source=("https://github.com/teleshoes/acpi_call/archive/${pkgver}.tar.gz")
md5sums=('0f697ed8339dab7f605951cab8276210')

build() {
  cd $pkgname-${pkgver}
  # needed for linux 4.12
  sed -i 's|asm/uaccess.h|linux/uaccess.h|' acpi_call.c

  make KVERSION=${_kver}
}
package() {
  cd $pkgname-${pkgver}
  install -dm 755 ${pkgdir}/lib/{modules/${_extramodules},modules-load.d}
  install -m 644 ${pkgname}.ko ${pkgdir}/lib/modules/${_extramodules}
  
  # sign the module
  _file=/usr/src/linux-${_kver}/scripts/sign-file
  _pem=/$HOME/signing_key.pem
  _key=/$HOME/signing_key.x509

  ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/acpi_call.ko
  
  gzip ${pkgdir}/lib/modules/${_extramodules}/${pkgname}.ko
  echo ${pkgname} > ${pkgdir}/lib/modules-load.d/${pkgname}.conf

  install -dm 755 ${pkgdir}/usr/share/${pkgname}
  cp -dr --no-preserve='ownership' {examples,support} ${pkgdir}/usr/share/${pkgname}/
  
  #automatically update acpi_call.install
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" ${startdir}/acpi_call.install
}
