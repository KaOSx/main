
_extramodules=extramodules-4.12
_kver="$(cat /lib/modules/${_extramodules}/version)"

pkgname=acpi_call
pkgver=1.1.0
pkgrel=34
pkgdesc="A kernel module that enables you to call parameterless ACPI methods by writing the method name to /proc/acpi/call, e.g. to turn off discrete graphics card in a dual graphics environment (like NVIDIA Optimus)."
arch=('x86_64')
url="http://github.com/mkottman/acpi_call"
license=('GPL')
depends=('linux>=4.12' 'linux<4.13' 'linux-headers')
makedepends=('git')
install=acpi_call.install
source=("https://github.com/mkottman/acpi_call/archive/v${pkgver}.tar.gz")
md5sums=('f69d40e130b0e5ed17ce8adb19e6dda1')

build() {
  cd ${srcdir}/$pkgname-${pkgver}
  # needed for linux 3.17
  sed -i 's|acpi/acpi.h|linux/acpi.h|' acpi_call.c
  # needed for linux 4.12
  sed -i 's|asm/uaccess.h|linux/uaccess.h|' acpi_call.c

  make KVERSION="${_kver}"
}
package() {
  cd ${srcdir}/$pkgname-${pkgver}
  install -dm 755 "${pkgdir}"/lib/{modules/${_extramodules},modules-load.d}
  install -m 644 ${pkgname}.ko "${pkgdir}"/lib/modules/${_extramodules}
  
  gzip "${pkgdir}"/lib/modules/${_extramodules}/${pkgname}.ko
  echo ${pkgname} > "${pkgdir}"/lib/modules-load.d/${pkgname}.conf

  install -dm 755 "${pkgdir}"/usr/share/${pkgname}
  cp -dr --no-preserve='ownership' {examples,support} "${pkgdir}"/usr/share/${pkgname}/
  
  #automatically update acpi_call.install
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/acpi_call.install"
}
