# keep vulkan header & loader version in sync
pkgname=vulkan-loader
pkgver=1.2.151
pkgrel=1
pkgdesc="Vulkan Header files and API registry"
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
depends=('mesa' 'wayland' 'libx11' 'libxkbcommon' 'libxrandr')
makedepends=('cmake' 'vulkan-headers' 'python3-lxml')
source=("https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz")
md5sums=('77e3fd817e3a1becda59b3a3a235ac7f')

build() {
  mkdir -p build
  cd build
  
  cmake ../Vulkan-Loader-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=ON \
    -DBUILD_WSI_XLIB_SUPPORT=ON \
    -DBUILD_WSI_WAYLAND_SUPPORT=ON \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
} 
