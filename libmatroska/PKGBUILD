
pkgname=libmatroska
pkgver=1.6.0
pkgrel=1
pkgdesc="C++ libary to parse Matroska files"
arch=('x86_64')
url="https://www.matroska.org/"
license=('LGPL')
depends=('libebml')
makedepends=('cmake')
options=('!staticlibs')
source=("https://dl.matroska.org/downloads/libmatroska/${pkgname}-${pkgver}.tar.xz")
sha512sums=('7908730aeae426e3f7a90c6cc9151686b453e03cc7046b87a655e51c78c6e5fde5c37d7e0db18c2c237ce853dbe428671b983801104a0860ab0ab1919234f30f')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=YES \
    -DDISABLE_PKGCONFIG=NO # .pc file needed for mkvtoolnix?
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
