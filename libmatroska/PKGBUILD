
pkgname=libmatroska
pkgver=1.5.1
pkgrel=1
pkgdesc="C++ libary to parse Matroska files"
arch=('x86_64')
url="https://www.matroska.org/"
license=('LGPL')
depends=('libebml')
makedepends=('cmake')
options=('!staticlibs')
source=("https://dl.matroska.org/downloads/libmatroska/${pkgname}-${pkgver}.tar.xz")
md5sums=('8564b68a430b1eb3983047e3d4c097ff')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=YES \
    -DDISABLE_PKGCONFIG=NO # .pc file needed for mkvtoolnix?
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
