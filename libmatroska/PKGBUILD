
pkgname=libmatroska
pkgver=1.7.1
pkgrel=1
pkgdesc="C++ libary to parse Matroska files"
arch=('x86_64')
url="https://www.matroska.org/"
license=('LGPL')
depends=('libebml')
makedepends=('cmake')
options=('!staticlibs')
source=("https://dl.matroska.org/downloads/libmatroska/${pkgname}-${pkgver}.tar.xz")
sha512sums=('a6f38d388f6c9bfe399b9263ba4ef0965cfb4e27b39ac04453d5c9a9db8eac719a04c87b58d88b612902297e17aa7437382a66332f3f5b3d41744a13351751fd')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=YES \
    -DDISABLE_PKGCONFIG=NO # .pc file needed for mkvtoolnix?
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
