
pkgname=xwayland
pkgver=23.2.6
pkgrel=1
pkgdesc="X.Org X servers for Wayland"
arch=('x86_64')
url="https://xorg.freedesktop.org"
license=('GPL2')
depends=('xorg-server' 'wayland-protocols')
makedepends=('libx11' 'xtrans' 'libxkbfile' 'libxmu' 'libxtst' 'libxres' 'valgrind' 'meson' 'ninja')
options=('!libtool')
groups=('xorg')
# https://gitlab.freedesktop.org/xorg/xserver/-/tree/xwayland-21.1
source=("https://xorg.freedesktop.org/archive/individual/xserver/${pkgname}-${pkgver}.tar.xz")
sha256sums=('1c9a366b4e7ccadba0f9bd313c59eae12d23bd72543b22a26eaf8b20835cfc6d')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dglamor=true \
    -Dxwayland_eglstream=false \
    -Dxvfb=false \
    -Dipv6=true \
    -Dxdmcp=false \
    -Dxcsecurity=true \
    -Ddri3=true \
    -Dxkb_dir=/usr/share/X11/xkb \
    -Dxkb_output_dir=/var/lib/xkb
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/

  # part of xorg-server, keep conflicting files there
  rm ${pkgdir}/usr/share/man/man1/Xserver.1
  rm ${pkgdir}/usr/lib/xorg/protocol.txt
}
