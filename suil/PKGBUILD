
pkgname=suil
pkgver=0.10.0
pkgrel=2
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="https://drobilla.net/software/suil/"
license=('custom:ISC')
depends=('lv2')
makedepends=('python2')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ba2f6bf631d8f3686772c2b5c9467628')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd ${pkgname}-${pkgver}

  python2 waf install --destdir=${pkgdir}

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

