pkgname=zug
pkgver=0.1.1
pkgrel=1
pkgdesc="C++ library providing transducers."
url='https://sinusoid.es/zug/'
arch=('x86_64')
license=('Boost')
makedepends=('cmake')
source=("https://github.com/arximboldi/zug/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('1b9c8f962e40baa6f0c6af35f957444850063d550078a3ebd0227727b8ef193c')
 
build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -Dzug_BUILD_TESTS=OFF \
    -Dzug_BUILD_EXAMPLES=OFF \
    -Dzug_BUILD_DOCS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

