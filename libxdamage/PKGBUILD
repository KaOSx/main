
pkgname=libxdamage
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 damaged region extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'xorgproto')
makedepends=('pkg-config')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXdamage-${pkgver}.tar.bz2")
sha256sums=('b734068643cac3b5f3d2c8279dd366b5bf28c7219d9e9d8717e1383995e0ea45')

build() {
  cd libXdamage-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static 
  make
}

package() {
  cd libXdamage-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
