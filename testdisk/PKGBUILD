
pkgname=testdisk
pkgver=7.2
pkgrel=5
pkgdesc="Checks and undeletes partitions + PhotoRec, signature based recovery tool"
arch=('x86_64')
url="http://www.cgsecurity.org/index.html?testdisk.html"
license=('GPL')
depends=('libjpeg' 'openssl' 'progsreiserfs' 'ntfs-3g' 'qt5-base')
makedepends=('pkgconfig' 'qt5-tools')
source=("http://www.cgsecurity.org/${pkgname}-${pkgver}-WIP.tar.bz2")
md5sums=('765434bf87a43b6b465def443e9af528')

build() {
  cd ${pkgbase}-${pkgver}-WIP
  sed -i 's|Exec=/usr/bin/qphotorec %F|Exec=kdesu /usr/bin/qphotorec %F|g' linux/qphotorec.desktop

  ./configure --prefix=/usr \
              --without-ewf \
              --enable-sudo
  make
}

package() {
  cd ${pkgbase}-${pkgver}-WIP

  make DESTDIR=${pkgdir} install
}

  
