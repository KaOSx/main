
pkgname=testdisk
pkgver=7.2
pkgrel=1
pkgdesc="Checks and undeletes partitions + PhotoRec, signature based recovery tool"
arch=('x86_64')
url="http://www.cgsecurity.org/index.html?testdisk.html"
license=('GPL')
depends=('libjpeg' 'openssl' 'progsreiserfs' 'ntfs-3g' 'qt5-base')
makedepends=('pkgconfig' 'qt5-tools')
provides=('qphotorec')
replaces=('qphotorec')
conflicts=('qphotorec')
source=("http://www.cgsecurity.org/${pkgname}-${pkgver}-WIP.tar.bz2")
md5sums=('f2690b5f3426c91b1d910591b09ceb11')

build() {
  cd $srcdir/$pkgname-$pkgver-WIP
  sed -i 's|Exec=/usr/bin/qphotorec %F|Exec=kdesu /usr/bin/qphotorec %F|g' linux/qphotorec.desktop
  
  # Qt5 changes
  sed -i '/<QWidget>/d' src/*.h
  sed -i '/<QWidget>/d' src/*.cpp
  sed -i 's/Qt::escape\(.*\)text())/QString\1text()).toHtmlEscaped()/g' src/qphotorec.cpp
  
  sed -i 's|qt4|qt5|g' configure
  
  CXXFLAGS="-std=c++11"

  export QTGUI_LIBS="$(pkg-config Qt5Widgets --libs)"
  export QTGUI_CFLAGS="-fPIC $(pkg-config Qt5Widgets --cflags)"
  ./configure --prefix=/usr \
              --without-ewf \
              --enable-sudo
  make
}

package() {
  cd $srcdir/$pkgbase-$pkgver-WIP
  
  make DESTDIR=$pkgdir install
}

  
