
pkgname=python3-babel
pkgver=2.13.1
pkgrel=1
pkgdesc="Integrated collection of utilities that assist in internationalizing and localizing Python applications"
arch=('x86_64')
license=('Apache')
url="https://babel.pocoo.org/en/latest/"
depends=('python3-pytz')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://pypi.io/packages/source/B/Babel/Babel-${pkgver}.tar.gz")
sha256sums=('33e0952d7dd6374af8dbf6768cc4ddf3ccfefc244f9986d4074704f2fbd18900')

build() {
    cd Babel-${pkgver}

    python3 -m build -nw
}

package() {
    cd Babel-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

