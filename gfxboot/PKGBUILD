
pkgname=gfxboot
pkgver=4.5.91
_pkgver=1.1
pkgrel=1
arch=('x86_64')
license=('GPL')
pkgdesc="Graphical Boot Logo tool"
url="https://www.opensuse.org"
depends=('perl-html-parser' 'freetype2' 'cpio' 'nasm')
makedepends=('rpmextract')
install=gfxboot.install
options=('docs')
source=("https://download.opensuse.org/source/tumbleweed/repo/oss/src/${pkgname}-${pkgver}-${_pkgver}.src.rpm"
        'gfxboot.html'
        'isolinux.patch')
sha256sums=('4137a56039632db25c96b0fc4d618787629cc4f06825a8e56caeb0fc308c9aae'
            '86a92b822cd19a17f4451adf158d984fa42161c4c0a3b45caf91053c573a9859'
            '233eb3ecf88a4a9bbeeae133694f4c2bf2b7f5898b382094e03134fe7633bb4c')

build() {
  cd ${srcdir}

  rpmextract.sh ${pkgname}-${pkgver}-${_pkgver}.src.rpm 

  for i in $(find -name '*.tar.xz');do tar -Jxf $i;done
  mv -f themes ${pkgname}-${pkgver}/

  #chown -R root:root ${pkgname}-${pkgver}

  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/isolinux.patch
  make -s 
}

package() {
  cd ${pkgname}-${pkgver}
  make -s DESTDIR=$pkgdir installsrc 

  install -d -m 755 ${pkgdir}/usr/{sbin,share/man/man8,share/doc/gfxboot} 
  install -m 755 gfxboot gfxboot-compile gfxboot-font ${pkgdir}/usr/sbin 
  install -m 644 doc/gfxboot.8 ${pkgdir}/usr/share/man/man8/ 
  install -m 644 ../gfxboot.html ${pkgdir}/usr/share/doc/gfxboot 
}

