
pkgname=samba
pkgver=4.4.3
#In case version contains a or b
_realver=4.4.3
pkgrel=3
arch=('x86_64')
pkgdesc="The standard Windows interoperability suite of programs for Linux and Unix."
url="http://www.samba.org"
license=('GPL3')
depends=('readline' 'popt' 'libldap' 'libcap' 'krb5' 'db' 'e2fsprogs' 'tdb' 'talloc'
         'cifs-utils' 'libcups' 'acl' 'pam' 'gamin' 'gnutls' 'libbsd' 'ldb' 'tevent'
         'libgcrypt' 'python2' 'libaio' 'iniparser' 'libarchive')
makedepends=('pkg-config' 'docbook-xsl')
provides=('smbclient')
options=('!makeflags')
backup=(etc/logrotate.d/samba
        etc/pam.d/samba
        etc/samba/smb.conf
        etc/conf.d/samba)
source=("http://us1.samba.org/samba/ftp/stable/${pkgname}-${_realver}.tar.gz"
        'samba.logrotate'
        'samba.pam'
        'samba.service'
        'smbd.service'
        'smbd.socket'
        'smbd@.service'
        'nmbd.service'
        'winbindd.service'
        'samba.conf'
        'samba.conf.d')
md5sums=('83b1af8a9899ab4de99f155fc42c83dd'
         '5697da77590ec092cc8a883bae06093c'
         '96f82c38f3f540b53f3e5144900acf17'
         'ee4763a656cf00d92bfda31b6bb2c5cb'
         '621a3c8ec9f05667c20ab4d5a04a86c9'
         'a78b9aa93eb14b0ac445897395693225'
         '70186fa43510bf99e3afd5951e814fbf'
         '90845b89f2321cb19c0b13f385f1782a'
         '64a52bc798033aeabfae3410bd559aa4'
         '49abd7b719e3713a3f75a8a50958e381'
         '6c447748a064d631435dbef0a3dcf32f')

build() {
  cd ${pkgname}-${_realver}
  #patch -p1 -i ${srcdir}/gnutls34.patch
  
  _samba4_idmap_modules=idmap_ad,idmap_rid,idmap_adex,idmap_hash,idmap_tdb2
  _samba4_pdb_modules=pdb_tdbsam,pdb_ldap,pdb_ads,pdb_smbpasswd,pdb_wbc_sam,pdb_samba4
  _samba4_auth_modules=auth_unix,auth_wbc,auth_server,auth_netlogond,auth_script,auth_samba4
  
  ./configure --prefix=/usr \
              --libdir=/usr/lib/ \
              --localstatedir=/var \
              --libexecdir=/usr/lib \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/cache/samba \
              --with-sockets-dir=/var/run/samba \
              --with-piddir=/var/run/ \
              --enable-fhs \
              --with-pam \
              --with-pammodulesdir=/usr/lib/security \
              --with-ads \
              --with-acl-support \
              --with-ldap \
              --with-winbind \
              --enable-gnutls \
              --disable-avahi \
              --with-shared-modules=${_samba4_idmap_modules},${_samba4_pdb_modules},${_samba4_auth_modules} \
              --disable-rpath-install
  make
}

package() {
  cd ${pkgname}-${_realver} 
  make DESTDIR=$pkgdir install
  
  mkdir -p ${pkgdir}/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${pkgdir}/usr/lib/cups/backend/smb
  
  install -d -m755 ${pkgdir}/etc/samba
  install -m644 ${srcdir}/samba-${_realver}/packaging/LSB/smb.conf ${pkgdir}/etc/samba/smb.conf.default
  
  mkdir -p ${pkgdir}/etc/samba/private
  chmod 700 ${pkgdir}/etc/samba/private
  
  install -D -m644 ${srcdir}/samba.logrotate ${pkgdir}/etc/logrotate.d/samba
  install -D -m644 ${srcdir}/samba.pam ${pkgdir}/etc/pam.d/samba
  
  # spool directory
  install -d -m1777 ${pkgdir}/var/spool/samba
  #sed -i 's|/usr/spool/samba|/var/spool/samba|g' ${pkgdir}/etc/samba/smb.conf.default
  
  # fix logrotate
  sed -i -e 's|log.%m|%m.log|g' ${pkgdir}/etc/samba/smb.conf.default
  
  # winbind krb5 locator
  mkdir -p ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  mv  ${pkgdir}/usr/lib/winbind_krb5_locator.so ${pkgdir}/usr/lib/krb5/plugins/libkrb5/
  
  # systemd files
  install -d -m755 ${pkgdir}/usr/lib/systemd/system

  install -m644 ${srcdir}/*.service ${pkgdir}/usr/lib/systemd/system/
  install -m644 ${srcdir}/*.socket ${pkgdir}/usr/lib/systemd/system/
  install -d -m755  ${pkgdir}/etc/conf.d
  install -m644 ${srcdir}/samba.conf.d ${pkgdir}/etc/conf.d/samba

  # create non-persistent dirs via tmpfiles instead of shipping packaged-in
  install -D -m644 ${srcdir}/samba.conf ${pkgdir}/usr/lib/tmpfiles.d/samba.conf
  
  rm -rf ${pkgdir}/var/run
  
  # copy ldap example
  install -D -m644 ${srcdir}/samba-${_realver}/examples/LDAP/samba.schema ${pkgdir}/usr/share/doc/samba/examples/LDAP/samba.schema
 }

