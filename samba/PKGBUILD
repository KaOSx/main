#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=samba
pkgver=3.6.12
#In case version contains a or b
_realver=3.6.12
pkgrel=1
arch=('x86_64')
url="http://www.samba.org"
license=('GPL3')
depends=('readline' 'popt' 'libldap' 'libcap' 'krb5' 'db' 'e2fsprogs' 'tdb' 'talloc'
         'cifs-utils' 'libcups' 'acl' 'pam' 'gamin' 'gnutls')
provides=('smbclient')
conflicts=('smbclient')
replaces=('smbclient')
options=(!makeflags)
backup=(etc/logrotate.d/samba
        etc/pam.d/samba
        etc/xinetd.d/swat)
install='samba.install'
source=("http://us1.samba.org/samba/ftp/stable/${pkgname}-${_realver}.tar.gz"
        'samba.logrotate'
        'swat.xinetd'
        'swat.socket'
        'swat@.service'
        'samba.pam'
        'smbd.service'
        'smbd.socket'
        'smbd@.service'
        'nmbd.service'
        'winbindd.service'
        'samba.conf')
md5sums=('430fd21a1acd26964d3ccf366df8709a'
         '5697da77590ec092cc8a883bae06093c'
         'a4bbfa39fee95bba2e7ad6b535fae7e6'
         '6db11f3dd2112a4b7a73007b189bef3f'
         'b0de262f9c62acd162e873658f33d063'
         '96f82c38f3f540b53f3e5144900acf17'
         '4d86a4e3b2ac4b35e12ac667d1d261a7'
         'a78b9aa93eb14b0ac445897395693225'
         '4f2a5266f88d016a78014bb02e118c7f'
         '46bac3375dcf25baea4699226410987a'
         '03257dd11fc041d59e612c02a54ef4c0'
         '49abd7b719e3713a3f75a8a50958e381')

build() {
  cd ${srcdir}/${pkgname}-${_realver}/source3
  ./configure --prefix=/usr \
              --libdir=/usr/lib/ \
              --localstatedir=/var \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/cache/samba \
              --with-piddir=/var/run/samba \
              --with-fhs \
              --with-pam \
              --with-pam_smbpass \
              --with-pammodulesdir=/usr/lib/security \
              --with-dnsupdate \
              --with-automount \
              --with-quotas \
              --with-ads \
              --with-acl-support \
              --with-libsmbclient \
              --with-syslog \
              --enable-external-libtalloc \
              --disable-dnssd \
              --disable-avahi \
              --with-shared-modules=idmap_ad,idmap_adex,idmap_rid,idmap_hash,idmap_tdb2 \
              --enable-external-libtdb
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_realver}/source3
  mkdir -p ${pkgdir}/etc/samba/private
  chmod 700 ${pkgdir}/etc/samba/private
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${pkgdir}/usr/lib/cups/backend/smb
  chmod 644 ${pkgdir}/usr/include/*.h
  mkdir -p ${pkgdir}/etc/samba
  cat ../examples/smb.conf.default | \
    sed 's|log file = .*$|log file = /var/log/samba/log.%m|g' >${pkgdir}/etc/samba/smb.conf.default
  install -D -m644 ../../samba.logrotate ${pkgdir}/etc/logrotate.d/samba
  install -D -m644 ../../swat.xinetd ${pkgdir}/etc/xinetd.d/swat
  install -D -m644 ../../samba.pam ${pkgdir}/etc/pam.d/samba
  # spool directory
  install -d -m1777 ${pkgdir}/var/spool/samba
  sed -i 's|/usr/spool/samba|/var/spool/samba|g' ${pkgdir}/etc/samba/smb.conf.default
  # fix logrotate
  sed -i -e 's|log.%m|%m.log|g' ${pkgdir}/etc/samba/smb.conf.default
  # nsswitch libraries
  install -D -m755 ${srcdir}/samba-${_realver}/nsswitch/libnss_wins.so ${pkgdir}/usr/lib/libnss_wins.so
  ln -s libnss_wins.so ${pkgdir}/usr/lib/libnss_wins.so.2
  install -D -m755 ${srcdir}/samba-${_realver}/nsswitch/libnss_winbind.so ${pkgdir}/usr/lib/libnss_winbind.so
  ln -s libnss_winbind.so ${pkgdir}/usr/lib/libnss_winbind.so.2
  # winbind krb5 locator
  mkdir -p ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  install -d -m 0755 ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  install -m 755 bin/winbind_krb5_locator.so ${pkgdir}/usr/lib/krb5/plugins/libkrb5/winbind_krb5_locator.so
  # systemd files
  install -d -m755 ${pkgdir}/usr/lib/systemd/system
  for i in smbd nmbd swat winbindd; do
    install -m644 ${srcdir}/*.service ${pkgdir}/usr/lib/systemd/system/
    install -m644 ${srcdir}/*.socket ${pkgdir}/usr/lib/systemd/system/
  done
  # create non-persistent dirs via tmpfiles instead of shipping packaged-in
  install -D -m644 ${srcdir}/samba.conf ${pkgdir}/usr/lib/tmpfiles.d/samba.conf
  rm -rf ${pkgdir}/var/run
  # remove tdb provided files
  for man in tdbbackup tdbdump tdbtool; do
      rm -f ${pkgdir}/usr/share/man/man8/${man}.8
  done
  # copy ldap example
  install -D -m644 ${srcdir}/samba-${_realver}/examples/LDAP/samba.schema ${pkgdir}/usr/share/doc/samba/examples/LDAP/samba.schema
 }

