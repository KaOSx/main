
# check tdb & ldb for updates before building
pkgname=samba
pkgver=4.12.10
_pkgver=4.12.10
pkgrel=2
arch=('x86_64')
pkgdesc="The standard Windows interoperability suite of programs for Linux and Unix."
url="https://www.samba.org"
license=('GPL3')
depends=('readline' 'popt' 'libldap' 'libcap' 'krb5' 'db' 'e2fsprogs' 'tdb' 'talloc'
         'cifs-utils' 'libcups' 'acl' 'pam' 'gnutls' 'libbsd' 'ldb' 'tevent'
         'libgcrypt' 'python3' 'libaio' 'iniparser' 'libarchive' 'jansson' 'perl-parse-yapp')
makedepends=('pkg-config' 'docbook-xsl' 'rpcsvc-proto')
provides=('smbclient')
options=('!makeflags')
backup=(etc/logrotate.d/samba
        etc/pam.d/samba
        etc/samba/smb.conf
        etc/conf.d/samba)
source=("https://us1.samba.org/samba/ftp/stable/${pkgname}-${_pkgver}.tar.gz"
        'samba.logrotate'
        'samba.pam'
        'samba.conf'
        'samba.conf.d')
md5sums=('0fe7c0b61f78b892ba9e15cf69a68f38'
         '995621522c6ec9b68c1b858ceed627ed'
         '96f82c38f3f540b53f3e5144900acf17'
         '49abd7b719e3713a3f75a8a50958e381'
         '6c447748a064d631435dbef0a3dcf32f')

build() {
  cd ${pkgname}-${_pkgver}
  #patch -p1 -i ${srcdir}/gnutls34.patch
  
  _samba4_idmap_modules=idmap_ad,idmap_rid,idmap_adex,idmap_hash,idmap_tdb2
  _samba4_pdb_modules=pdb_tdbsam,pdb_ldap,pdb_ads,pdb_smbpasswd,pdb_wbc_sam,pdb_samba4
  _samba4_auth_modules=auth_unix,auth_wbc,auth_server,auth_netlogond,auth_script,auth_samba4
  
  ./configure --prefix=/usr \
              --libdir=/usr/lib/ \
              --localstatedir=/var \
              --libexecdir=/usr/lib \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/cache/samba \
              --with-sockets-dir=/var/run/samba \
              --with-piddir=/var/run/ \
              --enable-fhs \
              --with-pam \
              --with-pammodulesdir=/usr/lib/security \
              --with-ads \
              --with-acl-support \
              --with-ldap \
              --with-winbind \
              --disable-avahi \
              --with-systemd \
              --systemd-install-services \
              --with-shared-modules=${_samba4_idmap_modules},${_samba4_pdb_modules},${_samba4_auth_modules} \
              --disable-rpath-install
  make
}

package() {
  cd ${pkgname}-${_pkgver} 
  make DESTDIR=$pkgdir install
  
  mkdir -p ${pkgdir}/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${pkgdir}/usr/lib/cups/backend/smb
  
  install -d -m755 ${pkgdir}/etc/samba
  install -m644 ${srcdir}/samba-${_pkgver}/examples/smb.conf.default ${pkgdir}/etc/samba/smb.conf.default
  
  mkdir -p ${pkgdir}/etc/samba/private
  chmod 700 ${pkgdir}/etc/samba/private
  
  install -D -m644 ${srcdir}/samba.logrotate ${pkgdir}/etc/logrotate.d/samba
  install -D -m644 ${srcdir}/samba.pam ${pkgdir}/etc/pam.d/samba
  
  # spool directory
  install -d -m1777 ${pkgdir}/var/spool/samba
  
  # fix logrotate
  sed -i -e 's|log.%m|%m.log|g' ${pkgdir}/etc/samba/smb.conf.default
  
  # winbind krb5 locator
  mkdir -p ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  mv  ${pkgdir}/usr/lib/samba/krb5/winbind_krb5_locator.so ${pkgdir}/usr/lib/krb5/plugins/libkrb5/
  
  # systemd files use /etc/conf.d
  sed -i -e 's|/usr/etc/sysconfig|/etc/conf.d|g' ${pkgdir}/usr/lib/systemd/system/*.service
  
  install -d -m755  ${pkgdir}/etc/conf.d
  install -m644 packaging/systemd/samba.sysconfig ${pkgdir}/etc/conf.d/samba

  # create non-persistent dirs via tmpfiles instead of shipping packaged-in
  install -D -m644 ${srcdir}/samba.conf ${pkgdir}/usr/lib/tmpfiles.d/samba.conf
  
  rm -rf ${pkgdir}/var/run
  
  # copy ldap example
  install -D -m644 ${srcdir}/samba-${_pkgver}/examples/LDAP/samba.schema ${pkgdir}/usr/share/doc/samba/examples/LDAP/samba.schema
 }

