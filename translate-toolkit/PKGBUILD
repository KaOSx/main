
pkgname=translate-toolkit
_pkgname=translate_toolkit
pkgver=3.13.3
pkgrel=1
pkgdesc="Toolkit to convert between various different translation formats, help process and validate localisations"
arch=('x86_64')
url="https://toolkit.translatehouse.org/index.html"
license=('GPL')
depends=('bash' 'python3-lxml' 'python3-six')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools-scm' 'python3-toml' 'python3-wheel')
#source=("https://github.com/translate/translate/releases/download/${pkgver}/translate-toolkit-${pkgver}.tar.gz")
source=("https://files.pythonhosted.org/packages/source/t/translate_toolkit/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('5bd73841a0ae99dbb583489879a4fa742860b3faa75ef2bb9d4f06f9e3195d75')

build() {
  cd ${_pkgname}-${pkgver}

  python3 -m build -nw
}

package() {
  cd ${_pkgname}-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -Dm644 LICENSES ${pkgdir}/usr/share/licenses/${pkgname}/LICENSES
}
