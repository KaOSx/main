
pkgname=libffado
pkgver=2.1.0
pkgrel=6
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++' 'libxml++' 'alsa-lib' 'dbus' 'libconfig'
         'dbus-c++')
makedepends=('scons' 'pyqt' 'python2')
optdepends=('python2: ffado-diag')
source=("http://www.ffado.org/files/libffado-$pkgver.tgz"
        '60-ffado.rules')
md5sums=('26bce2be0b9c1fa4e614f2f494edf388'
         '9934d85ca2a741e7f8c3a1f4da7c2895')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's:python $SOURCE:python2 $SOURCE:' SConstruct
  sed -i 's:python support/tools:python2 support/tools:' \
    support/tools/SConscript

  scons PREFIX=/usr ENABLE_OPTIMIZATIONS=yes UDEVDIR=/usr/lib/udev/rules.d 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # python2 fix
  for i in $(grep '^#!.*bin.*python' -R "$pkgdir" | sed 's/:.*//'); do
    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' "$i"
  done

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png "$pkgdir/usr/share/pixmaps/ffado.png"
    
  # systemd does not allow midsentence comments, should be fixed upstream soon
  rm "$pkgdir/usr/lib/udev/rules.d/60-ffado.rules"
  install -Dm644 "$srcdir/60-ffado.rules" "$pkgdir/usr/lib/udev/rules.d/60-ffado.rules"
}

