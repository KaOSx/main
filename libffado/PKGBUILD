#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer: abveritas[at]chakra-project[dot]org>

pkgname=libffado
pkgver=2.0.1.1
pkgrel=4
pkgdesc="Driver for FireWire audio devices"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++'
          'libxml++' 'alsa-lib' 'dbus' 'libconfig' 'dbus-c++')
makedepends=('scons' 'pyqt' 'python2')
optdepends=('pyqt: mixer applet'
            'python: ffado-diag')
provides=('ffado')
source=("http://chakra-linux.org/sources/libffado/ffado.tar.xz")
md5sums=('c890ca35115b2765bdc4fcba84020334')

_svntrunk=http://subversion.ffado.org/ffado/trunk/libffado
_svnmod=libffado

build() {
  cd "$srcdir/ffado"

  # python2 fix
  sed -i 's:python $SOURCE:python2 $SOURCE:' SConstruct
  sed -i 's:python support/tools:python2 support/tools:' \
    support/tools/SConscript

  scons PREFIX=/usr ENABLE_OPTIMIZATIONS=yes
}

package() {
  cd "$srcdir/ffado"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # add audio device rules
  install -Dm644 "$srcdir/ffado/libffado/60-ffado.rules" \
     "$pkgdir/usr/lib/udev/rules.d/60-ffado.rules"
  rm -r "$pkgdir/lib"

  # python2 fix
  for i in $(grep '^#!.*bin.*python' -R "$pkgdir" | sed 's/:.*//'); do
    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' "$i"
  done

  # desktop files & icons
  install -Dm644 "$srcdir/ffado/support/xdg/ffado.org-ffadomixer.desktop" \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 "$srcdir/ffado/support/xdg/hi64-apps-ffado.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

