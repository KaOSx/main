
pkgname=libffado
pkgver=2.3.0
pkgrel=2
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++' 'libxml++' 'alsa-lib' 'dbus' 'libconfig'
         'dbus-c++')
makedepends=('scons' 'jack')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        'gcc6.patch')
md5sums=('8f452977267200cfaf9b8e16ba3c92df'
         '7364ae3df75f958bd247ab9737a4987a')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i $srcdir/gcc6.patch
}

build() {
  cd ${pkgname}-${pkgver}

  scons PREFIX=/usr ENABLE_OPTIMIZATIONS=yes UDEVDIR=/usr/lib/udev/rules.d COMPILE_FLAGS="$CFLAGS -std=gnu++11"
}

package() {
  cd ${pkgname}-${pkgver}

  scons DESTDIR=${pkgdir} WILL_DEAL_WITH_XDG_MYSELF="True" install
}

