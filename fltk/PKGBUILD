
pkgname=fltk
pkgver=1.3.4
pkgrel=1
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libgl' 'libxcursor' 'glu')
makedepends=('mesa' 'doxygen' 'alsa-lib')
options=('!docs')
groups=('system')
source=("http://fltk.org/pub/fltk/${pkgver}/${pkgname}-${pkgver}-source.tar.gz")
md5sums=('c0e43ac259ac8c5d0f7634566e21e3f8')

build() {
  cd ${pkgname}-${pkgver}

  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-threads \
              --enable-xft \
              --enable-gl

  make
  cd documentation && make html
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  ( cd fluid && make DESTDIR=${pkgdir} install install-linux )

  chmod 644 ${pkgdir}/usr/lib/*.a
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

