
pkgname=fltk
pkgver=1.3.5
_pkgver=1.3.5
pkgrel=3
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
license=('custom:LGPL')
url="https://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libglvnd' 'libxcursor' 'glu')
makedepends=('mesa' 'doxygen' 'alsa-lib')
options=('!docs')
groups=('system')
source=("https://fltk.org/pub/fltk/1.3.5/${pkgname}-${_pkgver}-source.tar.bz2"
        'x-fluid.desktop')
md5sums=('0de2b45a1896be2b4a8cafae89b84248'
         '44cbb9e387f58c7de1bc9ed42d9a128f')

build() {
  cd ${pkgname}-${_pkgver}
  
  cp ${srcdir}/x-fluid.desktop fluid/

  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-threads \
              --enable-xft \
              --enable-gl

  make
  cd documentation && make html
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install
  ( cd fluid && make DESTDIR=${pkgdir} install install-linux )

  chmod 644 ${pkgdir}/usr/lib/*.a
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

