
pkgname=fltk
pkgver=1.3.3
pkgrel=2
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
screenshot="http://www.fltk.org/images/fltk-widgets-std.png"
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libgl' 'libxcursor' 'glu')
makedepends=('mesa' 'doxygen' 'alsa-lib')
options=('!docs')
groups=('system')
install="fltk.install"
source=("http://fltk.org/pub/fltk/${pkgver}/${pkgname}-${pkgver}-source.tar.gz")
md5sums=('9ccdb0d19dc104b87179bd9fd10822e3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-threads \
              --enable-xft \
              --enable-gl

  make
  cd documentation && make html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ( cd fluid && make DESTDIR="${pkgdir}" install install-linux )

  chmod 644 "${pkgdir}/usr/lib"/*.a
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

