
pkgname=fltk
pkgver=1.3.10
pkgrel=1
pkgdesc="Graphical user interface toolkit for X."
arch=('x86_64')
license=('custom:LGPL')
url="https://www.fltk.org/"
depends=('libjpeg-turbo' 'libpng' 'libxft' 'libxinerama' 'hicolor-icon-theme'
         'desktop-file-utils' 'xdg-utils' 'libglvnd' 'libxcursor' 'glu')
makedepends=('mesa' 'alsa-lib' 'cmake')
options=('!docs')
groups=('system')
source=("https://fltk.org/pub/fltk/${pkgver}/${pkgname}-${pkgver}-source.tar.bz2")
md5sums=('ec8075e5ba09f85b531e332b535fbf29')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DOPTION_BUILD_SHARED_LIBS=ON
   cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -D -m644 ${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
