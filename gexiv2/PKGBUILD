 
pkgname=gexiv2
pkgver=0.10.9
_pkgver=0.10
pkgrel=1
pkgdesc='GObject wrapper around the Exiv2 photo metadata library.'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=('x86_64')
license=('GPL2')
depends=('exiv2' 'glib2')
makedepends=('python2-gobject3' 'meson' 'ninja')
source=("https://ftp.gnome.org/pub/gnome/sources/gexiv2/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
        "https://gitlab.gnome.org/GNOME/gexiv2/commit/d8f96634e1df693eb0393cf82893a86dd2a92d2b.diff")
sha256sums=('8806234aa6fd1c345d46bf07a14e82771415071ca5ff63615b1ea62bd2fec0ed'
            'dab25b32a6e1e1e712bad28788e37276ab175c80fa8bf6a8ed0cb795cb507206')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix build with exiv2 0.27
  patch -p1 -i ${srcdir}/d8f96634e1df693eb0393cf82893a86dd2a92d2b.diff
}
            
build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release 
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
