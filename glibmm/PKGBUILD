
pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.58.0
_pkgver=2.58
pkgrel=1
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
options=('!libtool')
makedepends=('glib2' 'libsigc++' 'pkgconfig')
source=("https://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d34189237b99e88228e6f557f7d6e62f767fe356f395a244f5ad0e486254b645')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
