
pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.60.0
_pkgver=2.60
pkgrel=1
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
options=('!libtool')
makedepends=('glib2' 'libsigc++' 'pkgconfig')
source=("https://ftp.gnome.org/pub/GNOME/sources/glibmm/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a3a1b1c9805479a16c0018acd84b3bfff23a122aee9e3c5013bb81231aeef2bc')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
