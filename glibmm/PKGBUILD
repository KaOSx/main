
pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.58.1
_pkgver=2.58
pkgrel=1
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
options=('!libtool')
makedepends=('glib2' 'libsigc++' 'pkgconfig')
source=("https://ftp.gnome.org/pub/GNOME/sources/glibmm/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6e5fe03bdf1e220eeffd543e017fd2fb15bcec9235f0ffd50674aff9362a85f0')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
