
pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.64.2
_pkgver=2.64
pkgrel=2
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
options=('!libtool')
makedepends=('glib2' 'libsigc++' 'pkgconfig')
source=("https://ftp.gnome.org/pub/GNOME/sources/glibmm/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a75282e58d556d9b2bb44262b6f5fb76c824ac46a25a06f527108bec86b8d4ec')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++')
  
  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  
  cd ${pkgbase}-${pkgver}/docs
  make DESTDIR=${pkgdir} install
}
