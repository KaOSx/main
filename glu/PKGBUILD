
pkgname=glu
pkgver=9.0.3
pkgrel=1
pkgdesc="Mesa OpenGL Utility library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/mesa/glu"
license=('LGPL')
depends=('libglvnd')
makedepends=('meson' 'ninja')
options=('!libtool')
source=("https://archive.mesa3d.org//glu/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bd43fe12f374b1192eb15fe20e45ff456b9bc26ab57f0eee919f96ca0f8a330f')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Ddefault_library=shared \
    -Dgl_provider=glvnd
  ninja
}

check() {
  cd build

  meson test 
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

