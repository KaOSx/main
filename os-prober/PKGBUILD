pkgname=os-prober
pkgver=1.77
pkgrel=1
pkgdesc="Utility to detect other operating systems on a set of drives."
url="https://packages.debian.org/unstable/utils/os-prober"
arch=('x86_64')
license=('GPL')
depends=('sh')
makedepends=('gcc' 'sed')
source=("ftp://ftp.us.debian.org/debian/pool/main/o/os-prober/${pkgname}_${pkgver}.tar.xz")
#source=("http://ftp.free.org/pub/frugalware/frugalware-current/source/base/${pkgname}/${pkgname}_${pkgver}.tar.gz")
md5sums=('cfe55c2414868fb1478a4565887b9de3')

build() {
  cd ${pkgname}-${pkgver}

  # adjust lib dir to allow detection of 64-bit distros
  sed -e "s:/lib/ld\*\.so\*:/lib*/ld*.so*:g" -i os-probes/mounted/common/90linux-distro 

  rm -f Makefile
  make newns
}

package() {
  cd ${pkgname}-${pkgver}

  install -D -m755 linux-boot-prober ${pkgdir}/usr/bin/linux-boot-prober
  install -D -m755 os-prober ${pkgdir}/usr/bin/os-prober
  install -D -m755 newns ${pkgdir}/usr/lib/os-prober/newns
  install -D -m755 common.sh ${pkgdir}/usr/share/os-prober/common.sh
  
  for dir in os-probes os-probes/mounted os-probes/init linux-boot-probes linux-boot-probes/mounted; do
    install -dm755 ${pkgdir}/usr/lib/$dir
    install -m755 -t ${pkgdir}/usr/lib/$dir "$dir"/common/*
    [[ -d "$dir"/x86 ]] && cp -r "$dir"/x86/* ${pkgdir}/usr/lib/$dir
  done

  install -Dm755 os-probes/mounted/powerpc/20macosx ${pkgdir}/usr/lib/os-probes/mounted/20macosx

  install -dm755 ${pkgdir}/var/lib/os-prober
}



