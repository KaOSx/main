
pkgname=libva-utils
pkgver=2.13.0
pkgrel=1
pkgdesc="Collection of utilities and examples to exercise VA-API in accordance with the libva project."
arch=('x86_64')
url="https://github.com/intel/libva-utils"
license=('MIT')
depends=('libva')
makedepends=('meson' 'ninja')
source=("https://github.com/intel/libva-utils/archive/${pkgver}.tar.gz")
sha1sums=('e270193489b9f8ceb395ba399bc652d1e43f7335')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release 
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -m644 -D ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

