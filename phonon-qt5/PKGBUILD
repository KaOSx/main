
pkgname=phonon-qt5
_pkgname=phonon
pkgver=4.12.0
pkgrel=1
arch=('x86_64')
url="https://community.kde.org/Phonon"
license=('LGPL')
pkgdesc="The multimedia framework for KDE"
depends=('phonon-backend-qt5' 'pulseaudio' 'qt5-base')
makedepends=('extra-cmake-modules' 'qt5-tools')
source=("https://download.kde.org/stable/phonon/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('3287ffe0fbcc2d4aa1363f9e15747302d0b080090fe76e5f211d809ecb43f39a')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_QT6=OFF
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build
}
