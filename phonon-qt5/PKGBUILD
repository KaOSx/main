
pkgname=phonon-qt5
_pkgname=phonon
pkgver=4.10.3
pkgrel=1
arch=('x86_64')
url="https://phonon.kde.org"
license=('LGPL')
pkgdesc="The multimedia framework for KDE"
depends=('phonon-backend-qt5' 'pulseaudio' 'qt5-base')
makedepends=('extra-cmake-modules' 'qt5-tools')
source=("https://download.kde.org/stable/phonon/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('2e8b145669afa0e93833e4064b657677abc9413e4007fa5ddc91397c9bddc295')

build() {
  mkdir -p build
  
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON 
  make
}

package(){
  cd build
  
  make DESTDIR=${pkgdir} install
}
