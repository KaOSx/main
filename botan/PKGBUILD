
pkgname=botan
_pkgname=Botan
pkgver=3.6.0
pkgrel=1
pkgdesc='Botan (Japanese for peony flower) is a C++ cryptography.'
arch=('x86_64')
url='https://botan.randombit.net/'
license=('BSD')
depends=('python3' 'sqlite' 'xz' 'zlib')
#makedepends=('openssl')
source=("https://botan.randombit.net/releases/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('e6ec54a58c96914d7ce560f17a9a5659ebb987e053323bb9ca2bffeb90707e7a')

build() {
  cd ${_pkgname}-${pkgver}

  ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib \
    --with-sqlite3 \
    --with-os-feature='getrandom, getentropy' \
    --distribution-info=KaOS \
    --disable-static-library
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
