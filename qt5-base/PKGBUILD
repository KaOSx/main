
# build twice on icu updates, first without qt5-tools/make docs
pkgname=('qt5-base' 'qt5-base-docs')
pkgver=5.7.0
subver=5.7.0
_pkgver=5.7
pkgrel=2
pkgdesc=('A cross-platform application and UI framework')
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
makedepends=('sqlite' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-renderutil'
         'glib2' 'dbus' 'fontconfig' 'libsm' 'libxrandr' 'libxv' 'libxi' 'unixodbc' 'libjpeg-turbo'
         'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'systemd'
         'libxcursor' 'libxrender' 'libpng' 'libmariadbclient' 'icu' 'libgl' 'libxfixes' 'libxinerama'
         'libxkbcommon' 'libinput' 'harfbuzz' 'mtdev'
         'postgresql-libs' 'cups' 'python2' 'gperf' 'ruby' 'pulseaudio' 'qt5-tools')
options=('!libtool' 'debug')
_pkgfqn="qtbase-opensource-src-${subver}"
source=("https://download.qt-project.org/official_releases/qt/${_pkgver}/${pkgver}/submodules/$_pkgfqn.tar.xz"
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        'qdbus_locking.patch'
        'libdbus.patch'
        'qdbusconnection.patch'
        'drag_drop.patch')
md5sums=('184f9460b40752d71b15b827260580c2'
         '8a97270d27bad6bf4fa66bdf582e81e4'
         'a9320e76c6a6f3606cd3c006e3ebae90'
         '006098b54beb6bcac3d890a65dbae109'
         'bd2a06880b8703001ef8eca2de92c476')

build() {
    cd $srcdir/$_pkgfqn
    # http://kaosx.us/phpBB3/viewtopic.php?f=18&t=871#p4527, fix is in review for Qt 5.5
    #sed -i -e 's|"gurj",  // Gujarati|"gujr",  // Gujarati|' $srcdir/$_pkgfqn/src/platformsupport/fontdatabases/fontconfig/qfontconfigdatabase.cpp
    
    #patch -p1 -i $srcdir/qdbus_locking.patch
    #patch -p1 -i $srcdir/libdbus.patch
    #patch -p1 -i $srcdir/qdbusconnection.patch
    # https://bugreports.qt.io/browse/QTBUG-45812
    #patch -p1 -i $srcdir/drag_drop.patch
        
    unset QTDIR
    export PATH="$PWD/qtbase/bin:$PWD/qtrepotools/bin:$PATH"

    ./configure -confirm-license -opensource \
                -prefix /usr \
                -bindir /usr/lib/qt5/bin \
                -headerdir /usr/include/qt5 \
                -archdatadir /usr/lib/qt5 \
                -docdir /usr/share/doc/qt5 \
                -plugindir /usr/lib/qt5/plugins \
                -importdir /usr/lib/qt5/imports \
                -qmldir /usr/lib/qt5/qml \
                -datadir /usr/share/qt5 \
                -testsdir /usr/share/qt5/tests \
                -translationdir /usr/share/qt5/translations \
                -sysconfdir /etc/xdg/qt5 \
                -examplesdir /usr/share/qt5/examples \
                -system-harfbuzz \
                -system-sqlite \
                -openssl-linked \
                -nomake tests \
                -nomake examples \
                -optimized-qmake \
                -reduce-relocations \
                -dbus-linked \
                -feature-menu \
                -feature-textdate \
                -feature-ftp \
                -xcursor \
                -journald
     make
     make docs
}

package_qt5-base() {
    pkgdesc=('A cross-platform application and UI framework')
    depends=('sqlite' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-renderutil'
             'glib2' 'dbus' 'fontconfig' 'libsm' 'libxrandr' 'libxv' 'libxi' 'unixodbc' 'libjpeg-turbo'
             'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'systemd'
             'libxcursor' 'libxrender' 'libpng' 'libmariadbclient' 'icu' 'libgl' 'libxfixes' 'libxinerama'
             'libxkbcommon' 'libinput' 'harfbuzz' 'mtdev' 'libproxy')
    optdepends=('postgresql-libs: PostgreSQL driver')
    groups=('qt5')
    
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install
	
    cd "${srcdir}"
    install -D -m644 ${_pkgfqn}/LGPL_EXCEPTION.txt \
    ${pkgdir}/usr/share/licenses/qt5/LGPL_EXCEPTION.txt
    
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

package_qt5-base-docs() {
    pkgdesc="Documentation files for qt5-base"
    depends=('qt5-base')
    groups=('qt5-docs')
    
    cd $srcdir/$_pkgfqn
    make INSTALL_ROOT=$pkgdir install_docs
}
