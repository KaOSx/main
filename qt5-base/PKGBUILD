
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt5-base
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="A cross-platform application and UI framework"
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('sqlite' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-renderutil'
        'glib2' 'dbus' 'fontconfig' 'libsm' 'libxrandr' 'libxv' 'libxi' 'unixodbc' 'libjpeg-turbo'
        'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'systemd'
        'libxcursor' 'libxrender' 'libpng' 'mariadb' 'icu' 'libgl' 'libxfixes' 'libxinerama'
        'libxkbcommon' 'libinput' 'harfbuzz' 'mtdev' 'libproxy' 'double-conversion')
makedepends=('postgresql-libs' 'cups' 'gperf' 'ruby' 'pulseaudio' 'vulkan-headers')
optdepends=('postgresql-libs: PostgreSQL driver')
groups=('qt5')
options=('!libtool' 'debug')
_pkgfqn="qtbase-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/${_pkgfqn}.tar.xz"
        "https://github.com/qt/qtbase/commit/65243ad7aef48af4689ea4454916b504025b5051.diff")
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/${_pkgfqn}.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '2a36a06f2089330771ec991066e43d95')

build() {
    cd ${srcdir}/${_pkgfqn}
    
    # https://bugreports.qt.io/browse/QTBUG-86604
    # https://bugs.kde.org/show_bug.cgi?id=426496
    #patch -p1 -i ${srcdir}/65243ad7aef48af4689ea4454916b504025b5051.diff
    
    #sed -i 's|libs": "-lmysqlclient_r"|libs": "-lmariadb"|' src/sql/configure.json
        
    unset QTDIR
    export PATH="$PWD/qtbase/bin:$PWD/qtrepotools/bin:$PATH"

    ./configure -confirm-license -opensource \
                -prefix /usr \
                -bindir /usr/lib/qt5/bin \
                -headerdir /usr/include/qt5 \
                -archdatadir /usr/lib/qt5 \
                -docdir /usr/share/doc/qt5 \
                -plugindir /usr/lib/qt5/plugins \
                -importdir /usr/lib/qt5/imports \
                -qmldir /usr/lib/qt5/qml \
                -datadir /usr/share/qt5 \
                -testsdir /usr/share/qt5/tests \
                -translationdir /usr/share/qt5/translations \
                -sysconfdir /etc/xdg/qt5 \
                -examplesdir /usr/share/qt5/examples \
                -system-harfbuzz \
                -system-sqlite \
                -system-freetype \
                -openssl-linked \
                -nomake tests \
                -nomake examples \
                -optimized-qmake \
                -reduce-relocations \
                -dbus-linked \
                -feature-menu \
                -feature-textdate \
                -feature-ftp \
                -journald \
                -no-strip
                #-no-use-gold-linker
                #-opengl es2
     make
}

package() {
    cd ${srcdir}/${_pkgfqn}
    make INSTALL_ROOT=$pkgdir install
	
    cd ${srcdir}
    install -D -m644 ${_pkgfqn}/LICENSE* -t ${pkgdir}/usr/share/licenses/${pkgname}
    
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}

