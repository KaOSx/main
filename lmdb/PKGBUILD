
pkgname=lmdb
pkgver=0.9.29
pkgrel=2
pkgdesc="Fast, ultra-compact key-value embedded data store developed by Symas for the OpenLDAP Project."
url="https://www.openldap.org/software/repo.html"
license=('custom:OpenLDAP')
depends=('gcc')
arch=('x86_64')
source=("https://git.openldap.org/openldap/openldap/-/archive/LMDB_${pkgver}/openldap-LMDB_${pkgver}.tar.bz2"
        'lmdb.pc')
md5sums=('e6b0f60b07d420218062bf464b4d0aaf'
         'cd669dad02d583d4774e81d0fc64b004')

build() {
  cd openldap-LMDB_${pkgver}/libraries/liblmdb

  make prefix=/usr
}

package() {
  cd openldap-LMDB_${pkgver}/libraries/liblmdb
  install -m755 -d ${pkgdir}/usr/{bin,lib,include,man/man1,share}
  
  make DESTDIR=${pkgdir} prefix=/usr install
  mv ${pkgdir}/usr/man ${pkgdir}/usr/share/man
  
  install -m644 -D LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -m644 -D ${srcdir}/lmdb.pc ${pkgdir}/usr/lib/pkgconfig/lmdb.pc
}
