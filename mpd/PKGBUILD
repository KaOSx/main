
pkgname=mpd
pkgver=0.22.3
_pkgver=0.22
pkgrel=1
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files"
arch=('x86_64')
license=('GPL')
url="https://www.musicpd.org/"
depends=('libao' 'ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite' 'jack' 'libmms' 'wavpack' 'avahi' 'libid3tag' 'libmpdclient'
         'pulseaudio' 'mpg123' 'yajl' 'sdl_sound' 'fluidsynth' 'boost-libs'
         'libsoxr' 'libnfs' 'zziplib' 'zip' 'libupnp' 'libgme' 'opus' 'udisks2'
         'chromaprint' 'libcdio-paranoia' 'openal')
makedepends=('boost' 'xmlto' 'meson' 'ninja') # 'python3-sphinx')  
install=${pkgname}.install
backup=('etc/mpd.conf')
source=("https://www.musicpd.org/download/mpd/${_pkgver}/mpd-${pkgver}.tar.xz"
        'tmpfiles.d'
        'conf')
md5sums=('2e976ff8ee8790b9face4af8e4e0ec27'
         'e5cdccd201610a41e957d684ebc38985'
         'ca16fc9ce20ca382b620cd366c95647e')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    -Dsystemd_system_unit_dir=/usr/lib/systemd/system \
    -Dsystemd_user_unit_dir=/usr/lib/systemd/user \
    -Ddocumentation=disabled
  ninja
}

package() {
   cd build
   DESTDIR=${pkgdir} ninja install
   
   install -Dm644 ../conf ${pkgdir}/etc/mpd.conf
   install -Dm644 ../tmpfiles.d ${pkgdir}/usr/lib/tmpfiles.d/mpd.conf
   install -d -g 45 -o 45 ${pkgdir}/var/lib/mpd/playlists
}
