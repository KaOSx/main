
pkgname=mpd
pkgver=0.19.18
pkgrel=1
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files"
arch=('x86_64')
license=('GPL')
url="http://www.musicpd.org/"
depends=('libao' 'ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite' 'jack' 'libmms' 'wavpack' 'libmpcdec' 'avahi' 'libid3tag' 'libmpdclient'
         'pulseaudio' 'mpg123' 'yajl' 'sdl_sound' 'libcdio-paranoia' 'fluidsynth' 'boost-libs'
         'libsoxr' 'libnfs' 'zziplib' 'zip' 'libupnp')
makedepends=('boost' 'xmlto') #'doxygen' 
install=${pkgname}.install
backup=('etc/mpd.conf')
source=("http://www.musicpd.org/download/mpd/stable/mpd-${pkgver}.tar.xz"
        'tmpfiles.d'
        'conf')
md5sums=('8fea789a76611feaf8aa7076b32d8c3b'
         'e5cdccd201610a41e957d684ebc38985'
         'ca16fc9ce20ca382b620cd366c95647e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-jack \
    --enable-pulse \
    --enable-libmpdclient \
    --disable-documentation \
    --enable-pipe-output \
    --disable-libwrap \
    --disable-sidplay \
    --enable-zzip \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR=${pkgdir} install
   
   install -Dm644 ../conf ${pkgdir}/etc/mpd.conf
   install -Dm644 ../tmpfiles.d ${pkgdir}/usr/lib/tmpfiles.d/mpd.conf
   install -d -g 45 -o 45 ${pkgdir}/var/lib/mpd/playlists

   install -Dm644 ${pkgdir}/usr/lib/systemd/{system,user}/mpd.service
   sed '/WantedBy=/c WantedBy=default.target' -i ${pkgdir}/usr/lib/systemd/system/mpd.service
   sed '/WantedBy=/c WantedBy=default.target' -i ${pkgdir}/usr/lib/systemd/user/mpd.service
   sed '/\[Service\]/a User=mpd' -i ${pkgdir}/usr/lib/systemd/system/mpd.service
}
