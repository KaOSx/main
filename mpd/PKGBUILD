
pkgname=mpd
pkgver=0.20.21
_pkgver=0.20
pkgrel=3
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files"
arch=('x86_64')
license=('GPL')
url="https://www.musicpd.org/"
depends=('libao' 'ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite' 'jack' 'libmms' 'wavpack' 'avahi' 'libid3tag' 'libmpdclient'
         'pulseaudio' 'mpg123' 'yajl' 'sdl_sound' 'boost-libs'
         'libsoxr' 'libnfs' 'zziplib' 'zip' 'libupnp' 'libgme' 'opus') #'fluidsynth' 
makedepends=('boost' 'xmlto') #'doxygen' 
install=${pkgname}.install
backup=('etc/mpd.conf')
source=("https://www.musicpd.org/download/mpd/${_pkgver}/mpd-${pkgver}.tar.xz"
        'tmpfiles.d'
        'conf')
md5sums=('d93c3c86f5e0fc56cc2e1020f80f8b66'
         'e5cdccd201610a41e957d684ebc38985'
         'ca16fc9ce20ca382b620cd366c95647e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-jack \
    --enable-pulse \
    --enable-libmpdclient \
    --disable-documentation \
    --enable-pipe-output \
    --disable-libwrap \
    --disable-sidplay \
    --enable-zzip \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemduserunitdir=/usr/lib/systemd/user
  make
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR=${pkgdir} install
   
   install -Dm644 ../conf ${pkgdir}/etc/mpd.conf
   install -Dm644 ../tmpfiles.d ${pkgdir}/usr/lib/tmpfiles.d/mpd.conf
   install -d -g 45 -o 45 ${pkgdir}/var/lib/mpd/playlists
}
