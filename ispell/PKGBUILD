
pkgname=ispell
pkgver=3.4.00
pkgrel=2
pkgdesc="Fast screen-oriented spelling checker that shows you your errors in the context of the original file"
arch=('x86_64')
url="https://www.cs.hmc.edu/~geoff/ispell.html"
license=('BSD')
depends=('ncurses')
options=('!makeflags')
source=("http://www.cs.hmc.edu/~geoff/tars/${pkgname}-${pkgver}.tar.gz"
        'license.txt'
        'getline.patch')
md5sums=('051c0946858e15797aed999b21276226'
         'bf51b6181b9914dedc266ba970bb7319'
         '1dca21c5d56405fe592175c3dde2fda2')

build() {
	cd ${pkgname}-${pkgver}
	#patch -p0 -i ${srcdir}/getline.patch

	sed -i -e 's/#undef USG/#define USG/' \
	       -e 's|/usr/local|/usr|' \
               -e 's|/lib|/lib/ispell|' local.h.linux
	cp local.h.linux local.h
	make TMPDIR=/tmp all
}

package() {
	cd ${pkgname}-${pkgver}

    install -dm755 ${pkgdir}/usr/{bin,lib/ispell,share/man/man{1,5}}

	for b in buildhash findaffix icombine ijoin ispell iwhich munchlist tryaffix; do
		install -m755 $b ${pkgdir}/usr/bin/
	done

	install -m644 languages/american/americanmed.hash \
	    ${pkgdir}/usr/lib/ispell/americanmed.hash
	install -m644 languages/english/english.aff \
	    ${pkgdir}/usr/lib/ispell/english.aff
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/american.hash
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/english.hash

	install -Dm644 ../license.txt  ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
