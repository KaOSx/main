
_extramodules=extramodules-5.17
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-470xx' 'khd-nvidia-470xx')
pkgver=470.103.01
pkgrel=6
arch=('x86_64')
url="https://www.nvidia.com/page/home.html"
makedepends=('linux-headers' 'linux>=5.17.4' 'linux<5.18' "nvidia-470xx-utils=${pkgver}")
options=('!strip')
license=('custom')

#_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
_pkg="NVIDIA-Linux-x86_64-${pkgver}"

source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        'nvidia_supported')
md5sums=('f4ea68e482f51f7b358c8bec6162c27a'
         '282f73f1a7326339a7b0e0716eee4f76')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}"
    #patch -p1 -i ${srcdir}/linux-5.10.patch
    
    export IGNORE_CC_MISMATCH=yes

    cd "${srcdir}/${_pkg}/kernel"  
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-470xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=5.17.4' 'linux<5.18' "nvidia-470xx-utils=${pkgver}")
    conflicts=('nvidia' 'nvidia-390xx')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-drm.ko"
    
    # sign the module
    _file=/usr/src/linux-${_kver}/scripts/sign-file
    _pem=/$HOME/signing_key.pem
    _key=/$HOME/signing_key.x509

    ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/nvidia.ko
    ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/nvidia-modeset.ko
    ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko
    ${_file} sha256 ${_pem} ${_key} ${pkgdir}/lib/modules/${_extramodules}/nvidia-drm.ko
  
    gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}

package_khd-nvidia-470xx() {
    pkgdesc="KHD ids for nvidia-470xx ${pkgver}"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-xxx.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia_hwdb"
}
