
pkgname=libxt
pkgver=1.3.0
pkgrel=1
pkgdesc="X11 toolkit intrinsics library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libx11')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXt-${pkgver}.tar.xz")
sha256sums=('52820b3cdb827d08dc90bdfd1b0022a3ad8919b57a39808b12591973b331bf91')

build() {
  cd libXt-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXt-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
