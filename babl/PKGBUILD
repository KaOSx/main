
pkgname=babl
pkgver=0.1.110
_pkgver=0.1
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('x86_64')
url="https://gegl.org/babl/"
license=('LGPL3')
depends=('lcms2')
makedepends=('meson' 'ninja' 'gobject-introspection')
options=('!libtool' '!makeflags')
source=("https://ftp.gimp.org/pub/babl/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bf47be7540d6275389f66431ef03064df5376315e243d0bab448c6aa713f5743')

prepare() {
  cd ${pkgname}-${pkgver}
  # needed for newer meson
  #sed -e "s|not stable, Description|not stable, description|" -i meson.build
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dwith-docs=false \
    -Denable-vapi=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
