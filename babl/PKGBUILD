
pkgname=babl
pkgver=0.1.82
_pkgver=0.1
pkgrel=2
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('x86_64')
url="https://gegl.org/babl/"
license=('LGPL3')
depends=('lcms2')
makedepends=('meson' 'ninja' 'gobject-introspection')
options=('!libtool' '!makeflags')
source=("https://ftp.gimp.org/pub/babl/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c62d93d4ad6774cb8e3231bbbc7f2e61e551e7242d78640d757505ee1a9fadc5')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dwith-docs=false \
    -Denable-vapi=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
