
pkgname=xorg-fonts-75dpi
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org 75dpi fonts"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
groups=('xorg')
install=xfonts.install
source=("https://xorg.freedesktop.org/releases/individual/font/font-adobe-75dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-adobe-utopia-75dpi-1.0.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-75dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bh-lucidatypewriter-75dpi-1.0.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/font/font-bitstream-75dpi-1.0.4.tar.xz")
sha256sums=('1281a62dbeded169e495cae1a5b487e1f336f2b4d971d92911c59c103999b911'
            'a726245932d0724fa0c538c992811d63d597e5f53928f4048e9caf5623797760'
            '6026d8c073563dd3cbb4878d0076eed970debabd21423b3b61dd90441b9e7cda'
            '864e2c39ac61f04f693fc2c8aaaed24b298c2cd40283cec12eee459c5635e8f5'
            'aaeb34d87424a9c2b0cf0e8590704c90cb5b42c6a3b6a0ef9e4676ef773bf826')

build() {
  cd ${srcdir}

  for dir in *; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/75dpi
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}

  for dir in *; do
    if [ -d ${dir} ]; then
      pushd ${dir}
      make DESTDIR=${pkgdir} install
      install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-75dpi-*}
      popd
    fi
  done

  rm -f ${pkgdir}/usr/share/fonts/75dpi/fonts.*
}
