
pkgname=pyqt5-python2
pkgver=5.10
pkgrel=1
pkgdesc="A set of Python2 bindings for the Qt toolkit"
arch=('x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
depends=('sip' 'dbus-python2' 'qt5-multimedia' 'qt5-serialport' 'qt5-svg' 'qtwebkit-tp' 'pyqt5-python3')
makedepends=('qt5-tools')
source=("https://sourceforge.net/projects/pyqt/files/PyQt5/PyQt-${pkgver}/PyQt5_gpl-${pkgver}.tar.gz")
#source=("https://www.riverbankcomputing.com/static/Downloads/PyQt5/PyQt5_gpl-5.6.1.${_commit}.tar.gz")
sha1sums=('14d68357685b6c9c0db9e747772ca3f80e0c8def')

build() {
  cd PyQt5_gpl-${pkgver}

  python2 configure.py \
    --confirm-license \
    --qsci-api \
    --no-sip-files \
    -q /usr/lib/qt5/bin/qmake \
    --designer-plugindir /usr/lib/qt5/plugins/designer 

  #find -name 'Makefile' | xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package(){
  cd PyQt5_gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR=${pkgdir} INSTALL_ROOT=${pkgdir} install
  # provided by pyqt5-python3
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/qt5/plugins
  rm -r ${pkgdir}/usr/share
}
