
pkgname=at-spi2-core
pkgver=2.24.1
_pkgver=2.24
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="https://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=("https://download.gnome.org/sources/at-spi2-core/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('61d0a471e693292934a73f288ebff35c')

build() {
  cd ${pkgname}-${pkgver}

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core \
    --disable-xevie
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

