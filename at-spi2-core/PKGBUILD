
pkgname=at-spi2-core
pkgver=2.48.3
_pkgver=2.48
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('x86_64')
url="https://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection' 'meson' 'ninja')
conflicts=('atk' 'at-spi2-atk')
provides=('atk' 'at-spi2-atk')
replaces=('atk' 'at-spi2-atk')
options=('!libtool')
source=("https://download.gnome.org/sources/at-spi2-core/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('37316df43ca9989ce539d54cf429a768c28bb38a0b34950beadd0421827edf55')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/at-spi2-core
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

