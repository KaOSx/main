
pkgname=gd
pkgver=2.3.2
_pkgver=2.3.2
pkgrel=2
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="https://libgd.github.io/"
license=('custom')
depends=('libpng' 'fontconfig' 'libjpeg-turbo' 'libtiff' 'libxpm' 'libwebp' 'libavif')
makedepends=('ttf-liberation')
options=('!libtool')
source=("https://github.com/libgd/libgd/archive/gd-${_pkgver}.tar.gz")
md5sums=('541e5d9213597c976188614fa400248c')

build() {
  cd libgd-gd-${_pkgver}

  ./bootstrap.sh
  ./configure --prefix=/usr 
  make
}

check() {
  cd libgd-gd-${_pkgver}
  # avif/compare_avif_to_png fails
  make check
 }

package() {
  cd libgd-gd-${_pkgver}
  make DESTDIR=${pkgdir} install
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
