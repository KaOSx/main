
pkgname=glib-networking
pkgver=2.58.0
_pkgver=2.58
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1' 'libidn')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bdfa0255e031b8ee003cc283002536b77ee76450105f1dc6ab066b9bf4330068')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dinstalled_tests=false \
    -Dstatic_modules=false \
    -Dgnome_proxy_support=false
  ninja
}

check() {
  cd build
  # fails 1 test: 4/5 connection  FAIL  1.03 s (killed by signal 6 SIGABRT)
  meson test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}


