
pkgname=glib-networking
pkgver=2.64.3
_pkgver=2.64
pkgrel=2
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1') # 'gsettings-desktop-schemas')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('937a06b124052813bfc0b0b86bff42016ff01067582e1aca65bb6dbe0845a168')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dinstalled_tests=false \
    -Dstatic_modules=false \
    -Dgnome_proxy=disabled
  ninja
}

check() {
  cd build
  # fails 1 test: 4/5 connection  FAIL  1.03 s (killed by signal 6 SIGABRT)
  meson test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}


