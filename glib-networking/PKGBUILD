
pkgname=glib-networking
pkgver=2.50.0
_pkgver=2.50
pkgrel=2
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates')
makedepends=('intltool')
source=("https://download.gnome.org/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('4d06d0224646f274918b1cb6da9a07f6')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-installed-tests
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install testfiles_DATA=
}

