
pkgname=glib-networking
pkgver=2.70.0
_pkgver=2.70
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/glib-networking"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1') # 'gsettings-desktop-schemas')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('66b408e7afa86c582fe38963db56133869ab4b57d34e48ec56aba621940d6f35')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dinstalled_tests=false \
    -Dstatic_modules=false \
    -Dgnome_proxy=disabled
  ninja
}

check() {
  cd build
  # gnutls test timeout, other 4 ok
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


