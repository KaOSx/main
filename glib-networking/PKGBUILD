
pkgname=glib-networking
pkgver=2.68.1
_pkgver=2.68
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/glib-networking"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1') # 'gsettings-desktop-schemas')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d05d8bd124a9f53fc2b93b18f2386d512e4f48bc5a80470a7967224f3bf53b30')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dinstalled_tests=false \
    -Dstatic_modules=false \
    -Dgnome_proxy=disabled
  ninja
}

check() {
  cd build
  # gnutls test timeout, other 4 ok
  meson test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}


