
pkgname=glib-networking
pkgver=2.60.2
_pkgver=2.60
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1' 'gsettings-desktop-schemas') # 'libidn')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c022f3d10f55a5b7c31676fb001b4cb6401db8ab9f6e8418c69de00d0f268732')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dinstalled_tests=false \
    -Dstatic_modules=false
    #-Dgnome_proxy_support=false
  ninja
}

check() {
  cd build
  # fails 1 test: 4/5 connection  FAIL  1.03 s (killed by signal 6 SIGABRT)
  meson test
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}


