
pkgname=glib-networking
pkgver=2.76.0
_pkgver=2.76
pkgrel=3
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/glib-networking"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1') # 'gsettings-desktop-schemas')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('149a05a179e629a538be25662aa324b499d7c4549c5151db5373e780a1bf1b9a')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dgnome_proxy=disabled
  ninja
}

check() {
  cd build
  # 1 fail: 3/5 environment-libproxy            FAIL
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


