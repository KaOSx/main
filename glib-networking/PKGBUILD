
pkgname=glib-networking
pkgver=2.80.0
_pkgver=2.80
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/glib-networking"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates' 'libtasn1') # 'gsettings-desktop-schemas')
makedepends=('intltool' 'meson' 'ninja')
source=("https://download.gnome.org/sources/glib-networking/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d8f4f1aab213179ae3351617b59dab5de6bcc9e785021eee178998ebd4bb3acf')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dgnome_proxy=disabled
  ninja
}

check() {
  cd build
  # 0 fails
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


