
# fails to find perl bindings without running -make generate- in gdal/swig/perl on error
pkgname=gdal
pkgver=3.2.0
_pkgver=3.2.0
pkgrel=2
pkgdesc="A translator library for raster geospatial data formats"
arch=('x86_64')
url="https://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg-turbo' 'libpng' 'libtiff' 'netcdf'
         'poppler' 'python3-numpy' 'sqlite3' 'mariadb' 'postgresql-libs' 'cfitsio' 'unixodbc'
         'json-c' 'libspatialite' 'proj')
makedepends=('perl' 'swig')
options=('!libtool' '!makeflags')
provides=('python3-gdal')
replaces=('python3-gdal')
conflicts=('python3-gdal')
source=("https://download.osgeo.org/gdal/${pkgver}/${pkgname}-${pkgver}.tar.xz"
#source=("https://github.com/OSGeo/gdal/archive/v${_pkgver}.tar.gz"
        "https://github.com/OSGeo/gdal/commit/2b863dbabdc2a7d724f9fdf3e6ff20a142b619f6.diff")
md5sums=('6c573e09acce89cef7853ca58789871c'
         '5bc7a5e048a6a96d9f95d9ea480b7f5b')

prepare() {
  cd ${pkgname}-${_pkgver}
  # poppler 20.08.0 needed
  #patch -p1 -i ${srcdir}/2b863dbabdc2a7d724f9fdf3e6ff20a142b619f6.diff
}

build() {
  cd ${pkgname}-${_pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-netcdf \
    --with-libtiff \
    --with-sqlite3 \
    --with-geotiff \
    --with-mysql \
    --with-python=/usr/bin/python3 \
    --with-curl \
    --with-hdf5 \
    --with-perl \
    --with-geos \
    --with-png \
    --with-poppler \
    --with-spatialite

  make
}

package () {
  cd ${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/bin/*.dox
}
