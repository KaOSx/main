
pkgname=gdal
pkgver=2.3.0
pkgrel=2
pkgdesc="A translator library for raster geospatial data formats"
arch=('x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg-turbo' 'libpng' 'libtiff' 'netcdf'
         'poppler' 'python3-numpy' 'sqlite3' 'mariadb' 'postgresql-libs' 'cfitsio'
         'libspatialite' 'unixodbc' 'json-c')
makedepends=('perl' 'swig')
options=('!libtool' '!makeflags')
provides=('python3-gdal')
replaces=('python3-gdal')
conflicts=('python3-gdal')
#source=("http://download.osgeo.org/gdal/${pkgver}/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/OSGeo/gdal/archive/v${pkgver}.tar.gz")
md5sums=('591ddc9c6ffaa14abd932a7222bcd3fa')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export LDFLAGS="$LDFLAGS -Wl,--as-needed"

  cd ${pkgname}-${pkgver}/gdal

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-netcdf \
    --with-libtiff \
    --with-sqlite3 \
    --with-geotiff \
    --with-mysql \
    --with-python=/usr/bin/python3 \
    --with-curl \
    --with-hdf5 \
    --with-perl \
    --with-geos \
    --with-png \
    --with-poppler \
    --with-spatialite

  sed -i 's|PY_HAVE_SETUPTOOLS=1|PY_HAVE_SETUPTOOLS=|g' ./GDALmake.opt
  sed -i 's|EXE_DEP_LIBS|KILL_EXE_DEP_LIBS|' apps/GNUmakefile

  make
}

package () {
  cd ${pkgname}-${pkgver}/gdal
  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/bin/*.dox
}
