
pkgname=gdal
pkgver=2.1.2
pkgrel=3
pkgdesc="A translator library for raster geospatial data formats"
arch=('x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg-turbo' 'libpng' 'libtiff' 'netcdf'
         'poppler' 'python2-numpy' 'sqlite3' 'libmariadbclient' 'postgresql-libs' 'cfitsio'
         'libspatialite' 'unixodbc')
makedepends=('perl' 'swig')
options=('!libtool' '!makeflags')
source=("http://download.osgeo.org/gdal/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('bd283ff1f88dfe8d750dbc5ecb7c73a8')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export LDFLAGS="$LDFLAGS -Wl,--as-needed"

  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python --with-curl \
              --with-hdf5 --with-perl --with-geos --with-png \
              --with-poppler --with-spatialite

  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile
  #install -d ${pkgdir}/usr/lib/python2.7/site-packages/

  make
}

package () {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/bin/*.dox
}
