
pkgname=libsodium
pkgver=1.0.18
pkgrel=1
pkgdesc="Sodium is a portable, cross-compilable, installable, fork of NaCl with a compatible API."
arch=('x86_64')
url="https://github.com/jedisct1/libsodium"
license=('custom:ISC')
depends=('glibc')
options=('!libtool')
#source=("https://github.com/jedisct1/libsodium/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
source=("https://download.libsodium.org/libsodium/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('3ca9ebc13b6b4735acae0a6a4c4f9a95')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -d -m 755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}
