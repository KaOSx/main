
pkgname=libx11
pkgver=1.6.10
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('libxcb' 'xorgproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
license=('custom')
source=("https://xorg.freedesktop.org/releases/individual/lib/libX11-${pkgver}.tar.bz2")
sha256sums=('af48626989b8515c994777896bd7b7ba2bd5b1ef4e1efaee0a55d8852bbe6226')

build() {
  cd libX11-${pkgver}
  
  ./configure --prefix=/usr \
    --disable-static \
    --disable-xf86bigfont
  make
}

check() {
  cd libX11-${pkgver}
  
  make check
}

package() {
  cd libX11-${pkgver}
  make DESTDIR=${pkgdir} install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
