
pkgname=libx11
pkgver=1.6.6
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('libxcb' 'xorgproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
license=('custom')
source=("https://xorg.freedesktop.org/releases/individual/lib/libX11-${pkgver}.tar.bz2")
sha256sums=('65fe181d40ec77f45417710c6a67431814ab252d21c2e85c75dd1ed568af414f')

build() {
  cd libX11-${pkgver}
  
  ./configure --prefix=/usr --disable-static --disable-xf86bigfont
  make
}

check() {
  cd libX11-${pkgver}
  
  make check
}

package() {
  cd libX11-${pkgver}
  make DESTDIR=${pkgdir} install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
