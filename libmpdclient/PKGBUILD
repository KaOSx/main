
pkgname=libmpdclient
pkgver=2.19
pkgrel=1
pkgdesc='Asynchronous API library for interfacing MPD in the C, C++ & Objective C languages'
url='https://www.musicpd.org/libs/libmpdclient/'
license=('custom:BSD')
arch=('x86_64')
makedepends=('meson' 'ninja')
source=("https://www.musicpd.org/download/${pkgname}/2/${pkgname}-${pkgver}.tar.xz")
md5sums=('aee66cbf5ce2ebf1b976e9c6ade0feee')

build() {
    mkdir -p build
    cd build
    
    meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Ddocumentation=false \
    -Dtest=false
    ninja
}

package() {
    cd build
    DESTDIR=${pkgdir} ninja install

    install -D -m 644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
