# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Kurt J. Bosch <kujub [at] quantentunnel [dot] de>

pkgname=fbsplash
pkgver=1.5.4.3
pkgrel=2.2
pkgdesc="A userspace implementation of a splash screen for Linux (formerly known as gensplash)"
arch=('i686' 'x86_64')
url="http://fbsplash.berlios.de"
license=('GPL')
depends=('miscsplashutils' 'freetype2' 'libjpeg' 'libpng' 'libmng' 'lcms' 'gpm') 
optdepends=('kernel26: enable console background images'
            'initscripts-extras-fbsplash: boot/shutdown with fbsplash'
            'fbsplash-uswsusp: suspend to disk with fbsplash'
            'python: convert themes from splashy to fbsplash')
options=('!makeflags')
install=fbsplash.install
source=(http://dev.gentoo.org/~spock/projects/gensplash/archive/splashutils-${pkgver}.tar.bz2 fbsplash.git.patch)
md5sums=('c722cd4148817b9c50381d9bdc1ea6ef'
         '3a338c60ed0710c8b7e3e08929db521a')

build() {
  cd ${srcdir}/splashutils-${pkgver}

  # patch for building splashutils with glibc and dynamical linking
  patch -Np0 -i ${srcdir}/fbsplash.git.patch || return 1
  autoreconf

  # fix fbcondecor_ctl splash type
  sed -e 's,fbsplash_lib_init(fbspl_bootup),fbsplash_lib_init(fbspl_undef),' -i src/fbcon_decor_ctl.c

  ./configure --prefix=/usr --sysconfdir=/etc --without-klibc --with-gpm --with-mng --with-png --with-ttf --with-ttf-kernel --disable-fbcondecor || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  cd ${pkgdir}

  # fix duplicate slashes to get splash_cache_cleanup grep to work
  sed -r -e 's,^(export spl_.*="/)/+,\1,' -i sbin/splash-functions.sh

  # fix the path to splash_util
  sed -r -e 's,^(export spl_util=)\"/bin/,\1"/sbin/,' -i sbin/splash-functions.sh

  # provide the mountpoint needed by splash-functions.sh
  mkdir -p lib/splash/{cache,tmp}
}
