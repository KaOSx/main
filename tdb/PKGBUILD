
pkgname=tdb
pkgver=1.3.17
pkgrel=1
pkgdesc="Trivial Database similar to GDBM but allows simultaneous commits"
arch=('x86_64')
license=('GPL3')
url="https://tdb.samba.org/"
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=("https://samba.org/ftp/tdb/${pkgname}-${pkgver}.tar.gz")
md5sums=('7d06d8709188e07df853d9e91db88927')

build() {
   cd ${pkgname}-${pkgver}
   
   ./configure --prefix=/usr \
        --localstatedir=/var \
        --sysconfdir=/etc/samba
   make
}

package() {
   cd ${pkgname}-${pkgver}
   
   make DESTDIR=$pkgdir/ install
}


