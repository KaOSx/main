
pkgname=coin
pkgver=4.0.1
_commit=dfb56d294eeef3581e4322ed8881f478fc242dec
pkgrel=1
pkgdesc='OpenGL-based, 3D graphics library that has its roots in the Open Inventor 2.1 API.'
url='https://github.com/coin3d/coin'
license=('GPL')
arch=('x86_64')
depends=('libglvnd' 'expat' 'openal' 'fontconfig' 'zlib' 'freetype2' 'glu')
makedepends=('cmake' 'boost')
options=('!libtool')
source=("https://github.com/coin3d/coin/releases/download/v${pkgver}/coin-${pkgver}-src.tar.gz")
md5sums=('99ee9c7a5d510875eef84d6b02d3bf79')

build() {
  cmake -B build -S ${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCOIN_THREADSAFE=ON \
    -DUSE_EXTERNAL_EXPAT=ON \
    -DCOIN_BUILD_DOCUMENTATION=OFF \
    -DCOIN_BUILD_DOCUMENTATION_MAN=ON \
    -DCOIN_BUILD_TESTS=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}


