
pkgname=coin
pkgver=3.1.3
pkgrel=1
pkgdesc='High-level 3D graphics toolkit on top of OpenGL'
url='http://www.coin3d.org/'
license=('GPL')
arch=('x86_64')
depends=('libgl' 'expat')
makedepends=('doxygen')
optdepends=('openal: sound/dynamic linking support'
            'fontconfig: dynamic linking support'
            'zlib: dynamic linking support'
            'freetype2: dynamic linking support'
            'js: dynamic linking support')
options=('!libtool')
source=("https://bitbucket.org/Coin3D/coin/downloads/Coin-${pkgver}.tar.gz")
md5sums=('1538682f8d92cdf03e845c786879fbea')

build() {
	cd Coin-${pkgver}

	sed -i '/^#include "fonts\/freetype.h"$/i #include <cstdlib>\n#include <cmath>' src/fonts/freetype.cpp
	sed -i '/^#include <Inventor\/C\/basic.h>$/i #include <Inventor/C/errors/debugerror.h>' include/Inventor/SbBasic.h

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-optimization \
		--enable-3ds-import \
		--enable-javascript-api \
		--enable-threadsafe \
		--enable-exceptions \
		--enable-man \
		--enable-html \
		--with-mesa \
		--disable-debug \
		--enable-shared \
		--disable-maintainer-mode \
		--disable-dependency-tracking \
		--enable-system-expat

	make
}

package() {
	cd Coin-${pkgver}
	make DESTDIR=${pkgdir} install

	for _FILE in threads deprecated errors events ; do
		mv  "${pkgdir}/usr/share/man/man3/${_FILE}.3" "${pkgdir}/usr/share/man/man3/coin-${_FILE}.3"
	done
}

