
pkgname=coin
pkgver=3.1.3
pkgrel=5
pkgdesc='High-level 3D graphics toolkit on top of OpenGL'
url='https://bitbucket.org/Coin3D/coin/wiki/Home'
license=('GPL')
arch=('x86_64')
depends=('libgl' 'expat' 'openal' 'fontconfig' 'zlib' 'freetype2' 'js')
makedepends=('doxygen')
options=('!libtool')
source=("https://bitbucket.org/Coin3D/coin/downloads/Coin-${pkgver}.tar.gz"
        'memhandler.patch')
md5sums=('1538682f8d92cdf03e845c786879fbea'
         '25897deed88fdaf6a4950fce79211f9f')

build() {
    cd Coin-${pkgver}

    #sed -i '/^#include "fonts\/freetype.h"$/i #include <cstdlib>\n#include <cmath>' src/fonts/freetype.cpp
    sed -i '/^#include <Inventor\/C\/basic.h>$/i #include <Inventor/C/errors/debugerror.h>' include/Inventor/SbBasic.h
    patch -p1 -i ${srcdir}/memhandler.patch

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-optimization \
        --enable-3ds-import \
        --enable-javascript-api \
        --enable-threadsafe \
        --enable-exceptions \
        --enable-man \
        --enable-html \
        --with-mesa \
        --disable-debug \
        --enable-shared \
        --disable-maintainer-mode \
        --disable-dependency-tracking \
        --enable-system-expat

    make
}

package() {
    cd Coin-${pkgver}
    make DESTDIR=${pkgdir} install

    rm -r ${pkgdir}/usr/share/man/man3
}

