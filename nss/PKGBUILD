
pkgname=nss
pkgver=3.32
_pkgver=3_32
_nsprver=4.16
pkgrel=1
pkgdesc="Mozilla Network Security Services"
arch=('x86_64')
url="http://www.mozilla.org/projects/security/pki/nss/"
license=('MPL' 'GPL')
depends=("nspr>=${_nsprver}" 'sqlite3' 'zlib' 'sh')
makedepends=('perl')
options=('!strip' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/security/nss/releases/NSS_${_pkgver}_RTM/src/${pkgname}-${pkgver}.tar.gz"
        'nss.pc.in'
        'nss-config.in'
        'add_spi+cacert_ca_certs.patch'
        'ssl-renegotiate-transitional.patch')
sha1sums=('5951fc4c8f98395fea7b6ae9332e7795d40be865'
          'aa5b2c0aa38d3c1066d511336cf28d1333e3aebd'
          'cb744cc3e56b604e4754bc3c7d9f25bb9a0a136c'
          'be6ef0f124f188ebb76f4d83e13466bdcfb27dd3'
          '8a964a744ba098711b80c0d279a2993524e8eb92')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Adds the SPI Inc. and CAcert.org CA certificates - patch from Debian
  patch -p1 -i "${srcdir}/add_spi+cacert_ca_certs.patch"
  # Adds transitional SSL renegotiate support - patch from Debian
  #patch -p3 -i "${srcdir}/ssl-renegotiate-transitional.patch"

  #cd security/nss/lib/ckfw/builtins
  #make generate

  cd "${srcdir}/${pkgname}-${pkgver}/nss"
  unset CFLAGS
  unset CXXFLAGS
  export BUILD_OPT=1
  export NSS_USE_SYSTEM_SQLITE=1
  export NSS_ENABLE_ECC=1
  export NSPR_INCLUDE_DIR="`nspr-config --includedir`"
  export NSPR_LIB_DIR="`nspr-config --libdir`"
  export USE_64=1

  make -C coreconf
  make -C lib/dbm
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/include/nss"

  NSS_VMAJOR=`grep "#define.*NSS_VMAJOR" nss/lib/nss/nss.h | awk '{print $3}'`
  NSS_VMINOR=`grep "#define.*NSS_VMINOR" nss/lib/nss/nss.h | awk '{print $3}'`
  NSS_VPATCH=`grep "#define.*NSS_VPATCH" nss/lib/nss/nss.h | awk '{print $3}'`

  sed ../nss.pc.in -e "s,%libdir%,/usr/lib,g" \
  				-e "s,%prefix%,/usr,g" \
				-e "s,%exec_prefix%,/usr/bin,g" \
				-e "s,%includedir%,/usr/include/nss,g" \
				-e "s,%NSPR_VERSION%,${_nsprver},g" \
				-e "s,%NSS_VERSION%,${pkgver},g" > \
			"${pkgdir}/usr/lib/pkgconfig/nss.pc"
  ln -sf nss.pc "${pkgdir}/usr/lib/pkgconfig/mozilla-nss.pc"
  chmod 644 ${pkgdir}/usr/lib/pkgconfig/*.pc

  sed ../nss-config.in -e "s,@libdir@,/usr/lib,g" \
  				    -e "s,@prefix@,/usr/bin,g" \
				    -e "s,@exec_prefix@,/usr/bin,g" \
				    -e "s,@includedir@,/usr/include/nss,g" \
				    -e "s,@MOD_MAJOR_VERSION@,${NSS_VMAJOR},g" \
				    -e "s,@MOD_MINOR_VERSION@,${NSS_VMINOR},g" \
				    -e "s,@MOD_PATCH_VERSION@,${NSS_VPATCH},g" \
			    > "${pkgdir}/usr/bin/nss-config"
  chmod 755 "${pkgdir}/usr/bin/nss-config"

  cd dist/*.OBJ/bin
  install -t "$pkgdir/usr/bin" *util shlibsign signtool signver ssltap

  cd ../lib
  install -t "$pkgdir/usr/lib" *.so
  install -t "$pkgdir/usr/lib" -m644 libcrmf.a *.chk

  cd ../../public/nss
  install -t "$pkgdir/usr/include/nss" -m644 *.h
}

