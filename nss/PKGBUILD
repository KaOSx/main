pkgname=nss
pkgver=3.59
_pkgver=3_59
_nsprver=4.29
pkgrel=1
pkgdesc="Mozilla Network Security Services"
arch=('x86_64')
url="http://www.mozilla.org/projects/security/pki/nss/"
license=('MPL' 'GPL')
depends=("nspr>=${_nsprver}" 'sqlite3' 'zlib' 'sh')
makedepends=('gyp' 'perl' 'python3')
options=('!strip' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/security/nss/releases/NSS_${_pkgver}_RTM/src/${pkgname}-${pkgver}.tar.gz"
        'nss.pc.in'
        'nss-config.in'
        'add_spi+cacert_ca_certs.patch'
        'ssl-renegotiate-transitional.patch')
sha256sums=('e6298174caa8527beacdc2858f77ed098d7047c1792846040e27e420fed0ce24'
            'b9f1428ca2305bf30b109507ff335fa00bce5a7ce0434b50acd26ad7c47dd5bd'
            'e44ac5095b4d88f24ec7b2e6a9f1581560bd3ad41a3d198596d67ef22f67adb9'
            '12a849a5f255ba63466051bc57808968dc6933d17f0e9489cbbd23b28f399879'
            '12df04bccbf674db1eef7a519a28987927b5e9c107b1dc386686f05e64f49a97')

build() {
  cd ${pkgname}-${pkgver}
  # Adds the SPI Inc. and CAcert.org CA certificates - patch from Debian
  patch -p1 -i "${srcdir}/add_spi+cacert_ca_certs.patch"
  # Adds transitional SSL renegotiate support - patch from Debian
  #patch -p3 -i "${srcdir}/ssl-renegotiate-transitional.patch"

  #cd security/nss/lib/ckfw/builtins
  #make generate

  cd nss
  ./build.sh \
    --opt \
    --system-sqlite \
    --system-nspr \
    --enable-libpkix \
    --target x64 \
    --disable-tests
}

package() {
  cd ${pkgname}-${pkgver}
  install -m755 -d ${pkgdir}/usr/lib/pkgconfig
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 -d ${pkgdir}/usr/include/nss

  NSS_VMAJOR=`grep "#define.*NSS_VMAJOR" nss/lib/nss/nss.h | awk '{print $3}'`
  NSS_VMINOR=`grep "#define.*NSS_VMINOR" nss/lib/nss/nss.h | awk '{print $3}'`
  NSS_VPATCH=`grep "#define.*NSS_VPATCH" nss/lib/nss/nss.h | awk '{print $3}'`

  sed ../nss.pc.in -e "s,%libdir%,/usr/lib,g" \
  				-e "s,%prefix%,/usr,g" \
				-e "s,%exec_prefix%,/usr/bin,g" \
				-e "s,%includedir%,/usr/include/nss,g" \
				-e "s,%NSPR_VERSION%,${_nsprver},g" \
				-e "s,%NSS_VERSION%,${pkgver},g" > \
			"${pkgdir}/usr/lib/pkgconfig/nss.pc"
  ln -sf nss.pc ${pkgdir}/usr/lib/pkgconfig/mozilla-nss.pc
  chmod 644 ${pkgdir}/usr/lib/pkgconfig/*.pc

  sed ../nss-config.in -e "s,@libdir@,/usr/lib,g" \
  				    -e "s,@prefix@,/usr/bin,g" \
				    -e "s,@exec_prefix@,/usr/bin,g" \
				    -e "s,@includedir@,/usr/include/nss,g" \
				    -e "s,@MOD_MAJOR_VERSION@,${NSS_VMAJOR},g" \
				    -e "s,@MOD_MINOR_VERSION@,${NSS_VMINOR},g" \
				    -e "s,@MOD_PATCH_VERSION@,${NSS_VPATCH},g" \
			    > ${pkgdir}/usr/bin/nss-config
			    
  chmod 755 ${pkgdir}/usr/bin/nss-config

  cd dist/Release/bin
  install -t ${pkgdir}/usr/bin *util shlibsign signtool signver ssltap

  cd ../lib
  install -t ${pkgdir}/usr/lib *.so
  install -t ${pkgdir}/usr/lib -m644 *.chk

  cd ../../public/nss
  install -t ${pkgdir}/usr/include/nss -m644 *.h
}
