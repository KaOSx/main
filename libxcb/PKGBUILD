
pkgname=libxcb
pkgver=1.15
pkgrel=1
epoch=2
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xcb.freedesktop.org/"
depends=('xcb-proto' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python3' 'xorg-util-macros')
options=('!libtool')
license=('custom')
source=("https://xorg.freedesktop.org/archive/individual/lib/${pkgname}-${pkgver}.tar.xz")
sha256sums=('cc38744f817cf6814c847e2df37fcb8997357d72fa4bcbc228ae0fe47219a059')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi

  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package () {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
