
pkgname=gc
pkgver=7.2
_pkgver=7.2d
pkgrel=1
pkgdesc="A garbage collector for C and C++"
arch=('x86_64')
url="http://www.hpl.hp.com/personal/Hans_Boehm/gc/"
license=('GPL')
source=("http://www.hpl.hp.com/personal/Hans_Boehm/gc/gc_source/${pkgname}-${_pkgver}.tar.gz")
depends=('gcc-libs')
options=(!libtool)
md5sums=('91340b28c61753a789eb6077675d87d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr --enable-cplusplus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share/gc"
  install -m755 -d "${pkgdir}/usr/share/doc/gc"
  install -m644 doc/README{,.{changes,contributors,environment,linux}} \
    "${pkgdir}/usr/share/doc/gc/"

  sed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"
}
