 
pkgname=lpsolve
_pkgname=lp_solve
pkgver=5.5.2.5
_pkgver=5.5
pkgrel=2
pkgdesc="Mixed Integer Linear Programming (MILP) solver"
arch=('x86_64')
url="http://lpsolve.sourceforge.net/"
license=('LGPL2.1')
depends=('glibc')
source=("https://downloads.sourceforge.net/project/lpsolve/lpsolve/${pkgver}/${_pkgname}_${pkgver}_source.tar.gz"
        'cflags.patch')
md5sums=('3be57261fc41dd8e210f54017220d5f7'
         '7403f745d06619f59f52800b655d8751')

build() {
  cd ${_pkgname}_${_pkgver}
  patch -p1 -i $srcdir/cflags.patch
  # fix for current gcc/glibc
  sed -i -e 's|isnan(0)|isnan(0.0)|' lpsolve55/ccc
  sed -i -e 's|isnan(0)|isnan(0.0)|' lp_solve/ccc

  cd lpsolve55
  sh -x ccc
  #rm bin/ux*/liblpsolve55.a
  cd ../lp_solve
  sh -x ccc
}

package() {
  cd ${_pkgname}_${_pkgver}

 install -d ${pkgdir}/usr/{bin,lib,include/lpsolve}
 install -m 755 lp_solve/bin/ux*/lp_solve ${pkgdir}/usr/bin/
 install -m 755 lpsolve55/bin/ux*/liblpsolve55.so ${pkgdir}/usr/lib/
 install -m 644 lp*.h ${pkgdir}/usr/include/lpsolve/
}
