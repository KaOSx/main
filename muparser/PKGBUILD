
pkgname=muparser
pkgver=2.3.2
pkgrel=1
pkgdesc="Fast math parser library"
arch=('x86_64')
url="https://beltoforion.de/article.php?a=muparser"
depends=('gcc-libs')
makedepends=('cmake')
license=('custom')
options=('!makeflags')
source=("https://github.com/beltoforion/muparser/archive/v${pkgver}.tar.gz")
md5sums=('cbc1b284e03abc7081b3c30997959893')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -D -m644 ../${pkgname}-${pkgver}/License.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
