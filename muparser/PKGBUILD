
pkgname=muparser
pkgver=2.2.6
pkgrel=1
pkgdesc="Fast math parser library"
arch=('x86_64')
url="http://muparser.beltoforion.de/"
depends=('gcc-libs')
makedepends=('cmake')
license=('custom')
options=('!makeflags')
source=("https://github.com/beltoforion/muparser/archive/v${pkgver}.tar.gz")
md5sums=('f197b2815ca0422b2091788a78f2dc8a')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  install -D -m644 ../${pkgname}-${pkgver}/License.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
