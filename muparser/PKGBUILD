
pkgname=muparser
pkgver=2.3.3.1
_pkgver=2.3.3-1
pkgrel=1
pkgdesc="Fast math parser library"
arch=('x86_64')
url="https://beltoforion.de/article.php?a=muparser"
depends=('gcc-libs')
makedepends=('cmake')
license=('custom')
options=('!makeflags')
source=("https://github.com/beltoforion/muparser/archive/v${_pkgver}.tar.gz")
md5sums=('eb558545cbda3bb25d43f27e0a7c9e9a')

build() {
  cmake -B build -S ${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 ${pkgname}-${_pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
