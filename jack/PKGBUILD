
pkgname=jack
pkgver=0.126.0
pkgrel=1
pkgdesc="A low-latency audio server"
arch=('x86_64')
url="https://jackaudio.org/"
license=('GPL2' 'custom:LGPL2')
depends=('readline' 'libsamplerate' 'opus' 'libffado')
makedepends=('doxygen')
conflicts=('jack2')
backup=(etc/conf.d/jack-audio-connection-kit)
options=('!libtool')
install=${pkgname}.install
source=("https://github.com/jackaudio/jack1/releases/download/0.126.0/jack1-${pkgver}.tar.gz"
        "99-audio.conf"
        "40-hpet-permissions.rules")
md5sums=('5913c06644855f472894da53a624e63f'
         '4d928a76e3f3d77d037c42ab75ac0f0b'
         '23eba69d2dbbb800f68176e5d38b9af5')

build() {
  cd jack1-${pkgver}

  ./configure --prefix=/usr \
        --libdir=/usr/lib
  make
}

package() {
  cd jack1-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/99-audio.conf $pkgdir/etc/security/limits.d/99-audio.conf
  install -Dm644 ${srcdir}/40-hpet-permissions.rules ${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules
  install -Dm644 ${srcdir}/jack1-$pkgver/jack/jslist.h ${pkgdir}/usr/include/jack/jslist.h
}

