 
pkgname=xorg-fonttosfnt
pkgver=1.2.3
pkgrel=1
pkgdesc="Wraps a set of bdf or pcf bitmap fonts in a sfnt (TrueType or OpenType) wrapper."
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/app/fonttosfnt"
license=('custom')
depends=('freetype2' 'libfontenc')
makedepends=('xorgproto' 'xorg-util-macros')
# add to groups once it replaces xorg-bdftopcf
#groups=('xorg' 'xorg-apps')
source=("https://xorg.freedesktop.org/archive/individual/app/fonttosfnt-${pkgver}.tar.xz")
sha256sums=('aa7a93f240cbd0f5cdfe6be7c1b934b4f74d23de6257883a9f1b4bf21d7d61af')

build() {
  cd fonttosfnt-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd fonttosfnt-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
