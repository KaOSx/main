
pkgname=libatomic_ops
pkgver=7.6.4
_pkgver=7_6_4
pkgrel=1
pkgdesc="Provides semi-portable access to hardware-provided atomic memory update operations"
arch=('x86_64')
url="https://github.com/ivmai/libatomic_ops"
license=('GPL2' 'MIT')
depends=('glibc')
source=("https://github.com/ivmai/libatomic_ops/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('493859ecc543ca52b96926df97b7907c')

build() {
  cd ${pkgname}-${pkgver}

  #./autogen.sh
  ./configure --prefix=/usr \
    --disable-static \
    --enable-shared
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 doc/LICENSING.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
