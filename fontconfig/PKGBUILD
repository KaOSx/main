# check frescobaldi on updates
pkgname=fontconfig
pkgver=2.12.4
pkgrel=1
epoch=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
depends=('expat' 'freetype2')
makedepends=('gperf')
options=('!libtool')
install='fontconfig.install'
source=("http://www.fontconfig.org/release/${pkgname}-${pkgver}.tar.gz"
        '29-replace-bitmap-fonts.conf'
        'fc-cache-install.hook')
md5sums=('4fb01fc3f41760c41c69e37cc784b658'
         '526b95ebde1b07eb5dbc5d71fb613ed1'
         'ba07f578513842a325e397aa0f696319')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd  ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd  ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m644 ${srcdir}/29-replace-bitmap-fonts.conf ${pkgdir}/etc/fonts/conf.avail
  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
    ${pkgdir}/etc/fonts/conf.d/29-replace-bitmap-fonts.conf

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}
  
  install -Dm644 ../fc-cache-install.hook ${pkgdir}/usr/share/libalpm/hooks/fc-cache-install.hook
}
