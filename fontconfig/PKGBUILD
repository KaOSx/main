# check frescobaldi on updates
pkgname=fontconfig
pkgver=2.13.1
pkgrel=1
epoch=1
pkgdesc="A library for configuring and customizing font access"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
depends=('expat' 'freetype2')
makedepends=('gperf')
options=('!libtool')
install='fontconfig.install'
source=("https://www.freedesktop.org/software/fontconfig/release/${pkgname}-${pkgver}.tar.bz2"
        '29-replace-bitmap-fonts.conf'
        'fc-cache-install.hook')
sha256sums=('f655dd2a986d7aa97e052261b36aa67b0a64989496361eca8d604e6414006741'
            '752fe94e93fe6abfd4ff46653d312d497223bdffbecc4effd413b0010f8de2bf'
            '672f6a1c5e164671955ce807e670306194142a1794ce88df653aa717a972e274')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd  ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd  ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m644 ${srcdir}/29-replace-bitmap-fonts.conf ${pkgdir}/etc/fonts/conf.avail
  ln -s ../conf.avail/29-replace-bitmap-fonts.conf \
    ${pkgdir}/etc/fonts/conf.d/29-replace-bitmap-fonts.conf

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}
  
  install -Dm644 ../fc-cache-install.hook ${pkgdir}/usr/share/libalpm/hooks/fc-cache-install.hook
}
