
pkgname=python3
pkgver=3.5.3
pkgrel=2
_pybasever=3.5
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
license=('custom')
url="https://www.python.org"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('tk' 'sqlite' 'valgrind' 'bluez' 'libgl') # libgl for tests
optdepends=('tk: for tkinter')
options=('!makeflags')
source=("https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")
md5sums=('57d1f8bfbabf4f2500273fb0706e6f21')  

build() {
  cd Python-${pkgver}

  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Use the system copy   
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --without-ensurepip

  make
}

check() {
  cd Python-${pkgver}

  LD_LIBRARY_PATH=${srcdir}/Python-${pkgver}:${LD_LIBRARY_PATH} \
     ${srcdir}/Python-${pkgver}/python -m test.regrtest -x test_posixpath test_logging test_site test_urllib2_localnet test_pyexpat
}

package() {
  cd Python-${pkgver}
  make DESTDIR=${pkgdir} install maninstall

  # Conflicts with python2 
  rm ${pkgdir}/usr/bin/2to3

  ln -sf ../../libpython${_pybasever}mu.so \
    ${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so

  # Clean-up reference to build directory
  sed -i "s|${srcdir}/Python-${pkgver}:||" ${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
