
pkgname=python3
pkgver=3.3.4
pkgrel=1
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
license=('custom')
url="http://www.python.org"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('tk' 'sqlite3' 'valgrind')
optdepends=('tk: for tkinter')
options=('!makeflags')
source=("http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")
md5sums=('8fb961a20600aafafd249537af3ac637')  

build() {
  cd "${srcdir}/Python-${pkgver}"

  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Use the system copy of various libraries (expat, zlib and libffi)rather than copies shipped  
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-valgrind \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi

  make
}

#check() {
#  cd "${srcdir}/Python-${pkgver}"
#  LD_LIBRARY_PATH="${srcdir}/Python-${pkgver}":${LD_LIBRARY_PATH} \
#     "${srcdir}/Python-${pkgver}/python" -m test.regrtest -x test_posixpath test_logging
#}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" install maninstall

  # Fix conflicts with python2 
  rm ${pkgdir}/usr/bin/2to3
  #rm ${pkgdir}/usr/bin/python
  #rm ${pkgdir}/usr/bin/python-config
  #rm ${pkgdir}/usr/bin/idle
  #rm ${pkgdir}/usr/bin/pydoc

  # Why are these not done by default...
  #ln -sf python3               "${pkgdir}/usr/bin/python"
  #ln -sf python3-config        "${pkgdir}/usr/bin/python-config"
  #ln -sf idle3                 "${pkgdir}/usr/bin/idle"
  #ln -sf pydoc3                "${pkgdir}/usr/bin/pydoc"
  #ln -sf python${_pybasever}.1 "${pkgdir}/usr/share/man/man1/python3.1"

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}mu.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # Clean-up reference to build directory
  sed -i "s#$srcdir/Python-${pkgver}:##" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
