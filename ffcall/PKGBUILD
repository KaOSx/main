
pkgname=ffcall
_pkgname=libffcall
pkgver=2.5
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('x86_64')
url="https://www.gnu.org/software/libffcall/"
license=('GPLv2+')
options=('!libtool' '!makeflags')
source=("https://ftp.gnu.org/gnu/libffcall/${_pkgname}-${pkgver}.tar.gz")
md5sums=('4471b9d2342857125d2bbeee47dca66f')

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-pic \
    --cache-file=config.cache
  make
}

check() {
  cd ${_pkgname}-${pkgver}

  make check
}

package() {
  cd ${_pkgname}-${pkgver}

  install -d ${pkgdir}/usr/share/{man,doc/ffcall}
  make DESTDIR=${pkgdir} htmldir=/usr/share/doc/ffcall install
}
