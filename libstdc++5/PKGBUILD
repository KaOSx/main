
pkgname=libstdc++5
pkgver=3.3.6
pkgrel=6
pkgdesc="GNU Standard C++ library version 3"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL')
depends=('gcc-libs')
makedepends=('binutils' 'gcc')
options=('!libtool' '!makeflags')
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-core-${pkgver}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-g++-${pkgver}.tar.bz2"
        'gcc-3.4.3-no_multilib_amd64.patch')
md5sums=('18c52e6fb8966b7700665dca289d077f'
         '6b3d00b8d079805be1b895f7f6ce47a0' 
         '088e0807e677010cafe4e590e8711be1')

build(){
  export CFLAGS="-march=${CARCH/_/-} -O2 -pipe"
  export CXXFLAGS="-march=${CARCH/_/-} -O2 -pipe"
  cd ${srcdir}/gcc-${pkgver}

  patch -p0 -i ${srcdir}/gcc-3.4.3-no_multilib_amd64.patch
  sed -i "s|O_CREAT|O_CREAT, 0666|" gcc/collect2.c

  # No fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  mkdir ../gcc-build
  cd ../gcc-build
  ../gcc-${pkgver}/configure --prefix=/usr \
      --enable-shared \
      --enable-languages=c++ \
      --enable-threads=posix --enable-__cxa_atexit \
      --disable-multilib \
      --libdir=/usr/lib
  make all-target-libstdc++-v3 BOOT_CFLAGS="${CFLAGS}" STAGE1_CFLAGS="-O"
}

package() {
  cd ${srcdir}/gcc-build
  make DESTDIR=${pkgdir} install-target-libstdc++-v3 

  # Remove includefiles and libs provided by gcc
  rm -rf ${pkgdir}/usr/{include,share/locale}
  rm -f ${pkgdir}/usr/lib/*.a
  rm -f ${pkgdir}/usr/lib/libstdc++.so
}
