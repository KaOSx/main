
pkgname=xorg-utils
pkgver=8.3
pkgrel=1
pkgdesc="Collection of client utilities used to query the X server"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxi' 'libxtst' 'libxinerama' 'libxv' 'libdmx' 'libxft' 'libxaw'
         'libglvnd' 'libxcomposite' 'xorg-util-macros' 'xcb-util' 'libxrandr' 'libxkbfile')
makedepends=('pkgconfig' 'mesa' 'xorgproto')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/xdpyinfo-1.3.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xdriinfo-1.0.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xev-1.2.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xfd-1.1.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xfontsel-1.1.0.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xlsatoms-1.1.4.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xlsclients-1.1.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xlsfonts-1.0.8.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xprop-1.2.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xvinfo-1.1.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/xwininfo-1.1.6.tar.xz"
        'LICENSE.xdpyinfo' 'LICENSE.xfd' 'LICENSE.xlsclients' 
        'LICENSE.xprop' 'LICENSE.xwininfo' 'LICENSE.xev' 
        'LICENSE.xlsatoms' 'LICENSE.xlsfonts' 'LICENSE.xvinfo')
sha256sums=('a8ada581dbd7266440d7c3794fa89edf6b99b8857fc2e8c31042684f3af4822b'
            'dd838bae9d2b19ddd71fe6d30ed33abc7c85e19d223e79d35600db3fa44bf734'
            'c9461a4389714e0f33974f9e75934bdc38d836a0f059b8dc089c7cbf2ce36ec1'
            'd5470ffb66fd45a1e1b03d6df01f12d4caf0cf675cde0345cda237243e9076fd'
            '17052c3357bbfe44b8468675ae3d099c2427ba9fcac10540aef524ae4d77d1b4'
            'f4bfa15f56c066d326a5d5b292646708f25b9247506840b9047cd2687dcc71b7'
            '68baee57e70250ac4a7759fb78221831f97d88bc8e51dcc2e64eb3f8ca56bae3'
            '807f909eace684b866fc63b3e962729c120822a6c96e051ff51cf350b3ffb6cd'
            '4436e3148bb91a162406230d9f736a49ca8b50b74790015dc15d78d6ce8e825f'
            '3ede71ecb26d9614ccbc6916720285e95a2c7e0c5e19b8570eaaf72ad7c5c404'
            '3518897c17448df9ba99ad6d9bb1ca0f17bc0ed7c0fd61281b34ceed29a9253f'
            '51c98a0feafd5878eb66872476606fecf3636ca1609f57417de1a03bc6afda1e'
            'd64c68f0c3778c4f7c3ddd4883fd1fb24ba8d3df40601b77b18f66196354d831'
            'd64c68f0c3778c4f7c3ddd4883fd1fb24ba8d3df40601b77b18f66196354d831'
            'd6ef63e1927114ff2598c3ea8dd923471a2a80be059179dbb16c85cea8ac465f'
            'd001b100647b13c99916cbd11bec125eaa54a31ff175dfa05e863f67dda2de83'
            'b7025c627bcc01ec83b491d8a28776cfa7e9087612a8f35e41d849e4e797255a'
            '08faa724d9892fea6b61a186a44f879393dac5551a49300276a4b67c733d1760'
            'd309d199f5a6a3096d1708bba48252e4cedd45f442df7b79fc284805fa087120'
            '4859720734939acf2e7f81fb3f848afce7b8c1cfaa2d01968165344e320cb155')

build() {
  cd ${srcdir}

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man 
      make  
      popd
    fi
  done
}

package() {
  cd ${srcdir}

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install 
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} 
      popd
    fi
  done

  install -D -m644 LICENSE.* ${pkgdir}/usr/share/licenses/${pkgname}/  
}
