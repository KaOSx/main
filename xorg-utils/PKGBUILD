
pkgname=xorg-utils
pkgver=8.0
pkgrel=2
pkgdesc="Collection of client utilities used to query the X server"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxi' 'libxtst' 'libxinerama' 'libxv' 'libdmx' 'libxft' 'libxaw'
         'libglvnd' 'libxcomposite' 'xorg-util-macros' 'xcb-util' 'libxrandr' 'libxkbfile')
makedepends=('pkgconfig' 'mesa' 'xorgproto')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/xdpyinfo-1.3.2.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xdriinfo-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xev-1.2.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xfd-1.1.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xfontsel-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xlsatoms-1.1.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xlsclients-1.1.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xlsfonts-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xprop-1.2.5.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xvinfo-1.1.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/xwininfo-1.1.5.tar.bz2"
        'LICENSE.xdpyinfo' 'LICENSE.xfd' 'LICENSE.xlsclients' 
        'LICENSE.xprop' 'LICENSE.xwininfo' 'LICENSE.xev' 
        'LICENSE.xlsatoms' 'LICENSE.xlsfonts' 'LICENSE.xvinfo')
sha256sums=('30238ed915619e06ceb41721e5f747d67320555cc38d459e954839c189ccaf51'
            'd9ccd2c3e87899417acc9ea1f3e319a4198112babe1dc711273584f607449d51'
            'd700e08bfe751ed2dbf802baa204b056d0e49348b6eb3c6f9cb035d8ae4885e2'
            '192f938ba5830e84cc807d8ead2ad9655b43eaed6500c830f3e083aad10ed958'
            '25aa0b7c4262f5e99c07c2b96e00e4eb25b7e53f94fa803942af9d0e8da3001c'
            '57868f958c263976727881f3078e55b86b4a109dc578d2b92f5c6d690850a382'
            '773f2af49c7ea2c44fba4213bee64325875c1b3c9bc4bbcd8dac9261751809c1'
            '89b80b3a030006ab6cef717be286c12f2477894b227158b1e6133274f6ebd368'
            '9b92ed0316bf2486121d8bac88bd1878f16b43bd335f18009b1f941f1eca93a1'
            '0353220d6606077ba42363db65f50410759f9815352f77adc799e2adfa76e73f'
            '7a405441dfc476666c744f5fcd1bc8a75abf8b5b1d85db7b88b370982365080e'
            '51c98a0feafd5878eb66872476606fecf3636ca1609f57417de1a03bc6afda1e'
            'd64c68f0c3778c4f7c3ddd4883fd1fb24ba8d3df40601b77b18f66196354d831'
            'd64c68f0c3778c4f7c3ddd4883fd1fb24ba8d3df40601b77b18f66196354d831'
            'd6ef63e1927114ff2598c3ea8dd923471a2a80be059179dbb16c85cea8ac465f'
            'd001b100647b13c99916cbd11bec125eaa54a31ff175dfa05e863f67dda2de83'
            'b7025c627bcc01ec83b491d8a28776cfa7e9087612a8f35e41d849e4e797255a'
            '08faa724d9892fea6b61a186a44f879393dac5551a49300276a4b67c733d1760'
            'd309d199f5a6a3096d1708bba48252e4cedd45f442df7b79fc284805fa087120'
            '4859720734939acf2e7f81fb3f848afce7b8c1cfaa2d01968165344e320cb155')

build() {
  cd ${srcdir}
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man 
      make  
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install 
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} 
      popd
    fi
  done
  
  install -D -m644 LICENSE.* ${pkgdir}/usr/share/licenses/${pkgname}/  
}
