
pkgname=terminus-font
pkgver=4.49.1
_pkgver=4.49
pkgrel=2
arch=('x86_64')
url="https://sourceforge.net/projects/terminus-font/"
pkgdesc="A superb, monospace bitmap font (for X11 and console)"
license=('GPL2' 'custom:OFL')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
makedepends=('python3')
install=terminus-font.install
source=("https://downloads.sourceforge.net/project/terminus-font/${pkgname}-${_pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('4b8656f7bb34fe3f729774d06a94d4b204de3c21')

build() { 
  cd ${pkgname}-${pkgver}

  sh ./configure --prefix=/usr \
    --x11dir=/usr/share/fonts/local/ \
    --psfdir=/usr/share/kbd/consolefonts/ 
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 75-yes-terminus.conf ${pkgdir}/etc/fonts/conf.avail/75-yes-terminus.conf
  install -Dm644 OFL.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
