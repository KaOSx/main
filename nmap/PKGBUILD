
pkgname=nmap
pkgver=7.60
pkgrel=1
pkgdesc="Network exploration tool and security/port scanner"
arch=('x86_64')
url="https://nmap.org"
license=('GPL2')
depends=('pcre' 'openssl' 'libpcap>=1.0.0' 'lua')
makedepends=('python2')
source=("https://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2")
md5sums=('4e454266559ddf2c4e2109866c62560c')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib --without-gui
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove zenmap uninstall script
  rm ${pkgdir}/usr/bin/uninstall_zenmap
  # zenmap needs gtk
  rm ${pkgdir}/usr/share/applications/zenmap-root.desktop
  rm ${pkgdir}/usr/share/applications/zenmap.desktop

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

