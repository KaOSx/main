
pkgname=pyqt5-webengine
_pkgname=PyQtWebEngine
pkgver=5.15.7
pkgrel=1
pkgdesc="Set of Python3 bindings for the Qt Company's Qt5 WebEngine libraries"
arch=('x86_64')
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=('GPL')
depends=('sip' 'qtwebengine' 'pyqt5-python3')
makedepends=('pyqt-builder' 'python3-setuptools')
provides=('pyqtwebengine')
replaces=('pyqtwebengine')
conflicts=('pyqtwebengine')
source=("https://pypi.python.org/packages/source/P/PyQtWebEngine/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('f121ac6e4a2f96ac289619bcfc37f64e68362f24a346553f5d6c42efa4228a4d')

build() {
  cd ${_pkgname}-${pkgver}

  sip-build \
    --api-dir /usr/share/qt5/qsci/api/python/ \
    --qmake /usr/lib/qt5/bin/qmake \
    --no-make
  cd build
  make
}

package(){
  cd ${_pkgname}-${pkgver}/build

  make DESTDIR=${pkgdir} INSTALL_ROOT=${pkgdir} install -j1
}
