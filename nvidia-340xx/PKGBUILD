
_extramodules=extramodules-5.3
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia-340xx' 'khd-nvidia-340xx')
pkgver=340.107
pkgrel=26
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=5.3.6' 'linux<5.4' "nvidia-340xx-utils=${pkgver}")
license=('custom')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        "https://raw.github.com/KaOSx/hardware-detection/master/hwdb/nvidia_supported"
        'linux-5.0.patch'
        'linux-5.1.patch'
        'linux-5.3.patch')
md5sums=('ffca1879d77dfc491dfb0276ceb56cc1'
         '62d0cdb856cfae64753c0f3acf729d47'
         '66fdbc3ba583d2d01d72c5c41069c8d6'
         'ce78f38f325e4a8200210c56f6561df8'
         'f9206ff2a5b7a2aee3869e6e04f5cd73')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}"
    patch -p1 -i ${srcdir}/linux-5.0.patch
    patch -p1 -i ${srcdir}/linux-5.1.patch
    patch -p1 -i ${srcdir}/linux-5.3.patch
    # sed line needed for linux 4.9 & 340.101
    #sed -i 's/= DRIVER_GEM | DRIVER_PRIME,/= DRIVER_GEM | DRIVER_PRIME | DRIVER_LEGACY,/g' kernel/nv-drm.c
    
    cd "${srcdir}/${_pkg}/kernel"  
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia-340xx() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=5.3.6' 'linux<5.4' "nvidia-340xx-utils=${pkgver}")
    conflicts=('nvidia' 'nvidia-304xx')
    replaces=('nvidia-304xx')
    backup=('etc/X11/xorg.conf.d/20-nvidia.conf')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

package_khd-nvidia-340xx() {
    pkgdesc="KHD ids for nvidia $pkgver"
    arch=('x86_64')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-kernel.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia340xx_hwdb"
}
