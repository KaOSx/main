
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt6-connectivity
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="A cross-platform application and UI framework, QtBluetooth, QtNfc files"
arch=('x86_64')
url='https://github.com/qt/qtconnectivity'
license=('GPL3' 'LGPL')
depends=('qt6-declarative' 'bluez')
makedepends=('cmake' 'ninja')
groups=('qt6')
options=('!libtool')
_pkgfqn="qtconnectivity-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz")
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`)

build() {
    cmake -B build -S ${_pkgfqn} -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt6/bin); do
        ln -s /usr/lib/qt6/bin/${i} ${pkgdir}/usr/bin/${i}-qt6
    done

    install -D -m644 ${_pkgfqn}/LICENSE.GPL3 \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.GPL3
}

