
pkgname=nspr
pkgver=4.20
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="https://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=("https://ftp.mozilla.org/pub/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('2c8964913da89ffbaf464d49ce44d79e8804e1794ef9a8c52a7bff7224d1556e')

build() {
  cd ${pkgname}-${pkgver}

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --enable-64bit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chmod 644 ${pkgdir}/usr/lib/*.a

  ln -s nspr.pc ${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc
  rm -r ${pkgdir}/usr/bin/{compile-et.pl,prerr.properties} ${pkgdir}/usr/include/nspr/md
}
