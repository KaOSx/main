
pkgname=nspr
pkgver=4.26
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="https://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=("https://ftp.mozilla.org/pub/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('fc9d142d85b74ffd2e6374a0c9016f3f2dac074225e24df3070e5a72d31b773d')

build() {
  cd ${pkgname}-${pkgver}

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --enable-64bit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chmod 644 ${pkgdir}/usr/lib/*.a

  ln -s nspr.pc ${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc
  rm -r ${pkgdir}/usr/bin/{compile-et.pl,prerr.properties} ${pkgdir}/usr/include/nspr/md
}
