
pkgname=nspr
pkgver=4.36
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="https://firefox-source-docs.mozilla.org/nspr/index.html"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=("https://ftp.mozilla.org/pub/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('55dec317f1401cd2e5dba844d340b930ab7547f818179a4002bce62e6f1c6895')

build() {
  cd ${pkgname}-${pkgver}

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --enable-64bit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chmod 644 ${pkgdir}/usr/lib/*.a

  ln -s nspr.pc ${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc
  rm -r ${pkgdir}/usr/bin/{compile-et.pl,prerr.properties} ${pkgdir}/usr/include/nspr/md
}
