
pkgname=nspr
pkgver=4.34
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('x86_64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS/Reference/NSPR_functions"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=('!emptydirs')
source=("https://ftp.mozilla.org/pub/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
sha256sums=('beef011cd15d8f40794984d17014366513cec5719bf1a78f5e8a3e3a1cebf99c')

build() {
  cd ${pkgname}-${pkgver}

  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --enable-64bit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  chmod 644 ${pkgdir}/usr/lib/*.a

  ln -s nspr.pc ${pkgdir}/usr/lib/pkgconfig/mozilla-nspr.pc
  rm -r ${pkgdir}/usr/bin/{compile-et.pl,prerr.properties} ${pkgdir}/usr/include/nspr/md
}
