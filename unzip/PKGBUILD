
pkgname=unzip
pkgver=6.0
_pkgver=60
pkgrel=12
_commit=122050bac16fae82a460ff739fb1ca0f106e9d85
pkgdesc="Unpacks .zip archives such as those made by PKZIP"
arch=('x86_64')
url="https://infozip.sourceforge.net/UnZip.html"
license=('custom')
depends=('bzip2' 'bash')
#source=("https://downloads.sourceforge.net/infozip/unzip${_pkgver}.tar.gz"
source=("https://github.com/madler/unzip/archive/${_commit}.zip"
        'CVE2021-4217.patch')
md5sums=('6900f48882ad7f99be7d331cff081ea9'
         'a3c081f06d04840d36ab86bc312fb89f')

build() {
  cd ${pkgname}-${_commit}
  patch -p1 -i ${srcdir}/CVE2021-4217.patch

  # set CFLAGS -- from Debian
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -DACORN_FTYPE_NFS \
  -DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT -DUNICODE_SUPPORT \
  -DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD -DDATE_FORMAT=DF_YMD \
  -DUSE_BZIP2 -DNATIVE"

  sed -i "/MANDIR =/s#)/#)/share/#" unix/Makefile

  make -f unix/Makefile LOCAL_UNZIP="$CFLAGS" prefix=/usr LF2="" \
  D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 unzips
}

package() {
  cd ${pkgname}-${_commit}

  make -f unix/Makefile prefix=${pkgdir}/usr INSTALL_PROGRAM="install" install 

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/unzip/LICENSE 
}

