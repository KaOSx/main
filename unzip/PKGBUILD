
pkgname=unzip
pkgver=6.0
_pkgver=60
pkgrel=9
pkgdesc="Unpacks .zip archives such as those made by PKZIP"
arch=('x86_64')
url="http://infozip.sourceforge.net/UnZip.html"
license=('custom')
depends=('bzip2' 'bash')
source=("https://downloads.sourceforge.net/infozip/unzip${_pkgver}.tar.gz")
md5sums=('62b490407489521db863b523a7f86375')

build() {
  cd ${pkgname}${_pkgver}

  # set CFLAGS -- from Debian
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -DACORN_FTYPE_NFS \
  -DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT -DUNICODE_SUPPORT \
  -DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD -DDATE_FORMAT=DF_YMD \
  -DUSE_BZIP2 -DNATIVE"
  
  sed -i "/MANDIR =/s#)/#)/share/#" unix/Makefile

  make -f unix/Makefile LOCAL_UNZIP="$CFLAGS" prefix=/usr LF2="" \
  D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 unzips
}

package() {
  cd ${pkgname}${_pkgver}

  make -f unix/Makefile prefix=${pkgdir}/usr INSTALL_PROGRAM="install" install 

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/unzip/LICENSE 
}

