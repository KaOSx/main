
pkgname=spirv-llvm-translator
_pkgname=SPIRV-LLVM-Translator
pkgver=18.1.4
pkgrel=1
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM.'
arch=('x86_64')
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator'
license=('UoI-NCSA')
depends=('llvm' 'spirv-tools')
makedepends=('cmake' 'python3' 'spirv-headers')
source=("https://github.com/KhronosGroup/SPIRV-LLVM-Translator/archive/refs/tags/v${pkgver}.tar.gz")
md5sums=('b5cbda38b297d02133c5a0e5d6f2cadb')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${_pkgname}-${pkgver}/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

