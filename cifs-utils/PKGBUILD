#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dog]org>

pkgname=cifs-utils
pkgver=5.7
pkgrel=2
pkgdesc="CIFS filesystem user-space tools"
arch=('x86_64')
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap' 'keyutils' 'krb5' 'talloc')
source=("ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$pkgname-$pkgver.tar.bz2")
md5sums=('4215f12be8663f65d2c21dbbbafbc7f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # systemd support is broken in mount.cifs
  ./configure --prefix=/usr --disable-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  # mount.cifs uid, for root mounting fstab
  chmod +s $pkgdir/usr/bin/mount.cifs
}
