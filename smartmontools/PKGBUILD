
pkgname=smartmontools
pkgver=7.4
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
url="https://www.smartmontools.org/"
license=('GPL')
arch=('x86_64')
depends=('gcc-libs' 'libcap' 'bash')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'smartd.conf')
md5sums=('178d31a6ff5256c093227ab45a3f52aa'
         '28c1b80c41dac52d433961dcfb4f70e0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-drivedbdir \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemdenvfile=/etc/conf.d/smartd
  make
}

package() {
  cd ${pkgname}-${pkgver}

  sed -i -e "s|smartd_opts|SMARTD_ARGS|g" smartd.service

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm644 ../smartd.conf ${pkgdir}/etc/conf.d/smartd
}
