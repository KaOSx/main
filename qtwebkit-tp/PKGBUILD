
pkgname=qtwebkit-tp
pkgver=5.212.0.4
_pkgver=5.212.0-alpha4
_commit=5ccca3a720f7c2251c4ac8b28f25bd73524081f0
pkgrel=8
pkgdesc='Cross-platform application and UI framework, QtWebkit files (revived branch)'
arch=('x86_64')
url='https://github.com/qtwebkit/qtwebkit'
license=('LGPLv2')
depends=('qt5-sensors' 'qt5-location' 'icu' 'libxslt' 'libxcomposite' 
         'gst-plugins-base' 'gst-plugins-good' 'libwebp' 'mesa' 'qt5-serialport' 'qt5-webchannel'
         'qt5-imageformats' 'gst-libav' 'gst-plugins-bad' 'hyphen')  
makedepends=('gperf' 'sqlite' 'fontconfig' 'perl' 'python3' 'ruby' 'extra-cmake-modules' 'qt5-tools')
options=('!libtool')
provides=('qt5-webkit')
replaces=('qt5-webkit')
conflicts=('qt5-webkit')
groups=('qt5')
#source=("https://github.com/annulen/qtwebkit-snapshots/archive/${_commit}.zip"
source=("https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-${_pkgver}/qtwebkit-${_pkgver}.tar.xz"
        "https://raw.githubusercontent.com/Alexpux/MINGW-packages/master/mingw-w64-qtwebkit/0012-install-additional-pri-files.patch"
        "https://github.com/qtwebkit/qtwebkit/commit/d92b11fea65364fefa700249bd3340e0cd4c5b31.diff")
md5sums=('5b61a72497f06e51db09d57edc3c35fb'
         'd3997143a1b57cbbfbd4364713fea91d'
         'be6dad072132dbcc69d8d96374dc5705')

prepare() {
  cd qtwebkit-${_pkgver}

  #patch -p1 -i ${srcdir}/0012-install-additional-pri-files.patch
  # needed for cmake 3.10
  #patch -p1 -i ${srcdir}/f51554bf104ab0491370f66631fe46143a23d5c2.diff
  # Bison 3.7 needed
  patch -p1 -i ${srcdir}/d92b11fea65364fefa700249bd3340e0cd4c5b31.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../qtwebkit-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_INCLUDEDIR=/usr/include/qt5 \
    -DPORT=Qt \
    -DUSE_QT_MULTIMEDIA=OFF \
    -DENABLE_OPENGL=ON
  make
}

package() {
   cd build
   
   make DESTDIR=${pkgdir} install
}
