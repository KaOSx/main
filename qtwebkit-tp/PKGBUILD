
pkgname=qtwebkit-tp
pkgver=5.212.0
_pkgver=5.212.0-alpha
#_commit=8bed62ae51870074d3bc8f48791b19911bfc4d10
pkgrel=1
pkgdesc='Cross-platform application and UI framework, QtWebkit files (revived branch)'
arch=('x86_64')
url='https://github.com/annulen/webkit'
license=('LGPLv2')
depends=('qt5-sensors' 'qt5-location' 'icu' 'libxslt' 'libxcomposite' 
         'gst-plugins-base' 'gst-plugins-good' 'libwebp' 'mesa' 'qt5-serialport' 'qt5-webchannel'
         'qt5-imageformats' 'gst-libav' 'gst-plugins-bad' 'hyphen')  
makedepends=('gperf' 'sqlite' 'fontconfig' 'perl' 'python2' 'ruby' 'extra-cmake-modules' 'qt5-tools')
options=('!libtool')
provides=('qt5-webkit')
replaces=('qt5-webkit')
conflicts=('qt5-webkit')
groups=('qt5')
#source=("https://github.com/annulen/qtwebkit-snapshots/archive/${_commit}.zip"
source=("https://github.com/annulen/webkit/archive/qtwebkit-${_pkgver}.tar.gz"
        "https://raw.githubusercontent.com/Alexpux/MINGW-packages/master/mingw-w64-qtwebkit/0012-install-additional-pri-files.patch")
md5sums=('fd9743242efe4c2cd3ce2ae9627fac7d'
         'd3997143a1b57cbbfbd4364713fea91d')

prepare() {
  cd webkit-qtwebkit-${_pkgver}

  patch -p1 -i $srcdir/0012-install-additional-pri-files.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../webkit-qtwebkit-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_INCLUDEDIR=/usr/include/qt5 \
    -DPORT=Qt \
    -DUSE_QT_MULTIMEDIA=OFF \
    -DENABLE_OPENGL=ON
  make
}

package() {
   cd build
   
   make DESTDIR=${pkgdir} install
}
