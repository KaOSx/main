
pkgname=qtwebkit-tp
pkgver=5.6.2.2
_commit=8bed62ae51870074d3bc8f48791b19911bfc4d10
pkgrel=1
pkgdesc='Cross-platform application and UI framework, QtWebkit files (revived branch)'
arch=('x86_64')
url='https://github.com/annulen/webkit'
license=('LGPLv2')
depends=('qt5-sensors' 'qt5-location' 'icu' 'libxslt' 'libxcomposite' 
         'gst-plugins-base' 'gst-plugins-good' 'libwebp' 'mesa' 'qt5-serialport' 'qt5-webchannel'
         'qt5-imageformats' 'gst-libav' 'gst-plugins-bad' 'hyphen')  
makedepends=('gperf' 'sqlite' 'fontconfig' 'perl' 'python2' 'ruby' 'extra-cmake-modules' 'qt5-tools')
options=('!libtool')
provides=('qt5-webkit')
replaces=('qt5-webkit')
conflicts=('qt5-webkit')
groups=('qt5')
source=("https://github.com/annulen/qtwebkit-snapshots/archive/${_commit}.zip"
        "http://sigsegv.spdns.eu/~crazy/0001-fix-bug-393-and-406-v1.patch")
md5sums=('f24161a8aa669785290d7b2873a200a1'
         '5b576cb79ec7eed676c20c3dfc8617b1')

prepare() {
  cd qtwebkit-snapshots-${_commit}

  patch -p1 -i $srcdir/0001-fix-bug-393-and-406-v1.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../qtwebkit-snapshots-${_commit} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_INCLUDEDIR=/usr/include/qt5 \
    -DPORT=Qt \
    -DUSE_QT_MULTIMEDIA=OFF \
    -DENABLE_OPENGL=ON
  make
}

package() {
   cd build
   
   make DESTDIR=${pkgdir} install
}
