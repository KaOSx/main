
pkgname=qtwebkit-tp
pkgver=5.6.2.3
_pkgver=tp5
#_commit=8bed62ae51870074d3bc8f48791b19911bfc4d10
pkgrel=4
pkgdesc='Cross-platform application and UI framework, QtWebkit files (revived branch)'
arch=('x86_64')
url='https://github.com/annulen/webkit'
license=('LGPLv2')
depends=('qt5-sensors' 'qt5-location' 'icu' 'libxslt' 'libxcomposite' 
         'gst-plugins-base' 'gst-plugins-good' 'libwebp' 'mesa' 'qt5-serialport' 'qt5-webchannel'
         'qt5-imageformats' 'gst-libav' 'gst-plugins-bad' 'hyphen')  
makedepends=('gperf' 'sqlite' 'fontconfig' 'perl' 'python2' 'ruby' 'extra-cmake-modules' 'qt5-tools')
options=('!libtool')
provides=('qt5-webkit')
replaces=('qt5-webkit')
conflicts=('qt5-webkit')
groups=('qt5')
#source=("https://github.com/annulen/qtwebkit-snapshots/archive/${_commit}.zip"
source=("https://github.com/annulen/webkit/releases/download/qtwebkit-${_pkgver}/qtwebkit-${_pkgver}.tar.xz"
        "https://raw.githubusercontent.com/Alexpux/MINGW-packages/master/mingw-w64-qtwebkit/0012-install-additional-pri-files.patch"
        "https://github.com/annulen/webkit/commit/afe18f7d13f5c825ce323bfb756b249c0e163554.diff")
md5sums=('7fb7b992e7793d3e2827d4ad66c420cc'
         'd3997143a1b57cbbfbd4364713fea91d'
         '110e530f46d7c08097fc00703b3a4c27')

prepare() {
  cd qtwebkit-${_pkgver}

  patch -p1 -i $srcdir/0012-install-additional-pri-files.patch
  patch -p1 -i $srcdir/afe18f7d13f5c825ce323bfb756b249c0e163554.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../qtwebkit-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_INCLUDEDIR=/usr/include/qt5 \
    -DPORT=Qt \
    -DUSE_QT_MULTIMEDIA=OFF \
    -DENABLE_OPENGL=ON
  make
}

package() {
   cd build
   
   make DESTDIR=${pkgdir} install
}
