
pkgname=libebml
pkgver=1.4.4
pkgrel=1
pkgdesc="Extensible Binary Meta Language library"
arch=('x86_64')
url="https://matroska-org.github.io/libebml/"
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake')
options=('!staticlibs')
source=("https://dl.matroska.org/downloads/libebml/${pkgname}-${pkgver}.tar.xz")
sha512sums=('24f2bf51cd71f583bdd3bb33dbf20b5adeb8e96345de3abf3014f3ae1b0610c1bd04b802f26b4a1a26f884a696d6accf784bed1510f12538a6664cbd9ae51d6d')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=YES \
    -DDISABLE_PKGCONFIG=NO # .pc file is not needed for matroska, mkvtoolnix?
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
