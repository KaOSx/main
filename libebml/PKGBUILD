
pkgname=libebml
pkgver=1.3.6
pkgrel=1
pkgdesc="Extensible Binary Meta Language library"
arch=('x86_64')
url="http://matroska-org.github.io/libebml/"
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake')
options=('!staticlibs')
source=("https://github.com/Matroska-Org/libebml/archive/release-${pkgver}.tar.gz")
md5sums=('6213b32b39067107dc6b2bd4f9575187')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-release-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=YES \
    -DDISABLE_PKGCONFIG=NO # .pc file is not needed for matroska, mkvtoolnix?
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
