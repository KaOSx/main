
pkgname=bash-completion
pkgver=2.11
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('x86_64')
url="https://github.com/scop/bash-completion"
license=('GPL')
depends=('bash')
source=("https://github.com/scop/bash-completion/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('2514c6772d0de6254758b98c53f91861')

build() {
  cd ${pkgname}-${pkgver}

  # remove if /lib should become symlink to usr/lib
  sed -i '/lib\/modules/ s,/lib/modules,/usr&,' bash_completion

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR=${pkgdir} install

  # bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm -r ${pkgdir}/etc/profile.d
  # provided by pacman
  rm ${pkgdir}/usr/share/bash-completion/completions/makepkg
}

