
pkgbase=antlr2
pkgname=('antlr2' 'python2-antlr2')
pkgver=2.7.7
pkgrel=3
arch=('x86_64')
url="http://www.antlr2.org/"
license=('custom')
makedepends=('java-environment' 'python2' 'sh')
source=("http://www.antlr2.org/download/antlr-${pkgver}.tar.gz"
        'runantlr2.sh'
        'gcc4.4.patch')
md5sums=('01cc9a2a454dd33dcd8c856ec89af090'
        'e4aa827cf78f97c4e5fae086831192d6'
        '8574c93f40e6477e83c29f9b07de49da')

build() {
  cd antlr-${pkgver}

  patch -p0 -i ${srcdir}/gcc4.4.patch

  ./configure --prefix=/usr \
    --disable-examples \
    --disable-csharp
  make
}

package_antlr2() {
  pkgdesc="ANother Tool for Language Recognition, (formerly PCCTS) is a language tool that provides a framework for constructing recognizers, compilers, and translators from grammatical descriptions containing C++ or Java actions"
  depends=('sh')

  cd antlr-${pkgver}/lib/cpp
  make prefix=${pkgdir}/usr install

  install -Dm644 ${srcdir}/antlr-${pkgver}/antlr/antlr.jar \
    ${pkgdir}/usr/share/java/antlr2.jar
  install -Dm755 ${srcdir}/runantlr2.sh ${pkgdir}/usr/bin/runantlr2
  install -Dm644 ${srcdir}/antlr-${pkgver}/LICENSE.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-antlr2() {
  pkgdesc="ANother Tool for Language Recognition, (formerly PCCTS) is a language tool that provides a framework for constructing recognizers, compilers, and translators from grammatical descriptions containing C++ or Java actions (Python version)"
  depends=('antlr2' 'python2')

  cd ${srcdir}/antlr-${pkgver}/lib/python
  python2 setup.py install --root=${pkgdir} --optimize=1
}
