
pkgname=libavif
pkgver=1.0.2
pkgrel=1
pkgdesc="Library that aims to be a friendly, portable C implementation of the AV1 Image File Format"
arch=('x86_64')
url="https://aomediacodec.github.io/av1-avif/"
license=('BSD')
depends=('dav1d' 'libjpeg' 'libpng' 'zlib')
makedepends=('cmake' 'gdk-pixbuf2' 'nasm' 'pkg-config')
source=("https://github.com/AOMediaCodec/libavif/archive/refs/tags/v${pkgver}.tar.gz"
        "https://github.com/AOMediaCodec/libavif/commit/26224fe476ec660e44c816ca5b95d6ed08d1ad06.diff")
sha256sums=('de8bf79488c5b523b77358df8b85ae69c3078e6b3f1636fc1f313f952269ad20'
            'ce679ee6629aa0adec04f01373c17d51256853c7ef6795c12773824ad1f10704')

prepare() {
  cd ${pkgname}-${pkgver}
  # build fail https://github.com/AOMediaCodec/libavif/issues/1527
  #patch -p1 -i ${srcdir}/26224fe476ec660e44c816ca5b95d6ed08d1ad06.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
