#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas[at]chakra-project[dot]org

pkgname=perl-crypt-openssl-random
pkgver=0.04
pkgrel=7
pkgdesc="Interface to OpenSSL PRNG methods"
arch=('x86_64')
url="http://search.cpan.org/dist/Crypt-OpenSSL-Random"
depends=('perl' 'openssl')
license=('GPL')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/I/IR/IROBERTS/Crypt-OpenSSL-Random-$pkgver.tar.gz")
md5sums=('c56ac5dbdd46122eb9b8da59613b7b0a')

build() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"

  make test
}

package() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"

  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
