
pkgname=poppler
pkgver=24.11.0
pkgrel=2
arch=('x86_64')
pkgdesc="PDF rendering library based on xpdf 3.0"
url="https://poppler.freedesktop.org/"
license=('GPL')
depends=('libjpeg' 'gcc-libs' 'cairo' 'libxml2' 'fontconfig' 'openjpeg' 'lcms2' 'poppler-data'
         'nss' 'qt5-base' 'qt6-base' 'glib2')
makedepends=('boost' 'extra-cmake-modules' 'lcms2' 'icu' 'gobject-introspection')
options=('!libtool')
conflicts=('poppler-glib' 'poppler-qt5' 'poppler-qt')
provides=('poppler-glib' 'poppler-qt5' 'poppler-qt')
replaces=('poppler-glib' 'poppler-qt5' 'poppler-qt')
source=("https://poppler.freedesktop.org/${pkgname}-${pkgver}.tar.xz"
        'headers.diff')
md5sums=('a2456413b4219d010a4fa87a747d729e'
         '33116c5f68eac00081f5892de9fbb32d')

prepare() {
  cd ${pkgname}-${pkgver}
  # scribus needed
  patch -p1 -i ${srcdir}/headers.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_UNSTABLE_API_ABI_HEADERS=ON \
    -DBUILD_GTK_TESTS=OFF \
    -DENABLE_GLIB=ON 
  cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build
}



