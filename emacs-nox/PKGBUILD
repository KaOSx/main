
pkgname=emacs-nox
pkgver=27.1
pkgrel=1
pkgdesc='The Emacs Editor, without X11 support'
arch=('x86_64')
url="https://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'gpm' 'libxpm')
provides=('emacs')
options=('docs')
source=("https://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.xz")
md5sums=('f1f25356b68ec69caccd41d95988d23d')

build() {
  cd emacs-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --without-x \
    --without-sound
  make
}

package() {
  cd emacs-$pkgver
  make DESTDIR=${pkgdir} install

  #remove conflicts
  mv ${pkgdir}/usr/bin/{ctags,ctags.emacs}
  mv ${pkgdir}/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  rm ${pkgdir}/usr/share/info/info.info.gz
  
  #fix all the 777 perms on directories
  find ${pkgdir}/usr/share/emacs/$pkgver -type d -exec chmod 755 {} \;
  #fix user/root permissions on usr/share files
  find ${pkgdir}/usr/share/emacs/$pkgver -exec chown root.root {} \;
  #remove .desktop file and icons
  rm -rf ${pkgdir}/usr/share/{applications,icons}
}
