
pkgname=cdrtools
pkgver=3.02a09
_pkgver=3.02
pkgrel=2
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('x86_64')
url="http://cdrtools.sourceforge.net/private/cdrecord.html"
license=('CDDL')
depends=('acl' 'libcap')
conflicts=('cdrkit')
provides=('cdrkit')
replaces=('cdrkit')
options=('!makeflags')
backup=('etc/default/cdrecord' 'etc/default/rscsi')
install=cdrtools.install
source=("https://downloads.sf.net/cdrtools/cdrtools-${pkgver}.tar.bz2")
md5sums=('1e224a6adbbe4ad40047b9fddbb0e60c')

build() {
  cd cdrtools-${_pkgver}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  # install rscsi to /usr/bin instead of /usr/sbin
  sed -i 's|INSDIR=.*|INSDIR=bin|' rscsi/Makefile
  
  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/
}

package() {
  cd cdrtools-${_pkgver}

  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/ DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib/{*.a,profiled}

  # autoload 'sg' module needed by cdrecord
  install -d -m755 ${pkgdir}/usr/lib/modules-load.d/
  echo sg > ${pkgdir}/usr/lib/modules-load.d/cdrecord.conf

  # create symlinks for cdrkit compatibility
  cd ${pkgdir}/usr/bin
  ln -s cdrecord wodim
  ln -s readcd readom
  ln -s mkisofs genisoimage
  ln -s cdda2wav icedax
  
  # conflicts with man-pages
  rm -rf ${pkgdir}/usr/share/man/man3
}
