
pkgname=nbd
pkgver=3.26.1
pkgrel=1
pkgdesc="Tools for network block devices, allowing you to use remote block devices over TCP/IP."
arch=('x86_64')
url="https://nbd.sourceforge.io/"
license=('GPL')
source=("https://github.com/NetworkBlockDevice/nbd/releases/download/nbd-${pkgver}/${pkgname}-${pkgver}.tar.xz"
        'config'
        'nbd.service')
depends=('glib2')
sha1sums=('c6dd0fa6fdc1f1e38f6c80404642a5e97bec83a3'
          '7bda22e6fe40e6d7ec81783f23445868d1e31495'
          'd8bebeab7c43067026accc63f7cf443b91142f21')

build(){
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-syslog \
        --disable-manpages
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/config ${pkgdir}/etc/nbd-server/config
  install -D -m755 ${srcdir}/nbd.service ${pkgdir}/usr/lib/systemd/system/nbd.service
  install -D -m644 README.md ${pkgdir}/usr/share/doc/nbd/README.md
}
