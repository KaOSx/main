
pkgname=nbd
pkgver=3.16.1
pkgrel=1
pkgdesc="Tools for network block devices, allowing you to use remote block devices over TCP/IP."
arch=('x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=("https://downloads.sourceforge.net/nbd/${pkgname}-${pkgver}.tar.xz"
        'config'
        'nbd.service')
depends=('glib2')
sha1sums=('2b57f992cdb652fdd75b7d186791db0a9843fe1f'
          '7bda22e6fe40e6d7ec81783f23445868d1e31495'
          'd8bebeab7c43067026accc63f7cf443b91142f21')

build(){
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --enable-syslog
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/config ${pkgdir}/etc/nbd-server/config
  install -D -m755 ${srcdir}/nbd.service ${pkgdir}/usr/lib/systemd/system/nbd.service
  install -D -m644 README.md ${pkgdir}/usr/share/doc/nbd/README.md
}
