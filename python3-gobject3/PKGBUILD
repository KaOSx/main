
pkgname=python3-gobject3 
pkgbase=pygobject
pkgver=3.34.0
_pkgver=3.34
pkgrel=2
pkgdesc="Python3 bindings for GObject"
arch=('x86_64')
url="https://pygobject.readthedocs.io/en/latest/"
license=('LGPL')
depends=('python3' 'python3-cairo' 'gobject-introspection')
makedepends=('meson' 'ninja')
source=("https://ftp.gnome.org/pub/gnome/sources/pygobject/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
options=('!libtool')
sha256sums=('87e2c9aa785f352ef111dcc5f63df9b85cf6e05e52ff04f803ffbebdacf5271a')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgbase}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D python=/usr/bin/python3
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

