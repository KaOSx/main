
pkgname=python3-gobject3 
pkgbase=pygobject
pkgver=3.40.1
_pkgver=3.40
pkgrel=1
pkgdesc="Python3 bindings for GObject"
arch=('x86_64')
url="https://pygobject.readthedocs.io/en/latest/"
license=('LGPL')
depends=('python3' 'python3-cairo' 'gobject-introspection')
makedepends=('meson' 'ninja')
source=("https://ftp.gnome.org/pub/gnome/sources/pygobject/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
options=('!libtool')
sha256sums=('00c6d591f4cb40c335ab1fd3e8c17869ba15cfda54416fe363290af766790035')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgbase}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D python=/usr/bin/python3
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

