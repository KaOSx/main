
pkgname=libxshmfence
pkgver=1.3.2
pkgrel=1
pkgdesc="Tiny library that exposes a event API on top of Linux futexes"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('GPL')
depends=('glibc')
makedepends=('xorg-util-macros' 'xorgproto')
source=("https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz")
sha256sums=('870df257bc40b126d91b5a8f1da6ca8a524555268c50b59c0acd1a27f361606f')

build() {
	cd ${pkgname}-${pkgver}

	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}

	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir}/ install

	install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
	install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
