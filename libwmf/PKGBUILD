#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

pkgname=libwmf
pkgver=0.2.8.4
pkgrel=9
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)."
arch=(i686 x86_64)
url="http://wvware.sourceforge.net/libwmf.html"
license=("LGPL")
depends=('libpng>=1.4.0' 'libx11' 'freetype2' 'libjpeg>=8' 'gsfonts' 'expat>=2.0')
makedepends=('pkgconfig')
optdepends=('gdk-pixbuf2: for pixbuf loader, used in .install')
options=(!libtool)
install=libwmf.install
source=(http://downloads.sourceforge.net/sourceforge/wvware/${pkgname}-${pkgver}.tar.gz
	libpng14.patch)
md5sums=('d1177739bf1ceb07f57421f0cee191e0'
         '33d3c07a1be9f99fd88de220930c5550')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/libpng14.patch" 
  sed -i -e 's/src include fonts doc/src include fonts/g' Makefile.in
  ./configure --prefix=/usr \
              --with-gsfontdir=/usr/share/fonts/Type1 \
	      --with-fontdir=/usr/share/fonts/Type1 \
	      --with-gsfontmap=/usr/share/ghostscript/8.15/lib/Fontmap.GS \
              --disable-gtk2
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
  #Remove fonts, these are in gsfonts
  rm -rf ${pkgdir}/usr/share/fonts
  #Remove static GTK loader, can't use it anyways
  rm -f ${pkgdir}/usr/lib/gtk-2.0/*/loaders/*.a
}
