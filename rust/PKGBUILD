
pkgname=rust
pkgver=1.66.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language that runs fast, prevents segfaults, and guarantees thread safety.'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cmake' 'curl' 'libffi' 'make' 'python3' 'rust' 'ninja')
conflicts=('cargo')
options=('!strip')
# https://static.rust-lang.org/dist/index.html
source=("https://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.xz"
        'config.toml')
sha256sums=('0dc176e34fae9871f855a6ba4cb30fa19d69c5b4428d29281a07419c4950715c'
            '015d5bfadfebb80bc075e3cb8429cfa3f0d6715ce8bf81cb88499e1fa1bc5b3f')

build() {
  cd rustc-${pkgver}-src
  cp ${srcdir}/config.toml config.toml

  ./x.py build
}

package() {
  cd rustc-${pkgver}-src
  DESTDIR=${pkgdir} ./x.py install

  install -Dm644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-MIT

  #rm -r ${pkgdir}/buildsys
}
