
pkgname=rust
pkgver=1.26.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language that runs fast, prevents segfaults, and guarantees thread safety.'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cmake' 'curl' 'libffi' 'make' 'python2' 'rust')
conflicts=('cargo')
options=('!strip')
# https://static.rust-lang.org/dist/index.html
source=("https://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.xz"
        'config.toml')
sha256sums=('903b4727ad0c07e0edf9e27d5962c6e6f07f0f3c32e06c0f937bc79a087a32af'
            '5fe5891fc393c049527555d8f52bb3a882a9237c0f49e81156d56ce259c75152')

build() {
  cd rustc-${pkgver}-src
  cp ${srcdir}/config.toml config.toml

  ./x.py build
}

package() {
  cd rustc-${pkgver}-src
  DESTDIR=${pkgdir} ./x.py install

  install -Dm644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-MIT
}
