
pkgname=rust
pkgver=1.16.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language that runs fast, prevents segfaults, and guarantees thread safety.'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('cmake' 'curl' 'libffi' 'make' 'python2')
options=('!strip')
source=("http://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.gz")
md5sums=('c11d94d9e62f71838b20058f0797357a')

build() {
  cd rustc-${pkgver}-src

  ./configure --prefix=/usr \
        --disable-docs \
        --release-channel=stable \
        --disable-rustbuild
  make CFLAGS="$CFLAGS -fPIC -w"
}

package() {
  cd rustc-${pkgver}-src
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-MIT
}
