
pkgname=rust
pkgver=1.27.1
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language that runs fast, prevents segfaults, and guarantees thread safety.'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cmake' 'curl' 'libffi' 'make' 'python2' 'rust' 'ninja')
conflicts=('cargo')
options=('!strip')
# https://static.rust-lang.org/dist/index.html
source=("https://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.xz"
        'config.toml')
sha256sums=('948e2645057960ee1c03d9f0e8f78133a5f73d9ca9a24bc56126e395a212d25c'
            'f0bd3afc58cfc3b468f1947f97dba57a72eddb38da62b5aff5d22f267adc1ac0')

build() {
  cd rustc-${pkgver}-src
  cp ${srcdir}/config.toml config.toml

  ./x.py build
}

package() {
  cd rustc-${pkgver}-src
  DESTDIR=${pkgdir} ./x.py install

  install -Dm644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-MIT
}
