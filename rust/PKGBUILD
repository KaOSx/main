
pkgname=rust
pkgver=1.30.0
pkgrel=1
arch=('x86_64')
pkgdesc='Systems programming language that runs fast, prevents segfaults, and guarantees thread safety.'
url='https://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cmake' 'curl' 'libffi' 'make' 'python2' 'rust' 'ninja')
conflicts=('cargo')
options=('!strip')
# https://static.rust-lang.org/dist/index.html
source=("https://static.rust-lang.org/dist/rustc-${pkgver}-src.tar.xz"
        'config.toml')
sha256sums=('b9fc7304fd48d6d6a23fa4565d94353c7cc2659db452f34d69fbc01c4f67021d'
            'f04786339874a2d25ef0233fe10651350a72f5c9cd6abc3cee2fc0bb99da3ff3')

build() {
  cd rustc-${pkgver}-src
  cp ${srcdir}/config.toml config.toml

  ./x.py build
}

package() {
  cd rustc-${pkgver}-src
  DESTDIR=${pkgdir} ./x.py install

  install -Dm644 LICENSE-APACHE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT ${pkgdir}/usr/share/licenses/$pkgname/LICENSE-MIT
}
