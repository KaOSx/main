
pkgname=fontforge
pkgver=20230101
_pkgver=20230101
pkgrel=2
pkgdesc="An outline and bitmap font editor"
arch=('x86_64')
url="https://fontforge.github.io/"
license=('BSD')
depends=('libxi' 'libxml2' 'pango' 'giflib' 'libpng' 'libtiff' 'libjpeg-turbo' 'python3'
         'libtool' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake')
options=('!libtool' '!makeflags')
source=("https://github.com/fontforge/fontforge/releases/download/${_pkgver}/fontforge-${_pkgver}.tar.xz"
        'https://fontforge.github.io/cidmaps.tgz')
md5sums=('7043f25368ed25bcd75d168564919fb7'
         '063691163e592515b31514515bb3ce8c')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DOCS=OFF \
    -DENABLE_X11=ON \
    -DENABLE_LIBSPIRO=OFF \
    -DENABLE_FONTFORGE_EXTRAS=ON \
    -DENABLE_MAINTAINER_TOOLS=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -d ${pkgdir}/usr/share/fontforge
  install -m644 Adobe-* ${pkgdir}/usr/share/fontforge

  install -D -m644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
}
