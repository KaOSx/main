# Maintainer: Manuel Tortosa <manutortosa@gmail.com>

pkgname=enscript
pkgver=1.6.5.2
pkgrel=2
pkgdesc="Convert ASCII files to PostScript suitable for printing"
url="http://www.gnu.org/software/enscript/enscript.html"
arch=('i686' 'x86_64')
backup=('etc/enscript/enscript.cfg')
depends=('glibc')
license=('GPL2')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c1d2a0d4b44ca30634742b299881b0e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc/enscript \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
  || return 1

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install \
       prefix="${pkgdir}/usr" \
       sysconfdir="${pkgdir}/etc/enscript" \
       mandir="${pkgdir}/usr/share/man" \
       infodir="${pkgdir}/usr/share/info"
}

# vim: ts=2 sw=2 et ft=sh
