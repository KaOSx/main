
pkgname=freetds
pkgver=1.4.5
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='https://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('unixodbc' 'openssl')
options=('!libtool')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("https://www.freetds.org/files/stable/${pkgname}-${pkgver}.tar.bz2")
md5sums=('dccddf07fb549c0bb45f80178e8daeb9')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
            --sysconfdir=/etc/freetds \
            --mandir=/usr/share/man \
            --enable-msdblib \
            --disable-static \
            --with-tdsver=auto \
            --with-openssl \
            --with-unixodbc=/usr
    make
}

    package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install

    install -m644 samples/unixodbc.freetds.driver.template ${pkgdir}/etc/${pkgname}/
    install -m644 samples/unixodbc.jdbc.datasource.template ${pkgdir}/etc/${pkgname}/
    install -m644 samples/odbc.ini ${pkgdir}/etc/${pkgname}/odbc.ini.template
    }
