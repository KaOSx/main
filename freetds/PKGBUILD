
pkgname=freetds
pkgver=1.00.13
pkgrel=3
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('unixodbc' 'openssl')
options=('!libtool')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5aec03ac89d3b7026acaac5b6b0aaba4')

build() {
	cd ${pkgname}-${pkgver}
	
	./configure --prefix=/usr \
	      --sysconfdir=/etc/freetds \
	      --mandir=/usr/share/man \
	      --enable-msdblib \
	      --with-tdsver=7.0 \
	      --with-openssl \
	      --with-unixodbc=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	
	install -m644 samples/unixodbc.freetds.driver.template ${pkgdir}/etc/${pkgname}/
	install -m644 samples/unixodbc.jdbc.datasource.template ${pkgdir}/etc/${pkgname}/
	install -m644 samples/odbc.ini ${pkgdir}/etc/${pkgname}/odbc.ini.template
}
