
pkgname=freetds
pkgver=1.2.13
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='https://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('unixodbc' 'openssl')
options=('!libtool')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/${pkgname}-${pkgver}.tar.bz2")
md5sums=('93539d79ad513b56d1623386916870e1')

build() {
	cd ${pkgname}-${pkgver}
	
	./configure --prefix=/usr \
	      --sysconfdir=/etc/freetds \
	      --mandir=/usr/share/man \
	      --enable-msdblib \
	      --with-tdsver=7.2 \
	      --with-openssl \
	      --with-unixodbc=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	
	install -m644 samples/unixodbc.freetds.driver.template ${pkgdir}/etc/${pkgname}/
	install -m644 samples/unixodbc.jdbc.datasource.template ${pkgdir}/etc/${pkgname}/
	install -m644 samples/odbc.ini ${pkgdir}/etc/${pkgname}/odbc.ini.template
}
