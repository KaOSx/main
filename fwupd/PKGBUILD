 
pkgname=fwupd
pkgver=1.4.5
pkgrel=1
pkgdesc="Simple daemon to allow updating firmware on Linux automatic, safe and reliable"
arch=('x86_64')
url="https://fwupd.org/"
license=('LGPL2.1')
depends=('efivar' 'gcab' 'gpgme' 'json-glib' 'libarchive' 'libgudev' 'libgusb' 'libsoup'
         'libxmlb' 'modemmanager' 'polkit' 'python3' 'shared-mime-info' 'udisks2' 'tpm2-tss')
makedepends=('bash-completion' 'help2man' 'gnu-efi-libs' 'gobject-introspection' 
             'linux-headers' 'meson' 'noto-fonts' 'ninja' 'python3-cairo' 'python3-gobject3'
             'python3-pillow' 'vala')
backup=('etc/fwupd/daemon.conf'
        'etc/fwupd/redfish.conf'
        'etc/fwupd/remotes.d/fwupd-tests.conf'
        'etc/fwupd/remotes.d/lvfs-testing.conf'
        'etc/fwupd/remotes.d/lvfs.conf'
        'etc/fwupd/remotes.d/vendor-directory.conf'
        'etc/fwupd/remotes.d/vendor.conf'
        'etc/fwupd/thunderbolt.conf'
        'etc/fwupd/upower.conf')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bc1b625503d7e3ff73d7d69348b38e5337f40d60f6722d1820c4e2b530eff842')

build() {
    mkdir -p build
    cd build
    
    meson ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release \
        --libexecdir=lib \
        -Dplugin_tpm=true \
        -Dgtkdoc=false \
        -Dplugin_uefi=true \
        -Dplugin_dell=false
    ninja
}

check() {
    cd build
    
    meson test
}

package() {
    cd build
  
    DESTDIR=${pkgdir} ninja install

    install -d -o root -g 102 -m 700 ${pkgdir}/usr/share/polkit-1/rules.d
    rm -r ${pkgdir}/usr/share/installed-tests/
}
