 
pkgname=fwupd
pkgver=1.9.20
pkgrel=1
pkgdesc="Simple daemon to allow updating firmware on Linux automatic, safe and reliable"
arch=('x86_64')
url="https://fwupd.org/"
license=('LGPL2.1')
depends=('efivar' 'gcab' 'gpgme' 'json-glib' 'libarchive' 'libgudev' 'libgusb' 'libsoup'
         'libxmlb' 'modemmanager' 'polkit' 'python3' 'shared-mime-info' 'udisks2' 'tpm2-tss'
         'protobuf-c' 'libftdi')
makedepends=('bash-completion' 'help2man' 'gnu-efi-libs' 'gobject-introspection' 
             'linux-headers' 'meson' 'noto-fonts' 'ninja' 'python3-cairo' 'python3-gobject3'
             'python3-pillow' 'vala' 'python3-jinja')
backup=('etc/fwupd/fwupd.conf'
        'etc/fwupd/remotes.d/lvfs-testing.conf'
        'etc/fwupd/remotes.d/lvfs.conf'
        'etc/fwupd/remotes.d/vendor-directory.conf')
#source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz")
source=("https://github.com/fwupd/fwupd/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b86a2d7fe1b6cc0ac4f75f3351a78cb3c225880c9e20b932fa078ef472e9fcb2')

build() {
    mkdir -p build
    cd build

    meson setup ../${pkgname}-${pkgver} \
        --prefix=/usr \
        --buildtype=release \
        --libexecdir=lib \
        -Ddocs=disabled \
        -Defi_binary=false \
        -Dplugin_amdgpu=disabled \
        -Dplugin_modem_manager=disabled
    ninja
}

check() {
    cd build

    meson test
}

package() {
    cd build

    DESTDIR=${pkgdir} ninja install

    install -d -o root -g 102 -m 700 ${pkgdir}/usr/share/polkit-1/rules.d
    rm -r ${pkgdir}/usr/share/installed-tests/
}
