
pkgname=netcdf
pkgver=4.6.0
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="https://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
license=('custom')
depends=('gcc-libs' 'hdf5' 'curl')
options=('!libtool' '!makeflags')
source=("https://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz")
md5sums=('d41515e03b0376eebcec0ddc40ef9fe6')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4 
  make
}

check() {
  cd ${pkgname}-${pkgver}
 
  make check 
}

package() {
  cd ${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
