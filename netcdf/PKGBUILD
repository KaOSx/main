
pkgname=netcdf
pkgver=4.8.0
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="https://www.unidata.ucar.edu/downloads/netcdf/"
license=('custom')
depends=('gcc-libs' 'hdf5' 'curl')
options=('!libtool' '!makeflags')
source=("https://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-c-${pkgver}.tar.gz")
md5sums=('a1e31625e2f270aa8044578d7320962c')

build() {
  cd ${pkgname}-c-${pkgver}
  export CPPFLAGS+=" -DH5_USE_110_API"

  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4
  make
}

check() {
  cd ${pkgname}-c-${pkgver}

  make check 
}

package() {
  cd ${pkgname}-c-${pkgver}

  make DESTDIR=${pkgdir} install

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
