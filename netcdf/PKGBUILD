
pkgname=netcdf
pkgver=4.4.1
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
license=('custom')
depends=('gcc-libs' 'hdf5' 'curl')
options=('!libtool' '!makeflags')
source=("http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz")
md5sums=('7843e35b661c99e1d49e60791d5072d8')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # one test fails, known upstream https://github.com/Unidata/netcdf-c/issues/244
  make check 
}

package() {
  cd ${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
