
pkgname=netcdf
pkgver=4.8.1
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="https://www.unidata.ucar.edu/downloads/netcdf/"
license=('custom')
depends=('gcc-libs' 'hdf5' 'curl')
options=('!libtool' '!makeflags')
source=("https://downloads.unidata.ucar.edu/netcdf-c/${pkgver}/src/netcdf-c-${pkgver}.tar.gz")
md5sums=('8da8665f1f185b85bc8127bc0bad0ee6')

build() {
  cd ${pkgname}-c-${pkgver}
  export CPPFLAGS+=" -DH5_USE_110_API"

  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4
  make
}

check() {
  cd ${pkgname}-c-${pkgver}
  # 2 tests fail in 4.8.1
  make check 
}

package() {
  cd ${pkgname}-c-${pkgver}

  make DESTDIR=${pkgdir} install

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
