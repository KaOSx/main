
pkgname=netcdf
pkgver=4.7.2
pkgrel=1
pkgdesc="Network Common Data Form interface for array-oriented data access and corresponding library."
arch=("x86_64")
url="https://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
license=('custom')
depends=('gcc-libs' 'hdf5' 'curl')
options=('!libtool' '!makeflags')
source=("https://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-c-${pkgver}.tar.gz")
md5sums=('101713cc2f8606be476902888f056582')

build() {
  cd ${pkgname}-c-${pkgver}
  
  ./configure --prefix=/usr \
        --enable-shared \
        --enable-netcdf-4 
  make
}

check() {
  cd ${pkgname}-c-${pkgver}
 
  make check 
}

package() {
  cd ${pkgname}-c-${pkgver}

  make DESTDIR=${pkgdir} install

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
