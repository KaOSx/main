
pkgname=ldb
pkgver=2.6.1
pkgrel=2
epoch=2
pkgdesc="Provide a fast database with an LDAP-like API designed to be used within an application."
arch=('x86_64')
url="https://ldb.samba.org/"
license=('GPL3')
depends=('talloc' 'tevent' 'tdb' 'popt' 'python3' 'lmdb' 'libxcrypt' 'libldap')
source=("https://samba.org/ftp/ldb/${pkgname}-${pkgver}.tar.gz")
md5sums=('3a5f54f511fb237b83e1f34e2c7e25cd')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --disable-rpath \
        --disable-rpath-install \
        --bundled-libraries=cmocka \
        --builtin-libraries=replace \
        --with-modulesdir=/usr/lib/ldb/modules \
        --with-privatelibdir=/usr/lib/ldb
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR=${pkgdir}/ install
}

