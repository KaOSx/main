
pkgname=libxfixes
pkgver=6.0.0
pkgrel=1
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'xorgproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXfixes-${pkgver}.tar.bz2")
sha256sums=('a7c1a24da53e0b46cac5aea79094b4b2257321c621b258729bc3139149245b4c')

build() {
  cd libXfixes-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static
  make
}

package() {
  cd libXfixes-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
