
pkgname=xorg-xinit
pkgver=1.4.2
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'xorg-server-utils')
makedepends=('xorg-util-macros')
optdepends=('xterm')
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/xinit-${pkgver}.tar.xz"
        '06_move_serverauthfile_into_tmp.diff'
        'fs25361.patch'
        'xserverrc'
        "https://gitlab.freedesktop.org/xorg/app/xinit/-/commit/e2430bcb8327682340d985f89ad5137efa2f0d4f.diff")
options=('!emptydirs')
sha256sums=('b7d8dc8d22ef9f15985a10b606ee4f2aad6828befa437359934647e88d331f23'
            'edf016cf1cfc174ba83e4f31d72a736fb2c0201e537cb184c5e2fe92c8121c6a'
            '866a478630d31bbf01c7fe7cd2a6db3912b67f693c6417d77536a5bd8d169ddd'
            '5e87bff928c143d9db9966048f6530e83e9e9a1cac09ce4c25f8aabc08611b6f'
            '71c84795c557721ee4b4495f04ff5d1e9c1e84e36bdd0a921e5734a94ef2f3ba')

build() {
  cd xinit-${pkgver}
  patch -p1 -i ${srcdir}/06_move_serverauthfile_into_tmp.diff
  patch -p1 -i ${srcdir}/fs25361.patch
  #patch -p1 -i ${srcdir}/e2430bcb8327682340d985f89ad5137efa2f0d4f.diff
  sed -i -e 's|XSLASHGLOB.sh|XSLASHGLOB|' xinitrc.cpp
  
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/etc/skel
  install -m644 ${srcdir}/xserverrc ${pkgdir}/etc/X11/xinit/xserverrc
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
