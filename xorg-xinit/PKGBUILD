
pkgname=xorg-xinit
pkgver=1.3.4
pkgrel=3
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'inetutils' 'xorg-server-utils')
makedepends=('xorg-util-macros')
optdepends=('xterm')
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/xinit-${pkgver}.tar.bz2"
        '06_move_serverauthfile_into_tmp.diff'
        'fs25361.patch'
        'xserverrc')
options=('!emptydirs')
md5sums=('4e928452dfaf73851413a2d8b8c76388'
         'abd072ec435ce084cf4e89c58f83d45c'
         '73a597d3fb7f5b9487318ac8cfd4bedf'
         '4deba1b7f2cc7f93b304b956023e65d6')

build() {
  cd xinit-${pkgver}
  patch -p1 -i ${srcdir}/06_move_serverauthfile_into_tmp.diff
  patch -p1 -i ${srcdir}/fs25361.patch
  sed -i -e 's|XSLASHGLOB.sh|XSLASHGLOB|' xinitrc.cpp
  
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/etc/skel
  install -m644 ${srcdir}/xserverrc ${pkgdir}/etc/X11/xinit/xserverrc
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
