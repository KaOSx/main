
pkgname=r
pkgver=4.0.3
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics"
arch=('x86_64')
license=('GPL')
url="https://www.r-project.org/"
depends=('lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff' 'icu'
         'ncurses' 'pcre2' 'readline' 'zlib' 'perl' 'gcc-libs' 'zip' 'unzip'
         'tk' 'libxt' 'libxmu' 'pango' 'xz' 'desktop-file-utils')
makedepends=('openjdk' 'gcc-fortran')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags' '!emptydirs')
source=("https://cran.r-project.org/src/base/R-4/R-${pkgver}.tar.gz"
        'r.desktop'
        'r.png'
        'R.conf')
md5sums=('8ecf46afa36c9aff9904aad5ca422c6d'
         'f6d54d32e510d90c748a0d16d2abcbdc'
         '00659f39e90627fe87f1645df5d4e3a7'
         '1dfa62c812aed9642f6e4ac34999b9fe')

build() {
   cd R-${pkgver}
   sed -i 's|$(rsharedir)/texmf|${datarootdir}/texmf|' share/Makefile.in
   sed -i 's|test ${makeinfo_version_min} -lt 7|test ${makeinfo_version_min} -lt 0|' configure
   sed -i 's|ZLIB_VERSION, "1.2.5", 5|ZLIB_VERSION, "1.2.1", 6|' configure
   
   ./configure  --prefix=/usr \
        --libdir=/usr/lib \
        --sysconfdir=/etc/R \
        --datarootdir=/usr/share \
            rsharedir=/usr/share/R/ \
            rincludedir=/usr/include/R/ \
            rdocdir=/usr/share/doc/R/ \
        --with-x \
        --enable-R-shlib \
        --with-lapack \
        --with-blas \
        F77=gfortran \
        LIBnn=lib
   make
   
   cd src/nmath/standalone
   make shared
}

package() {
   cd R-${pkgver}
   make -j1 DESTDIR=${pkgdir} install
   
   cd src/nmath/standalone
   make DESTDIR=${pkgdir} install

   # R wrapper scripts.
   sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
   rm ${pkgdir}/usr/lib/R/bin/R
   cd ${pkgdir}/usr/lib/R/bin
   ln -s ../../../bin/R

  install -Dm644 ${srcdir}/r.desktop ${pkgdir}/usr/share/applications/r.desktop
  install -Dm644 ${srcdir}/r.png ${pkgdir}/usr/share/pixmaps/r.png

  install -d ${pkgdir}/etc/R
  cd ${pkgdir}/usr/lib/R/etc
  for i in *; do
    mv -f ${i} ${pkgdir}/etc/R
    ln -s /etc/R/${i} ${i}
  done
  
  install -Dm644 ${srcdir}/R.conf ${pkgdir}/etc/ld.so.conf.d/R.conf
}
