
pkgname=syslinux
pkgver=6.03
pkgrel=4
arch=('x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc' 'util-linux')
makedepends=('nasm' 'python2' 'gnu-efi-libs')
optdepends=('mtools:           For mkdiskimage and syslinux support'
            'gptfdisk:         For GPT support'
            'efibootmgr:       For EFI support'
            'dosfstools:       For EFI support')
backup=('boot/syslinux/syslinux.cfg')
install=syslinux.install
options=('!makeflags' '!emptydirs')
source=("https://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.xz"
        'syslinux.cfg'
        'syslinux-install_update'
        'gcc5.patch')
md5sums=('92a253df9211e9c20172796ecf388f13'
         '6461abfb17e54dc997cea77c7a312e20'
         '3fcba3efe6beda6fc4935c1914393377'
         '3fc0ea96652b100f71be89d6ac4df8d7')
         
prepare() {
    cd ${pkgname}-${pkgver}
    
    sed 's|/usr/man|/usr/share/man|g' -i mk/syslinux.mk
    patch -p1 -i ${srcdir}/gcc5.patch
}

_build_syslinux_bios() {
    
    rm -rf ${srcdir}/${pkgname}-${pkgver}-bios/ 
    cp -r ${srcdir}/${pkgname}-${pkgver} ${srcdir}/${pkgname}-${pkgver}-bios
    cd ${srcdir}/${pkgname}-${pkgver}-bios/
    
    ## Do not build syslinux with default LDFLAGS
    unset LDFLAGS
    
    make bios
    make bios installer
    
}

_build_syslinux_efi64() {
    
    rm -rf ${srcdir}/${pkgname}-${pkgver}-efi64/ 
    cp -r ${srcdir}/${pkgname}-${pkgver} ${srcdir}/${pkgname}-${pkgver}-efi64
    cd ${srcdir}/${pkgname}-${pkgver}-efi64/
    
    ## Unset all compiler FLAGS for efi64 build
    unset CFLAGS
    unset CPPFLAGS
    unset CXXFLAGS
    unset LDFLAGS
    unset MAKEFLAGS
    
    make efi64
    make efi64 installer
    
}


build() {

    cd ${pkgname}-${pkgver}/

    ## Build syslinux-efi
    _build_syslinux_efi64
    
    ## Build syslinux-bios
    _build_syslinux_bios
    
}

_package_syslinux_bios() {

    cd ${srcdir}/${pkgname}-${pkgver}-bios/
    
    ## Install Syslinux bios
    make INSTALLROOT=${pkgdir}/ AUXDIR=/usr/lib/syslinux/bios/ bios install
    
    rm ${pkgdir}/usr/lib/syslinux/bios/syslinux.com
    
    ## Remove com32, dosutil and diag dirs
    rm -rf ${pkgdir}/usr/lib/syslinux/bios/dosutil/
    rm -rf ${pkgdir}/usr/lib/syslinux/bios/diag/
    rm -rf ${pkgdir}/usr/lib/syslinux/bios/com32/
    
    ## Install docs
    install -d ${pkgdir}/usr/share/doc
    cp -ar ${srcdir}/${pkgname}-${pkgver}/doc ${pkgdir}/usr/share/doc/syslinux
    
    ## Install the default configuration
    install -D -m0644 ${srcdir}/syslinux.cfg ${pkgdir}/boot/syslinux/syslinux.cfg
    
    ## Install the installation and update script
    ## This script is maintained at git://gist.github.com/772138.git
    ## Script not yet updated for syslinux-efi
    install -D -m0755 ${srcdir}/syslinux-install_update ${pkgdir}/usr/sbin/syslinux-install_update
    
}

package() { 

    cd ${pkgname}-${pkgver}-efi64/
    make INSTALLROOT=${pkgdir}/ AUXDIR=/usr/lib/syslinux/ efi64 install

    _package_syslinux_bios
    
}
