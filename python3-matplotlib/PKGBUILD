
pkgname=python3-matplotlib
pkgver=3.8.4
pkgrel=1
pkgdesc="Python plotting library, making publication quality plots"
arch=('x86_64')
url='https://matplotlib.org'
license=('custom')
depends=('python3-pytz' 'python3-numpy' 'python3-cairo' 'pyqt6-python3' 'python3-dateutil'
         'python3-pyparsing' 'python3-certifi' 'python3-cycler' 'tk' 'ghostscript' 'texlive-bin' 'pybind11')
makedepends=('python3-gobject3' 'python3-six' 'python3-build' 'python3-installer' 'python3-setuptools-scm' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/m/matplotlib/matplotlib-${pkgver}.tar.gz"
        'numpy.diff')
#source=("https://github.com/matplotlib/matplotlib/archive/v${pkgver}.tar.gz")
sha256sums=('8aac397d5e9ec158960e31c381c5ffc52ddd52bd9a47717e2a694038167dffea'
            '858487c49396d850623ffb2fcaa659a2e8a439db42a680b591704d62bf4546d3')

prepare() {
  cd matplotlib-${pkgver}

  patch -p1 -i ${srcdir}/numpy.diff
}

build() {
   cd matplotlib-${pkgver}

   python3 -m build -nw
}

package() {
   cd matplotlib-${pkgver}
   python3 -m installer --destdir=${pkgdir} dist/*.whl

   install -dm755 ${pkgdir}/usr/share/licenses/python3-matplotlib
   install -m 644 doc/users/project/license.rst ${pkgdir}/usr/share/licenses/python3-matplotlib
}

