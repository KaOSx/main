
pkgname=octave
pkgver=4.0.3
pkgrel=2
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk'
         'arpack' 'suitesparse' 'gperf' 'glu' 'gnuplot' 'gl2ps' 'ghostscript') # 'qscintilla-qt5')
makedepends=('texlive-core' 'gcc-fortran' 'python2')
options=('!emptydirs')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.xz")
md5sums=('73b140bc69ac21cfaa3e473b91b6842b')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --enable-shared \
       --disable-static \
       --with-quantum-depth=16 \
       --with-umfpack="-lumfpack -lsuitesparseconfig" 
       #MOC=moc UIC=uic RCC=rcc

  LANG=C make
}

package(){
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # add path to ld.so.conf.d
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo /usr/lib/${pkgname}/${pkgver} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
  # remove .desktop until ported to Qt 5
  rm ${pkgdir}/usr/share/applications/www.octave.org-octave.desktop
}
