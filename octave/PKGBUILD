
pkgname=octave
pkgver=4.2.1
pkgrel=4
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="https://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk' 'libsndfile'
         'arpack' 'suitesparse' 'gperf' 'glu' 'gnuplot' 'gl2ps' 'ghostscript' 'qscintilla-qt5')
makedepends=('texlive-core' 'gcc-fortran' 'python2' 'qt5-tools')
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/octave/octave-${pkgver}.tar.lz")
md5sums=('ca5106425e12df2bef23df72ba58f3c4')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --enable-shared \
       --disable-static \
       --with-quantum-depth=16 \
       --with-umfpack="-lumfpack -lsuitesparseconfig" 

  LANG=C make
}

package(){
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # add path to ld.so.conf.d
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo /usr/lib/${pkgname}/${pkgver} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
  # remove .desktop until ported to Qt 5
  #rm ${pkgdir}/usr/share/applications/www.octave.org-octave.desktop
}
