
pkgname=octave
pkgver=3.6.4
pkgrel=3
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk'
         'arpack' 'suitesparse' 'gperf')
makedepends=('texlive-core' 'gcc-fortran' 'python2')
optdepends=('texinfo: for help-support in octave')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2")
options=('!emptydirs')
install='octave.install'
md5sums=('e0d3e5e3d38a66d3f8593ba065c6e2fd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  autoreconf -vfi

  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --enable-shared --disable-static \
       --with-umfpack="-lumfpack -lsuitesparseconfig" 

  LANG=C make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}
