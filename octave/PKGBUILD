
pkgname=octave
pkgver=9.1.0
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('x86_64')
url="https://www.octave.org"
license=('GPL')
depends=('fftw' 'curl' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk' 'libsndfile'
         'arpack' 'suitesparse' 'gperf' 'glu' 'gnuplot' 'gl2ps' 'ghostscript' 'qscintilla-qt5')
makedepends=('texlive-core' 'gcc-fortran' 'qt5-tools')
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/octave/octave-${pkgver}.tar.lz"
        '0001-Use-reentrant-libqhull_r.patch'
        "https://hg.savannah.gnu.org/hgweb/octave/raw-rev/aaffac4fbe30")
md5sums=('06102a83f3f63dc4b039ccbba9b8b6d8'
         'a53f7eea9e8f23884787eaa4d9c6b797'
         'efc12e6bbd7b38c9ae16fa7f125be257')

build() {
  cd ${pkgname}-${pkgver}
  # suitespares 7.0.0 needed, from https://savannah.gnu.org/bugs/?63684
  #patch -p1 -i ${srcdir}/aaffac4fbe30

  autoreconf -vfi

  ./configure --prefix=/usr \
       --libexecdir=/usr/lib \
       --enable-shared \
       --disable-static \
       --with-quantum-depth=16 \
       --with-umfpack="-lumfpack -lsuitesparseconfig" \
       --disable-docs

  LANG=C make
}

package(){
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # add path to ld.so.conf.d
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo /usr/lib/${pkgname}/${pkgver} > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
}
