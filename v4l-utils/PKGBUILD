
pkgname=v4l-utils
pkgver=1.28.1
pkgrel=1
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('x86_64')
url="https://linuxtv.org/"
license=('LGPL')
depends=('glibc' 'gcc-libs' 'sysfsutils' 'libjpeg-turbo' 'qt6-base' 'qt6-5compat')
makedepends=('clang' 'meson' 'ninja')
options=('!libtool')
source=("https://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.xz")
md5sums=('6716de513a1fd2e1edb404a46a455855')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dgconv=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  rm -f ${pkgdir}/usr/bin/ivtv-ctl
  rm -f ${pkgdir}/usr/share/applications/qvidcap.desktop
}

