
pkgname=python2-docutils
_pkgname=docutils
pkgver=0.15
pkgrel=2
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX"
arch=('x86_64')
url="http://docutils.sourceforge.net"
license=('custom')
depends=('python2')
source=("https://downloads.sourceforge.net/docutils/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('2fe4b3761a05c89ffccee6f7b4279696d4a7ebfe')

build() {
  cd ${_pkgname}-${pkgver}
  
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir} --optimize=1
  
  for _f in ${pkgdir}/usr/bin/*.py; do
      mv -v ${_f} ${_f%.py}2.py
  done

  for _f in ${pkgdir}/usr/bin/*.py; do
      ln -s $(basename $_f) ${pkgdir}/usr/bin/$(basename $_f .py)
  done

  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt
  install -D -m644 licenses/python* ${pkgdir}/usr/share/licenses/${pkgname}/
}
