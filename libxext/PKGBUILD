
pkgname=libxext
pkgver=1.3.5
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'xorgproto')
makedepends=('pkg-config')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXext-${pkgver}.tar.xz")
sha256sums=('db14c0c895c57ea33a8559de8cb2b93dc76c42ea4a39e294d175938a133d7bca')

build() {
  cd libXext-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static
  make
}

package() {
  cd libXext-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
