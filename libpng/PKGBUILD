
pkgname=libpng
pkgver=1.6.26
_apngver=1.6.25
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/libpng/${pkgname}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
md5sums=('faed9bb495d2e12dd0c9ec561ca60cd8'
         '0e38f51075a7d77f646e44dcdb0c0a3a')

build() {
  cd ${pkgname}-${pkgver}

  # http://sourceforge.net/projects/libpng-apng/
  sed -i '/pngpriv.h/,/#define PNG_BGR/s|0x[0-9]*|&U|' ${srcdir}/libpng-$_apngver-apng.patch
  patch -p1 -i ${srcdir}/libpng-${_apngver}-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png ${pkgdir}/usr/bin/
  install -D -m644 ../../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
