
pkgname=libpng
pkgver=1.6.40
_apngver=1.6.39
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/libpng/${pkgname}-${pkgver}.tar.xz"
        #"https://sourceforge.net/projects/apng/files/libpng/libpng16/libpng-${_apngver}-apng.patch.gz"
        "https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
sha256sums=('535b479b2467ff231a3ec6d92a525906fb8ef27978be4f66dbe05d3f3a01b3a1'
            '4ac4b6eaba00cde212c48db65cb94291073febca22c5c79fda87091502e80cfd')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/libpng-${_apngver}-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd contrib/pngminus
  make PNGLIB_SHARED="-L${pkgdir}/usr/lib -lpng" png2pnm pnm2png
  install -m755 png2pnm pnm2png ${pkgdir}/usr/bin/
  install -D -m644 ../../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
