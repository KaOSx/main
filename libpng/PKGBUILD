
pkgname=libpng
pkgver=1.6.32
_apngver=1.6.32
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/libpng/${pkgname}-${pkgver}.tar.xz"
        "https://sourceforge.net/projects/apng/files/libpng/libpng16/libpng-${_apngver}-apng.patch.gz"
        'pngminus.diff')
        #"https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
md5sums=('e01be057a9369183c959b793a685ad15'
         '86b44700cfcf5000b99bfff7d28f4345'
         '8ac1338f2459bbd59536bfa8c56f2166')

build() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/libpng-${_apngver}-apng.patch
  patch -p1 -i ${srcdir}/pngminus.diff

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png ${pkgdir}/usr/bin/
  install -D -m644 ../../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
