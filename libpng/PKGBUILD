
pkgname=libpng
pkgver=1.6.31
_apngver=1.6.31
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/libpng/${pkgname}-${pkgver}.tar.xz"
        "https://sourceforge.net/projects/apng/files/libpng/libpng16/libpng-${_apngver}-apng.patch.gz")
        #"https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
md5sums=('1b34eab440263e32cfa39d19413fad54'
         '469b846c1e705a3b6f2e6d9dbdff08d3')

build() {
  cd ${pkgname}-${pkgver}

  # http://sourceforge.net/projects/libpng-apng/
  patch -p0 -i ${srcdir}/libpng-${_apngver}-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png ${pkgdir}/usr/bin/
  install -D -m644 ../../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
