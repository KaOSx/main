
pkgname=libpng
pkgver=1.6.29
_apngver=1.6.29
pkgrel=2
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/libpng/${pkgname}-${pkgver}.tar.xz"
        "https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")
md5sums=('3245dbd76ea91e1437507357b858ec97'
         '75ebf0972827a0079e8d0bd03fcbe6f7')

build() {
  cd ${pkgname}-${pkgver}

  # http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i ${srcdir}/libpng-${_apngver}-apng.patch

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png ${pkgdir}/usr/bin/
  install -D -m644 ../../LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
