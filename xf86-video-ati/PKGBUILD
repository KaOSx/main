
pkgname=xf86-video-ati
pkgver=19.1.0
pkgrel=3
pkgdesc="X.org ati video driver"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libpciaccess' 'libdrm' 'systemd' 'pixman' 'mesa')
makedepends=('xorg-server' 'xorgproto')
groups=('xorg-video-drivers')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
        "https://gitlab.freedesktop.org/xorg/driver/xf86-video-ati/-/commit/f223035f4ffcff2a9296d1e907a5193f8e8845a3.diff"
        "https://gitlab.freedesktop.org/xorg/driver/xf86-video-ati/-/commit/8da3e4561ef82bb78c9a17b8cd8bf139b9cfd680.diff")
sha256sums=('659f5a1629eea5f5334d9b39b18e6807a63aa1efa33c1236d9cc53acbb223c49'
            '4c2d27e51c947229cb1fef5bc09b111738821df7444668548d2bcf32b47ebab5'
            '05c83497fe828ec5485585c10aa65eb00fe4016fbc3a5caeb71f29a8e0ea16c3')

build() { 
    cd ${pkgname}-${pkgver}
    # gcc 10 needed
    patch -p1 -i ${srcdir}/f223035f4ffcff2a9296d1e907a5193f8e8845a3.diff
    # xorg-server 21.1 needed
    patch -p1 -i ${srcdir}/8da3e4561ef82bb78c9a17b8cd8bf139b9cfd680.diff

    ./configure \
        --prefix=/usr \
        --enable-dependency-tracking \
        --enable-glamor
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 

    install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
