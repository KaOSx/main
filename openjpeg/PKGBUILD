
pkgname=openjpeg
pkgver=2.1.2
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
makedepends=('cmake' 'doxygen')
options=('!libtool')
source=("https://github.com/uclouvain/openjpeg/archive/v${pkgver}.tar.gz"
        "https://github.com/szukw000/openjpeg/commit/cadff5fb.diff")
md5sums=('40a7bfdcc66280b3c1402a0eb1a27624'
         '38d96e38cf1ed041309268f355423867')

prepare() {
 cd $pkgname-${pkgver}
 # fix 2 CVEs
 patch -p1 -i $srcdir/cadff5fb.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_DOC=ON
  make 
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -m755 -d ${pkgdir}/usr/share/licenses/openjpeg
  install -m644 ../${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/openjpeg/LICENSE
  # from doc build, just use the man pages
  rm -r ${pkgdir}/usr/share/doc/html
}

