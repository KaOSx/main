
pkgname=openjpeg
pkgver=1.5.2
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=('x86_64')
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib' 'libpng' 'libtiff')
source=("http://sourceforge.net/projects/openjpeg.mirror/files/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c41772c30fb1c272358b3707233134a1')

options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -fi
  # use system libs, informative output during build
  rm -rf thirdparty
  ./configure --prefix=/usr \
        --disable-static --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
  ln "${pkgdir}/usr/include/openjpeg-1.5/openjpeg.h" "${pkgdir}/usr/include/openjpeg.h"
}

