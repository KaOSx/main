
pkgname=python3-numpy
pkgver=1.19.2
_pkgver=1.19.2
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="https://www.numpy.org/"
depends=('lapack' 'python3-nose' 'python3-cython')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('74d0cf50aa28af81874aca3e67560945afd783b2a006913577d6cddc35a824a6')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python3 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python3-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python3-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python3.8
  ln -sf /usr/lib/python3.8/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python3.8/numpy
}
