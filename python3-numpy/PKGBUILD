
pkgname=python3-numpy
pkgver=1.14.2
_pkgver=1.14.2
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://www.numpy.org/"
depends=('lapack' 'python3-nose')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('ddbcda194f49e0cf0663fa8131cb9d7a3b876d14dea0047d3c5fdfaf20adbb40')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python3 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python3-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python3-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python3.5
  ln -sf /usr/lib/python3.5/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python3.5/numpy
}
