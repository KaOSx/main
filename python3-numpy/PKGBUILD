
pkgname=python3-numpy
pkgver=1.17.2
_pkgver=1.17.2
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="https://www.numpy.org/"
depends=('lapack' 'python3-nose')
source=("https://github.com/numpy/numpy/releases/download/v${pkgver}/numpy-${_pkgver}.tar.gz")
sha256sums=('81a4f748dcfa80a7071ad8f3d9f8edb9f8bc1f0a9bdd19bfd44fd42c02bd286c')

build() {
  cd numpy-${pkgver}
  export Atlas=None
  export LDFLAGS="$LDFLAGS -shared"
  
  python3 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd numpy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root=${pkgdir} --optimize=1

  install -m755 -d ${pkgdir}/usr/share/licenses/python3-numpy
  install -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/python3-numpy/
  
  install -m755 -d ${pkgdir}/usr/include/python3.6
  ln -sf /usr/lib/python3.6/site-packages/numpy/core/include/numpy ${pkgdir}/usr/include/python3.6/numpy
}
