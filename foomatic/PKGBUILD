 
pkgbase="foomatic"
pkgname=('foomatic-db' 'foomatic-filters' 'foomatic-db-engine' 'foomatic-db-nonfree')
arch=('x86_64') 
_snapdate=20140330
_filtersver=4.0.17
_relver=4.0
pkgver=${_filtersver}_${_snapdate}
pkgrel=1
makedepends=('cups' 'perl' 'libxml2' 'enscript' 'perl' 'net-snmp' 'bash')
source=("http://www.openprinting.org/download/foomatic/${pkgbase}-filters-${_relver}-${_snapdate}.tar.gz"
        "http://www.openprinting.org/download/foomatic/${pkgbase}-db-engine-${_relver}-${_snapdate}.tar.gz"
        "http://www.openprinting.org/download/foomatic/${pkgbase}-db-${_relver}-${_snapdate}.tar.gz"
        "http://www.openprinting.org/download/foomatic/${pkgbase}-db-nonfree-${_snapdate}.tar.gz")
url="http://www.linuxprinting.org/foomatic.html"
options=('!emptydirs')
md5sums=('27173d2fde0dcb8ed60f795bc78f6ebc'
         'a90eab37900a6a523f4f3d6c5360c2a9'
         'd9f2efdeeb7fd247dcc619b23ae199b6'
         '21e4b6295744af26a79ceaf1a9c8f3e3') 

package_foomatic-db() {

  pkgdesc="Foomatic - The collected knowledge about printers, drivers, and driver options in XML files, used by foomatic-db-engine to generate PPD files."
  arch=('x86_64')
  license=('GPL' 'custom')
  depends=('perl' 'libxml2')

  cd "${srcdir}/${pkgname}-${_snapdate}"
  ./configure --prefix=/usr
  make DESTDIR="${pkgdir}" install 
  install -v -Dm0644 "${srcdir}/${pkgname}-${_snapdate}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_foomatic-filters() {

  pkgdesc="Foomatic - Filter scripts used by the printer spoolers to convert the incoming PostScript data into the printer's native format."
  arch=('x86_64')
  license=('GPL')
  backup=(etc/foomatic/filter.conf)
  depends=('glibc' 'dbus-core')
  optdepends=('perl:      for the "beh" Backend End Handler used by cups'
              'net-snmp:  certain (mostly HP) printers need it to work')

  cd "${srcdir}/foomatic-filters-${_relver}-${_snapdate}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make 
  make DESTDIR="${pkgdir}" install 
  # part of cups-filters
  rm ${pkgdir}/usr/lib/cups/filter/foomatic-rip
  rm ${pkgdir}/usr/share/man/man1/foomatic-rip.1
}

package_foomatic-db-engine() {

  pkgdesc="Foomatic - Foomatic's database engine generates PPD files from the data in Foomatic's XML database. It also contains scripts to directly generate print queues and handle jobs."
  arch=('x86_64')
  license=('GPL')
  depends=('perl' 'libxml2' 'foomatic-filters' 'bash')

  cd "${srcdir}/foomatic-db-engine-${_relver}-${_snapdate}"
  ./configure --prefix=/usr
  make 
  eval `perl -V:archname`
  make DESTDIR=${pkgdir} \
       INSTALLARCHLIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITELIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITEARCH=/usr/lib/perl5/vendor_perl/ install 
  /usr/bin/find ${pkgdir} -name '.packlist' -delete
  # fix permissions
  chmod 755 ${pkgdir}/usr/lib{,/perl5,/perl5/vendor_perl}
}

package_foomatic-db-nonfree() {

  pkgdesc="Foomatic - database extension consisting of manufacturer-supplied PPD files released under non-free licenses"
  arch=('any')
  license=('custom')
  depends=('perl' 'libxml2' "foomatic-db-engine" 'foomatic-db')

  cd "${srcdir}/foomatic-db-nonfree-${_snapdate}"
  ./configure --prefix=/usr
  make DESTDIR="${pkgdir}" install 
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/foomatic-db-nonfree/COPYING"
}
