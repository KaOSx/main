 
pkgbase="foomatic"
pkgname=('foomatic-db' 'foomatic-db-engine' 'foomatic-db-nonfree')
arch=('x86_64') 
pkgver=20220127
_commit=0a400987587af5c9138bf1eb8197ed115874a210
_db_engine_commit=bd265b77a9f66f672bf1e3f0803145f2eccabf06
_db_nonfree_commit=6ddae02ac89240c019f8b5026cfe70e30fd2b3db
pkgrel=1
epoch=2
url="https://www.linuxprinting.org/foomatic.html"
makedepends=('cups' 'perl' 'libxml2' 'enscript' 'perl' 'net-snmp' 'bash')
source=("https://github.com/OpenPrinting/foomatic-db-engine-4/archive/${_db_engine_commit}.zip"
        "https://github.com/OpenPrinting/foomatic-db/archive/${_commit}.zip"
        "https://github.com/OpenPrinting/foomatic-db-nonfree/archive/${_db_nonfree_commit}.zip")
options=('!emptydirs')
md5sums=('d18c75b01cc68394d12f26b7ee0535cd'
         '959a82dc92bea53c959951d79e975f6c'
         '8da836a712bacd7aeaaebad36c98ccbf') 

package_foomatic-db() {

  pkgdesc="Foomatic - The collected knowledge about printers, drivers, and driver options in XML files, used by foomatic-db-engine to generate PPD files."
  arch=('x86_64')
  license=('GPL' 'custom')
  depends=('perl' 'libxml2')

  cd ${pkgname}-${_commit}
  ./make_configure
  ./configure --prefix=/usr
  make DESTDIR=${pkgdir} install 
  install -v -Dm0644 ${srcdir}/${pkgname}-${_commit}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

package_foomatic-db-engine() {

  pkgdesc="Foomatic - Foomatic's database engine generates PPD files from the data in Foomatic's XML database. It also contains scripts to directly generate print queues and handle jobs."
  arch=('x86_64')
  license=('GPL')
  depends=('perl' 'libxml2' 'cups-filters' 'bash')

  cd foomatic-db-engine-4-${_db_engine_commit}
  ./make_configure
  ./configure --prefix=/usr
  make 
  eval `perl -V:archname`
  make DESTDIR=${pkgdir} \
       INSTALLARCHLIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITELIB=/usr/lib/perl5/vendor_perl/ \
       INSTALLSITEARCH=/usr/lib/perl5/vendor_perl/ install 
  /usr/bin/find ${pkgdir} -name '.packlist' -delete
  # fix permissions
  chmod 755 ${pkgdir}/usr/lib{,/perl5,/perl5/vendor_perl}
}

package_foomatic-db-nonfree() {

  pkgdesc="Foomatic - database extension consisting of manufacturer-supplied PPD files released under non-free licenses"
  arch=('x86_64')
  license=('custom')
  depends=('perl' 'libxml2' "foomatic-db-engine" 'foomatic-db')

  cd foomatic-db-nonfree-${_db_nonfree_commit}
  ./make_configure
  ./configure --prefix=/usr
  make DESTDIR=${pkgdir} install 
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/foomatic-db-nonfree/COPYING
}
