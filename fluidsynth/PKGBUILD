
pkgname=fluidsynth
pkgver=2.1.7
pkgrel=1
pkgdesc="Real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="https://www.fluidsynth.org/"
depends=('glib2' 'jack' 'dbus' 'pulseaudio' 'portaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("https://github.com/FluidSynth/fluidsynth/archive/v${pkgver}.tar.gz")
md5sums=('19398b69c5d6c471fc9372caa2b7b69f')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -Denable-ladspa=ON \
    -Denable-portaudio=ON \
    -DLIB_SUFFIX="" \
    -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir}/ install

  install -Dm644 fluidsynth.service ${pkgdir}/usr/lib/systemd/system/fluidsynth.service
  install -Dm644 fluidsynth.conf ${pkgdir}/etc/conf.d/fluidsynth
}
