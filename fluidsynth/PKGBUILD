
pkgname=fluidsynth
pkgver=2.0.5
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'dbus' 'pulseaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("https://github.com/FluidSynth/fluidsynth/archive/v${pkgver}.tar.gz"
        'fluidsynth.service')
md5sums=('4e08e701e25de820e7c403c5dc62c734'
         'e0406748a154409907df62292be32e8a')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX="" \
          -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install

  install -Dm644 fluidsynth.service ${pkgdir}/usr/lib/systemd/system/fluidsynth.service
}
