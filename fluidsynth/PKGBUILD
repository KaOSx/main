
pkgname=fluidsynth
pkgver=2.2.6
pkgrel=1
pkgdesc="Real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="https://www.fluidsynth.org/"
depends=('glib2' 'jack2' 'dbus' 'pulseaudio' 'portaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("https://github.com/FluidSynth/fluidsynth/archive/v${pkgver}.tar.gz")
md5sums=('5219fd45100d7dd470c252218eb2f333')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -Denable-ladspa=ON \
    -Denable-portaudio=ON \
    -DLIB_SUFFIX="" \
    -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 build/fluidsynth.service ${pkgdir}/usr/lib/systemd/system/fluidsynth.service
  install -Dm644 build/fluidsynth.conf ${pkgdir}/etc/conf.d/fluidsynth
}
