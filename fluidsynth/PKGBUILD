
pkgname=fluidsynth
pkgver=1.1.6
pkgrel=10
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'dbus' 'pulseaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("https://sourceforge.net/projects/fluidsynth/files/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'fluidsynth.service')
md5sums=('ae5aca6de824b4173667cbd3a310b263'
         'e0406748a154409907df62292be32e8a')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX=""
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install

  install -Dm644 ../fluidsynth.service ${pkgdir}/usr/lib/systemd/system/fluidsynth.service
}
