
pkgname=xsd
pkgver=4.0.0
_pkgver=4.0.0+dep
pkgrel=9
pkgdesc="An open-source, cross-platform W3C XML Schema to C++ data binding compiler"
arch=('x86_64')
url="http://www.codesynthesis.com/products/xsd"
license=('GPL2')
depends=('boost-libs' 'xerces-c')
source=("http://www.codesynthesis.com/download/xsd/4.0/${pkgname}-${_pkgver}.tar.bz2"
        "xsdcxx.patch")
md5sums=('ae64d7fcd258addc9b045fe3f96208bb'
         'd49e0bbd3bae200f2c6bb7e37326a6ef')

build() {
  cd ${pkgname}-${_pkgver}
  patch -p1 -i ${srcdir}/xsdcxx.patch

  make 
}

package() {
  cd ${pkgname}-${_pkgver}
  make install_prefix=${pkgdir}/usr install

  # Fix conflicts with mono
  mv ${pkgdir}/usr/bin/xsd{,cxx}
  mv ${pkgdir}/usr/share/man/man1/xsd{,cxx}.1
}
