
pkgname=libxft
pkgver=2.3.2
pkgrel=3
pkgdesc="FreeType-based font drawing library for X"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libXft-${pkgver}.tar.bz2")
md5sums=('331b3a2a3a1a78b5b44cfbd43f86fcfe')

build() {
  cd libXft-${pkgver}
  
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-static
  make
}

package() {
  cd libXft-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/

}
