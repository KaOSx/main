
pkgname=gdb
pkgver=8.1.1
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('x86_64')
url="https://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2' 'guile')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
options=('!libtool' '!buildflags')
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
md5sums=('372fa63a53adeee1bfbfd581d8da9e34')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --disable-nls \
    --with-system-readline \
    --with-python=/usr/bin/python2 \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # files in binutils
  rm ${pkgdir}/usr/include/{ansidecl,bfd,bfdlink,dis-asm,symcat,plugin-api}.h
  rm ${pkgdir}/usr/lib/{libbfd,libopcodes}.a
  rm ${pkgdir}/usr/share/info/bfd.info
}
