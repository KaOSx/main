
pkgname=gdb
pkgver=15.1
pkgrel=2
pkgdesc="The GNU Debugger"
arch=('x86_64')
url="https://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python3' 'guile' 'source-highlight' 'boost-libs')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
options=('!libtool' '!buildflags')
source=("https://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz")
sha256sums=('38254eacd4572134bca9c5a5aa4d4ca564cbbd30c369d881f733fb6b903354f2')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/gdb-12.1-readline-8.2-build.patch

  mkdir -p build
  cd build

  ../configure --prefix=/usr \
    --disable-nls \
    --with-system-readline \
    --with-python=/usr/bin/python3 \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install

  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # files in binutils
  rm ${pkgdir}/usr/include/{ansidecl,bfd,bfdlink,ctf-api,ctf,diagnostics,dis-asm,symcat,plugin-api}.h
  rm ${pkgdir}/usr/lib/{libbfd,libctf-nobfd,libctf,libopcodes}.a
  rm ${pkgdir}/usr/share/info/bfd.info
}
