
pkgname=libmtp
pkgver=1.1.18
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=('x86_64')
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libusb' 'libgcrypt')
options=('!libtool')
source=("https://downloads.sourceforge.net/libmtp/${pkgname}-${pkgver}.tar.gz"
        '69-libmtp.rules')
md5sums=('7915496daa3f4ea3e095f0161f83d4d4'
         'ac75769980e937022e9657777b7a5ac6')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  # systemd errors, clean line in udev rules
  sed -i "/^Unable to open/d" ${pkgdir}/usr/lib/udev/rules.d/69-libmtp.rules
  # android devices failed to use MTP since linux 4.14, use adjusted rules file until proper fix
  #install -D -m644 ${srcdir}/69-libmtp.rules ${pkgdir}/etc/udev/rules.d/69-libmtp.rules
}

