
pkgname=shiboken2
_pkgname=pyside-setup-opensource-src
pkgver=5.15.9
_pkgver=5.15.9
pkgrel=1
pkgdesc="Python binding generator that Qt for Python uses to create the PySide module."
arch=('x86_64')
license=('LGPL')
url="https://wiki.qt.io/Qt_for_Python/Shiboken"
depends=('python3' 'qt5-xmlpatterns' 'libxslt' 'clang')
makedepends=('cmake' 'python3-sphinx')
source=("https://download.qt.io/official_releases/QtForPython/${pkgname}/PySide2-${pkgver}-src/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('2ea5917652036a9007d66ba4dc0aa75f381a3a25ccf0fa70fa2d9e9c8c9dacae')

prepare() {
  cd ${_pkgname}-${_pkgver}

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  sed -i -e 's|import importlib|import importlib.machinery|' sources/shiboken2/libshiboken/embed/signature_bootstrap.py
  sed -i -e 's|#ifndef Py_LIMITED_API|#if PY_VERSION_HEX < 0x03010000|' sources/shiboken2/libshiboken/pep384impl.cpp
}

build() {
  cmake -B build -S ${_pkgname}-${_pkgver}/sources/shiboken2 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

