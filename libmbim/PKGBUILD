
pkgname=libmbim
pkgver=1.14.2
pkgrel=1
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('python2' 'vala')
options=('!libtool')
source=("https://www.freedesktop.org/software/libmbim/${pkgname}-${pkgver}.tar.xz")
md5sums=('40b1543119b65f9a75f264a3a5abf33b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static 
        
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
