
pkgname=libmbim
pkgver=1.30.0
pkgrel=3
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('help2man' 'meson' 'ninja' 'python3' 'vala')
options=('!libtool')
source=("https://gitlab.freedesktop.org/mobile-broadband/libmbim/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('caea8fe12909fa92642936b0ab3c107ad40126c19f953180a320f7402500bd7b')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dintrospection=false \
    -Dgtk_doc=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
