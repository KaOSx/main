
pkgname=libmbim
pkgver=1.26.4
pkgrel=1
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('python3' 'vala')
options=('!libtool')
source=("https://www.freedesktop.org/software/libmbim/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f688cec4c4586a17575f5e327448ce62f2000ef6a07c9e4589873d4a68568ad9')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-introspection=no
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
