
pkgname=libmbim
pkgver=1.20.4
pkgrel=1
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('python3' 'vala')
options=('!libtool')
source=("https://www.freedesktop.org/software/libmbim/${pkgname}-${pkgver}.tar.xz")
md5sums=('d38d637e33cde8a91484f53a9f6c4d49')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static 
        
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
