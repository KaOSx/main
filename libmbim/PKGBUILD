
pkgname=libmbim
pkgver=1.28.4
pkgrel=1
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('help2man' 'meson' 'ninja' 'python3' 'vala')
options=('!libtool')
source=("https://gitlab.freedesktop.org/mobile-broadband/libmbim/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('2b3f3ae3caf7b4b970cfed0c2fc276e9ba8c436d7792b2e9e369d7ba272fa848')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dintrospection=false \
    -Dgtk_doc=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
