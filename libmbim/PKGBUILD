
pkgname=libmbim
pkgver=1.26.2
pkgrel=1
pkgdesc="Glib-based library for talking to WWAN modems and devices which speak the Mobile Interface Broadband Model"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'systemd')
makedepends=('python3' 'vala')
options=('!libtool')
source=("https://www.freedesktop.org/software/libmbim/${pkgname}-${pkgver}.tar.xz")
sha256sums=('10c77bf5b5eb8c92ba80e9b519923ad9b898362bc8e1928e2bc9a17eeba649af')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-introspection=no
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
