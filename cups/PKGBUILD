
pkgbase="cups"
pkgname=('libcups' 'cups')
pkgver=1.7.4
pkgrel=1
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
makedepends=('libtiff' 'libpng' 'acl' 'pam' 'xdg-utils' 'krb5' 'openssl' 'cups-filters' 'bc'
             'xinetd' 'gzip' 'autoconf' 'libusb' 'dbus-core' 'avahi'  'hicolor-icon-theme' 'systemd'
             'colord' 'libpaper')
source=("http://www.cups.org/software/${pkgver}/cups-${pkgver}-source.tar.bz2"
        'cups-no-export-ssllibs.patch'
        'cups-no-gcrypt.patch'
        'cups-no-gzip-man.patch'
        'cups-systemd-socket2.patch'
        'cups.logrotate'
        'cups.pam')
md5sums=('1a2295c2b2d2f422db2e50f40ed2fb99'
         '3ba9e3410df1dc3015463d615ef91b3b'
         '3733c23e77eb503bd94cc368e02830dc'
         '90c30380d4c8cd48a908cfdadae1ea24'
         'd081092aebdfe07aced2466808abf966'
         'ec2fdabb60b430ce1fb643078e9fc7c0'
         '96f82c38f3f540b53f3e5144900acf17')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  # add systemd socket support - Gentoo patch -2 version
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-print/cups/files/cups-1.5.0-systemd-socket-2.patch?revision=1.1
  patch -p1 -i ${srcdir}/cups-systemd-socket2.patch
  
  # Do not export SSL libs in cups-config
  patch -p1 -i ${srcdir}/cups-no-export-ssllibs.patch

  ##patch -p1 -i ${srcdir}/cups-no-gcrypt.patch
  # don't zip man pages in make install, let makepkg do that / Fedora
  patch -p1 -i ${srcdir}/cups-no-gzip-man.patch
  
  # Rebuild configure script for not zipping man-pages.
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --libdir=/usr/lib \
     --with-systemdsystemunitdir=/usr/lib/systemd/system \
     --with-logdir=/var/log/cups \
     --with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon \
     --with-cups-group=lp \
     --enable-pam=yes \
     --enable-raw-printing \
     --enable-dbus \
     --with-dbusdir=/etc/dbus-1 \
     --enable-ssl=yes \
     --enable-openssl \
     --enable-threads \
     --enable-avahi\
     --enable-libpaper \
     --with-php=/usr/bin/php-cgi \
     --with-optim="$CFLAGS"
  make
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"

  make -k check || /bin/true
}

package_libcups() {
pkgdesc="The CUPS Printing System - client libraries and headers"
depends=('gnutls' 'libtiff' 'libpng' 'krb5' 'avahi')
backup=(etc/cups/client.conf)

  cd ${srcdir}/${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-headers install-libs
  # put this into the libs pkg to make other software find the libs(no pkg-config file included)
  mkdir -p ${pkgdir}/usr/bin 
  install -m755 ${srcdir}/${pkgbase}-${pkgver}/cups-config ${pkgdir}/usr/bin/cups-config
  
  # install client.conf man page and config file
  install -dm755 ${pkgdir}/usr/share/man/man5
  #install -Dm644  ${srcdir}/${pkgbase}-${pkgver}/man/client.conf.5.gz ${pkgdir}/usr/share/man/man5/
  install -Dm644  ${srcdir}/${pkgbase}-${pkgver}/man/client.conf.5 ${pkgdir}/usr/share/man/man5/
  install -dm755 ${pkgdir}/etc/cups
  touch ${pkgdir}/etc/cups/client.conf
  echo "# see 'man client.conf'" >> ${pkgdir}/etc/cups/client.conf
  echo "ServerName /var/run/cups/cups.sock #  alternative: ServerName hostname-or-ip-address[:port] of a remote server" >> ${pkgdir}/etc/cups/client.conf
  chgrp lp ${pkgdir}/etc/cups/client.conf
}

package_cups() {
pkgdesc="The CUPS Printing System - daemon package"
install=cups.install
backup=(etc/cups/cupsd.conf
        etc/cups/snmp.conf
        etc/cups/printers.conf
        etc/cups/classes.conf
        etc/cups/cups-files.conf
        etc/cups/subscriptions.conf
        etc/dbus-1/system.d/cups.conf
        etc/logrotate.d/cups
        etc/pam.d/cups)
depends=('acl' 'pam' "libcups>=${pkgver}" 'cups-filters' 'bc' 'colord' 'libusb' 'dbus' 'systemd'
         'hicolor-icon-theme' 'libpaper')
optdepends=('xdg-utils: xdg .desktop file support')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-data install-exec

  # this one we ship in the libcups pkg
  rm -f ${pkgdir}/usr/bin/cups-config

  # kill the sysv stuff
  rm -rf ${pkgdir}/etc/rc*.d
  rm -rf ${pkgdir}/etc/init.d
  install -D -m644 ../cups.logrotate ${pkgdir}/etc/logrotate.d/cups
  install -D -m644 ../cups.pam ${pkgdir}/etc/pam.d/cups
  
  # fix perms on /var/spool and /etc
  chmod 755 ${pkgdir}/var/spool
  chmod 755 ${pkgdir}/etc

  # install ssl directory where to store the certs, solves some samba issues
  install -dm700 -g lp ${pkgdir}/etc/cups/ssl
  # remove directory from package, we create it in cups rc.d file
  rm -rf ${pkgdir}/var/run

  # install some more configuration files that will get filled by cupsd
  touch ${pkgdir}/etc/cups/printers.conf
  touch ${pkgdir}/etc/cups/classes.conf
  touch ${pkgdir}/etc/cups/subscriptions.conf 
  chgrp lp ${pkgdir}/etc/cups/{printers.conf,classes.conf,subscriptions.conf}
  
  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' ${pkgdir}/usr/share/applications/cups.desktop
  
  # compress some driver files, adopted from Fedora
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f
  
  # remove client.conf man page
  rm -f ${pkgdir}/usr/share/man/man5/client.conf.5
  
  # remove files now part of cups-filters
  rm -v ${pkgdir}/usr/share/cups/banners/*
  rm -v ${pkgdir}/usr/share/cups/data/testprint
  # comment out all conversion rules which use any of the removed filters
  #perl -p -i -e 's:^(.*\s+bannertops\s*)$:#\1:' /usr/share/cups/mime/mime.convs

  # Provide native service
  ln -s cups.service "$pkgdir/usr/lib/systemd/system/cupsd.service"
}
