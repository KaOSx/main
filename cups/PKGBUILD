
pkgbase=cups
pkgname=('libcups' 'cups')
pkgver=2.3.3
pkgrel=1
arch=('x86_64')
license=('GPL')
url="https://www.cups.org/"
makedepends=('libtiff' 'libpng' 'acl' 'pam' 'xdg-utils' 'krb5' 'openssl' 'cups-filters' 'bc'
             'xinetd' 'gzip' 'autoconf' 'libusb' 'dbus' 'avahi'  'hicolor-icon-theme' 'systemd'
             'colord' 'libpaper' 'gnutls')
source=("https://github.com/apple/cups/releases/download/v${pkgver}/cups-${pkgver}-source.tar.gz"
        'cups-no-export-ssllibs.patch'
        'cups-no-gcrypt.patch'
        'cups.logrotate'
        'cups.pam')
md5sums=('412434ceefbdf3ec71bc9188a035f589'
         '3ba9e3410df1dc3015463d615ef91b3b'
         '928b4ec806db4170179ad4d1c8074ff9'
         'ec2fdabb60b430ce1fb643078e9fc7c0'
         '96f82c38f3f540b53f3e5144900acf17')

build() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i ${srcdir}/cups-no-export-ssllibs.patch
  
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --libdir=/usr/lib \
     --with-logdir=/var/log/cups \
     --with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon \
     --with-cups-group=lp \
     --enable-pam=yes \
     --enable-raw-printing \
     --enable-dbus \
     --with-dbusdir=/etc/dbus-1 \
     --enable-ssl=yes \
     --enable-threads \
     --enable-avahi\
     --enable-libpaper \
     --with-php=/usr/bin/php-cgi \
     --with-optim="$CFLAGS"
  make
}

check() {
  cd ${pkgbase}-${pkgver}

  make -k check || /bin/true
}

package_libcups() {
pkgdesc="The CUPS Printing System - client libraries and headers"
depends=('gnutls' 'libtiff' 'libpng' 'krb5' 'avahi')
backup=(etc/cups/client.conf)

  cd ${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-headers install-libs
 
  mkdir -p ${pkgdir}/usr/bin 
  install -m755 ${srcdir}/${pkgbase}-${pkgver}/cups-config ${pkgdir}/usr/bin/cups-config
  
  install -dm755 ${pkgdir}/etc/cups
  touch ${pkgdir}/etc/cups/client.conf
  echo "# see 'man client.conf'" >> ${pkgdir}/etc/cups/client.conf
  echo "ServerName /run/cups/cups.sock #  alternative: ServerName hostname-or-ip-address[:port] of a remote server" >> ${pkgdir}/etc/cups/client.conf
  chgrp lp ${pkgdir}/etc/cups/client.conf
}

package_cups() {
pkgdesc="The CUPS Printing System - daemon package"
install=cups.install
backup=(etc/cups/cupsd.conf
        etc/cups/snmp.conf
        etc/cups/printers.conf
        etc/cups/classes.conf
        etc/cups/cups-files.conf
        etc/cups/subscriptions.conf
        etc/dbus-1/system.d/cups.conf
        etc/logrotate.d/cups
        etc/pam.d/cups)
depends=('acl' 'pam' "libcups>=${pkgver}" 'cups-filters' 'bc' 'colord' 'libusb' 'dbus' 'systemd'
         'hicolor-icon-theme' 'libpaper')
optdepends=('xdg-utils: xdg .desktop file support')

  cd ${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-data install-exec

  # part of libcups
  rm -f ${pkgdir}/usr/bin/cups-config

  rm -rf ${pkgdir}/etc/rc*.d
  rm -rf ${pkgdir}/etc/init.d
  
  install -D -m644 ../cups.logrotate ${pkgdir}/etc/logrotate.d/cups
  install -D -m644 ../cups.pam ${pkgdir}/etc/pam.d/cups
  
  chmod 755 ${pkgdir}/var/spool
  chmod 755 ${pkgdir}/etc

  # certs dir
  install -dm700 -g lp ${pkgdir}/etc/cups/ssl
  # created with cupsd 
  rm -rf ${pkgdir}/var/run

  # cupsd conf files
  touch ${pkgdir}/etc/cups/printers.conf
  touch ${pkgdir}/etc/cups/classes.conf
  touch ${pkgdir}/etc/cups/subscriptions.conf 
  chgrp lp ${pkgdir}/etc/cups/{printers.conf,classes.conf,subscriptions.conf}
  
  # .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' ${pkgdir}/usr/share/applications/cups.desktop
  
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f
  
  #rm -f ${pkgdir}/usr/share/man/man5/client.conf.5
  
  # part of cups-filters
  #rm -v ${pkgdir}/usr/share/cups/banners/*
  #rm -v ${pkgdir}/usr/share/cups/data/testprint
}
