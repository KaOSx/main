# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakraproject.org

pkgbase=cups
pkgname=('libcups' 'cups')
pkgver=1.5.3
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cups.org/"
makedepends=('libtiff>=3.9.2-2' 'libpng>=1.4.0'  'acl' 'openslp' 'pam' 'xdg-utils' 'krb5' 'gnutls>=2.8.3' 'poppler>=0.12.3' 'xinetd' 'gzip' 'autoconf' 'php' 'libusb' 'dbus-core' 'avahi'  'hicolor-icon-theme' 'systemd')
source=("ftp://ftp.easysw.com/pub/cups/${pkgver}/${pkgbase}-${pkgver}-source.tar.bz2"
        #cups-avahi-1-config.patch
        #cups-avahi-2-backend.patch
        #cups-avahi-3-timeouts.patch
        #cups-avahi-4-poll.patch
        #cups-avahi-5-services.patch
        cups-no-export-ssllibs.patch
        cups-no-gcrypt.patch
        cups-no-gzip-man.patch
        cups cups.logrotate cups.pam
        cups-systemd-socket.patch)
md5sums=('e1ad15257aa6f162414ea3beae0c5df8'
         #'fe3906f7733f8f63e05daf025d05abf3'
         #'80e2d7d6ffcf125f917dca795e161efd'
         #'eef9b879d9bdfb04c2042f57d07969b4'
         #'9f82b56a2119bfc2ee46a345f6229a2c'
         #'ca64df54a3a2dc53c28e6291e8d53d8f'
         '9b8467a1e51d360096b70e2c3c081e6c'
         '3733c23e77eb503bd94cc368e02830dc'
         'd8fce4246d42186d9b190c1f07f5b911'
         '9657daa21760bb0b5fa3d8b51d5e01a1'
         'f861b18f4446c43918c8643dcbbd7f6d'
         '96f82c38f3f540b53f3e5144900acf17'
         '4d89eef028c012fbb89652bd719f342a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Avahi support in the dnssd backend. patches from upstream/FC RawHide
  #patch -Np1 -i ${srcdir}/cups-avahi-1-config.patch
  #patch -Np1 -i ${srcdir}/cups-avahi-2-backend.patch
  #patch -Np1 -i ${srcdir}/cups-avahi-3-timeouts.patch
  #patch -Np1 -i ${srcdir}/cups-avahi-4-poll.patch
  #patch -Np1 -i ${srcdir}/cups-avahi-5-services.patch

  # add systemd socket support - Fedora patch
  patch -p1 -i ${srcdir}/cups-systemd-socket.patch

  # Do not export SSL libs in cups-config
  patch -p1 -i "${srcdir}/cups-no-export-ssllibs.patch"
  patch -p1 -i "${srcdir}/cups-no-gcrypt.patch"
  
  # Rebuild configure script for --enable-avahi.
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --libdir=/usr/lib \
              --with-logdir=/var/log/cups \
              --with-docdir=/usr/share/cups/doc \
              --with-cups-user=daemon \
              --with-cups-group=lp \
              --enable-pam=yes \
              --disable-ldap \
              --enable-raw-printing \
              --enable-dbus \
              --with-dbusdir=/etc/dbus-1 \
              --enable-ssl=yes \
              --enable-gnutls \
              --enable-threads \
              --disable-avahi \
              --with-php=/usr/bin/php-cgi \
              --with-pdftops=pdftops \
              --with-optim="${CFLAGS}"

  make 
}

package_libcups() {
pkgdesc="The CUPS Printing System - client libraries and headers."
depends=('gnutls>=2.8.3' 'libtiff>=3.9.2-2' 'libpng>=1.4.0' 'krb5' 'avahi')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make BUILDROOT=${pkgdir} install-headers install-libs
  # put this into the libs pkg to make other software find the libs(no pkg-config file included)
  mkdir -p "${pkgdir}/usr/bin"
  install -m755 "${srcdir}/${pkgbase}-${pkgver}/cups-config" "${pkgdir}/usr/bin/cups-config"
}

package_cups() {
pkgdesc="The CUPS Printing System - deamon package"
install=cups.install
backup=(etc/cups/cupsd.conf
        etc/cups/snmp.conf
        etc/cups/printers.conf
        etc/cups/classes.conf
        etc/cups/client.conf
        etc/cups/subscriptions.conf
        etc/dbus-1/system.d/cups.conf
        etc/logrotate.d/cups
        etc/pam.d/cups
        etc/xinetd.d/cups-lpd)
depends=('acl' 'openslp' 'pam' "libcups>=${pkgver}" 'xdg-utils' 'poppler>=0.12.3' 'dbus-core' 'hicolor-icon-theme')
optdepends=('php: for included phpcups.so module')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make BUILDROOT="${pkgdir}" install-data install-exec 

  # this one we ship in the libcups pkg
  rm -f "${pkgdir}/usr/bin/cups-config"

  # kill the sysv stuff
  rm -rf "${pkgdir}"/etc/rc*.d
  rm -rf "${pkgdir}"/etc/init.d
  install -D -m755 ../cups "${pkgdir}/etc/rc.d/cups"
  install -D -m644 ../cups.logrotate "${pkgdir}/etc/logrotate.d/cups"
  install -D -m644 ../cups.pam "${pkgdir}/etc/pam.d/cups"
  
  # fix perms on /var/spool and /etc
  chmod 755 "${pkgdir}/var/spool"
  chmod 755 "${pkgdir}/etc"
  
  # serial backend needs to run as root (http://bugs.archlinux.org/task/20396)
  chmod 700 "${pkgdir}/usr/lib/cups/backend/serial"

  # install ssl directory where to store the certs, solves some samba issues
  install -dm700 -g lp "${pkgdir}/etc/cups/ssl"
  install -dm511 -g lp "${pkgdir}/var/run/cups/certs"

  # install some more configuration files that will get filled by cupsd
  touch "${pkgdir}/etc/cups/printers.conf"
  touch "${pkgdir}/etc/cups/classes.conf"
  touch "${pkgdir}/etc/cups/client.conf"
  echo "# see 'man client.conf'" >> "${pkgdir}/etc/cups/client.conf"
  echo "ServerName /var/run/cups/cups.sock #  alternative: ServerName hostname-or-ip-address[:port] of a remote server" >> "${pkgdir}/etc/cups/client.conf"
  touch "${pkgdir}/etc/cups/subscriptions.conf"
  chgrp lp "${pkgdir}"/etc/cups/{printers.conf,classes.conf,client.conf,subscriptions.conf}
  
  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' "${pkgdir}/usr/share/applications/cups.desktop"
  
  # compress some driver files, adopted from Fedora
  find "${pkgdir}/usr/share/cups/model" -name "*.ppd" | xargs gzip -n9f
}

# vim:set ts=2 sw=2 et:
