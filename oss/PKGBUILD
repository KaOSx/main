#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>


pkgname=oss
pkgver=4.2_2006
pkgrel=1
pkgdesc="Open Sound System UNIX audio architecture"
arch=('i686' 'x86_64')
url="http://developer.opensound.com/"
license=('GPL2')
depends=('gcc' 'make' 'linux-headers' 'module-init-tools' 'libtool' 'sed')
makedepends=('pkgconfig' 'gawk' 'gtk2' 'findutils')
optdepends=('gtk2: for graphical mixer (ossxmix)')
conflicts=('oss-linux' 'oss-linux-free' 'oss-testing' 'libflashsupport' 'libflashsupport-pulse' 'libflashsupport-oss')
replaces=('libflashsupport-oss')
install="oss.install"
backup=('usr/lib/oss/soundon.user')
source=("http://www.4front-tech.com/developer/sources/stable/gpl/oss-v${pkgver/_*}-build${pkgver/*_}-src-gpl.tar.bz2"
        'oss.rm-init-scripts.patch' 'oss.rc' 'oss.soundon.patch' 'remove-hal.patch' 'oss.service')
md5sums=('ca25c06bb7d0c6495e1b6f55d1bad96c'
         'b9a380a0ac8896390d71ac13676f27e1'
         '8ca7cdf94c56ab02890eb4aba6a4995f'
         '65f07fe241bfbf912f76d8b6d8f276b5'
         'cd7f1dc6166bba8c94d96f3a28e948a5'
         '8db0255b380dc6672993e627d1cd8ca6')

build() {
  _dir="oss-v${pkgver/_*}-build${pkgver/*_}-src-gpl"
  cd "${srcdir}/${_dir}"

  # Avoid these flags conflicting with OSS build system.
  unset CFLAGS
  unset LDFLAGS
  unset OSFLAGS
  unset LIBRARIES
  export NO_WARNING_CHECKS=yes

  # Compile libflashsupport.so only in packaging time, so we avoid
  # conflicts with other packages and ease package management.
  msg "Building libflashsupport.so."
  cd oss/lib
  gcc -shared -fPIC -O2 -Wall -Werror flashsupport.c -o libflashsupport.so 

  msg "Preparing the build environment."
  cd "${srcdir}"

  # Create build directory and configure
  rm -rf build
  mkdir build && cd build
  "${srcdir}/${_dir}/configure" --enable-libsalsa=NO --regparm

  msg "Building OSS."
  make build

  msg "Patching init scripts."
  cd "${srcdir}/build/prototype"
  rm usr/lib/oss/etc/S89oss
  patch -b -p0 -i "${srcdir}/oss.rm-init-scripts.patch"
  patch -b -p0 -i "${srcdir}/oss.soundon.patch"
        # remove hal dependency
  patch -p0 -i "${srcdir}/remove-hal.patch"
  rm -rf usr/lib/oss/scripts/oss_usb-create-devices usr/lib/oss/scripts/90-oss_usb-create-device.fdi
}

package() {
  _dir="oss-v${pkgver/_*}-build${pkgver/*_}-src-gpl"
  cd "${srcdir}/${_dir}"

  msg "Copying files."

  # Install libflashsupport.so
  install -D -m755 oss/lib/libflashsupport.so \
    "${pkgdir}/usr/lib/libflashsupport.so"
  rm -f oss/lib/{flashsupport.c,libflashsupport.so}

  cd "${srcdir}/build/prototype"
  cp -a * "${pkgdir}"
  chmod -R a+r "${pkgdir}" # All files can have read permission (FS#13815)
  find "${pkgdir}" -type d -exec chmod a+x \{\} \; # Make namcap happy
  install -D -m755 "${srcdir}/oss.rc" "${pkgdir}/etc/rc.d/oss"

  install -Dm644 "$srcdir/oss.service" "$pkgdir/usr/lib/systemd/system/oss.service"
}

# vim:set ts=2 sw=2 et:
