
pkgname=python3-tomli
_pkgname=tomli
pkgver=2.1.0
pkgrel=1
pkgdesc="A lil' TOML parser."
arch=('x86_64')
url='https://github.com/hukkin/tomli'
license=('MIT')
depends=('python3')
makedepends=('python3-build' 'python3-flit-core' 'python3-installer' 'python3-toml')
source=("https://files.pythonhosted.org/packages/source/t/tomli/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('3f646cae2aec94e17d04973e4249548320197cfabdf130015d023de4b74d8ab8')

build() {
  cd ${_pkgname}-${pkgver}

  python3 -m build -nw
}

package() {
  cd ${_pkgname}-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}


