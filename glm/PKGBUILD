
pkgname=glm
pkgver=0.9.8.3
pkgrel=1
pkgdesc="Header only C++ mathematics library for graphics software based on the OpenGL Shading Language (GLSL) specifications"
arch=('x86_64')
license=('MIT')
url="http://glm.g-truc.net"
source=("https://github.com/g-truc/glm/releases/download/${pkgver}/glm-${pkgver}.zip")
md5sums=('8f1c44a6486f0060bc7f7e3e32b4d169')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/glm
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../glm/copying.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

