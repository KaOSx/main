# Maintainer : Anke Boersma <abveritas@chakra-project.org>

pkgname=farstream
pkgver=0.1.2
pkgrel=2
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL2.1')
depends=('libnice' 'gstreamer0.10-base' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-good' 'gstreamer0.10-ffmpeg')
makedepends=('gobject-introspection' 'gstreamer0.10-python' 'pygobject')
conflicts=('farsight2')
replaces=('farsight2')
provides=('farsight2')
options=(!libtool)
source=(http://freedesktop.org/software/$pkgname/releases/$pkgname/$pkgname-$pkgver.tar.gz
        vp8.patch)
md5sums=('5d6e561b3688d0d0c8906fec4f356df3'
         'afc478a071ed347e1e1ab39aced14cdb')

build() {
  cd  "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/vp8.patch"
  ./configure --prefix=/usr \
    --with-package-name='Chakra-Project farstream package' \
    --with-package-origin='http://www.chakra-project.org' \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
