
pkgname=python3-pyelftools
pkgver=0.31
pkgrel=2
pkgdesc="Python library for parsing and analyzing ELF files and DWARF debugging information."
arch=('x86_64')
license=('custom:Public Domain')
url="https://github.com/eliben/pyelftools"
depends=('python3')
makedepends=('python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://files.pythonhosted.org/packages/source/p/pyelftools/pyelftools-${pkgver}.tar.gz")
sha256sums=('c774416b10310156879443b81187d182d8d9ee499660380e645918b50bc88f99')

build() {
    cd pyelftools-${pkgver}

    python3 -m build -nw
}

package() {
    cd pyelftools-${pkgver}

    python3 -m installer --destdir=${pkgdir} dist/*.whl
    install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
