
pkgname=hdf5
pkgver=1.10.4
_patchver=1.10.4
_pkgver=1.10
pkgrel=1
arch=('x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="https://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh')
options=('!libtool')
source=("https://www.hdfgroup.org/ftp/HDF5/releases/hdf5-${_pkgver}/hdf5-${_patchver}/src/${pkgname}-${_patchver}.tar.bz2")
md5sums=('886148d0cc9ffd3c8e1fce0bd75ed07b')

build() {
  cd ${pkgname}-${_patchver}
  
  ./configure --prefix=/usr \
    --disable-static \
    --enable-hl \
    --enable-build-mode=production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --with-pthread=/usr/lib/ \
    --disable-sharedlib-rpath
  make 
}

package() {
  cd ${pkgname}-${_patchver}

  make -j1 prefix=${pkgdir}/usr install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE 
}

