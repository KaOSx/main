
pkgname=hdf5
pkgver=1.14.5
_pkgver=1.14-5
pkgrel=1
arch=('x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="https://www.hdfgroup.org/solutions/hdf5/"
license=('custom')
depends=('zlib' 'sh')
makedepends=('cmake')
options=('!libtool')
#source=("https://www.hdfgroup.org/ftp/HDF5/releases/hdf5-${_pkgver}/hdf5-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
source=("https://github.com/HDFGroup/hdf5/releases/download/hdf5_${pkgver}/hdf5-${pkgver}.tar.gz")
sha256sums=('ec2e13c52e60f9a01491bb3158cb3778c985697131fc6a342262d32a26e58e44')

prepare() {
  cd ${pkgname}-${pkgver}

  #touch release_docs/INSTALL_VMS.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DONLY_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DHDF5_INSTALL_LIB_DIR=lib \
    -DHDF5_BUILD_HL_LIB=ON \
    -DHDF5_BUILD_CPP_LIB=OFF \
    -DHDF5_BUILD_FORTRAN=OFF \
    -DHDF5_ENABLE_Z_LIB_SUPPORT=ON \
    -DHDF5_ENABLE_SZIP_SUPPORT=OFF \
    -DHDF5_ENABLE_SZIP_ENCODING=OFF \
    -DHDF5_ENABLE_PARALLEL=OFF
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  mv ${pkgdir}/usr/share/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm ${pkgdir}/usr/share/RELEASE.txt
  rm ${pkgdir}/usr/share/USING_HDF5_CMake.txt
}

