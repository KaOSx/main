
pkgname=hdf5
pkgver=1.12.1
_pkgver=1.12
pkgrel=1
arch=('x86_64')
pkgdesc="General purpose library and file format for storing scientific data"
url="https://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh')
makedepends=('cmake')
options=('!libtool')
source=("https://www.hdfgroup.org/ftp/HDF5/releases/hdf5-${_pkgver}/hdf5-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('aaf9f532b3eda83d3d3adc9f8b40a9b763152218fa45349c3bc77502ca1f8f1c')

prepare() {
  cd ${pkgname}-${pkgver}

  #touch release_docs/INSTALL_VMS.txt
}

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DONLY_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DHDF5_INSTALL_LIB_DIR=lib \
    -DHDF5_BUILD_HL_LIB=ON \
    -DHDF5_BUILD_CPP_LIB=OFF \
    -DHDF5_BUILD_FORTRAN=OFF \
    -DHDF5_ENABLE_Z_LIB_SUPPORT=ON \
    -DHDF5_ENABLE_SZIP_SUPPORT=OFF \
    -DHDF5_ENABLE_SZIP_ENCODING=OFF \
    -DHDF5_ENABLE_PARALLEL=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  mv ${pkgdir}/usr/share/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm ${pkgdir}/usr/share/RELEASE.txt
  rm ${pkgdir}/usr/share/USING_HDF5_CMake.txt
}

