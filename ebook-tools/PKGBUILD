
pkgname=ebook-tools
pkgver=0.2.2
pkgrel=7
pkgdesc="Tools for accessing and converting various ebook file formats"
arch=('x86_64')
url="https://sourceforge.net/projects/ebook-tools"
license=('custom')
depends=('libzip' 'libxml2')
makedepends=('pkgconfig' 'cmake' 'doxygen')
source=("https://downloads.sourceforge.net/sourceforge/ebook-tools/${pkgname}-${pkgver}.tar.gz"
        'navpoint.patch')
md5sums=('67bce67ceb72dcc3578d6a81ef92b29b'
         '845f07ffb5611c20822ae29ae1742a55')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/navpoint.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
