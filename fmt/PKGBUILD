
pkgname=fmt
pkgver=10.2.0
pkgrel=1
pkgdesc='Open-source formatting library providing an alternative to C stdio and C++ iostreams.'
arch=('x86_64')
url="https://fmt.dev/latest/index.html"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake' 'python3-pip')
source=("https://github.com/fmtlib/fmt/releases/download/${pkgver}/${pkgname}-${pkgver}.zip")
md5sums=('947394bc2a86d39556158f84572e0851')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON \
    -DFMT_DOC=OFF \
    -DFMT_TEST=OFF
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
  install -Dm 644 ${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/fmt
}
