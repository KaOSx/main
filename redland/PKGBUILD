
pkgname=redland
pkgver=1.0.17
pkgrel=7
url="http://librdf.org/"
license=("GPL")
arch=('x86_64')
pkgdesc="Library that provides a high-level interface to RDF data"
depends=('rasqal' 'raptor' 'libtool' 'postgresql-libs' 'mariadb' 'sqlite3' 'unixodbc')
options=('!libtool')
conflicts=('redland-storage-mysql'
           'redland-storage-postgresql'
           'redland-storage-virtuoso'
           'redland-storage-sqlite')
provides=('redland-storage-mysql'
          'redland-storage-postgresql'
          'redland-storage-virtuoso'
          'redland-storage-sqlite')
replaces=('redland-storage-mysql'
          'redland-storage-postgresql'
          'redland-storage-virtuoso'
          'redland-storage-sqlite')
source=("http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz"
        'rpath.diff')
md5sums=('e5be03eda13ef68aabab6e42aa67715e'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --enable-release \
    --disable-static \
    --disable-gtk-doc \
    --with-raptor=system \
    --with-rasqal=system \
    --with-sqlite=3 \
    --with-bdb=no

  patch -p0 -i ${srcdir}/rpath.diff

  make
}

package() { 
  cd ${pkgbase}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

