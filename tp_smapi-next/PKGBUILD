
pkgname=tp_smapi-next
_pkgname=tp_smapi
pkgver=0.42
pkgrel=78
_extramodules=/lib/modules/extramodules-4.14-next
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=('linux-next>=4.14' 'linux-next<4.15')
makedepends=('linux-next-headers')
install="${pkgname}.install"
source=("https://github.com/evgeni/tp_smapi/releases/download/tp-smapi%2F${pkgver}/${_pkgname}-${pkgver}.tgz")
md5sums=('9796e92c86a53dd84823ca503a69c49e')

build() {
  cd ${_pkgname}-${pkgver}

 _kernver=$(< "${_extramodules}/version")

  sed -ri -e "s/^(KVER\s*:=).*$/\1 ${_kernver}/" \
	  -e "s#^(KBASE\s*:=).*\$#\1 ${_extramodules%/*}/${_kernver}#" Makefile

  make HDAPS=1
}

package() {
  _kernver=$(< "${_extramodules}/version")

  make -C "${_extramodules%/*}/${_kernver}/build" \
    INSTALL_MOD_PATH="${pkgdir}/${_extramodules%%/lib/*}" \
    M="${srcdir}/${_pkgname}-${pkgver}" modules_install

  cd ${pkgdir}/${_extramodules%/*}
  mv ${_kernver}/extra ${_extramodules##*/}
  rmdir ${_kernver}

  # compress kernel modules
  find ${pkgdir} -name "*.ko" -exec gzip -9 {} +

  # load module on startup
  echo tp_smapi > ${srcdir}/${pkgname}.conf
  install -Dm644 ${srcdir}/${pkgname}.conf ${pkgdir}/lib/modules-load.d/${pkgname}.conf

  # update kernel version in install file
  sed -ri "s|^(extramodules=).*\$|\1${_extramodules}|" ${startdir}/${pkgname}.install
}
