
pkgname=tp_smapi-next
_pkgname=tp_smapi
pkgver=0.43
pkgrel=398
_extramodules=/lib/modules/extramodules-6.4-next
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=('linux-next>=6.4' 'linux-next<6.5.0')
makedepends=('linux-next-headers' 'elfutils')
install="${pkgname}.install"
source=("https://github.com/evgeni/tp_smapi/releases/download/tp-smapi%2F${pkgver}/${_pkgname}-${pkgver}.tgz"
        "https://github.com/evgeni/tp_smapi/commit/0c3398b1acf2a2cabd9cee91dc3fe3d35805fa8b.diff")
md5sums=('c0f48a68aa83826220e965491bd32d17'
         'ed7f95a89f16fc56712268bb772db968')

build() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0c3398b1acf2a2cabd9cee91dc3fe3d35805fa8b.diff

 _kernver=$(< "${_extramodules}/version")

  sed -i -e 's|$(shell uname -r)|6.4.3-1-next|' Makefile

  make HDAPS=1
}

package() {
  _kernver=$(< "${_extramodules}/version")

  make -C "${_extramodules%/*}/${_kernver}/build" \
    INSTALL_MOD_PATH="${pkgdir}/${_extramodules%%/lib/*}" \
    M="${srcdir}/${_pkgname}-${pkgver}" modules_install

  cd ${pkgdir}/${_extramodules%/*}
  mv ${_kernver}/updates ${_extramodules##*/}
  rmdir ${_kernver}

  # compress kernel modules
  find ${pkgdir} -name "*.ko" -exec gzip -9 {} +

  # load module on startup
  echo tp_smapi > ${srcdir}/${pkgname}.conf
  install -Dm644 ${srcdir}/${pkgname}.conf ${pkgdir}/lib/modules-load.d/${pkgname}.conf

  # update kernel version in install file
  sed -ri "s|^(extramodules=).*\$|\1${_extramodules}|" ${startdir}/${pkgname}.install
}
