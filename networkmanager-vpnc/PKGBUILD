
pkgname=networkmanager-vpnc
pkgver=1.2.8
_pkgver=1.2
pkgrel=1
pkgdesc="NetworkManager VPN plugin for vpnc"
arch=('x86_64')
license=('GPL')
url="https://wiki.gnome.org/Projects/NetworkManager/VPN"
depends=('networkmanager' 'vpnc')
makedepends=('intltool') 
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/${_pkgver}/NetworkManager-vpnc-${pkgver}.tar.xz"
        'gnome.patch')
sha256sums=('d65e31aa53c8fdc3fde7ef84a4daa979a605c23fd31ceff647bf7eaaa99afbcc'
            '58c701c27b22640d95c3e014d7e399be7ade160f2e7bb013f114c3151d8f1324')

build() {
  cd NetworkManager-vpnc-${pkgver}
  patch -p1 -i ${srcdir}/gnome.patch

  autoreconf -vi
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--localstatedir=/var \
    --without-gnome \
    --without-gtk4 \
	--disable-static \
	--disable-more-warnings \
	--with-dist-version=KaOS

  make
}

package() {
  cd NetworkManager-vpnc-${pkgver}

  make DESTDIR=${pkgdir} install 
}
