
pkgname=recode
_pkgname=Recode
pkgver=3.6
pkgrel=8
pkgdesc="Converts files between various character sets and usages"
arch=('x86_64')
url="http://www.gnu.org/software/recode/recode.html"
license=('GPL' 'LGPL')
depends=('glibc' 'texinfo')
options=('!libtool')
source=("https://github.com/pinard/Reco/archive/v${pkgver}.tar.gz"
        # https://gitweb.gentoo.org/repo/gentoo.git/plain/app-text/recode/files/
        'recode-gentoo.patch'
        "http://archive.debian.org/debian/pool/main/r/recode/recode_3.6-17.diff.gz")
md5sums=('977d5e836dc6540b120d00ecdf14d25d'
         '2b35d5d757f57a6927fe0c673134bcbb'
         '3716ae269074e31add3d29387ff09438')

build() {
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/recode-gentoo.patch
  patch -p1 -i ${srcdir}/recode_3.6-17.diff
  sed -i '1i#include <stdlib.h>' src/argmatch.c
  
  rm -f acinclude.m4
  autoreconf -fi
  libtoolize
  
  ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --infodir=/usr/share/info 
  make 
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 
  
  rm ${pkgdir}/usr/share/info/dir
  gzip ${pkgdir}/usr/share/info/*
}
