
pkgname=openslp
pkgver=2.0.0
pkgrel=4
pkgdesc="Open-source implementation of Service Location Protocol"
arch=('x86_64')
url="http://www.openslp.org"
license=('BSD')
depends=('glibc' 'bash' 'openssl')
backup=('etc/slp.conf' 'etc/slp.reg' 'etc/slp.spi')
options=('!libtool')
source=("https://downloads.sourceforge.net/sourceforge/openslp/${pkgname}-${pkgver}.tar.gz"
        'openslp.service')
md5sums=('18cf7940bcc444e32592cf34e84f833f'
         '296dc3c8f75e7a1823fcb9dd97ea0971')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --disable-static \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-slpv1 \
    --enable-slpv2-security
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 ../openslp.service ${pkgdir}/usr/lib/systemd/system/openslp.service
}
