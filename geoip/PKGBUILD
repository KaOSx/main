
pkgname=geoip
pkgver=1.6.12
pkgrel=2
pkgdesc="Non-DNS IP-to-country resolver C library & utils"
arch=('x86_64')
license=('GPL')
url="https://www.maxmind.com/app/c"
depends=('zlib' 'geoip-database')
backup=('etc/geoip/GeoIP.conf')
options=('!libtool' '!emptydirs')
#source=("http://www.maxmind.com/download/geoip/api/c/GeoIP-${pkgver}.tar.gz")
source=("https://github.com/maxmind/geoip-api-c/releases/download/v${pkgver}/GeoIP-${pkgver}.tar.gz")
md5sums=('77d496cc40daa1dbc2b97365807d64d7')

build() {
  cd GeoIP-${pkgver}

  #autoreconf -vi
  ./bootstrap
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc/geoip

  make
}

check() {
  cd GeoIP-${pkgver}

  make check
}

package() {
  cd GeoIP-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

