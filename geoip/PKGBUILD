
pkgname=geoip
pkgver=1.6.10
pkgrel=1
pkgdesc="Non-DNS IP-to-country resolver C library & utils"
arch=('x86_64')
license=('GPL')
url="http://www.maxmind.com/app/c"
depends=('zlib' 'geoip-database')
backup=('etc/geoip/GeoIP.conf')
options=('!libtool' '!emptydirs')
#source=("http://www.maxmind.com/download/geoip/api/c/GeoIP-${pkgver}.tar.gz")
source=("https://github.com/maxmind/geoip-api-c/releases/download/v${pkgver}/GeoIP-${pkgver}.tar.gz")
md5sums=('d95f82e3d93c72c88f7f57b435577c27')

build() {
  cd GeoIP-${pkgver}

  #autoreconf -vi
  ./bootstrap
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc/geoip

  make
}

check() {
  cd GeoIP-${pkgver}

  make check
}

package() {
  cd GeoIP-${pkgver}
  
  make DESTDIR=${pkgdir} install
}

