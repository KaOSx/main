
pkgname=gdk-pixbuf2
pkgver=2.42.10
_pkgver=2.42
pkgrel=4
pkgdesc="An image loading library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/gdk-pixbuf"
license=('LGPL')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg-turbo')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
install=gdk-pixbuf2.install
source=("https://download.gnome.org/sources/gdk-pixbuf/${_pkgver}/gdk-pixbuf-${pkgver}.tar.xz"
        'gdk-pixbuf-query-loaders.hook')
sha256sums=('ee9b6c75d13ba096907a2e3c6b27b61bcd17f5c7ebeab5a5b439d2f2e39fe44b'
            '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

build() {
  mkdir -p build
  cd build

  meson setup ../gdk-pixbuf-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D installed_tests=false \
    -D gtk_doc=false \
    -D man=false \
    -D tests=false \
    -D installed_tests=false
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../gdk-pixbuf-query-loaders.hook ${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}
