
pkgname=gdk-pixbuf2
pkgver=2.42.8
_pkgver=2.42
pkgrel=3
pkgdesc="An image loading library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/gdk-pixbuf"
license=('LGPL')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg-turbo')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
install=gdk-pixbuf2.install
source=("https://download.gnome.org/sources/gdk-pixbuf/${_pkgver}/gdk-pixbuf-${pkgver}.tar.xz"
        'gdk-pixbuf-query-loaders.hook')
sha256sums=('84acea3acb2411b29134b32015a5b1aaa62844b19c4b1ef8b8971c6b0759f4c6'
            '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

build() {
  mkdir -p build
  cd build

  meson ../gdk-pixbuf-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D installed_tests=false
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../gdk-pixbuf-query-loaders.hook ${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}
