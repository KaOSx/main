
pkgname=gdk-pixbuf2
pkgver=2.42.12
_pkgver=2.42
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/gdk-pixbuf"
license=('LGPL')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg-turbo')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
install=gdk-pixbuf2.install
source=("https://download.gnome.org/sources/gdk-pixbuf/${_pkgver}/gdk-pixbuf-${pkgver}.tar.xz"
        'gdk-pixbuf-query-loaders.hook')
sha256sums=('b9505b3445b9a7e48ced34760c3bcb73e966df3ac94c95a148cb669ab748e3c7'
            '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

build() {
  mkdir -p build
  cd build

  meson setup ../gdk-pixbuf-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D installed_tests=false \
    -D gtk_doc=false \
    -D man=false \
    -D tests=false \
    -D installed_tests=false
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -Dm644 ../gdk-pixbuf-query-loaders.hook ${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}
