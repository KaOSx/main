
pkgname=gdk-pixbuf2
pkgver=2.38.0
_pkgver=2.38
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg-turbo' 'libx11')
makedepends=('gobject-introspection' 'meson' 'ninja')
options=('!libtool')
install=gdk-pixbuf2.install
source=("https://download.gnome.org/sources/gdk-pixbuf/${_pkgver}/gdk-pixbuf-${pkgver}.tar.xz"
        'gdk-pixbuf-query-loaders.hook')
sha256sums=('dd50973c7757bcde15de6bcd3a6d462a445efd552604ae6435a0532fbbadae47'
            '963afcfacde9fd36031d93c8aa4c94a669ab1dbe9ba0d2cf17599b7d358e54c2')

build() {
  mkdir -p build
  cd build
  
  meson ../gdk-pixbuf-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -D installed_tests=false
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  install -Dm644 ../gdk-pixbuf-query-loaders.hook ${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}
