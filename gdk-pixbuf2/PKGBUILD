
pkgname=gdk-pixbuf2
pkgver=2.36.9
_pkgver=2.36
pkgrel=2
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg-turbo' 'libx11')
makedepends=('gobject-introspection')
options=('!libtool')
install=gdk-pixbuf2.install
source=("https://download.gnome.org/sources/gdk-pixbuf/${_pkgver}/gdk-pixbuf-${pkgver}.tar.xz"
        'gdk-pixbuf-query-loaders.hook')
sha256sums=('8013b271ff1a691514b5bbc9b99f6ed456422d4da4a721a9db0b783abe8e740a'
            '963afcfacde9fd36031d93c8aa4c94a669ab1dbe9ba0d2cf17599b7d358e54c2')

build() {
  cd gdk-pixbuf-${pkgver}

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../gdk-pixbuf-query-loaders.hook ${pkgdir}/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook
}
