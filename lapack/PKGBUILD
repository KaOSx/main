
pkgname=lapack
pkgver=3.7.0
pkgrel=1
arch=('x86_64')
url="http://www.netlib.org/lapack"
pkgdesc="Linear Algebra PACKage"
depends=('gcc-libs')
makedepends=('gcc' 'cmake')
conflicts=('blas' 'atlas-lapack')
replaces=('blas' 'atlas-lapack')
provides=('blas')
license=("custom")
source=("http://www.netlib.org/lapack/${pkgname}-${pkgver}.tgz")
md5sums=('697bb8d67c7d336a0f339cc9dd0fa72f')

build() {
  cd ${srcdir}

  install -d build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE=ON 
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
  
  # install the test bin options
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 ${srcdir}/build/bin/* ${pkgdir}/usr/bin
  
  install -m755 -d ${pkgdir}/usr/share/licenses/lapack
  install -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE  ${pkgdir}/usr/share/licenses/lapack/
}

