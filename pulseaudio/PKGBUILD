
pkgname=pulseaudio
pkgdesc="A featureful, general-purpose sound server"
pkgver=17.0
_commit=613c2994af42ff08fe0a9d0e4bfce42ef781e8f6
pkgrel=1
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=('GPL' 'LGPL')
depends=('dbus' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm' 'libsndfile' 
         'rtkit' 'speex' 'tdb' 'fftw' 'orc' 'systemd'
         'sbc' 'libsoxr' 'openssl' 'bluez' 'gst-plugins-base') # 'webrtc-audio-processing'
makedepends=('attr' 'libltdl' 'avahi' 'intltool' 'jack2' 'pkgconfig' 'sh' 'meson'
             'ninja' 'check')
optdepends=('avahi: zeroconf support'
            'jack2: jack support')
backup=('etc/pulse/{daemon.conf,default.pa,system.pa}' 'etc/asound.conf')
options=('!emptydirs' '!libtool' '!makeflags')
source=("https://freedesktop.org/software/pulseaudio/releases/${pkgname}-${pkgver}.tar.xz"
        'asound.conf')
#source=("https://github.com/pulseaudio/pulseaudio/archive/${_commit}.zip")
sha256sums=('053794d6671a3e397d849e478a80b82a63cb9d8ca296bd35b73317bb5ceb87b5'
            '65166739e5b799da225ad8ae057f5958637afb9dbec15d28be7a5ea54fb8db25')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
      --prefix=/usr \
      --buildtype=release \
      --libexecdir=/usr/lib \
      -Dudevrulesdir=/usr/lib/udev/rules.d \
      -Dtests=false \
      -Dgstreamer=enabled \
      -Ddoxygen=false
  ninja    
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  # Disable cork-request module
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
    -i ${pkgdir}/usr/bin/start-pulseaudio-x11

  #rm ${pkgdir}/etc/xdg/autostart/pulseaudio.desktop

  # fix no sound after logout https://github.com/KaOSx/apps/issues/112
  sed -e 's|load-module module-esound-protocol-unix|##load-module module-esound-protocol-unix|' \
    -i ${pkgdir}/etc/pulse/default.pa

  # former pulseaudio-alsa, integrated into pulseaudio
  install -Dm644 ../asound.conf ${pkgdir}/etc/asound.conf
}
