
pkgname=pulseaudio
pkgdesc="A featureful, general-purpose sound server"
pkgver=7.99.2
pkgrel=1
arch=('x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('dbus' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm'
         'json-c' 'rtkit' 'libltdl' 'speex' 'tdb'
         'fftw' 'orc' 'webrtc-audio-processing' 'systemd' 'sbc' 'libsoxr')
makedepends=('attr' 'libltdl' 'avahi' 'bluez' 'intltool' 'jack' 'openssl'
             'libsndfile' 'pkgconfig' 'sh')
optdepends=('avahi: zeroconf support'
            'bluez: bluetooth support'
            'jack: jack support'
            'openssl: RAOP support')
backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
provides=("libpulse=${pkgver}")
options=('!emptydirs' '!libtool' '!makeflags')
source=("http://freedesktop.org/software/pulseaudio/releases/${pkgname}-${pkgver}.tar.xz")
#source=("$pkgname-$pkgver::git://anongit.freedesktop.org/pulseaudio/pulseaudio#commit=50186d7")
md5sums=('0028dea2cc88be8e874b63e9d1f4bd17')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib \
               --localstatedir=/var \
               --with-udev-rules-dir=/usr/lib/udev/rules.d \
               --with-database=tdb \
               --disable-rpath \
               --disable-bluez4 \
               --disable-default-build-tests

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Disable cork-request module
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "${pkgdir}/usr/bin/start-pulseaudio-x11"

  rm "${pkgdir}/etc/dbus-1/system.d/pulseaudio-system.conf"
  #rm "${pkgdir}/etc/xdg/autostart/pulseaudio.desktop"
}
