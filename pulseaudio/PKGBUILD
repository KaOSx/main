
pkgname=pulseaudio
pkgdesc="A featureful, general-purpose sound server"
pkgver=13.0
_commit=613c2994af42ff08fe0a9d0e4bfce42ef781e8f6
pkgrel=3
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=('GPL' 'LGPL')
depends=('dbus' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm' 'libsndfile' 
         'rtkit' 'speex' 'tdb' 'fftw' 'orc' 'webrtc-audio-processing' 'systemd'
         'sbc' 'libsoxr' 'openssl' 'bluez')
makedepends=('attr' 'libltdl' 'avahi' 'intltool' 'jack'
             'pkgconfig' 'sh' 'meson' 'ninja' 'check')
optdepends=('avahi: zeroconf support'
            'jack: jack support')
provides=('pulseaudio-alsa')
conflicts=('pulseaudio-alsa')
replaces=('pulseaudio-alsa')
backup=('etc/pulse/{daemon.conf,default.pa,system.pa}' 'etc/asound.conf')
options=('!emptydirs' '!libtool' '!makeflags')
source=("https://freedesktop.org/software/pulseaudio/releases/${pkgname}-${pkgver}.tar.xz"
        'asound.conf')
#source=("https://github.com/pulseaudio/pulseaudio/archive/${_commit}.zip")
sha256sums=('961b23ca1acfd28f2bc87414c27bb40e12436efcf2158d29721b1e89f3f28057'
            '65166739e5b799da225ad8ae057f5958637afb9dbec15d28be7a5ea54fb8db25')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
      --prefix=/usr \
      --buildtype=release \
      --libexecdir=/usr/lib \
      -Dudevrulesdir=/usr/lib/udev/rules.d
  ninja    
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  # Disable cork-request module
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
    -i ${pkgdir}/usr/bin/start-pulseaudio-x11

  rm ${pkgdir}/etc/dbus-1/system.d/pulseaudio-system.conf
  #rm ${pkgdir}/etc/xdg/autostart/pulseaudio.desktop
  
  # fix no sound after logout https://github.com/KaOSx/apps/issues/112
  sed -e 's|load-module module-esound-protocol-unix|##load-module module-esound-protocol-unix|' \
    -i ${pkgdir}/etc/pulse/default.pa
    
  # former pulseaudio-alsa, integrated into pulseaudio
  install -Dm644 ../asound.conf ${pkgdir}/etc/asound.conf
}
