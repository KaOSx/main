
pkgname=pulseaudio
pkgdesc="A featureful, general-purpose sound server"
pkgver=11.0
pkgrel=1
arch=('x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('dbus' 'xcb-util' 'libasyncns' 'libcap' 'libxtst' 'libsm'
         'libsndfile' 'rtkit' 'libltdl' 'speex' 'tdb'
         'fftw' 'orc' 'webrtc-audio-processing' 'systemd' 'sbc' 'libsoxr')
makedepends=('attr' 'libltdl' 'avahi' 'bluez' 'intltool' 'jack' 'openssl'
             'pkgconfig' 'sh')
optdepends=('avahi: zeroconf support'
            'bluez: bluetooth support'
            'jack: jack support'
            'openssl: RAOP support')
backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
provides=("libpulse=${pkgver}")
options=('!emptydirs' '!libtool' '!makeflags')
source=("https://freedesktop.org/software/pulseaudio/releases/${pkgname}-${pkgver}.tar.xz")
#source=("$pkgname-$pkgver::git://anongit.freedesktop.org/pulseaudio/pulseaudio#commit=50186d7")
sha1sums=('df04f9ffb3fc1de9255f8b88e5d824fcd942607d')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-database=tdb \
    --disable-rpath \
    --disable-bluez4 \
    --disable-default-build-tests

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Disable cork-request module
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
    -i ${pkgdir}/usr/bin/start-pulseaudio-x11

  rm ${pkgdir}/etc/dbus-1/system.d/pulseaudio-system.conf
  #rm ${pkgdir}/etc/xdg/autostart/pulseaudio.desktop
  
  # fix no sound after logout https://github.com/KaOSx/apps/issues/112
  sed -e 's|load-module module-esound-protocol-unix|##load-module module-esound-protocol-unix|' \
    -i ${pkgdir}/etc/pulse/default.pa
}
