
pkgname=spatialindex
pkgver=1.9.2
pkgrel=1
pkgdesc="Extensible framework that will support robust spatial indexing methods."
arch=('x86_64')
url="https://libspatialindex.org/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://github.com/libspatialindex/libspatialindex/releases/download/${pkgver}/${pkgname}-src-${pkgver}.tar.bz2")
sha256sums=('455f920d0ef420118034025c6d56812956b263d2c9069de93db77a7645794449')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-src-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir}/ install
  
  install -D -m644 ../${pkgname}-src-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

