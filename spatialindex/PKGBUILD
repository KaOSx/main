
pkgname=spatialindex
pkgver=2.0.0
pkgrel=1
pkgdesc="Extensible framework that will support robust spatial indexing methods."
arch=('x86_64')
url="https://libspatialindex.org/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://github.com/libspatialindex/libspatialindex/releases/download/${pkgver}/${pkgname}-src-${pkgver}.tar.bz2")
sha256sums=('949e3fdcad406a63075811ab1b11afcc4afddc035fbc69a3acfc8b655b82e9a5')

build() {
  cmake -B build -S ${pkgname}-src-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 ${pkgname}-src-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

