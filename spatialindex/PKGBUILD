
pkgname=spatialindex
pkgver=1.9.3
pkgrel=3
pkgdesc="Extensible framework that will support robust spatial indexing methods."
arch=('x86_64')
url="https://libspatialindex.org/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://github.com/libspatialindex/libspatialindex/releases/download/${pkgver}/${pkgname}-src-${pkgver}.tar.bz2")
sha256sums=('4a529431cfa80443ab4dcd45a4b25aebbabe1c0ce2fa1665039c80e999dcc50a')

build() {
  cmake -B build -S ${pkgname}-src-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -D -m644 ${pkgname}-src-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

