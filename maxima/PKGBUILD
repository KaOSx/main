
pkgname=maxima
pkgver=5.38.1
pkgrel=6
pkgdesc="Maxima - a sophisticated Computer Algebra System."
arch=('x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=1.3.11' 'texinfo' 'sh')
optdepends=('gnuplot: plotting capabilities' \
            'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
options=('!makeflags' '!zipman') 
source=("http://sourceforge.net/projects/maxima/files/Maxima-source/${pkgver}-source/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('bc21a56e0e413c24a31408ee5900cb59'
         '24aa81126fbb8b726854e5a80d4c2415')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/lib \
              --enable-sbcl \
              --with-default-lisp=sbcl

  touch doc/info/maxima.info
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

  install -d ${pkgdir}/usr/share/pixmaps
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
