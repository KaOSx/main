
pkgname=maxima
pkgver=5.42.1
pkgrel=3
pkgdesc="Maxima - a sophisticated Computer Algebra System."
arch=('x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=1.4.14' 'texinfo' 'sh')
optdepends=('gnuplot: plotting capabilities' \
            'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
options=('!makeflags' '!zipman') 
source=("https://sourceforge.net/projects/maxima/files/Maxima-source/${pkgver}-source/${pkgname}-${pkgver}.tar.gz")
md5sums=('83856add7cec9a43af894bd511b13249')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/lib \
              --enable-sbcl \
              --with-default-lisp=sbcl

  touch doc/info/maxima.info
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
