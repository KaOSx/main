
pkgname=maxima
pkgver=5.43.2
pkgrel=3
pkgdesc="Maxima - a sophisticated Computer Algebra System."
arch=('x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=2.0.2' 'texinfo' 'sh' 'gnuplot' 'tk')
makedepends=('python3')
options=('!makeflags' '!zipman') 
source=("https://sourceforge.net/projects/maxima/files/Maxima-source/${pkgver}-source/${pkgname}-${pkgver}.tar.gz")
md5sums=('ff334e89324dc4b1cd2aa89e1faaf436')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/lib \
              --enable-sbcl \
              --with-default-lisp=sbcl

  touch doc/info/maxima.info
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
