
pkgname=djvulibre
pkgver=3.5.28
_pkgver=3969bdfd2c904895ce091913feb7e91b2eca54ae
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
arch=("x86_64")
license=('GPL')
url="http://djvu.sourceforge.net/"
depends=('gcc-libs' 'libtiff' 'bash' 'hicolor-icon-theme')
makedepends=('librsvg')
options=('!libtool')
source=("https://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
#source=("http://sourceforge.net/code-snapshots/git/d/dj/djvu/djvulibre-git.git/djvu-djvulibre-git-3969bdfd2c904895ce091913feb7e91b2eca54ae.zip")
md5sums=('341e89fc2b9dad33068696c7ce63c834')

build() {
  cd ${pkgname}-${pkgver}
  
  ./autogen.sh
  ./configure --prefix=/usr --disable-desktopfiles
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  #Install icons for mime-types.
  install -m755 -d ${pkgdir}/usr/share/icons/hicolor/{22x22,32x32,48x48,64x64}/mimetypes
  for sz in 22 32 48 64; do
    install -m644 desktopfiles/prebuilt-hi${sz}-djvu.png ${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png
  done
}
