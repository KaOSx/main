
_extramodules=extramodules-4.4
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=('nvidia' 'khd-nvidia')
pkgver=364.19
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
makedepends=('linux-headers' 'linux>=4.4' 'linux<4.5' "nvidia-utils=${pkgver}")
license=('custom')
# get from github once hardware-detection is up:
source=('nvidia_supported')
md5sums=('282f73f1a7326339a7b0e0716eee4f76')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source+=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run")
md5sums+=('ad7a0b1855b3913390fb75b4cc3a26dc')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    #cd "${_pkg}"
    #patch -p0 -i ${srcdir}/nv-drm.patch

    cd "${srcdir}/${_pkg}/kernel"  
    make SYSSRC=/lib/modules/"${_kver}/build" module
}

package_nvidia() {
    pkgdesc="NVIDIA drivers for linux."
    depends=('linux>=4.4' 'linux<4.5' "nvidia-utils=${pkgver}")
    conflicts=('nvidia-340xx')
    install=nvidia.install
    
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-drm.ko"
    gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}

package_khd-nvidia() {
    pkgdesc="KHD ids for nvidia $pkgver"
    arch=('x86_64')
    conflicts=('chd-nvidia')
    replaces=('chd-nvidia')

    cd "${srcdir}/${_pkg}"

    install -dm755 "${pkgdir}/opt/kdeos/hwdb"
    sh -e "${srcdir}/nvidia_supported" README.txt kernel/nv-xxx.o \
        > "${pkgdir}/opt/kdeos/hwdb/hw_nvidia_hwdb"
}
