
pkgname=graphite
pkgver=1.3.1
pkgrel=1
arch=('x86_64')
url="http://projects.palaso.org/projects/graphitedev/wiki"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('custom_SIL Dual license')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2')
replaces=('libgraphite' 'pango-graphite')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz"
        'cmakepath.patch')
md5sums=('0c4b5b0ebd59ff36837029bb60efc5b1'
         '00353b67941dbc30b76a43253760769e')

build() {
  cd "${srcdir}"
  # patch from Slackware
  pushd graphite2-${pkgver}
  patch -p1 -i ${srcdir}/cmakepath.patch
  popd
  
  mkdir build
  
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF 
  make
}

check() {
  cd "${srcdir}"/build
  
  ctest || true
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}