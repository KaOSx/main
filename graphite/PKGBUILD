
pkgname=graphite
pkgver=1.3.13
pkgrel=2
arch=('x86_64')
url="https://scripts.sil.org/cms/scripts/page.php?site_id=projects&item_id=graphite_home"
pkgdesc='Reimplementation of the SIL Graphite text processing engine'
license=('custom_SIL Dual license')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python3-fonttools')
replaces=('libgraphite' 'pango-graphite')
options=('!libtool' '!emptydirs')
source=("https://github.com/silnrsi/graphite/releases/download/${pkgver}/graphite2-${pkgver}.tgz"
        'cmakepath.patch')
sha1sums=('bd3494575a1ff1c5b4ea796444274dab1e5992c9'
          '7283d73e21f28a0b4221af9ce7998d68fcced16a')

build() {
  cd ${srcdir}
  # patch from Slackware
  pushd graphite2-${pkgver}
  patch -p1 -i ${srcdir}/cmakepath.patch
  popd
  
  mkdir -p build
  
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF 
  make
}

check() {
  cd ${srcdir}/build
  
  ctest || true
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir/ install

  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/graphite2-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
