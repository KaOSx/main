
pkgname=spirv-tools
_pkgname=SPIRV-Tools
pkgver=2024.2
pkgrel=1
pkgdesc='The SPIR-V Tools project provides an API and commands for processing SPIR-V modules.'
arch=('x86_64')
url='https://github.com/KhronosGroup/SPIRV-Tools'
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cmake' 'python3' 'spirv-headers')
source=("https://github.com/KhronosGroup/SPIRV-Tools/archive/v${pkgver}.tar.gz")
md5sums=('bde778ea43ee00f81ced9e441d69a0c9')

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DSPIRV_WERROR=OFF \
    -DSPIRV_TOOLS_BUILD_STATIC=OFF \
    -DSPIRV-Headers_SOURCE_DIR=/usr
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 ${_pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

