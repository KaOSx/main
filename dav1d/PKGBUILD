
pkgname=dav1d
pkgver=1.4.0
pkgrel=1
pkgdesc='New AV1 cross-platform decoder, open-source, and focused on speed and correctness.'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD-2')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm')
source=("https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('7661648c95755db3d61857b3fdc427fa6d3271a573e84fd11c235441965e9397')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
