
pkgname=dav1d
pkgver=0.6.0
pkgrel=1
pkgdesc='New AV1 cross-platform decoder, open-source, and focused on speed and correctness.'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD-2')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm')
source=("https://downloads.videolan.org/pub/videolan/dav1d/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('099f3115bbdd1ac2338dff73d5fccc1a9a98bf7c1228bd1affe7f62fc529b370')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release
  ninja
}

check() {
  cd build
  
  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
