
pkgname=dav1d
pkgver=0.7.0
pkgrel=1
pkgdesc='New AV1 cross-platform decoder, open-source, and focused on speed and correctness.'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD-2')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm')
source=("https://downloads.videolan.org/pub/videolan/dav1d/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d3e2445006b9be4cf53625e7e37cf1693eca1aa361868e7ff5f309e2549e9e75')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release
  ninja
}

check() {
  cd build
  
  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
  
  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
