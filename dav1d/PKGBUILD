
pkgname=dav1d
pkgver=1.4.1
pkgrel=1
pkgdesc='New AV1 cross-platform decoder, open-source, and focused on speed and correctness.'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD-2')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm')
source=("https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('ab02c6c72c69b2b24726251f028b7cb57d5b3659eeec9f67f6cecb2322b127d8')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
