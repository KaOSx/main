
pkgname=dav1d
pkgver=1.2.0
pkgrel=1
pkgdesc='New AV1 cross-platform decoder, open-source, and focused on speed and correctness.'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD-2')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm')
source=("https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('05cedc43127e00a86c68b8a49a5f68e2dc22b9baa10b1e12a5e3bc5b37876a6b')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install

  install -D -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
