
pkgname=flac
pkgver=1.4.0
pkgrel=1
pkgdesc="Free Lossless Audio Codec"
arch=('x86_64')
url="https://xiph.org/flac/"
license=('custom:Xiph' 'LGPL' 'GPL' 'FDL')
depends=('libogg')
makedepends=('nasm') 
options=('!libtool' '!makeflags')
source=("https://ftp.osuosl.org/pub/xiph/releases/flac/${pkgname}-${pkgver}.tar.xz")
sha256sums=('af41c0733c93c237c3e52f64dd87e3b0d9af38259f1c7d11e8cbf583c48c2506')

build() {
  cd ${pkgname}-${pkgver}

  # run prior to building:
  export LC_ALL="en_US.UTF-8"
  ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-shared \
        --disable-sse \
        --disable-rpath \
        --with-pic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -D -m644 COPYING.Xiph ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
