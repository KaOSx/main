
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

# check in tools/scripts/version_resolver.py for used Chromium
pkgbase=qtwebengine
pkgname=('qtwebengine' 'qtwebengine-examples')
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="Chromium-based web engine to replace Qt WebKit"
url="http://blog.qt.digia.com/blog/2014/01/23/qt-webengine-technology-preview-available/"
arch=('x86_64')
license=('GPL3')
makedepends=('qt5-declarative' 'python2-jinja' 'mesa' 'systemd' 'pango' 'nss' 'speex' 'libevent' 'libcups'
         'libxslt' 'libpng' 'libjpeg-turbo' 'libwebp' 'zlib' 'pciutils' 'jsoncpp' 'pulseaudio' 'libxcomposite'
         'qt5-webchannel' 'ffmpeg' 'libxss' 'protobuf' 'opus' # not used 'libvpx'
         'gperf' 'yasm' 'openssh' 'icu' 'qtwebengine')
_pkgfqn="qtwebengine-opensource-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
#source=("https://download.qt-project.org/development_releases/qt/${_pkgver}/${subver}/submodules/$_pkgfqn.tar.xz"
        'nss321.diff'
        'demobrowser.desktop'
        'quicknanobrowser.desktop'
        'markdowneditor.desktop')
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '5c297bd337ffab4a2d075a7c85d55ef3'
         'f11d24801322ef41f8a44eedc5e104c2'
         '6d62ba455c7d70870fb28c9362c9c591'
         '5ea5aa48c198a8de1398fc9e7a4abd14')

build() {
   cd $_pkgfqn/   
   
   mkdir build   
   cd build 
   #export GYP_DEFINES="python_ver=2.7 use_pulseaudio=1 use_pango=1 use_cairo=1 use_cups=1"
   
   /usr/lib/qt5/bin/qmake WEBENGINE_CONFIG+="use_proprietary_codecs use_system_ffmpeg use_spellcheck use_nss=0" ../qtwebengine.pro
   make 
   
   cd ..
   cd examples/webenginewidgets/demobrowser/
   /usr/lib/qt5/bin/qmake
   make
   
   cd ..
   cd markdowneditor/
   /usr/lib/qt5/bin/qmake
   make
   
   cd ../..
   cd webengine/quicknanobrowser/
   /usr/lib/qt5/bin/qmake
   make
}

package_qtwebengine() {
   pkgdesc="Chromium-based web engine to replace Qt WebKit"
   depends=('qt5-declarative' 'python2-jinja' 'mesa' 'systemd' 'pango' 'nss' 'speex' 'libevent' 'libcups'
            'libxslt' 'libpng' 'libjpeg-turbo' 'libwebp' 'zlib' 'pciutils' 'jsoncpp' 'pulseaudio' 'libxcomposite'
            'qt5-webchannel' 'ffmpeg' 'libxss' 'protobuf' 'opus' 'libvpx')
    groups=('qt5')
            
   cd $_pkgfqn/build/   
   
   make INSTALL_ROOT="${pkgdir}" install
}

package_qtwebengine-examples() {
    pkgdesc="Chromium-based web engine examples browsers and editor"
    depends=('qtwebengine')
    
    cd $_pkgfqn/
    
    install -D -m755 examples/webenginewidgets/demobrowser/demobrowser ${pkgdir}/usr/bin/demobrowser
    install -D -m755 examples/webengine/quicknanobrowser/quicknanobrowser ${pkgdir}/usr/bin/quicknanobrowser
    install -D -m755 examples/webenginewidgets/markdowneditor/markdowneditor ${pkgdir}/usr/bin/markdowneditor
    
    install -D -m644 ${srcdir}/demobrowser.desktop ${pkgdir}/usr/share/applications/demobrowser.desktop
    install -D -m644 ${srcdir}/quicknanobrowser.desktop ${pkgdir}/usr/share/applications/quicknanobrowser.desktop
    install -D -m644 ${srcdir}/markdowneditor.desktop ${pkgdir}/usr/share/applications/markdowneditor.desktop
    
    install -D -m644 examples/webenginewidgets/demobrowser/data/demobrowser.svg \
    ${pkgdir}/usr/share/icons/hicolor/scalable/apps/demobrowser.svg
}
