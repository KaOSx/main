
pkgname=mesa-amber
_pkgname=mesa
pkgver=21.3.9
pkgrel=7
arch=('x86_64')
pkgdesc="Classic Mesa 3-D graphics libraries and include files"
url="https://www.mesa3d.org/"
license=('custom')
depends=('libx11' 'libxt' 'gcc-libs' 'libvdpau' 'xorgproto' 'wayland' 'elfutils'
         'libdrm' 'libxxf86vm' 'libxdamage' 'expat' 'libxshmfence' 'libxvmc' 
         'libxrandr' 'libglvnd' 'zstd' 'mesa') 
makedepends=('libva' 'libxml2' 'imake' 'clang' 'python3-mako'
             'wayland-protocols' 'meson' 'ninja' 'valgrind' 'vulkan-loader')
#conflicts=('mesa')
#provides=('mesa')
source=("https://mesa.freedesktop.org/archive/${_pkgname}-${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/pzanoni/mesa/-/commit/168f177b8a910004de768b1a89a0adea383fa6bc.diff"
        'LICENSE')
sha256sums=('91fe6f20339884e37a7c47bfba42fb1cd397512c9ed5ad423de369f047cd8b5c'
            '348275bfda308f2e8cc79e2a72ecc8362672076f795779ff750818f11c69e811'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

prepare() {
    cd ${_pkgname}-${pkgver}

    #patch -p1 -i ${srcdir}/168f177b8a910004de768b1a89a0adea383fa6bc.diff
}

build() {
    mkdir -p build
    cd build

  # use order in meson_options.txt to list build options, fill options from meson.build
  meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release \
    -Db_ndebug=true \
    -Dplatforms=x11,wayland \
    -Damber=true \
    -Dglvnd-vendor-name=amber \
    -Ddri3=enabled \
    -Ddri-drivers=i915,i965,r100,r200,nouveau \
    -Dgallium-drivers=nouveau,svga,swrast \
    -Dgallium-extra-hud=true \
    -Dgallium-vdpau=disabled \
    -Dgallium-xvmc=disabled \
    -Dgallium-omx=disabled \
    -Dgallium-va=enabled \
    -Dgallium-xa=disabled \
    -Dgallium-nine=true \
    -Dgallium-opencl=disabled \
    -Dvulkan-drivers= \
    -Dshared-glapi=enabled \
    -Dgles1=enabled \
    -Dgles2=enabled \
    -Dopengl=true \
    -Dgbm=disabled \
    -Dglx=dri \
    -Degl=enabled \
    -Dglvnd=true \
    -Dllvm=disabled \
    -Dvalgrind=disabled \
    -Dlibunwind=disabled \
    -Dlmsensors=disabled \
    -Dbuild-tests=false \
    -Dosmesa=false
    ninja
}

package() {
  cd build  
  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/share/licenses/mesa-amber
  install -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/mesa-amber/

  # Make mesa-amber co-installable with mesa, remove conflicting files, rename i915 so files
  #rm -r ${pkgdir}/usr/lib/libgbm.so*
  rm -r ${pkgdir}/usr/lib/d3d
  rm -r ${pkgdir}/usr/lib/libglapi.so*
  rm -r ${pkgdir}/usr/lib/dri/nouveau_dri.so
  rm -r ${pkgdir}/usr/lib/dri/nouveau_drv_video.so
  #rm -r ${pkgdir}/usr/lib/dri/radeonsi_dri.so
  #rm -r ${pkgdir}/usr/lib/dri/radeonsi_drv_video.so
  rm -r ${pkgdir}/usr/lib/dri/swrast_dri.so
  rm -r ${pkgdir}/usr/lib/dri/kms_swrast_dri.so
  rm -r ${pkgdir}/usr/lib/dri/vmwgfx_dri.so
  rm -r ${pkgdir}/usr/lib/pkgconfig
  rm -r ${pkgdir}/usr/include
  mv ${pkgdir}/usr/share/drirc.d/00-mesa-defaults.conf ${pkgdir}/usr/share/drirc.d/00-amber-defaults.conf
  #mv ${pkgdir}/usr/lib/dri/i915_dri.so ${pkgdir}/usr/lib/dri/i915c_dri.so
}
