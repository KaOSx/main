
pkgname=libblockdev
pkgver=3.0.3
_pkgver=3.0.3-1
pkgrel=2
pkgdesc="C library supporting GObject introspection for manipulation of block devices."
arch=('x86_64')
url="http://storaged.org/libblockdev/"
license=('LGPL')
depends=('btrfs-progs' 'cryptsetup' 'kmod' 'libbytesize' 'lvm2' 'nss' 'parted' 
         'python3' 'systemd' 'util-linux' 'volume_key' 'mpfr' 'ndctl' 'libyaml' 'dmraid' 'libnvme')
makedepends=('gobject-introspection' 'python3-six')
source=("https://github.com/storaged-project/libblockdev/releases/download/${_pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/storaged-project/libblockdev/commit/2ae0d949eb87142b0212e5953a0e5ad1a146ed6b.diff")
md5sums=('fb4cd3812b7b5a8f2bd950799fbd37f6'
         'ce5d91a81d67eb3ee8ed8df8b9c25fab')

prepare() {
    cd ${pkgname}-${pkgver}

    patch -p1 -i ${srcdir}/2ae0d949eb87142b0212e5953a0e5ad1a146ed6b.diff
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --with-python3=yes
        #--without-dm 
        #--without-mdraid
        #--without-lvm \
        #--without-lvm_dbus \
        #--without-mpath

    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR=${pkgdir}/ install
}
 
