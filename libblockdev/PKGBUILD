
pkgname=libblockdev
pkgver=2.8.1
_pkgver=2.8-1
pkgrel=2
pkgdesc="C library supporting GObject introspection for manipulation of block devices."
arch=('x86_64')
url="http://www-rhstorage.rhcloud.com/projects/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'cryptsetup' 'kmod' 'libbytesize' 'lvm2' 'nss' 'parted' 
         'python3' 'systemd' 'util-linux' 'volume_key')
makedepends=('gobject-introspection' 'python3-six') # 'dmraid')
source=("https://github.com/rhinstaller/libblockdev/archive/${pkgname}-${_pkgver}.tar.gz"
        "https://github.com/rhinstaller/libblockdev/commit/50dec146ddd6c77f5989aa359a545ab676437169.diff"
        "https://github.com/rhinstaller/libblockdev/commit/5d6617ffaec72a38a61f7e6fe2a102a9a3a5ca94.diff")
md5sums=('7199200a0596647e4592b838dcfce56b'
         '9610c23df7c73e74ddebffce971614c3'
         '1728a015f4102da2c27732e512bbd549')

prepare() {
    cd ${pkgname}-${pkgname}-${_pkgver}
    
    patch -p1 -i ${srcdir}/50dec146ddd6c77f5989aa359a545ab676437169.diff
    patch -p1 -i ${srcdir}/5d6617ffaec72a38a61f7e6fe2a102a9a3a5ca94.diff
}

build() {
    cd ${pkgname}-${pkgname}-${_pkgver}
    
    ./autogen.sh
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --with-python3=yes \
        --without-dm 
        #--without-mdraid
        #--without-lvm \
        #--without-lvm_dbus \
        #--without-mpath
        
    make
}

package() {
    cd ${pkgname}-${pkgname}-${_pkgver}

    make DESTDIR=${pkgdir}/ install
}
 
