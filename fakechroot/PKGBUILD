
pkgname=fakechroot
pkgver=2.19
pkgrel=1
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="https://github.com/dex4er/fakechroot"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=("http://ftp.debian.org/debian/pool/main/f/fakechroot/${pkgname}_${pkgver}.orig.tar.gz")
md5sums=('db6378420c769232e69508bb78612c34')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot --sysconfdir=/etc
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
