
pkgname=fakechroot
pkgver=2.20.1
pkgrel=1
pkgdesc="Gives a fake chroot environment"
arch=('x86_64')
url="https://github.com/dex4er/fakechroot"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=("https://github.com/dex4er/fakechroot/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('bf67c9b3a5f282f310ba8beda2fc6057')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot --sysconfdir=/etc
  make 
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
