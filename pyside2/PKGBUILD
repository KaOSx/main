
pkgname=pyside2
_pkgname=pyside-setup-opensource-src
pkgver=5.15.2
pkgrel=1
pkgdesc="Plugin (front-end) for Generator Runner. It generates bindings for C++ libraries using CPython source code"
arch=('x86_64')
license=('LGPL')
url="https://wiki.qt.io/Qt_for_Python"
depends=('phonon-qt5' 'qt5-3d' 'qt5-charts' 'qt5-datavis3d' 'qt5-quickcontrols2' 'qt5-script'
         'qt5-svg' 'qt5-tools' 'qt5-websockets' 'qt5-multimedia' 'qt5-remoteobjects' 'qt5-sensors'
         'qt5-serialport' 'qt5-speech' 'qtwebengine' 'shiboken2')
makedepends=('cmake' 'python3-sphinx' 'python3-setuptools')
#source=("git://code.qt.io/pyside/pyside-setup.git")
source=("https://download.qt.io/official_releases/QtForPython/${pkgname}/PySide2-${pkgver}-src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('e9bb6b57d39eb6cf1720cd3589a8b76a')

build() {
  cmake -B build -S ${_pkgname}-${pkgver}/sources/pyside2 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

