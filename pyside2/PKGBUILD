
pkgname=pyside2
_pkgname=pyside-setup-opensource-src
pkgver=5.15.11
_pkgver=5.15.11
pkgrel=1
pkgdesc="Plugin (front-end) for Generator Runner. It generates bindings for C++ libraries using CPython source code"
arch=('x86_64')
license=('LGPL')
url="https://wiki.qt.io/Qt_for_Python"
depends=('phonon-qt5' 'qt5-3d' 'qt5-charts' 'qt5-datavis3d' 'qt5-quickcontrols2' 'qt5-script'
         'qt5-svg' 'qt5-tools' 'qt5-websockets' 'qt5-multimedia' 'qt5-remoteobjects' 'qt5-sensors'
         'qt5-serialport' 'qt5-speech' 'qtwebengine' 'shiboken2')
makedepends=('cmake' 'python3-sphinx' 'python3-setuptools' 'clang')
#source=("git://code.qt.io/pyside/pyside-setup.git")
source=("https://download.qt.io/official_releases/QtForPython/${pkgname}/PySide2-${pkgver}-src/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('da567cd3b7854d27a0b4afe3e89de8b2f98b7a6d57393be56f1fc13f770faf29')

prepare() {
  cd ${_pkgname}-${_pkgver}

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  #sed -i -e 's|import importlib|import importlib.machinery|' sources/shiboken2/libshiboken/embed/signature_bootstrap.py
  #sed -i -e 's|#ifndef Py_LIMITED_API|#if PY_VERSION_HEX < 0x03010000|' sources/shiboken2/libshiboken/pep384impl.cpp
}

build() {
  cmake -B build -S ${_pkgname}-${_pkgver}/sources/pyside2 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python3
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}

