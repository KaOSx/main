
pkgname=serd
pkgver=0.30.0
pkgrel=1
pkgdesc="A lightweight C library for RDF syntax which supports reading and writing Turtle and NTriples."
arch=('x86_64')
url="https://drobilla.net/software/serd/"
license=('custom:ISC')
depends=('glibc')
makedepends=('python2')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('82243362b8b5a47dd46dac7ae893f562')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR=${pkgdir} python2 ./waf install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

