
pkgname=serd
pkgver=0.26.0
pkgrel=1
pkgdesc="A lightweight C library for RDF syntax which supports reading and writing Turtle and NTriples."
arch=('x86_64')
url="http://drobilla.net/software/serd/"
license=('custom:ISC')
depends=('glibc')
makedepends=('python2')
source=("http://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('bf5a7627bbacb7aa75e38b0777b4517e')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR=${pkgdir} python2 ./waf install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

