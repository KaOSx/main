
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt6-base
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=2
pkgdesc="A cross-platform application and UI framework"
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('sqlite' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-renderutil' 'xcb-util-cursor'
        'glib2' 'dbus' 'fontconfig' 'libsm' 'libxrandr' 'libxv' 'libxi' 'unixodbc' 'libjpeg-turbo'
        'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'systemd'
        'libxcursor' 'libxrender' 'libpng' 'mariadb' 'icu' 'libgl' 'libxfixes' 'libxinerama'
        'libxkbcommon' 'libinput' 'harfbuzz' 'mtdev' 'libproxy' 'double-conversion' 'vulkan-validationlayers' 'brotli')
makedepends=('postgresql-libs' 'cups' 'gperf' 'ruby' 'pulseaudio' 'cmake' 'ninja' 'valgrind' 'at-spi2-core')
provides=('qt6-x11extras')
replaces=('qt6-x11extras')
conflicts=('qt6-x11extras')
optdepends=('postgresql-libs: PostgreSQL driver')
groups=('qt6')
options=('!libtool' 'debug')
_pkgfqn="qtbase-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/${_pkgfqn}.tar.xz"
        "https://github.com/qt/qtbase/commit/8af35d27e8f02bbb99aef4ac495ed406e50e3cca.diff")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`
         '5455fe3d40253d56316e7708cfe65ea3')

prepare() {
  cd ${_pkgfqn}
  # libxkbcommon > 1.6 needed
  patch -p1 -i ${srcdir}/8af35d27e8f02bbb99aef4ac495ed406e50e3cca.diff
}

build() {
    cmake -B build -S ${_pkgfqn} -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_BINDIR=lib/qt6/bin \
        -DINSTALL_INCLUDEDIR=include/qt6 \
        -DINSTALL_LIBEXECDIR=lib \
        -DINSTALL_ARCHDATADIR=lib/qt6 \
        -DINSTALL_DOCDIR=share/doc/qt6 \
        -DINSTALL_PLUGINSDIR=lib/qt6/plugins \
        -DINSTALL_DATADIR=share/qt6 \
        -DINSTALL_MKSPECSDIR=lib/qt6/mkspecs \
        -DINSTALL_EXAMPLESDIR=share/doc/qt6/examples \
        -DBUILD_TESTING=OFF \
        -DINPUT_dbus=linked \
        -DFEATURE_no_direct_extern_access=ON \
        -DQT_FEATURE_journald=ON \
        -DINPUT_openssl=linked \
        -DFEATURE_libproxy=ON \
        -DFEATURE_system_freetype=ON \
        -DFEATURE_system_sqlite=ON \
        -DQT_FEATURE_system_xcb_xinput=ON \
        -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    install -D -m644 ${_pkgfqn}/LICENSES/* -t ${pkgdir}/usr/share/licenses/${pkgname}

    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt6/bin); do
        ln -s /usr/lib/qt6/bin/${i} ${pkgdir}/usr/bin/${i}-qt6
    done
}

