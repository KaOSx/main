
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=qt6-base
pkgver=${qtver}
subver=${subver}
_pkgver=${_qtver}
pkgrel=1
pkgdesc="A cross-platform application and UI framework"
arch=('x86_64')
url='https://www.qt.io/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
depends=('sqlite' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-renderutil'
        'glib2' 'dbus' 'fontconfig' 'libsm' 'libxrandr' 'libxv' 'libxi' 'unixodbc' 'libjpeg-turbo'
        'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'systemd'
        'libxcursor' 'libxrender' 'libpng' 'mariadb' 'icu' 'libgl' 'libxfixes' 'libxinerama'
        'libxkbcommon' 'libinput' 'harfbuzz' 'mtdev' 'libproxy' 'double-conversion')
makedepends=('postgresql-libs' 'cups' 'gperf' 'ruby' 'pulseaudio' 'vulkan-headers' 'cmake')
optdepends=('postgresql-libs: PostgreSQL driver')
groups=('qt6')
options=('!libtool' 'debug')
_pkgfqn="qtbase-everywhere-src-${subver}"
source=("${_qtmirror}/${_pkgver}/${subver}/submodules/${_pkgfqn}.tar.xz")
md5sums=(`grep ${_pkgfqn}.tar.xz ../qt.md5 | cut -d" " -f1`)

build() {
    cmake -B build -S ${_pkgfqn} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DINSTALL_BINDIR=lib/qt6/bin \
        -DINSTALL_INCLUDEDIR=include/qt6 \
        -DINSTALL_LIBEXECDIR=lib \
        -DINSTALL_ARCHDATADIR=lib/qt6 \
        -DINSTALL_DOCDIR=share/doc/qt6 \
        -DINSTALL_PLUGINSDIR=lib/qt6/plugins \
        -DINSTALL_DATADIR=share/qt6 \
        -DINSTALL_MKSPECSDIR=lib/qt6/mkspecs \
        -DINSTALL_EXAMPLESDIR=share/doc/qt6/examples \
        -DBUILD_TESTING=OFF \
        -DBUILD_EXAMPLES=OFF \
        -DINPUT_dbus=linked \
        -DFEATURE_reduce_relocations=ON \
        -DQT_FEATURE_journald=ON \
        -DINPUT_openssl=linked \
        -DFEATURE_system_freetype=ON \
        -DFEATURE_system_sqlite=ON
    cmake --build build
}

package() {
    DESTDIR=${pkgdir} cmake --install build

    install -D -m644 ${_pkgfqn}/LICENSE* -t ${pkgdir}/usr/share/licenses/${pkgname}
    
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt6/bin); do
        ln -s /usr/lib/qt6/bin/${i} ${pkgdir}/usr/bin/${i}-qt6
    done
}

