
pkgname=doxygen
pkgver=1.8.12
pkgrel=2
pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
license=('GPL')
arch=('x86_64')
url="http://www.doxygen.org/"
depends=('gcc-libs')
makedepends=('flex' 'texlive-core' 'ghostscript' 'texlive-latexextra' 'python2' 'qt5-base' 'cmake')
optdepends=('qt5-base: for doxywizard')
source=("ftp://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz"
        "https://github.com/doxygen/doxygen/commit/850f0ce7eaf0a689a8e8ef7f3ca05b7a152ab0ad.diff")
md5sums=('08e0f7850c4d22cb5188da226b209a96'
         '3f96b9a842df2fa5823757935bf5f2c9')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i $srcdir/850f0ce7eaf0a689a8e8ef7f3ca05b7a152ab0ad.diff
}


build() { 
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Dbuild_wizard=YES \
    -Dbuild_doc=ON
  make
  make docs
}

package() {
  cd build
  
  make DESTDIR=$pkgdir install
}
