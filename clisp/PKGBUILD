
pkgname=clisp
pkgver=2.49
pkgrel=4
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('x86_64')
license=('GPL')
url="http://clisp.cons.org/"
depends=('readline' 'libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
source=("https://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2")
md5sums=('1962b99d5e530390ec3829236d168649')

build() {
  cd ${pkgname}-${pkgver}
  sed -i 's|(:name "rl_readline_state") (:type int)|(:name "rl_readline_state") (:type ulong)|g' modules/readline/readline.lisp
  
  ./configure --prefix=/usr --with-readline --with-ffcall src
  
  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make 
  sed -i 's|http://www.lisp.org/HyperSpec/|http://www.lispworks.com/reference/HyperSpec/|g' config.lisp
  make 
}

package() {
  cd ${pkgname}-${pkgver}/src
  
  make DESTDIR=$pkgdir install 
}
