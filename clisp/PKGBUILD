
pkgname=clisp
pkgver=2.50.0
_commit=de01f0f47bb44d3a0f9e842464cf2520b238f356
pkgrel=2
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('x86_64')
license=('GPL')
url="https://clisp.sourceforge.io/"
depends=('readline' 'libsigsegv' 'libxcrypt' 'libunistring')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
#source=("https://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2")
source=("https://gitlab.com/gnu-clisp/clisp/-/archive/${_commit}/clisp-${_commit}.tar.bz2")
md5sums=('4219191380f97b29d6603b0d58768c32')

build() {
  cd ${pkgname}-${_commit}

  ./configure --prefix=/usr --with-readline --with-ffcall src

  cd src

  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make 
}

package() {
  cd ${pkgname}-${_commit}/src

  make DESTDIR=${pkgdir} install 
}
