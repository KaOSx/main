
pkgname=aspell-de
pkgver=20160407
pkgrel=1
pkgdesc="German dictionary for aspell"
arch=('x86_64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
source=("http://j3e.de/ispell/igerman98/dict/igerman98-${pkgver}.tar.bz2")
md5sums=('5e5ce15bbfd56a6f89e7789e63fffad0')

build() {
  cd igerman98-${pkgver}

  LANGS="de_DE de_AT de_CH"
  for l in ${LANGS}; do
    make aspell/${l}.rws
  done
}

package() {
  cd igerman98-${pkgver}/aspell

  install -d ${pkgdir}/usr/lib/aspell-0.60

  LANGS="de_DE de_AT de_CH"
  for l in ${LANGS}; do
    install -m644 ${l}.alias ${l}.dat ${l}_affix.dat ${l}.multi ${l}.rws \
      ${pkgdir}/usr/lib/aspell-0.60/
  done

  echo "add de_DE.multi" > ${pkgdir}/usr/lib/aspell-0.60/deutsch.alias
  echo "add de_DE.multi" > ${pkgdir}/usr/lib/aspell-0.60/german.alias
  echo "add de_CH.multi" > ${pkgdir}/usr/lib/aspell-0.60/swiss.alias
}
