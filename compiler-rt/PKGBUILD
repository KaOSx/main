
pkgname=compiler-rt
pkgver=15.0.4
pkgrel=1
pkgdesc="Compiler runtime libraries for use in Clang and LLVM projects"
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=("llvm=${pkgver}" 'gcc')
makedepends=('cmake' 'python3')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/${pkgname}-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/cmake-${pkgver}.src.tar.xz")
md5sums=('2c987e17ed71e1e548c3b2d1afde00fd'
         '79cf74619c8281dddfbc39db93ef4fe2')

build() {
    cmake -B build -S ${pkgname}-${pkgver}.src \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_MODULE_PATH="${srcdir}/cmake-${pkgver}.src/Modules" \
        -DCOMPILER_RT_INSTALL_PATH=/usr/lib/clang/${pkgver}
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    install -D -m644 ${pkgname}-${pkgver}.src/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
