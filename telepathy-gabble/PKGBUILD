
pkgname=telepathy-gabble
pkgver=0.18.4
pkgrel=4
pkgdesc="A Jabber/XMPP connection manager that handles single- and multi-user chats and voice/video calls."
arch=('x86_64')
url="https://telepathy.freedesktop.org/components/telepathy-gabble"
license=('GPL')
depends=('telepathy-glib' 'libsoup' 'libnice' 'util-linux' 'sqlite' 'openssl')
makedepends=('libxslt' 'python3')
groups=('telepathy')
options=('!libtool' '!emptydirs')
source=("https://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/gentoo/gentoo/master/net-voip/telepathy-gabble/files/telepathy-gabble-0.18.4-openssl-1.1.patch"
        'gabble-python3.patch')
md5sums=('64334f42b1822dbc22b823f22f9e968f'
         '21d7fd8258608c970055ed174045d863'
         '51be311431253c146a55d580d8293579')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/telepathy-gabble-0.18.4-openssl-1.1.patch
  patch -p1 -i ${srcdir}/gabble-python3.patch

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/telepathy \
    --with-tls=openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}

