
pkgname=enblend-enfuse
pkgver=4.2
pkgrel=31
pkgdesc="Intelligent blend tool for overlapping picture"
arch=("x86_64")
license=('GPL')
url="https://enblend.sourceforge.net"
depends=('openexr' 'lcms2' 'libpng' 'vigra' 'gsl')
makedepends=('boost' 'mesa' 'help2man' 'texlive-latexextra' 'clang')
replaces=('enblend')
conflicts=('enblend')
provides=('enblend')
source=("https://downloads.sourceforge.net/sourceforge/enblend/${pkgname}-${pkgver}.tar.gz"
        'gcc.patch')
md5sums=('e26751f393862cecfd1a113003787001'
         '14c14b48771f83e195a04d0355f87dd5')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/gcc.patch

  export CC=/usr/bin/clang
  export CXX=/usr/bin/clang++

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
