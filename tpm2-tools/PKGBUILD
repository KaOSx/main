
pkgname=tpm2-tools
pkgver=5.2
pkgrel=1
pkgdesc='The source repository for the Trusted Platform Module (TPM2.0) tools based on tpm2-tss'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-tools'
license=('BSD')
depends=('curl' 'openssl' 'tpm2-tss' 'util-linux')
makedepends=('iproute2' 'pyyaml')
source=("https://github.com/tpm2-software/tpm2-tools/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c0b402f6a7b3456e8eb2445211e2d41c46c7e769e05fe4d8909ff64119f7a630')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --localstatedir=/var \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install

    install -Dm644 doc/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
