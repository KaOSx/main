# Maintainer: Neophytos Kolokotronis <tetris4@gmail.com>
# Contributions from Arch: https://www.archlinux.org/packages/extra/x86_64/p7zip/

pkgname=p7zip
pkgver=9.20.1
pkgrel=2
pkgdesc="A command-line port of the 7zip compression utility"
arch=('x86_64')
license=('GPL')
url="http://p7zip.sourceforge.net"
depends=('gcc-libs' 'bash')
makedepends=('yasm' 'nasm')
options=('!makeflags')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver}_src_all.tar.bz2"
        7z.desktop)
md5sums=('bd6caaea567dc0d995c990c5cc883c89'
         '4a21a2c956840140a0ee859440968751')

build() {
	cd "${srcdir}/${pkgname}_${pkgver}"

        cp makefile.linux_amd64_asm makefile.machine

	make all3 OPTFLAGS="${CXXFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}"

	make install \
		DEST_DIR="${pkgdir}" \
		DEST_HOME="/usr" \
		DEST_MAN="/usr/share/man"

	# Licenses
	install -d "${pkgdir}"/usr/share/licenses/p7zip
	ln -s -t "${pkgdir}"/usr/share/licenses/p7zip \
		/usr/share/doc/p7zip/DOCS/License.txt \
		/usr/share/doc/p7zip/DOCS/unRarLicense.txt

	# Integration with stuff...
	ln -s 7zCon.sfx "${pkgdir}"/usr/lib/p7zip/7z.sfx
	chmod -R u+w "${pkgdir}/usr"
}
