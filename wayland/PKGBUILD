
pkgname=wayland
pkgver=1.18.0
pkgrel=1
pkgdesc="Wayland the modern display server."
arch=('x86_64')
url="https://wayland.freedesktop.org"
license=('MIT')
depends=('libffi' 'expat')
makedepends=('doxygen' 'xmlto' 'graphviz' 'meson' 'ninja')
options=('!makeflags' '!libtool' 'debug')
source=("https://wayland.freedesktop.org/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4675a79f091020817a98fd0484e7208c8762242266967f55a67776936c2e294d')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release 
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
  install -Dm 644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

