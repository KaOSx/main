
pkgname=graphicsmagick
pkgver=1.3.22
pkgrel=2
pkgdesc="Image processing system"
arch=('x86_64')
url="http://www.graphicsmagick.org/"
license=('MIT')
depends=('bzip2' 'freetype2' 'ghostscript' 'jasper' 'lcms2' 'libsm' 'libpng' 'libxext'
	'libtiff' 'libwmf' 'libxml2' 'libtool' 'xz' 'perl')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/graphicsmagick/${pkgname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz")
md5sums=('32e6ae274aa9318d2879978dd1e417a0')

build() {
  cd "${srcdir}/GraphicsMagick-$pkgver"

  ./configure --prefix=/usr \
              --with-perl \
              --enable-shared \
              --with-modules \
              --with-quantum-depth=16 \
              --with-gs-font-dir=/usr/share/fonts/Type1 
  make
  
  cd PerlMagick
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/GraphicsMagick-$pkgver"
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/$pkgname/Copyright.txt" 
  
  cd PerlMagick
  make DESTDIR="${pkgdir}" install

  # Remove perllocal.pod and .packlist
  rm -rf "${pkgdir}/usr/lib/perl5/core_perl"
  rm "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Graphics/Magick/.packlist"
}