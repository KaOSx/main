
pkgname=graphicsmagick
pkgver=1.3.19
pkgrel=1
pkgdesc="Image processing system"
arch=('x86_64')
url="http://www.graphicsmagick.org/"
license=('MIT')
depends=('bzip2' 'freetype2' 'ghostscript' 'jasper' 'lcms2' 'libsm' 
	'libtiff' 'libwmf' 'libxml2' 'libltdl' 'xz')
makedepends=('perl')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz")
md5sums=('e2795d7bdc2f3917804e40c8cae1993e')

build() {
  cd "${srcdir}/GraphicsMagick-$pkgver"

  ./configure --prefix=/usr \
              --with-perl \
              --enable-shared \
              --with-gs-font-dir=/usr/share/fonts/Type1 
  make 
}

package() {
  cd "${srcdir}/GraphicsMagick-$pkgver"
  make DESTDIR="${pkgdir}" install
  
  # Install MIT license
  install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/$pkgname/Copyright.txt" 

  # Install perl bindings
  # The patching was introduced in order to build perl module without installing package itself and
  # not to introduce unnecessary path into LD_RUN_PATH
  cd PerlMagick 
  sed -i -e "s:'LDDLFLAGS'  => \"\(.*\)\":'LDDLFLAGS'  => \"-L${pkgdir}/usr/lib \1\":" Makefile.PL
  perl Makefile.PL INSTALLDIRS=vendor PREFIX=/usr DESTDIR="${pkgdir}" 
  sed -i -e "s/LDLOADLIBS =/LDLOADLIBS = -lGraphicsMagick/" Makefile
  make 
  make install 

  # Remove perllocal.pod and .packlist
  rm -rf "${pkgdir}/usr/lib/perl5/core_perl"
  rm "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Graphics/Magick/.packlist"
}