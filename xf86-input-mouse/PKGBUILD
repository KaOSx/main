
pkgname=xf86-input-mouse
pkgver=1.9.3
pkgrel=2
pkgdesc="X.org mouse input driver"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('glibc' 'xorgproto')
makedepends=('xorg-server')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=("https://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('93ecb350604d05be98b7d4e5db3b8155a44890069a7d8d6b800c0bea79c85cc5')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
