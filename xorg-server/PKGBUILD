
pkgname=xorg-server
pkgver=1.16.2
# major updates build xorg-server/xf86-input-evdev/xorg-server again
pkgrel=1
pkgdesc="X.Org X servers"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('xproto' 'randrproto' 'renderproto' 'xextproto' 'inputproto' 'kbproto' 'fontsproto'
         'videoproto' 'dri2proto' 'dri3proto' 'xineramaproto' 'xorg-util-macros' 'pixman'
         'libpciaccess' 'libxfont' 'libxi' 'mesa' 'libgcrypt' 'libxv' 'libxau' 'libxaw'
         'libxrender' 'libdmx' 'libdrm' 'libxfixes' 'libxext' 'libxdmcp' 'libxshmfence'
         'presentproto' 'systemd' 'xf86-input-evdev' 'xkeyboard-config' 'xorg-xkb-utils'
         'xorg-fonts-misc' 'xf86dgaproto' 'xcb-util' 'xcb-util-image' 'xcb-util-keysyms'
         'xcb-util-renderutil' 'xcb-util-wm' 'libepoxy' 'libgl')
makedepends=('libx11' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto'
             'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'libxkbfile'
             'libxmu' 'dmxproto' 'libxtst' 'libxres' 'links')
#backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
options=('!libtool')
provides=('glamor-egl')
conflicts=('glamor-egl')
replaces=('glamor-egl')
groups=('xorg')
source=("http://xorg.freedesktop.org/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2"
        'nvidia-drm-outputclass.conf'
        'xvfb-run'
        'xvfb-run.1')
md5sums=('89620960b13515db8d0a8dbb92a1378a'
         '1b0626939623e92c04809fb76cb505dd'
         '52fd3effd80d7bc6c1660d4ecf23d31c'
         '376c70308715cd2643f7bff936d9934b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -fiv
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-xwayland \
      --enable-glamor \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-config-udev \
      --enable-systemd-logind \
      --enable-suid-wrapper \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --libexecdir=/usr/bin \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/var/lib/xkb"

  # https://bugs.archlinux.org/task/41259 
  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/nvidia-drm-outputclass.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var/log"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
