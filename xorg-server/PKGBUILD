
pkgname=xorg-server
pkgver=21.1.5
# major updates build xorg-server/xf86-input-evdev/xorg-server again
pkgrel=1
pkgdesc="X.Org X servers"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org"
depends=('xorgproto' 'xorg-util-macros' 'pixman'
         'libpciaccess' 'libxfont2' 'libxi' 'mesa' 'libgcrypt' 'libxv' 'libxau' 'libxaw'
         'libxrender' 'libdmx' 'libdrm' 'libxfixes' 'libxext' 'libxdmcp' 'libxshmfence' 'libxcvt'
         'systemd' 'xf86-input-evdev' 'xkeyboard-config' 'xorg-xkb-utils'
         'xorg-fonts-misc' 'xcb-util' 'xcb-util-image' 'xcb-util-keysyms'
         'xcb-util-renderutil' 'xcb-util-wm' 'libepoxy' 'libglvnd' 'nettle')
makedepends=('libx11' 'xtrans' 'libxkbfile' 'libxmu' 'libxtst' 'libxres' 'links' 'meson' 'ninja')
#backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
options=('!libtool')
provides=('glamor-egl')
conflicts=('glamor-egl' 'xf86-video-modesetting' )
replaces=('glamor-egl' 'xf86-video-modesetting')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/xserver/${pkgname}-${pkgver}.tar.xz"
        'xvfb-run'
        'xvfb-run.1'
        "https://gitlab.freedesktop.org/xorg/xserver/-/commit/05b3c681ea2f478c0cb941c2f8279919cf78de6d.diff")
sha256sums=('05b0c8301cee7c5a78083df421ba3a071891553541a417b3d85adf5cf20f8720'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            'b2e3e5472842ba88b20b0b86d55ba45b0a923df190f8874da4abbb33e808b38b')

prepare() {
  cd ${pkgname}-${pkgver}

  #patch -p1 -R -i ${srcdir}/05b3c681ea2f478c0cb941c2f8279919cf78de6d.diff
  #sed -i 's|dtrace_tmpl =|#dtrace_tmpl =|' include/meson.build
}

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/xorg-server \
    -D xorg=true \
    -D xephyr=true \
    -D glamor=true \
    -D xnest=true \
    -D xvfb=true \
    -D builder_addr="demm@kaosx.us" \
    -D ipv6=true \
    -D xkb_dir=/usr/share/X11/xkb \
    -D xkb_output_dir=/var/lib/xkb \
    -D vendor_name="KaOS" \
    -D vendor_web="https://kaosx.us" \
    -D dtrace=false \
    -D suid_wrapper=true \
    -D systemd_logind=true \
    -D xcsecurity=true \
    -D devel-docs=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -m755 -d ${pkgdir}/usr/lib/xorg
  install -m644 ../${pkgname}-${pkgver}/dix/protocol.txt ${pkgdir}/usr/lib/xorg/

  install -m755 ${srcdir}/xvfb-run ${pkgdir}/usr/bin/
  install -m644 ${srcdir}/xvfb-run.1 ${pkgdir}/usr/share/man/man1/

  install -m755 -d ${pkgdir}/var/lib/xkb
  install -m644 ../${pkgname}-${pkgver}/xkb/README.compiled ${pkgdir}/var/lib/xkb/

  install -m755 -d ${pkgdir}/etc/X11
  mv ${pkgdir}/usr/share/X11/xorg.conf.d ${pkgdir}/etc/X11/

  rmdir ${pkgdir}/usr/share/X11

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
