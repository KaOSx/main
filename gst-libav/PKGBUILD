
pkgname=gst-libav
pkgver=1.14.2
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2')
makedepends=('yasm' 'python3')
options=('!emptydirs' '!libtool')
source=("https://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8a351c39c5cfc2bbd31ca434ec4a290a730a26efbdea962fdd8306dce5c576de')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --with-libav-extra-configure="--enable-runtime-cpudetect" \
      --with-package-name="GStreamer libav Plugin (KaOS)" \
      --with-package-origin="https://kaosx.us/"
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
