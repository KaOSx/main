
pkgname=gst-libav
pkgver=1.22.10
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2' 'ffmpeg')
makedepends=('yasm' 'python3' 'meson' 'ninja')
options=('!emptydirs' '!libtool')
source=("https://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d6dda7aa38a44173278de675ccd92acff0abf473f7bc02e7d1cdd4ce0f3b7642')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Libav (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # no FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
