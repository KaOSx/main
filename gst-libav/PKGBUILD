
pkgname=gst-libav
pkgver=1.20.3
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2' 'ffmpeg')
makedepends=('yasm' 'python3' 'meson' 'ninja')
options=('!emptydirs' '!libtool')
source=("https://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3fedd10560fcdfaa1b6462cbf79a38c4e7b57d7f390359393fc0cef6dbf27dfe')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Good (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # 2 fail: 5/6 generic_plugin_test        FAIL             0.17s   exit status 1
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
