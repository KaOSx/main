
pkgname=gst-libav
pkgver=1.12.4
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2')
makedepends=('yasm' 'python3')
options=('!emptydirs' '!libtool')
source=("https://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2a56aa5d2d8cd912f2bce17f174713d2c417ca298f1f9c28ee66d4aa1e1d9e62')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --with-libav-extra-configure="--enable-runtime-cpudetect" \
      --with-package-name="GStreamer libav Plugin (KaOS)" \
      --with-package-origin="https://kaosx.us/"

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
