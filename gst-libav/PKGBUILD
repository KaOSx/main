
pkgname=gst-libav
pkgver=1.8.3
pkgrel=3
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="http://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2')
makedepends=('yasm' 'python3')
options=('!emptydirs' '!libtool')
source=("http://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
md5sums=('b51a736147bacb40f85827a4e0ae0d2c')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --with-libav-extra-configure="--enable-runtime-cpudetect" \
      --with-package-name="GStreamer libav Plugin (KaOS)" \
      --with-package-origin="http://kaosx.us/"

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
