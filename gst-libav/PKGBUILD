
pkgname=gst-libav
pkgver=1.24.9
pkgrel=1
pkgdesc="Gstreamer libav Plugin"
arch=('x86_64')
license=('GPL')
url="https://gstreamer.freedesktop.org/"
depends=('gst-plugins-base' 'bzip2' 'ffmpeg')
makedepends=('yasm' 'python3' 'meson' 'ninja')
options=('!emptydirs' '!libtool')
source=("https://gstreamer.freedesktop.org/src/gst-libav/${pkgname}-${pkgver}.tar.xz")
sha256sums=('32682e9ae508ee01f4fb134b3a520081e2ac007220997577624b1d16171d456c')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dpackage-name="GStreamer Libav (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" 
  ninja
}

check() {
  cd build
  # no FAIL
  ninja test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
