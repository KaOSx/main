
pkgname=libwebp
pkgver=1.3.1
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=('x86_64')
url="https://developers.google.com/speed/webp/"
license=('BSD')
depends=('libpng' 'libjpeg-turbo' 'libtiff' 'freeglut' 'giflib')
makedepends=('mesa' 'glu')
options=('!libtool')
source=("https://storage.googleapis.com/downloads.webmproject.org/releases/webp/${pkgname}-${pkgver}.tar.gz")
md5sums=('0ff59e5598753f47519e3e61c12f8cfd')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
     --enable-swap-16bit-csp \
     --enable-experimental \
     --enable-libwebpmux \
     --enable-libwebpdemux \
     --enable-libwebpdecoder \
     --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir}/ install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
