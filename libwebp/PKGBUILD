
pkgname=libwebp
pkgver=0.6.0
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=('x86_64')
url="https://developers.google.com/speed/webp/"
license=('BSD')
depends=('libpng' 'libjpeg-turbo' 'libtiff' 'giflib')
makedepends=('freeglut' 'mesa' 'glu')
optdepends=('freeglut: vwebp viewer')
options=('!libtool')
source=("https://storage.googleapis.com/downloads.webmproject.org/releases/webp/${pkgname}-${pkgver}.tar.gz")
md5sums=('19a6e926ab1721268df03161b84bb4a0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
     --enable-swap-16bit-csp \
     --enable-experimental \
     --enable-libwebpmux \
     --enable-libwebpdemux \
     --enable-libwebpdecoder \
     --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir}/ install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
