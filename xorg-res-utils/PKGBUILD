
pkgname=xorg-res-utils
pkgver=1.0.9
pkgrel=1
pkgdesc="X.Org X11 resource utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw')
makedepends=('pkgconfig')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/appres-1.0.6.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/editres-1.0.8.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/listres-1.0.5.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/viewres-1.0.6.tar.bz2"
        'LICENSE.listres'
        'LICENSE.viewres')
sha256sums=('8b2257e2a0a1ad8330323aec23f07c333075d7fe4e6efd88e0c18fba8223590b'
            '83cf5dffb1883635fd9c6a8dc48ff9e560f6c6d8ce1a0e929d5f409cba18b6f9'
            'ce2a00bbe7d2eb8d75177006f343c80443a22d52570c48a43c6fe70ea074dc9d'
            '11c15db02066c1d5fc3febf9b185d16c1112f87e37c4b101a484dcefffb80934'
            'c4023f9b75d3e51b0f4298785c1a6f1b7b1eca7155e267f22cb5c9a09edf67cd'
            'c4023f9b75d3e51b0f4298785c1a6f1b7b1eca7155e267f22cb5c9a09edf67cd')

build() {
  cd ${srcdir}

  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-xprint
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}

  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      make DESTDIR=${pkgdir} install
      install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done

  install -D -m644 LICENSE.listres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.listres
  install -D -m644 LICENSE.viewres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.viewres
}
