
pkgname=xorg-res-utils
pkgver=1.0.6
pkgrel=3
pkgdesc="X.Org X11 resource utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw')
makedepends=('pkgconfig')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/appres-1.0.4.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/editres-1.0.6.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/listres-1.0.3.tar.bz2"
        "https://xorg.freedesktop.org/releases/individual/app/viewres-1.0.4.tar.bz2"
        'LICENSE.listres'
        'LICENSE.viewres')
md5sums=('ed598f0599d726963c1bdfdc691960d9'
         '623322610e4040393e0ff2a69e6612cd'
         '4e8edcc03001203c101e79d880d55762'
         'e4651e55dccb0ae234e49bdc0b984fc9'
         '9c9b17ae5e9be0c570dabe9e54f54f6d'
         '9c9b17ae5e9be0c570dabe9e54f54f6d')

build() {
  cd ${srcdir}
  
  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-xprint
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}
  
  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      make DESTDIR=${pkgdir} install
      install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done

  install -D -m644 LICENSE.listres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.listres
  install -D -m644 LICENSE.viewres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.viewres
}
