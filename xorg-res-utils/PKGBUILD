
pkgname=xorg-res-utils
pkgver=1.0.10
pkgrel=2
pkgdesc="X.Org X11 resource utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxkbfile')
makedepends=('pkgconfig')
groups=('xorg')
source=("https://xorg.freedesktop.org/releases/individual/app/appres-1.0.7.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/editres-1.0.9.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/listres-1.0.6.tar.xz"
        "https://xorg.freedesktop.org/releases/individual/app/viewres-1.0.7.tar.xz"
        'LICENSE.listres'
        'LICENSE.viewres')
sha256sums=('1114b189239fd87a8d1db433edcb4486346d29912132b91eaeee5667f13b819f'
            'cdfc3f7a6f0ecea417af785b1f46469d9c21a3543a765480a3090fd65e3db3c6'
            '4d1c53efd69ba654f2877e1577e994c78874b0512fa1b0666cffcf452aee43ca'
            'b15a62085b1a10f55ae1cf17b7ded75b72b21be240c68071685db377c4afc628'
            'c4023f9b75d3e51b0f4298785c1a6f1b7b1eca7155e267f22cb5c9a09edf67cd'
            'c4023f9b75d3e51b0f4298785c1a6f1b7b1eca7155e267f22cb5c9a09edf67cd')

build() {
  cd ${srcdir}

  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      ./configure --prefix=/usr --mandir=/usr/share/man --disable-xprint
      make
      popd
    fi
  done
}

package() {
  cd ${srcdir}

  for i in *; do
    if [ -d ${i} ]; then
      pushd ${i}
      make DESTDIR=${pkgdir} install
      install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      popd
    fi
  done

  install -D -m644 LICENSE.listres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.listres
  install -D -m644 LICENSE.viewres ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.viewres
}
