
pkgname=exiv2
pkgver=0.28.1
pkgrel=2
pkgdesc="Exif and Iptc metadata manipulation library and tools."
arch=('x86_64')
url="https://exiv2.org"
license=('GPL')
depends=('gcc-libs' 'zlib' 'expat')
makedepends=('cmake')
options=('!libtool')
source=("https://github.com/Exiv2/exiv2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/Exiv2/exiv2/commit/c351c7cce317571934abf693055779a59df30d6e.diff"
        "https://github.com/Exiv2/exiv2/pull/2832/commits/65322f38d0ef6427c0f4ccb81c149ca6aa001289.diff")
#source=("https://github.com/Exiv2/exiv2/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('3078651f995cb6313b1041f07f4dd1bf0e9e4d394d6e2adc6e92ad0b621291fa'
            '3f5d5f338b1e66e8c8facc5f9173e8b8dce910e67268acaf38c04b618019227a'
            '9bc960433907fcf3df2bfbac48be292f16d18524d270861984434ac294ca9156')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/c351c7cce317571934abf693055779a59df30d6e.diff
  patch -p1 -i ${srcdir}/65322f38d0ef6427c0f4ccb81c149ca6aa001289.diff
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DEXIV2_BUILD_UNIT_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DEXIV2_BUILD_SAMPLES=OFF \
    -DEXIV2_ENABLE_WEBREADY=ON \
    -DEXIV2_ENABLE_CURL=ON \
    -DEXIV2_ENABLE_BMFF=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
