
pkgname=xf86-video-vesa
pkgver=2.6.0
pkgrel=1
pkgdesc="X.org vesa video driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('pkg-config' 'xorg-server' 'xorgproto')
groups=('xorg' 'xorg-video-drivers')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.xz"
        'revert-kernelcheck.patch')
sha256sums=('10082771b1a8f659c60b3d9b70456a4b59948a75b3cade2dfa33b8b05e549bb1'
            'ef591a342cea65f1b08e84771ae0de84395c98ac8a71739dbf5c0a7f7a36543c')

build() {
  cd ${pkgname}-${pkgver}

  patch -p1 -R -i ${srcdir}/revert-kernelcheck.patch

  ./configure --prefix=/usr 
  make 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install 
  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
