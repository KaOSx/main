
pkgname=pygtk
pkgver=2.24.0
pkgrel=4
pkgdesc="Python bindings for the GTK widget set"
arch=('x86_64')
license=('LGPL')
depends=('python2-cairo' 'python2-gobject' 'python2-numpy' 'gtk2')
options=('!libtool')
url="http://www.pygtk.org/"
source=("http://ftp.gnome.org/pub/gnome/sources/pygtk/2.24/${pkgname}-${pkgver}.tar.bz2"
        'python27.patch')
md5sums=('a1051d5794fd7696d3c1af6422d17a49'
         '12acfacd26f19c504a0a2d0edeb66121')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/python27.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -m644 gtk/gtk-extrafuncs.defs ${pkgdir}/usr/share/pygtk/2.0/defs/
}
