
pkgname=openbabel
pkgver=2.3.2
pkgrel=3
pkgdesc="Open, collaborative project allowing anyone to search, convert,
         analyze, or store data from molecular modeling, chemistry, solid-state materials,
         biochemistry, or related areas"
arch=('x86_64')
url="http://openbabel.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'libxml2' 'eigen')
makedepends=('cmake')
options=('!libtool')
source=("http://downloads.sourceforge.net/openbabel/${pkgname}-${pkgver}.tar.gz")
md5sums=('9b0007560d9d838b40ab4ad06daf5610')

prepare() {
  cd ${pkgname}-${pkgver}
  
  sed -i -e 's|if __GNUC__ == 4  //&&|if __GNUC__ >= 4  //&&|' include/openbabel/shared_ptr.h
}

build() {
  mkdir -p build
  
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
