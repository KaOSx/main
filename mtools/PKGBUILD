
pkgname=mtools
pkgver=4.0.46
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('GPL')
depends=('sh' 'libx11' 'libsm')
makedepends=('texinfo')
backup=('etc/mtools.conf')
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.gz")
md5sums=('6a36efeaf91afd5070e8709bbace392b')

build() {
  cd ${pkgname}-${pkgver}

  sed -i '/^SAMPLE FILE$/s:^:# :' mtools.conf
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make -j 1 DESTDIR=${pkgdir} install
  install -Dm644 mtools.conf ${pkgdir}/etc/mtools.conf
}
