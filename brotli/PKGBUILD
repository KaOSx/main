
pkgname=brotli
pkgver=1.0.9
pkgrel=2
pkgdesc='Generic-purpose lossless compression algorithm that compresses data using a combination of a modern variant of the LZ77 algorithm'
url='https://github.com/google/brotli'
arch=('x86_64')
license=('MIT')
makedepends=('cmake')
source=("https://github.com/google/brotli/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('f9e8d81d0405ba66d181529af42a3354f838c939095ff99930da6aa9cdf6fe46')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
	-DBUILD_SHARED_LIBS=True
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm 644 ${pkgname}-${pkgver}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}
}

