
pkgname=lm_sensors
pkgver=3.4.0
pkgrel=4
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring"
arch=('x86_64')
url="https://github.com/groeck/lm-sensors"
license=('GPL' 'LGPL')
depends=('perl' 'sysfsutils')
makedepends=('rrdtool')
backup=('etc/sensors3.conf' 'etc/conf.d/healthd' 'etc/conf.d/sensord')
options=('!emptydirs')
source=("http://pkgs.fedoraproject.org/repo/pkgs/lm_sensors/lm_sensors-3.4.0.tar.bz2/c03675ae9d43d60322110c679416901a/lm_sensors-${pkgver}.tar.bz2" 
        'healthd' 
        'healthd.conf' 
        'sensord.conf' 
        'healthd.service')
md5sums=('c03675ae9d43d60322110c679416901a'
         'b68bdb73bb4d28ebdb36e2d92278582d'
         'f3ed015c38bdbe9b4e17d38833fde9cc'
         'd398a55842fb5f2365a011512df6fc3c'
         'daf19e142d59f015040814172742502b')

build() {
  cd ${pkgname}-${pkgver}
  sed -i 's|/etc/sysconfig|/etc/conf.d|' prog/{detect/sensors-detect,init/lm_sensors.service}
  sed -i 's/EnvironmentFile=/EnvironmentFile=-/' prog/init/lm_sensors.service
  
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make BUILD_STATIC_LIB=0 \
    PREFIX=/usr MANDIR=/usr/share/man DESTDIR=${pkgdir} install
    
  install -D -m644 prog/init/fancontrol.service ${pkgdir}/usr/lib/systemd/system/fancontrol.service
  install -D -m644 prog/init/lm_sensors.service ${pkgdir}/usr/lib/systemd/system/lm_sensors.service
  
  install -D -m755 ${srcdir}/healthd ${pkgdir}/usr/sbin/healthd
  install -D -m644 ${srcdir}/healthd.conf ${pkgdir}/etc/conf.d/healthd
  install -D -m644 ${srcdir}/sensord.conf ${pkgdir}/etc/conf.d/sensord
  install -D -m644 ${srcdir}/healthd.service ${pkgdir}/usr/lib/systemd/system/healthd.service
}
