
pkgname=meson
pkgver=0.55.3
pkgrel=1
pkgdesc="Open source build system meant to be both extremely fast, and as user friendly as possible."
arch=('x86_64')
url="https://mesonbuild.com"
license=('GPL')
depends=('ninja' 'python3-setuptools')
makedepends=('googletest' 'sdl2' 'itstool')
source=("https://github.com/mesonbuild/meson/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('99777acd77838c8669e1421fda8e31c5')

build() {
  cd ${pkgname}-${pkgver}
  
  python3 setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  
  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  
  python3 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1 --skip-build
}
