
pkgname=meson
pkgver=1.3.0
pkgrel=1
pkgdesc="Open source build system meant to be both extremely fast, and as user friendly as possible."
arch=('x86_64')
url="https://mesonbuild.com"
license=('GPL')
depends=('ninja' 'python3')
makedepends=('googletest' 'sdl2' 'itstool' 'python3-build' 'python3-installer' 'python3-setuptools' 'python3-wheel')
source=("https://github.com/mesonbuild/meson/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('35dc15e4f94ca5fd30dbe2d88a672254')

build() {
  cd ${pkgname}-${pkgver}

  python3 -m build -nw
}

check() {
  cd ${pkgname}-${pkgver}

  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}

  python3 -m installer --destdir=${pkgdir} dist/*.whl
}
