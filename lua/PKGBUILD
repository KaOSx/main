
pkgname=lua
# adjust lua.pc when moving from 5.2
pkgver=5.4.6
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications." 
arch=('x86_64')
url="https://www.lua.org/" 
depends=('readline' 'ncurses') 
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("https://www.lua.org/ftp/${pkgname}-${pkgver}.tar.gz"
        'lua-arch.patch'
        'lua.pc')
sha256sums=('7d5ea1b9cb6aa0b59ca3dde1c6adcb57ef83a1ba8e5432c0ecd06bf439b3ad88'
            'c5615d8a9e2f77f727d3e98c989cf1b313b9fc3a68dacf405dfa22f915e11618'
            'ea5339e07aeee8161c18c465819d111e82b454a9a73a3ffdb5e46c8dbf3fa9b0')

build() { 
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lua-arch.patch

  export CFLAGS="$CFLAGS -fPIC"
  make MYCFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux-readline
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_DATA="cp -d" \
    TO_LIB="liblua.a liblua.so liblua.so.5.4 liblua.so.${pkgver}" \
    INSTALL_TOP=${pkgdir}/usr \
    INSTALL_MAN=${pkgdir}/usr/share/man/man1 \
    install

  install -D -m644 ../../lua.pc ${pkgdir}/usr/lib/pkgconfig/lua.pc
  install -D -m644 doc/readme.html ${pkgdir}/usr/share/licenses/${pkgname}/readme.html
  
  ln -sf /usr/bin/lua ${pkgdir}/usr/bin/lua5.4
  ln -s liblua.so.${pkgver} ${pkgdir}/usr/lib/liblua5.4.so
  ln -s liblua.so.${pkgver} ${pkgdir}/usr/lib/liblua${pkgver}.so

  install -d ${pkgdir}/usr/share/doc/lua
  install -m644 doc/*.{gif,png,css,html} ${pkgdir}/usr/share/doc/lua
}
