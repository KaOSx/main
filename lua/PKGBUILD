
pkgname=lua
# adjust lua.pc when moving from 5.2
pkgver=5.2.4
pkgrel=2
pkgdesc="A powerful light-weight programming language designed for extending applications." 
arch=('x86_64')
url="https://www.lua.org/" 
depends=('readline' 'ncurses') 
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("https://www.lua.org/ftp/${pkgname}-${pkgver}.tar.gz"
        'lua-arch.patch'
        'lua.pc')
sha1sums=('ef15259421197e3d85b7d6e4871b8c26fd82c1cf'
          '84bf5c3fed26e6a3e317a8f29cbdc68fc3607cf5'
          '51626bcde2dcbba70f15e99b43b04344164b5fdf')

build() { 
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lua-arch.patch
  
  export CFLAGS="$CFLAGS -fPIC"
  make MYCFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_DATA="cp -d" \
    TO_LIB="liblua.a liblua.so liblua.so.5.2 liblua.so.${pkgver}" \
    INSTALL_TOP=${pkgdir}/usr \
    INSTALL_MAN=${pkgdir}/usr/share/man/man1 \
    install
    
  install -D -m644 ../../lua.pc ${pkgdir}/usr/lib/pkgconfig/lua.pc
  install -D -m644 doc/readme.html ${pkgdir}/usr/share/licenses/${pkgname}/readme.html
  
  ln -sf /usr/bin/lua ${pkgdir}/usr/bin/lua5.2
  ln -s liblua.so.${pkgver} ${pkgdir}/usr/lib/liblua5.2.so
  ln -s liblua.so.${pkgver} ${pkgdir}/usr/lib/liblua${pkgver}.so

  install -d ${pkgdir}/usr/share/doc/lua
  install -m644 doc/*.{gif,png,css,html} ${pkgdir}/usr/share/doc/lua
}
