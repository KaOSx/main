#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=networkmanager
_pkgname=NetworkManager
pkgver=0.9.8.0
pkgrel=1
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="http://www.gnome.org/projects/$_pkgname/"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'systemd' 'wpa_supplicant' 'ppp' 'dhcpcd'
         'libsoup')
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection')
optdepends=('modemmanager: for modem management service'
            'dhclient: alternative DHCP/DHCPv6 client'
            'iptables: Connection sharing'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support'
            'openresolv: openresolv support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install

source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:3}/$_pkgname-$pkgver.tar.xz"
        'NetworkManager.conf' 
        'disable_set_hostname.patch' 
        'dnsmasq-path.patch')
md5sums=('38d28f6bd9220d85dfff47210706195c'
         '1653159d6634fb62d3a5c548b7a56151'
         '6b5415350c04304a28d29294441a07ae'
         '60fd44b3934587f27e88a59c9e140ba4')

build() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../disable_set_hostname.patch
  patch -p1 -i ../dnsmasq-path.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/usr/sbin/dhcpcd \
    --with-iptables=/usr/sbin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/sbin/resolvconf \
    --with-session-tracking=systemd \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system 

  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 ../NetworkManager.conf "$pkgdir/etc/NetworkManager/"

  rm -r "$pkgdir/var/run"  
}
