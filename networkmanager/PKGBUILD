
pkgname=networkmanager
_pkgname=NetworkManager
pkgver=1.10.8
_pkgver=1.10
pkgrel=1
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="https://wiki.gnome.org/Projects/NetworkManager"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'systemd' 'wpa_supplicant' 'ppp' 'dhcpcd'
         'modemmanager' 'libndp' 'libteam' 'libsoup' 'iptables' 'bluez') # 'libgudev' no longer needed for 1.8)
makedepends=('intltool' 'dhclient' 'gobject-introspection' 'netctl' 'python2-gobject3')
optdepends=('dhclient: alternative DHCP/DHCPv6 client'
            'dnsmasq: Connection sharing')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
source=("https://download.gnome.org/sources/NetworkManager/${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'NetworkManager.conf'
        'nmdevice.patch')
sha256sums=('eb4ac8ce75fed5ec804f409caec7b54342d4e01512baf7d7fc119fd40ac8a938'
            '57a0d0fc875a0c6b1729eac7e50907cc60a6d6aa834adc56b8c851edacf5fbd4'
            'f70cf461ec944f8234d47a69d1aa8505382c6161009c7dc5c1f66c86a505fe09')

build() {
  cd ${_pkgname}-${pkgver}

  #patch -p1 -i ${srcdir}/nmdevice.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/usr/sbin/dhcpcd \
    --with-dhcpcd-supports-ipv6 \
    --with-pppd=/usr/sbin/pppd \
    --with-iptables=/usr/sbin/iptables \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-kernel-firmware-dir=/lib/firmware \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/sbin/resolvconf \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --enable-modify-system \
    --with-pppd-plugin-dir=/usr/lib/pppd/2.4.7

  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m644 ../NetworkManager.conf ${pkgdir}/etc/NetworkManager/
  install -m755 -d ${pkgdir}/etc/NetworkManager/dnsmasq.d

  #rm -r ${pkgdir}/var/run
}
