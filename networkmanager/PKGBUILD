
pkgname=networkmanager
_pkgname=NetworkManager
pkgver=1.48.10
_pkgver=1.48
pkgrel=1
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="https://wiki.gnome.org/Projects/NetworkManager"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'systemd' 'ppp' 'dhcpcd'
         'modemmanager' 'libndp' 'libteam' 'curl' 'iptables' 'bluez' 'iwd' 'mobile-broadband-provider-info') # 'libgudev' no longer needed for 1.8)'wpa_supplicant' 
makedepends=('intltool' 'dhclient' 'gobject-introspection' 'netctl' 'python3-gobject3'
             'meson' 'ninja' 'python3-packaging')
optdepends=('dhclient: alternative DHCP/DHCPv6 client'
            'dnsmasq: Connection sharing')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
source=("https://download.gnome.org/sources/NetworkManager/${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'NetworkManager.conf'
        'nmdevice.patch')
sha256sums=('5dc188fdffcf2d23c89d34b1e6319a6b20203e12eaec24b30037b7ea8ac8c613'
            '57a0d0fc875a0c6b1729eac7e50907cc60a6d6aa834adc56b8c851edacf5fbd4'
            'f70cf461ec944f8234d47a69d1aa8505382c6161009c7dc5c1f66c86a505fe09')

build() {
  mkdir -p build
  cd build

  meson setup ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/networkmanager \
    -Dsystemdsystemunitdir=/usr/lib/systemd/system \
    -Dudev_dir=/usr/lib/udev \
    -Ddbus_conf_dir=/usr/share/dbus-1/system.d \
    -Dkernel_firmware_dir=/lib/firmware \
    -Diptables=/usr/sbin/iptables \
    -Ddnsmasq=/usr/bin/dnsmasq \
    -Ddist_version=${pkgver}-${pkgrel} \
    -Dsession_tracking_consolekit=false \
    -Dsession_tracking=systemd \
    -Dsuspend_resume=systemd \
    -Dmodify_system=true \
    -Dselinux=false \
    -Dconfig_wifi_backend_default=iwd \
    -Dlibaudit=no \
    -Diwd=true \
    -Dpppd=/usr/sbin/pppd \
    -Dpppd_plugin_dir=/usr/lib/pppd/2.5.0 \
    -Dmodem_manager=true \
    -Dnmtui=false \
    -Difupdown=false \
    -Dresolvconf=/usr/sbin/resolvconf \
    -Ddhcpcd=true \
    -Dlibpsl=true \
    -Dcrypto=nss \
    -Dqt=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -m644 ../NetworkManager.conf ${pkgdir}/etc/NetworkManager/
  install -m755 -d ${pkgdir}/etc/NetworkManager/dnsmasq.d
}
