
pkgname=networkmanager
_pkgname=NetworkManager
pkgver=1.10.2
_pkgver=1.10
pkgrel=1
pkgdesc="Network Management daemon"
arch=('x86_64')
license=('GPL')
url="https://wiki.gnome.org/Projects/NetworkManager"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'systemd' 'wpa_supplicant' 'ppp' 'dhcpcd'
         'modemmanager' 'libndp' 'libteam' 'libsoup') # 'libgudev' no longer needed for 1.8)
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection' 'netctl' 'python2-gobject3')
optdepends=('dhclient: alternative DHCP/DHCPv6 client'
            'iptables: Connection sharing'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support'
            'openresolv: openresolv support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
source=("https://download.gnome.org/sources/NetworkManager/${_pkgver}/${_pkgname}-${pkgver}.tar.xz"
        'NetworkManager.conf'
        'nm_exported.patch')
sha256sums=('169c34f50770e3c96b431f7d2cff654455246f2e6ccd46eccfb4454d4595625b'
            '57a0d0fc875a0c6b1729eac7e50907cc60a6d6aa834adc56b8c851edacf5fbd4'
            '13452fde1a87132835ad0fa3065744e3f30fa77c0ca27e84221805c4f2d2aa9e')

build() {
  cd ${_pkgname}-${pkgver}

  # https://forum.kaosx.us/d/1593-new-networkmanager-1-4-0-1-and-plasma-nm-icon
  #patch -p1 -i ${srcdir}/nm_exported.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/usr/sbin/dhcpcd \
    --with-pppd=/usr/sbin/pppd \
    --with-iptables=/usr/sbin/iptables \
    --with-dnsmasq=/usr/bin/dnsmasq \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-kernel-firmware-dir=/lib/firmware \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/sbin/resolvconf \
    --with-session-tracking=systemd \
    --with-modem-manager-1 \
    --disable-static \
    --enable-more-warnings=no \
    --enable-modify-system \
    --with-pppd-plugin-dir=/usr/lib/pppd/2.4.7

  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -m644 ../NetworkManager.conf ${pkgdir}/etc/NetworkManager/
  install -m755 -d ${pkgdir}/etc/NetworkManager/dnsmasq.d

  #rm -r ${pkgdir}/var/run
}
