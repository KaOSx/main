
pkgname=fprintd
pkgver=1.90.4
pkgrel=1
pkgdesc="Daemon that provides fingerprint scanning functionality over D-Bus."
arch=('x86_64')
url="https://fprint.freedesktop.org/"
license=('LGPL')
depends=('libfprint' 'glib2' 'polkit' 'dbus-glib')
makedepends=('meson' 'ninja' 'dbus-python3' 'python3-cairo')
options=('!emptydirs' '!libtool')
source=("https://gitlab.freedesktop.org/libfprint/fprintd/-/archive/v${pkgver}/fprintd-v${pkgver}.tar.bz2")
md5sums=('0c928894f291e5200f5fa8dbad220a9e')

prepare() {
  cd ${pkgname}-v${pkgver}
  
  sed -i -e "s|'dbusmock': true|'dbusmock': false|g" meson.build
  sed -i -e "s|'gi': true|'gi': false|g" meson.build
  sed -i -e "s|FPrint': true|FPrint': false|g" meson.build
  sed -i -e "s|subdir('tests')|#subdir('tests')|g" meson.build
}

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-v${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dman=false \
    -Dpam=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}


