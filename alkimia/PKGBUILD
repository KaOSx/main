
pkgname=alkimia
pkgver=7.0.1
pkgrel=1
arch=('x86_64')
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC."
url="http://kde-apps.org/content/show.php/libalkimia?content=137323"
depends=('kcoreaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'doxygen')
conflicts=('kf5-alkimia')
replaces=('kf5-alkimia')
license=('LGPL')
source=("https://download.kde.org/stable/alkimia/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('42c49025b3f8489dcbc5df55101ef95bc920e160b9d5ecf573a88062883931bb')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
 
  make DESTDIR=${pkgdir} install  
}
