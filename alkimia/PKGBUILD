
pkgname=alkimia
pkgver=8.1.0
pkgrel=1
arch=('x86_64')
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC."
url="https://community.kde.org/Alkimia/libalkimia"
depends=('kconfig' 'kcoreaddons' 'kcompletion' 'ki18n' 'kpackage' 'plasma-framework' 'knewstuff' 'kio'
         'kiconthemes' 'ktextwidgets' 'kxmlgui' 'qtwebengine')
makedepends=('extra-cmake-modules' 'kdoctools' 'doxygen')
license=('LGPL')
source=("https://download.kde.org/stable/alkimia/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('916807352707b0490cdd9ca65682eff73b00ca523029bda6fe7a2749a1bc927c')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_WEBKIT=OFF \
    -DBUILD_WITH_WEBENGINE=ON \
    -DAPPDATA_INSTALL_DIR=/usr/share \
    -DBUILD_TOOLS=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
