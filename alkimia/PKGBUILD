
pkgname=alkimia
pkgver=8.0.3
pkgrel=1
arch=('x86_64')
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC."
url="https://community.kde.org/Alkimia/libalkimia"
depends=('kconfig' 'kcoreaddons' 'kdelibs4support' 'kcompletion' 'ki18n' 'kpackage' 'plasma-framework' 'knewstuff' 'kio'
         'kiconthemes' 'ktextwidgets')
makedepends=('extra-cmake-modules' 'kdoctools' 'doxygen')
license=('LGPL')
source=("https://download.kde.org/stable/alkimia/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('1e57ed9279557d8e3575e7556a81b08dff4e882e9f5ad8502da63bfc98c7b33e')

build() {
  mkdir -p build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt5/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_WEBKIT=OFF \
    -DAPPDATA_INSTALL_DIR=/usr/share
  make
}

package() {
  cd build
 
  make DESTDIR=${pkgdir} install  
}
