
pkgname=ccid
pkgver=1.4.27
_pkgver=4218
pkgrel=1
pkgdesc="Generic USB Chip/Smart Card Interface Devices driver"
arch=('x86_64')
url="https://pcsclite.alioth.debian.org/ccid.html"
license=('LGPL' 'GPL')
depends=('pcsclite' 'libusbx' 'flex')
makedepends=('pkg-config')
backup=(etc/reader.conf.d/libccidtwin)
source=("https://alioth.debian.org/frs/download.php/file/${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('09f5a468902fcb6ea3bfb066fd097d84')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-twinserial \
    --enable-serialconfdir=/etc/reader.conf.d
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  # move the configuration file in /etc and create a symbolic link
  mv ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist ${pkgdir}/etc/libccid_Info.plist
  ln -s /etc/libccid_Info.plist ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist
  
  install -D src/92_pcscd_ccid.rules ${pkgdir}/etc/udev/rules.d/92_pcscd_ccid.rules
}
