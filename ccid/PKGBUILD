
pkgname=ccid
pkgver=1.5.0
pkgrel=1
pkgdesc="Generic USB Chip/Smart Card Interface Devices driver"
arch=('x86_64')
url="https://ccid.apdu.fr"
license=('LGPL' 'GPL')
depends=('pcsclite' 'libusbx' 'flex')
makedepends=('pkg-config')
backup=(etc/reader.conf.d/libccidtwin)
source=("https://ccid.apdu.fr/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f246d6601856775310c86b841b616de5')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-twinserial \
    --enable-serialconfdir=/etc/reader.conf.d
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist ${pkgdir}/etc/libccid_Info.plist
  ln -s /etc/libccid_Info.plist ${pkgdir}/usr/lib/pcsc/drivers/ifd-ccid.bundle/Contents/Info.plist

  install -D src/92_pcscd_ccid.rules ${pkgdir}/etc/udev/rules.d/92_pcscd_ccid.rules
}
