
pkgbase=gtkmm
pkgname=('gtkmm' 'gtkmm-docs')
pkgver=2.24.5
_pkgver=2.24
pkgrel=3
url="http://www.gtkmm.org/"
arch=('x86_64')
makedepends=('gtk2' 'pangomm' 'atkmm' 'glibmm-docs')
license=('LGPL')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/GNOME/sources/gtkmm/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('6c59ae8bbff48fad9132f23af347acf1')

build() {
  cd ${pkgbase}-${pkgver}

  ./configure --prefix=/usr
  make
}

package_gtkmm() {
  pkgdesc="C++ bindings for gtk2"
  depends=('gtk2' 'pangomm' 'atkmm')
  cd ${pkgbase}-${pkgver}

  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile

  make DESTDIR=${pkgdir} install
}

package_gtkmm-docs() {
  pkgdesc="Developer documentation for gtkmm v2"

  cd ${pkgbase}-${pkgver}
  make -C docs DESTDIR=${pkgdir} install
}
