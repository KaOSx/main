
pkgname=ms-gsl
pkgver=4.1.0
pkgrel=1
pkgdesc="Guideline Support Library implementation by Microsoft."
arch=('x86_64')
url="https://github.com/Microsoft/GSL"
license=('MIT')
makedepends=('cmake')
source=("https://github.com/microsoft/GSL/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('14255cb38a415ba0cc4f696969562be7d0ed36bbaf13c5e4748870babf130c48')

build() {
  cmake -B build -S GSL-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGSL_TEST=OFF
  cmake --build build
}

package() {
  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 GSL-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
