
pkgname=gnu-efi-libs
pkgver=3.0.11
_pkgver=3.0.11
pkgrel=1
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="https://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64')
options=('!strip' '!makeflags' '!buildflags')
makedepends=('pciutils')
source=("https://download.sourceforge.net/gnu-efi/gnu-efi-${pkgver}.tar.bz2")
sha1sums=('72277c84e9a5fbe5be989ac358b4b2561950854c')

build() {
	cd gnu-efi-${_pkgver}/
	
	make lib gnuefi inc apps
}

package() {
	cd gnu-efi-${_pkgver}/
	
	make INSTALLROOT=${pkgdir} PREFIX=/usr LIBDIR=/usr/lib install
	
	install -d ${pkgdir}/usr/share/gnu-efi/apps/x86_64/
	install -D -m0644 ${srcdir}/gnu-efi-${_pkgver}/x86_64/apps/*.efi ${pkgdir}/usr/share/gnu-efi/apps/x86_64/
}
