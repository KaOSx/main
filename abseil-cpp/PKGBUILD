
pkgname=abseil-cpp
pkgver=20200923.3
pkgrel=1
pkgdesc="Open-source collection of C++ code designed to augment the C++ standard library."
arch=('x86_64')
url="https://abseil.io/"
license=('Apache 2.0')
makedepends=('cmake')
source=("https://github.com/abseil/abseil-cpp/archive/${pkgver}.tar.gz")
md5sums=('daba6e99c7a84e2242a0107bbd873669')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DABSL_ENABLE_INSTALL=TRUE \
    -DABSL_RUN_TESTS=OFF \
    -DABSL_USE_GOOGLETEST_HEAD=OFF \
    -DCMAKE_CXX_STANDARD=17
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build
}
