
pkgname=gtkmm3
_pkgname=gtkmm
pkgver=3.24.5
_pkgver=3.24
pkgrel=3
url="https://www.gtkmm.org/"
arch=('x86_64')
pkgdesc="C++ bindings for gtk3"
depends=('gtk3' 'pangomm' 'atkmm' 'gdk-pixbuf2')
makedepends=('meson' 'ninja' 'mm-common')
license=('LGPL')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/GNOME/sources/gtkmm/${_pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('856333de86689f6a81c123f2db15d85db9addc438bc3574c36f15736aeae22e6')

build() {
  mkdir -p build
  cd build

  meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dbuild-demos=false \
    -Dbuild-tests=false \
    -Dbuild-documentation=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

