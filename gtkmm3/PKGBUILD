
pkgname=gtkmm3
_pkgname=gtkmm
pkgver=3.24.4
_pkgver=3.24
pkgrel=1
url="https://www.gtkmm.org/"
arch=('x86_64')
pkgdesc="C++ bindings for gtk3"
depends=('gtk3' 'pangomm' 'atkmm' 'gdk-pixbuf2')
makedepends=('meson' 'ninja')
license=('LGPL')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/GNOME/sources/gtkmm/${_pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('9beb71c3e90cfcfb790396b51e3f5e7169966751efd4f3ef9697114be3be6743')

build() {
  mkdir -p build
  cd build
  
  meson ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dbuild-demos=false \
    -Dbuild-tests=false \
    -Dbuild-documentation=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}

