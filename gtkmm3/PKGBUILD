
pkgname=gtkmm3
_pkgname=gtkmm
pkgver=3.24.7
_pkgver=3.24
pkgrel=3
url="https://www.gtkmm.org/"
arch=('x86_64')
pkgdesc="C++ bindings for gtk3"
depends=('gtk3' 'pangomm' 'atkmm' 'gdk-pixbuf2')
makedepends=('meson' 'ninja' 'mm-common')
license=('LGPL')
options=('!libtool' '!emptydirs')
source=("https://download.gnome.org/sources/gtkmm/${_pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('1d7a35af9c5ceccacb244ee3c2deb9b245720d8510ac5c7e6f4b6f9947e6789c')

build() {
  mkdir -p build
  cd build

  meson setup ../${_pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    -Dbuild-demos=false \
    -Dbuild-tests=false \
    -Dbuild-documentation=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}

