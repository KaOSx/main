
pkgname=libsoup
pkgver=2.68.2
_pkgver=2.68
pkgrel=1
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
url="https://www.gnome.org"
license=('LGPL')
depends=('glib2' 'libxml2' 'gnutls' 'sqlite3' 'glib-networking')
makedepends=('intltool' 'gobject-introspection' 'vala' 'meson' 'ninja')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/gnome/sources/libsoup/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('51ad3001a946fe3bcf29b692dc9ffe05cdf702ea6ca0ee8c3099a99a2f4e3933')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dgnome=false \
    -Dtls_check=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
