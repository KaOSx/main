
pkgname=libsoup
pkgver=2.68.4
_pkgver=2.68
pkgrel=1
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
url="https://www.gnome.org"
license=('LGPL')
depends=('glib2' 'libxml2' 'gnutls' 'sqlite3' 'glib-networking')
makedepends=('intltool' 'gobject-introspection' 'vala' 'meson' 'ninja')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/gnome/sources/libsoup/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2d50b12922cc516ab6a7c35844d42f9c8a331668bbdf139232743d82582b3294')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dgnome=false \
    -Dtls_check=false
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install
}
