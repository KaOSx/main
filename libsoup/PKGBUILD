
pkgname=libsoup
pkgver=2.74.2
_pkgver=2.74
pkgrel=1
pkgdesc="GNOME HTTP Library - base library"
arch=('x86_64')
url="https://www.gnome.org"
license=('LGPL')
depends=('glib2' 'gnutls' 'sqlite3' 'glib-networking')
makedepends=('intltool' 'gobject-introspection' 'vala' 'meson' 'ninja')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnome.org/pub/gnome/sources/libsoup/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f0a427656e5fe19e1df71c107e88dfa1b2e673c25c547b7823b6018b40d01159')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib/${pkgname} \
    -Dgnome=false \
    -Dtls_check=false
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
