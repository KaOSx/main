
pkgname=geoip-database
pkgver=20140107
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('x86_64')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
source=("GeoIP-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz"
        "GeoIPv6-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz"
        "http://geolite.maxmind.com/download/geoip/database/LICENSE.txt")
noextract=("GeoIP-${pkgver}.dat.gz"
           "GeoIPv6-${pkgver}.dat.gz")
md5sums=('ed08051b7418da82bd636fdb430aac8a'
         'ff5fe5a865e549df62d6b8770a3d5e05'
         'a1381bd1aa0a0c91dc31b3f1e847cf4a')

build() {
  cd "${srcdir}"

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c ${_database_name}-${pkgver}.dat.gz >${_database_name}.dat
  done
}

check() {
  cd "${srcdir}"

  for _database in {GeoIP,GeoIPv6}.dat; do
    if ! grep -q 'MaxMind Inc All Rights Reserved' ${_database}; then
      error "Copyright notice is missing from ${_database}; something is fishy!"
      return 1
    fi
  done
}

package() {
  cd "${srcdir}"

  install -d "${pkgdir}/usr/share/GeoIP"
  install -m0644 -t "${pkgdir}/usr/share/GeoIP" GeoIP.dat GeoIPv6.dat

  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
