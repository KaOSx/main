
pkgname=geoip-database
pkgver=20200610
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('x86_64')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
# version: curl -sI https://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz | grep ^Last-Modified: | cut -d' ' -f2- | tr -d '\r'
# https://mailfud.org/geoip-legacy/
source=("GeoIP-${pkgver}.dat.gz::https://mailfud.org/geoip-legacy/GeoIP.dat.gz"
        "GeoIPv6-${pkgver}.dat.gz::https://mailfud.org/geoip-legacy/GeoIPv6.dat.gz"
        "https://github.com/mschmitt/GeoLite2xtables/blob/master/LICENSE.txt")
noextract=("GeoIP-${pkgver}.dat.gz"
           "GeoIPv6-${pkgver}.dat.gz")
md5sums=('f22a3023a8540fc7777291348d479222'
         '001fa438682cd4f00f9988aee4e96540'
         '3c183a07d9417065a5ad5735497a0963')

build() {
  cd ${srcdir}

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c ${_database_name}-${pkgver}.dat.gz >${_database_name}.dat
  done
}

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/share/GeoIP
  install -m0644 -t ${pkgdir}/usr/share/GeoIP GeoIP.dat GeoIPv6.dat

  install -Dm0644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
