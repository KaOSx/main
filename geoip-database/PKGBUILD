
pkgname=geoip-database
pkgver=20181009
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('x86_64')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
# version: curl -sI https://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz | grep ^Last-Modified: | cut -d' ' -f2- | tr -d '\r'
source=("GeoIP-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz"
        "GeoIPv6-${pkgver}.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz"
        "http://geolite.maxmind.com/download/geoip/database/LICENSE.txt")
noextract=("GeoIP-${pkgver}.dat.gz"
           "GeoIPv6-${pkgver}.dat.gz")
md5sums=('d538e57ad9268fdc7955c6cf9a37c4a9'
         '54c5e3e98c1fe3f3a0aa51f1c4a88753'
         'a1381bd1aa0a0c91dc31b3f1e847cf4a')

build() {
  cd ${srcdir}

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c ${_database_name}-${pkgver}.dat.gz >${_database_name}.dat
  done
}

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/share/GeoIP
  install -m0644 -t ${pkgdir}/usr/share/GeoIP GeoIP.dat GeoIPv6.dat

  install -Dm0644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
