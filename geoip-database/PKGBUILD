
pkgname=geoip-database
pkgver=20241026
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('x86_64')
url="https://www.maxmind.com/en/geoip-databases"
license=('custom:OPEN DATA LICENSE')
# version: curl -sI https://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz | grep ^Last-Modified: | cut -d' ' -f2- | tr -d '\r'
# https://mailfud.org/geoip-legacy/
source=("GeoIP-${pkgver}.dat.gz::https://mailfud.org/geoip-legacy/GeoIP.dat.gz"
        "GeoIPv6-${pkgver}.dat.gz::https://mailfud.org/geoip-legacy/GeoIPv6.dat.gz"
        "https://github.com/mschmitt/GeoLite2xtables/blob/master/LICENSE.txt")
noextract=("GeoIP-${pkgver}.dat.gz"
           "GeoIPv6-${pkgver}.dat.gz")
md5sums=('a766a6de67e6280b433c29a41bac922f'
         '709a3166affc9c774a7ffadaf74b18ac'
         '8fd4bb5a1e35bdb7ba8dee32d34f1ce9')

build() {
  cd ${srcdir}

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c ${_database_name}-${pkgver}.dat.gz >${_database_name}.dat
  done
}

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/share/GeoIP
  install -m0644 -t ${pkgdir}/usr/share/GeoIP GeoIP.dat GeoIPv6.dat

  install -Dm0644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
