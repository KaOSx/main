
pkgname=libice
pkgver=1.0.10
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('glibc' 'xorgproto')
makedepends=('pkgconfig' 'xtrans')
options=('!libtool')
source=("https://xorg.freedesktop.org/releases/individual/lib/libICE-${pkgver}.tar.bz2")
license=('custom')
sha256sums=('6f86dce12cf4bcaf5c37dddd8b1b64ed2ddf1ef7b218f22b9942595fb747c348')

build() {
  cd libICE-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libICE-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
