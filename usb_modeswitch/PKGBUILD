
pkgname=usb_modeswitch
pkgver=2.6.0
pkgrel=1
_pkgdata=20191128
pkgdesc="Activating switchable USB devices on Linux."
arch=('x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL3')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make')
groups=('system')
backup=("etc/${pkgname}.conf")
source=("http://www.draisberghof.de/${pkgname}/usb-modeswitch-${pkgver}.tar.bz2"
        "http://www.draisberghof.de/${pkgname}/usb-modeswitch-data-${_pkgdata}.tar.bz2"
        "usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/${pkgname}/device_reference.txt"
        '40-usb_modeswitch.diff')
md5sums=('be73dcc84025794081a1d4d4e5a75e4c'
         'e8fce7eb949cbe16c61fb71bade4cc17'
         '91f3b3638f013fd343bd98b251ce646f'
         'fdeec64154f1e7d8f671c5e2d187ab46')

prepare() {
  cd usb-modeswitch-data-${_pkgdata}
  # exclude flip phones, see https://forum.kaosx.us/d/1790-kaos-and-doro-626/12
  patch -p1 -i ${srcdir}/40-usb_modeswitch.diff
}
         
build() {
  cd usb-modeswitch-${pkgver}/
  make clean
  
  make
}

package() {
  cd usb-modeswitch-${pkgver}/

  mkdir -p ${pkgdir}/usr/lib/udev
  mkdir -p ${pkgdir}/etc
  mkdir -p ${pkgdir}/usr/share/man/man1
  mkdir -p ${pkgdir}/usr/lib/systemd/system

  make DESTDIR=${pkgdir} UDEVDIR=${pkgdir}/usr/lib/udev SYSDIR=${pkgdir}/usr/lib/systemd/system install

  cp ${srcdir}/usb-modeswitch-${pkgver}-device_reference.txt ${pkgdir}/etc/usb_modeswitch.setup

  cd ${srcdir}/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=${pkgdir} install RULESDIR=${pkgdir}/usr/lib/udev/rules.d
}
