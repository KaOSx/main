
pkgname=libqmi
pkgver=1.24.14
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://cgit.freedesktop.org/libqmi"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev')
makedepends=('python3' 'help2man')
options=('!libtool')
source=("https://www.freedesktop.org/software/libqmi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('208fe3e94dabe1d8117d1023d841737b6fe92aaff21ef8f35bbed15417ee507f')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # one test fails since glib2 2.52.2
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
