
pkgname=libqmi
pkgver=1.24.6
pkgrel=2
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://cgit.freedesktop.org/libqmi"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev')
makedepends=('python3' 'help2man')
options=('!libtool')
source=("https://www.freedesktop.org/software/libqmi/${pkgname}-${pkgver}.tar.xz")
md5sums=('5d50233394a33e43dee3e70e197323e5')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # one test fails since glib2 2.52.2
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
