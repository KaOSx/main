
pkgname=libqmi
pkgver=1.34.0
pkgrel=3
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'libmbim')
makedepends=('meson' 'ninja' 'python3' 'help2man')
options=('!libtool')
source=("https://gitlab.freedesktop.org/mobile-broadband/libqmi/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('87be6e07b48e1429c3688cc547730c9a2389f9dd5bbe5fdf2b33280ef886e37e')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dintrospection=false \
    -Dgtk_doc=false \
    -Dcollection=basic \
    -Dqrtr=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
