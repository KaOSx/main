
pkgname=libqmi
pkgver=1.26.2
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://cgit.freedesktop.org/libqmi"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'libmbim')
makedepends=('python3' 'help2man')
options=('!libtool')
source=("https://www.freedesktop.org/software/libqmi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('133467b2fe0706850eb587c0103ae6e61b97ce6e2c440d66358f2b72b0db7b03')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib \
    --enable-collection=basic \
    --enable-introspection=no
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # one test fails since glib2 2.52.2
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
