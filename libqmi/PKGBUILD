
pkgname=libqmi
pkgver=1.32.0
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'libmbim')
makedepends=('meson' 'ninja' 'python3' 'help2man')
options=('!libtool')
source=("https://gitlab.freedesktop.org/mobile-broadband/libqmi/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('07a427e57d568b17b8158f47ad2500e67ecbe135c0864ffd9743105e199053ea')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dintrospection=false \
    -Dgtk_doc=false \
    -Dcollection=basic \
    -Dqrtr=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
