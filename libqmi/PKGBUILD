
pkgname=libqmi
pkgver=1.16.0
pkgrel=2
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="http://cgit.freedesktop.org/libqmi"
license=('GPL2')
depends=('glib2' 'bash')
makedepends=('python2' 'help2man')
options=('!libtool')
source=("http://www.freedesktop.org/software/libqmi/${pkgname}-${pkgver}.tar.xz")
md5sums=('4970c110f160b33637a3515004c637b2')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
