
pkgname=libqmi
pkgver=1.32.2
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'libmbim')
makedepends=('meson' 'ninja' 'python3' 'help2man')
options=('!libtool')
source=("https://gitlab.freedesktop.org/mobile-broadband/libqmi/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('1fde620bdbc28fac3ae3e6cc916025a66145e8736816d62137715559ee03ae63')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Dintrospection=false \
    -Dgtk_doc=false \
    -Dcollection=basic \
    -Dqrtr=false
  ninja
}

check() {
  cd build

  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
