
pkgname=libqmi
pkgver=1.26.8
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=('GPL2')
depends=('glib2' 'bash' 'libgudev' 'libmbim')
makedepends=('python3' 'help2man')
options=('!libtool')
source=("https://www.freedesktop.org/software/libqmi/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ef76dc95ab0a06321a1bd25e875489cba12c9f6611974ca0135cf067bb20c960')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib \
    --enable-collection=basic \
    --enable-introspection=no
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # one test fails since glib2 2.52.2
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
