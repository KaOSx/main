
pkgname=bluez
pkgver=5.46
pkgrel=2
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('x86_64')
license=('GPL2')
depends=('libical' 'dbus' 'glib2' 'systemd')
makedepends=('libusb')
optdepends=("cups: CUPS backend")
options=('!libtool')
backup=('etc/dbus-1/system.d/bluetooth.conf'
        'etc/bluetooth/main.conf')
install=bluez.install
source=("https://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.xz"
        'bluetooth.modprobe'
        'sdpd_request.patch')
md5sums=('913f35d6fa4ca5772c53adb936bf1947'
         '671c15e99d7154c2df987b71c5851b3d'
         '7da654eb47f7832e2e7c30f852d46c2f')

build() {
  cd ${pkgname}-${pkgver}
  # https://www.armis.com/blueborne/ CVE CVE-2017-1000250
  patch -p1 -i ${srcdir}/sdpd_request.patch
  
  autoreconf -vfi
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-sixaxis \
    --enable-library
  make
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/bluetooth
  install -Dm644 src/main.conf ${pkgdir}/etc/bluetooth/main.conf

  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis
  cp -a doc/*.txt ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis/

  install -dm755 ${pkgdir}/usr/lib/modprobe.d
  install -Dm644 ../bluetooth.modprobe ${pkgdir}/usr/lib/modprobe.d/bluetooth-usb.conf
}
