
pkgname=bluez
pkgver=5.68
pkgrel=1
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('x86_64')
license=('GPL2')
depends=('libical' 'dbus' 'glib2' 'systemd' 'json-c')
makedepends=('libusb' 'python3-docutils')
optdepends=("cups: CUPS backend")
options=('!libtool')
backup=('etc/dbus-1/system.d/bluetooth.conf'
        'etc/bluetooth/main.conf')
install=bluez.install
source=("https://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.xz"
        'bluetooth.modprobe')
sha256sums=('fc505e6445cb579a55cacee6821fe70d633921522043d322b696de0a175ff933'
            '46c021be659c9a1c4e55afd04df0c059af1f3d98a96338236412e449bf7477b4')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vfi
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-sixaxis \
    --enable-library \
    --enable-deprecated \
    --enable-mesh \
    --enable-hid2hci
  make
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/etc/bluetooth
  install -Dm644 src/main.conf ${pkgdir}/etc/bluetooth/main.conf

  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis
  cp -a doc/*.txt ${pkgdir}/usr/share/doc/${pkgname}/dbus-apis/

  install -dm755 ${pkgdir}/usr/lib/modprobe.d
  install -Dm644 ../bluetooth.modprobe ${pkgdir}/usr/lib/modprobe.d/bluetooth-usb.conf
}
