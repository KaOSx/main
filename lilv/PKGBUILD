
pkgname=lilv
pkgver=0.24.8
pkgrel=2
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('sratom' 'jack' 'python3')
makedepends=('swig')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a2eb196d97c3c3b21de77cfbe4489e51')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python3 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest

  python3 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python3 waf install --destdir=${pkgdir}

  install -Dm644 COPYING  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

