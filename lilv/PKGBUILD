
pkgname=lilv
pkgver=0.24.14
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="https://drobilla.net/software/lilv.html"
license=('custom:ISC')
depends=('sratom' 'python3')
makedepends=('swig')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2b129163c0ea2a13a06d70025170cb87')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python3 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest

  python3 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python3 waf install --destdir=${pkgdir}

  install -Dm644 COPYING  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

