
pkgname=lilv
pkgver=0.24.4
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('python2-numpy' 'sratom' 'jack')
makedepends=('swig')
source=("http://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fb340958a6df5a683bf25e291493dc4d')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest \
                        --bindings

  python2 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python2 waf install --destdir=${pkgdir}

  install -Dm644 COPYING  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

