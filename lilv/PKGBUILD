
pkgname=lilv
pkgver=0.24.2
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications"
arch=('x86_64')
url="http://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('python2-numpy' 'sratom' 'jack')
makedepends=('swig')
source=("http://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2")
md5sums=('cae7e1700f83b0f1272f437919a68458')

build() {
  cd ${pkgname}-${pkgver}
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest \
                        --bindings

  python2 waf build $MAKEFLAGS
}

package() {
  cd ${pkgname}-${pkgver}

  python2 waf install --destdir=${pkgdir}

  install -Dm644 COPYING  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

