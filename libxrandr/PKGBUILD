
pkgname=libxrandr
pkgver=1.5.1
pkgrel=1
pkgdesc="X11 RandR extension library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'libxrender' 'randrproto>=1.5.0')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xorg.freedesktop.org/releases/individual/lib/libXrandr-${pkgver}.tar.bz2")
md5sums=('28e486f1d491b757173dd85ba34ee884')

build() {
  cd libXrandr-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libXrandr-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
