
pkgname=texlive-bin
pkgver=2017.44590
pkgrel=9
pkgdesc="TeX Live binaries"
url='https://tug.org/texlive/'
license=('GPL')
arch=('x86_64')
depends=('t1lib' 'gd' 'poppler' 'libsigsegv' 'zziplib' 'libpng' 'libjpeg-turbo' 'freetype2'
         'cairo' 'pixman' 'graphite' 'icu' 'harfbuzz' 'libpaper')
makedepends=('perl' 'clisp' 'ffcall')
optdepends=('ed: for texconfig')
options=('!libtool' '!strip')
#install="texlive.install"
# full credit for this PKGBUILD goes to RÃ©my Oudompheng <remy@archlinux.org>
source=("https://mirrors.kernel.org/archlinux/other/texlive/texlive-bin-source-${pkgver}.tar.xz"
        'poppler0.59.patch'
        'poppler0.64.patch')
md5sums=('aef593aa0021ececdaeb0845e85ec70b'
         '555722f2912c01f18ace4f6b1996403c'
         '794ba80ebfea46fee4f2745c9d8bb9cf')

prepare() {
   cd source
   patch -p1 -i ${srcdir}/poppler0.59.patch
   patch -p1 -i ${srcdir}/poppler0.64.patch
   # t4ht expects to be in /usr/share/texmf/bin/t4ht
   sed -i s/SELFAUTOPARENT/TEXMFROOT/ texk/tex4htk/t4ht.c
   ## prevent compiling Xdvi with libXp
   sed -i~ 's|-lXp ||' texk/xdvik/configure
}

build() {
   #############################################################
   ### configure
   cd source
   mkdir -p Work
   cd Work
   
   echo "--> Initial configuration..."
   # we use temporary prefix to avoid messing the existing
   # $pkgdir/usr/share/texmf tree
   ../configure --prefix=/usr -C \
     --sysconfdir=/etc \
     --datarootdir=/usr/share \
     --datadir=/usr/share \
     --mandir=/usr/share/man \
     --disable-native-texlive-build \
     --with-banner-add="/KaOS" \
     --disable-multiplatform \
     --disable-dialog \
     --disable-psutils \
     --disable-t1utils \
     --disable-bibtexu \
     --disable-xz \
     --enable-shared \
     --disable-static \
     --with-system-zlib \
     --with-system-zziplib \
     --with-system-pnglib \
     --with-system-ncurses \
     --with-system-t1lib \
     --with-system-gd \
     --with-system-poppler \
     --with-system-xpdf \
     --with-system-freetype2 \
     --with-system-pixman \
     --with-system-cairo \
     --with-system-harfbuzz \
     --with-system-icu \
     --with-system-gmp \
     --with-system-mpfr \
     --with-system-libpaper \
     --with-system-graphite \
     --with-freetype2-libdir=/usr/lib \
     --with-freetype2-include=/usr/include/freetype2 \
     --with-xdvi-x-toolkit=xaw \
     --disable-dump-share \
     --disable-aleph \
     --enable-luatex \
     --with-clisp-runtime=default \
     --enable-xindy --disable-xindy-rules --disable-xindy-docs
   #############################################################
   ### make
   echo "-------------------------------------------------------"
   echo "--> Building the whole beast ..."
   echo "-------------------------------------------------------"
   make
}

package() {
   cd "$srcdir"

   #############################################################
   ### install
   cd source
   # fixes for xindy
   find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \;

   echo "-------------------------------------------------------"
   echo "--> Proceeding with make install ..."
   echo "-------------------------------------------------------"
   cd Work
   make DESTDIR="${pkgdir}" texmf="$pkgdir"/usr/share/texmf install
   rm -rf "${pkgdir}"/usr/{texmf,share/texmf-dist}

   ## symlink engines by hand. texlinks has moved to texlive-core...
   mkdir -p ${pkgdir}/usr/bin
   ln -s eptex ${pkgdir}/usr/bin/platex
   ln -s euptex ${pkgdir}/usr/bin/uplatex
   ln -s luatex ${pkgdir}/usr/bin/dvilualatex
   ln -s luatex ${pkgdir}/usr/bin/dviluatex
   ln -s luatex ${pkgdir}/usr/bin/lualatex
   ln -s pdftex ${pkgdir}/usr/bin/amstex
   ln -s pdftex ${pkgdir}/usr/bin/cslatex
   ln -s pdftex ${pkgdir}/usr/bin/csplain
   ln -s pdftex ${pkgdir}/usr/bin/eplain
   ln -s pdftex ${pkgdir}/usr/bin/etex
   ln -s pdftex ${pkgdir}/usr/bin/jadetex
   ln -s pdftex ${pkgdir}/usr/bin/latex
   ln -s tex ${pkgdir}/usr/bin/lollipop
   ln -s pdftex ${pkgdir}/usr/bin/mex
   ln -s pdftex ${pkgdir}/usr/bin/mllatex
   ln -s pdftex ${pkgdir}/usr/bin/mltex
   ln -s pdftex ${pkgdir}/usr/bin/pdfetex
   ln -s pdftex ${pkgdir}/usr/bin/pdfcslatex
   ln -s pdftex ${pkgdir}/usr/bin/pdfcsplain
   ln -s pdftex ${pkgdir}/usr/bin/pdfjadetex
   ln -s pdftex ${pkgdir}/usr/bin/pdflatex
   ln -s pdftex ${pkgdir}/usr/bin/pdfmex
   ln -s pdftex ${pkgdir}/usr/bin/pdfxmltex
   ln -s pdftex ${pkgdir}/usr/bin/texsis
   ln -s pdftex ${pkgdir}/usr/bin/utf8mex
   ln -s pdftex ${pkgdir}/usr/bin/xmltex
   ln -s xetex ${pkgdir}/usr/bin/xelatex

   #############################################################
   # remove dangling symlinks
   _bibtexextra_scripts="
bbl2bib
bibdoiadd
bibexport
bibmradd
bibzbladd
listbib
ltx2crossrefxml
multibibliography
urlbst
"
   _core_scripts="
a2ping
a5toa4
adhocfilelist
afm2afm
allcm
allec
allneeded
arara
arlatex
autoinst
bundledoc
checkcites
checklistings
chkweb
cjk-gs-integrate
context
contextjit
ctanify
ctanupload
de-macro
depythontex
deweb
dosepsbin
dtxgen
dvi2fax
dviasm
dvired
e2pall
epstopdf
findhyph
fmtutil
fmtutil-sys
fmtutil-user
fontinst
fragmaster
ht
htcontext
htlatex
htmex
httex
httexi
htxelatex
htxetex
installfont-tl
kpsepath
kpsetool
kpsewhere
kpsexpand
latex-git-log
latex-papersize
latex2man
latex2nemeth
latexdiff
latexdiff-vc
latexfileversion
latexindent
latexmk
latexpand
latexrevise
listings-ext.sh
ltxfileinfo
ltximg
lua2dox_filter
luaotfload-tool
luatools
lwarpmk
make4ht
match_parens
mf2pt1
mk4ht
mkjobtexmf
mkt1font
mktexfmt
mptopdf
mtxrun
mtxrunjit
ot2kpx
pdf180
pdf270
pdf90
pdfatfi
pdfbook
pdfbook2
pdfcrop
pdfflip
pdfjam
pdfjam-pocketmod
pdfjam-slides3up
pdfjam-slides6up
pdfjoin
pdflatexpicscale
pdfnup
pdfpun
pdfxup
pfarrei
pkfix
pkfix-helper
ps2eps
ps2frag
pslatex
purifyeps
pythontex
repstopdf
rpdfcrop
rungs
simpdftex
srcredact
sty2dtx
tex4ebook
texconfig
texconfig-dialog
texconfig-sys
texcount
texdef
texdiff
texdirflatten
texdoc
texdoctk
texexec
texfot
texindy
texlinks
texliveonfly
texloganalyser
texmfstart
texosquery
texosquery-jre5
texosquery-jre8
thumbpdf
typeoutfileinfo
updmap
updmap-sys
updmap-user
vpl2ovp
vpl2vpl
xhlatex
xindy
"
   _games_scripts="rubikrotation"
   _humanities_scripts="diadia"
   _langcyrillic_scripts="rubibtex rumakeindex"
   _langextra_scripts="ebong"
   _langgreek_scripts="mkgrkindex"
   _langjapanese_scripts="convbkmk ptex2pdf
kanji-fontmap-creator
kanji-config-updmap
kanji-config-updmap-sys
kanji-config-updmap-user
"
   _langkorean_scripts="jamo-normalize komkindex ttf2kotexfont"
   _latexextra_scripts="
authorindex
exceltex
makedtx
makeglossaries
makeglossaries-lite
pdfannotextractor
perltex
ps4pdf
splitindex
svn-multi
vpe
yplan"
   _music_scripts="lily-glyph-commands lily-image-commands lily-rebuild-pdfs m-tx musixtex musixflx pmxchords"
   _pictures_scripts="cachepic epspdf epspdftk fig4latex getmapdl mathspic mkpic pn2pdf"
   _pstricks_scripts="pedigree pst2pdf"
   _science_scripts="pygmentex ulqda"
   for s in \
        ${_bibtexextra_scripts} \
        ${_core_scripts}       \
        ${_games_scripts}       \
        ${_htmlxml_scripts}    \
        ${_langcyrillic_scripts} \
        ${_langextra_scripts}  \
        ${_langgreek_scripts}  \
        ${_langjapanese_scripts} \
        ${_langkorean_scripts} \
        ${_latexextra_scripts} \
        ${_music_scripts}      \
        ${_pictures_scripts}   \
        ${_pstricks_scripts}   \
        ${_science_scripts}    \
        tlmgr; do
       ! readlink -e "$pkgdir"/usr/bin/$s && rm "$pkgdir"/usr/bin/$s
   done
   ###################################################################
}

