

_extramodules=extramodules-5.3
_kver=$(cat /lib/modules/${_extramodules}/version)

pkgname=nvidia-bumblebee
pkgver=440.36
pkgrel=1
epoch=1
pkgdesc="NVIDIA drivers for linux. Packaged for Bumblebee"
arch=('x86_64')
url="https://www.nvidia.com/page/home.html"
depends=('linux>=5.3.6' 'linux<5.4' "nvidia-utils-bumblebee")
makedepends=('linux-headers')
provides=("nvidia=${pkgver}")
conflicts=('nvidia')
license=('custom')
install=nvidia.install
options=('!strip')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"
#_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
        'linux-4.10.patch')
#source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}.run")
md5sums=('52c480e1e14bc6b2ffe7ad9482d519f1'
         '4ab00fe5ab144083caa6c6994feea7e6')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    
    cd "${_pkg}"
    #patch -p1 -i ${srcdir}/linux-4.10.patch
    
    export IGNORE_CC_MISMATCH=yes
 
    cd "${srcdir}/${_pkg}/kernel" 
    make SYSSRC=/lib/modules/"${_kver}/build" module
}
 
package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    #install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-modeset.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-modeset.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-uvm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-uvm.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia-drm.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia-drm.ko"
    gzip "${pkgdir}/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
}
