
pkgname=cups-filters
pkgver=1.28.6
pkgrel=1
pkgdesc="OpenPrinting CUPS Filters"
arch=('x86_64')
url="http://www.linuxfoundation.org/collaborate/workgroups/openprinting"
license=('GPL')
depends=('lcms2' 'poppler' 'qpdf')
makedepends=('ghostscript' 'ttf-dejavu' 'python3') 
optdepends=('ghostscript: for non-PostScript printers to print with CUPS to convert PostScript to raster images'
            'foomatic-db: drivers use Ghostscript to convert PostScript to a printable form directly'
            'foomatic-db-engine: drivers use Ghostscript to convert PostScript to a printable form directly'
            'foomatic-db-nonfree: drivers use Ghostscript to convert PostScript to a printable form directly')
conflicts=('foomatic-filters')
provides=('foomatic-filters')
replaces=('foomatic-filters')
backup=(etc/fonts/conf.d/99pdftoopvp.conf)
options=('!libtool')
source=("https://www.openprinting.org/download/cups-filters/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/OpenPrinting/cups-filters/commit/50d984a621524c2e2857ad84eb4b71933c8821be.diff")
md5sums=('87cde9cdd8edd7d7443ec0ed2e61ebd4'
         '895186801ce1c988738e5306b09fe2c8')

build() {
  cd ${pkgname}-${pkgver}
  #patch -p1 -i ${srcdir}/50d984a621524c2e2857ad84eb4b71933c8821be.diff
  #sed -e "s|cpp/poppler-version.h|poppler/cpp/poppler-version.h|" -i filter/pdftoopvp/oprs/OPRS.cxx
  
  ./configure --prefix=/usr  \
    --sysconfdir=/etc \
    --with-rcdir=no \
    --enable-avahi \
    --with-test-font-path=/usr/share/fonts/TTF/DejaVuSans.ttf \
    --disable-mutool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=$pkgdir/ install

  rm -f ${pkgdir}/usr/lib/*.a
  # systemd service
  install -Dm644 utils/cups-browsed.service ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
  sed -i 's|cups.service|org.cups.cupsd.service|g' ${pkgdir}/usr/lib/systemd/system/cups-browsed.service
}
