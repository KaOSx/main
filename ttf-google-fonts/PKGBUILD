
pkgname=ttf-google-fonts
pkgver=20190926
_commit=ed510bbd260ce9a07dd0db6feb8cd1e75471df98
pkgrel=1
pkgdesc="TrueType fonts from the Google Fonts project"
arch=('x86_64')
url="https://github.com/google/fonts"
license=('various')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale')
makedepends=('git')
provides=('ttf-raleway')
conflicts=('ttf-raleway')
install=${pkgname}.install
source=("https://github.com/google/fonts/archive/${_commit}.zip")
md5sums=('597085d6e3ed1c2beb57886fb925fa6f')

package() {
  install -dm755 ${pkgdir}/usr/share/fonts/TTF
  find . -type f -name \*.ttf -exec install -Dm644 '{}' \
    ${pkgdir}/usr/share/fonts/TTF \;

  # remove droid, noto, cantarell & lohit font packages provided files
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Cantarell-*.ttf" -delete
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Droid*.ttf" -delete
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Noto*.ttf" -delete
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Lohit*.ttf" -delete
  
  find . -type f -name OFL.txt -exec install -Dm644 '{}' \
    ${pkgdir}/usr/share/licenses/${pkgname}/{} \;
  install -Dm644 ${srcdir}/fonts-${_commit}/ufl/ubuntu/UFL.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/UFL.txt
}
