
pkgname=ttf-google-fonts
pkgver=20161115
_commit=d9f970f66c8a6d2f7ca683948401ce854a5844be
pkgrel=1
pkgdesc="TrueType fonts from the Google Fonts project"
arch=('x86_64')
url="https://github.com/google/fonts"
license=('various')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontdir' 'xorg-mkfontscale')
makedepends=('git')
provides=('ttf-raleway')
conflicts=('ttf-raleway')
install=${pkgname}.install
source=("https://github.com/google/fonts/archive/${_commit}.zip")
md5sums=('b65e4c3c8d95bc1aa2ae6e542903da63')

package() {
  install -dm755 ${pkgdir}/usr/share/fonts/TTF
  find . -type f -name \*.ttf -exec install -Dm644 '{}' \
    ${pkgdir}/usr/share/fonts/TTF \;

  # remove droid, noto & cantarell font packages provided files
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Cantarell-*.ttf" -delete
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Droid*.ttf" -delete
  find ${pkgdir}/usr/share/fonts/TTF -type f -name "Noto*.ttf" -delete
  
  find . -type f -name OFL.txt -exec install -Dm644 '{}' \
    ${pkgdir}/usr/share/licenses/${pkgname}/{} \;
  install -Dm644 ${srcdir}/fonts-${_commit}/ufl/ubuntu/UFL.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/UFL.txt
}
