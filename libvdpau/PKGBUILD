
pkgname=libvdpau
pkgver=1.3
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/vdpau/libvdpau"
depends=('gcc-libs' 'libxext')
makedepends=('dri2proto' 'meson' 'ninja')
options=('!libtool')
license=('custom')
source=("https://gitlab.freedesktop.org/vdpau/libvdpau/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('b5a52eeac9417edbc396f26c40591ba5df0cd18285f68d84614ef8f06196e50e')

build() {
  mkdir -p build
  cd build
  
  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release 
  ninja
}

package() {
  cd build
  
  DESTDIR=${pkgdir} ninja install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ 
}
