
pkgname=libvdpau
pkgver=1.2
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/vdpau/libvdpau"
depends=('gcc-libs' 'libxext')
makedepends=('dri2proto')
options=('!libtool')
license=('custom')
source=("https://gitlab.freedesktop.org/vdpau/libvdpau/uploads/14b620084c027d546fa0b3f083b800c6/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('6a499b186f524e1c16b4f5b57a6a2de70dfceb25c4ee546515f26073cd33fa06')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ 
}
