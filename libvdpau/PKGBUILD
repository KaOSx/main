
pkgname=libvdpau
pkgver=1.5
pkgrel=2
pkgdesc="Nvidia VDPAU library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/vdpau/libvdpau"
depends=('gcc-libs' 'libxext')
makedepends=('dri2proto' 'meson' 'ninja')
options=('!libtool')
license=('custom')
source=("https://gitlab.freedesktop.org/vdpau/libvdpau/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('a5d50a42b8c288febc07151ab643ac8de06a18446965c7241f89b4e810821913')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --buildtype=release 
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ 
}
