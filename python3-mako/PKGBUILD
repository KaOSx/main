
pkgname=python3-mako
_pkgname=Mako
pkgver=1.1.5
pkgrel=1
arch=('x86_64')
pkgdesc="Template library written in Python to provide a familiar, non-XML syntax which compiles into Python modules for maximum performance"
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python3-setuptools' 'python3-markupsafe')
source=("https://pypi.io/packages/source/M/Mako/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('169fa52af22a91900d852e937400e79f535496191c63712e3b9fda5a9bed6fc3')

build() {
    cd ${_pkgname}-${pkgver}

    python3 setup.py build
}

package() {
    cd ${_pkgname}-${pkgver}
    python3 setup.py install --root=${pkgdir} --optimize=1

    install -D LICENSE ${pkgdir}/usr/share/licenses/python-mako/COPYING
}
