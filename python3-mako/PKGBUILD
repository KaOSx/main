
pkgname=python3-mako
_pkgname=Mako
pkgver=1.3.1
pkgrel=1
arch=('x86_64')
pkgdesc="Template library written in Python to provide a familiar, non-XML syntax which compiles into Python modules for maximum performance"
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python3-setuptools' 'python3-markupsafe')
makedepends=('python3-build' 'python3-installer' 'python3-wheel')
source=("https://pypi.io/packages/source/M/Mako/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('baee30b9c61718e093130298e678abed0dbfa1b411fcc4c1ab4df87cd631a0f2')

build() {
    cd ${_pkgname}-${pkgver}

    python3 -m build -nw
}

package() {
    cd ${_pkgname}-${pkgver}
    python3 -m installer --destdir=${pkgdir} dist/*.whl

    install -D LICENSE ${pkgdir}/usr/share/licenses/python-mako/COPYING
}
