
_doc_root=/usr/share/nginx
_server_root=/etc/nginx
_conf_path=${_server_root}/conf
_tmp_path=/var/lib/nginx
_log_path=/var/log/nginx
_user=http
_group=http
# even numbers are for stable
pkgname=nginx
pkgver=1.18.0
pkgrel=1
pkgdesc="Small, but very powerful and efficient web server and mail proxy"
arch=('x86_64')
depends=('pcre' 'zlib' 'openssl' 'geoip' 'gd')
url="https://nginx.org"
license=('2-clause BSD')
backup=("etc/nginx/conf/fastcgi.conf"
        "etc/nginx/conf/fastcgi_params"
        "etc/nginx/conf/koi-win"
        "etc/nginx/conf/koi-utf"
        "etc/nginx/conf/mime.types"
        "etc/nginx/conf/nginx.conf"
        "etc/nginx/conf/scgi_params"
        "etc/nginx/conf/uwsgi_params"
        "etc/nginx/conf/win-utf"
        "etc/logrotate.d/nginx")
source=("https://nginx.org/download/nginx-${pkgver}.tar.gz"
        'nginx.service'
        'logrotate')
md5sums=('b2d33d24d89b8b1f87ff5d251aa27eb8'
         'c5b7a8068499262a4e40f890c2b855d3'
         '19a26a61c8afe78defb8b4544f79a9a0')

build() {
    cd nginx-${pkgver}
    ./configure \
        --prefix=${_server_root} \
        --sbin-path=/usr/sbin/nginx \
        --pid-path=/run/nginx.pid \
        --lock-path=/run/lock/nginx.lock \
        --http-client-body-temp-path=${_tmp_path}/client_body_temp \
        --http-proxy-temp-path=${_tmp_path}/proxy_temp \
        --http-fastcgi-temp-path=${_tmp_path}/fastcgi_temp \
        --http-log-path=${_log_path}/access.log \
        --error-log-path=stderr \
        --user=${_user} --group=${_group} \
        --with-http_stub_status_module \
        --with-http_dav_module \
        --with-http_gzip_static_module \
        --http-scgi-temp-path=${_tmp_path}/scgi \
        --http-uwsgi-temp-path=${_tmp_path}/uwsgi \
        --with-mail \
        --with-mail_ssl_module \
        --with-pcre-jit \
        --with-file-aio \
        --with-http_addition_module \
        --with-http_auth_request_module \
        --with-http_dav_module \
        --with-http_degradation_module \
        --with-http_flv_module \
        --with-http_geoip_module \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_image_filter_module \
        --with-http_mp4_module \
        --with-http_random_index_module \
        --with-http_realip_module \
        --with-http_secure_link_module \
        --with-http_v2_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --with-http_sub_module \

    make
}

package() {
	cd nginx-${pkgver}
	make DESTDIR=${pkgdir} install

	sed -i -e "s|\<user\s\+\w\+;|user $_user;|g" \
            -e '44s|html|/usr/share/nginx/html|' \
            -e '54s|html|/usr/share/nginx/html|' ${pkgdir}/${_conf_path}/nginx.conf

	install -d ${pkgdir}/${_tmp_path}

	# move default document root outside server root
	install -d ${pkgdir}/${_doc_root}
	mv ${pkgdir}/${_server_root}/html/ ${pkgdir}/${_doc_root}/
	
	rm -rf ${pkgdir}/run

	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/nginx/LICENSE
	install -Dm644 ${srcdir}/nginx.service ${pkgdir}/usr/lib/systemd/system/nginx.service
	install -Dm644 ${srcdir}/logrotate ${pkgdir}/etc/logrotate.d/nginx
}
