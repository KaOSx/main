
pkgname=libvorbis
pkgver=1.3.7
pkgrel=1
pkgdesc="General purpose audio and music encoding format contemporary to MPEG-4's AAC and TwinVQ"
arch=('x86_64')
license=('custom')
url="https://www.xiph.org/vorbis/"
depends=('libogg')
options=('!libtool')
source=("https://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b33cc4934322bcbf6efcbacf49e3ca01aadbea4114ec9589d1b1e9d20f72954b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  
  make check
}


package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
