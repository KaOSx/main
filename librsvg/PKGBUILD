
pkgname=librsvg
pkgver=2.58.0
_pkgver=2.58
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="https://live.gnome.org/LibRsvg"
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gobject-introspection' 'rust' 'python3-docutils')
options=('!libtool')
source=("https://ftp.gnome.org/pub/gnome/sources/librsvg/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d7c444a926406b59790be0deae196e18ed26059da573fa1aa9ec9ca7658a559c')

build() {
  cd ${pkgname}-${pkgver}
  # issue with current cargo https://github.com/rust-lang/cargo/issues/3340#issuecomment-263699137
  #export SSL_CERT_FILE=/etc/ssl/certs/ca-certificates.crt

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --disable-gtk-doc
  make 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
