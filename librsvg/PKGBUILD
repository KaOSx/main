
pkgname=librsvg
pkgver=2.59.2
_pkgver=2.59
pkgrel=1
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=('x86_64')
url="https://live.gnome.org/LibRsvg"
license=('LGPL')
depends=('dav1d' 'gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('gobject-introspection' 'cargo-c' 'python3-docutils' 'meson' 'ninja')
options=('!libtool')
source=("https://ftp.gnome.org/pub/gnome/sources/librsvg/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ecd293fb0cc338c170171bbc7bcfbea6725d041c95f31385dc935409933e4597')

prepare() {
  cd ${pkgname}-${pkgver}

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=/usr/lib \
    -Ddocs=disabled
  ninja
}

package() {
  cd build
  DESTDIR=${pkgdir} ninja install
}
