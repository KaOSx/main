
pkgname=dbus-sharp
pkgver=0.7.0
pkgrel=2
pkgdesc="C# implementation of D-Bus"
arch=('any')
url="http://github.com/mono/dbus-sharp/"
license=('custom')
depends=('mono')
makedepends=('pkgconfig')
options=(!makeflags)
source=("https://github.com/downloads/mono/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	'dbus-sharp-0.7.0-nogtk.patch')
md5sums=('1964fc341dcbaeda859c53cee295d042'
	 '2ac099fbfdbd206d57dd1e5cf3ab4e97')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  patch -Np1 -i ${srcdir}/dbus-sharp-0.7.0-nogtk.patch

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -D COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
