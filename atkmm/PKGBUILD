
pkgbase=atkmm
pkgname=('atkmm' 'atkmm-docs')
pkgver=2.24.2
_pkgver=2.24
pkgrel=2
arch=('x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=("http://ftp.gnome.org/pub/GNOME/sources/atkmm/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('d53b60b0f1be597e86070954a49cf0c3')

build() {
  cd ${pkgbase}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd ${pkgbase}-${pkgver}
  make -C doc DESTDIR=${pkgdir} install
}
