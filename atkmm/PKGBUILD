
pkgbase=atkmm
pkgname=('atkmm' 'atkmm-docs')
pkgver=2.28.0
_pkgver=2.28
pkgrel=5
arch=('x86_64')
makedepends=('atk' 'glibmm' 'glibmm-docs' 'libsigc++')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=("http://ftp.gnome.org/pub/GNOME/sources/atkmm/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
sha256sums=('4c4cfc917fd42d3879ce997b463428d6982affa0fb660cafcc0bc2d9afcedd3a')

build() {
  cd ${pkgbase}-${pkgver}
  
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk' 'glibmm' 'libsigc++')

  cd ${pkgbase}-${pkgver}
  sed -i -e 's|^doc_subdirs|#doc_subdirs|' Makefile
  make DESTDIR=${pkgdir} install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd ${pkgbase}-${pkgver}
  make -C doc DESTDIR=${pkgdir} install
}
