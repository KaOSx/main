
pkgname=sdl2
pkgver=2.0.16
pkgrel=2
pkgdesc="Library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64')
url="https://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxcursor' 'libxext' 'libxrender' 'libx11' 'libglvnd' 'pulseaudio'
         'alsa-lib' 'mesa' 'wayland-protocols' 'pipewire')
makedepends=('cmake' 'libxrandr' 'libxinerama' 'libxkbcommon' 'fcitx')
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")
md5sums=('98b8a1535a757ea1d03ae44e2fb20247')

build() {
cmake -B build -S SDL2-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSDL_STATIC=OFF \
    -DARTS=OFF \
    -DESD=OFF \
    -DNAS=OFF \
    -DALSA=ON \
    -DHIDAPI=ON \
    -DPULSEAUDIO_SHARED=ON \
    -DVIDEO_WAYLAND=ON \
    -DRPATH=OFF \
    -DCLOCK_GETTIME=ON \
    -DJACK_SHARED=ON \
    -DPIPEWIRE_SHARED=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 SDL2-${pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
