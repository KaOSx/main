
pkgname=sdl2
pkgver=2.0.6
pkgrel=1
pkgdesc="Library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64')
url="https://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxcursor' 'libxext' 'libxrender' 'libx11' 'libgl' 'pulseaudio'
         'alsa-lib' 'mesa')
makedepends=('libxrandr' 'libxinerama' 'libxkbcommon' 'fcitx')
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz")
md5sums=('4c2d602766b48b8f1b19a6f903ef5ecf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  
  ../SDL2-${pkgver}/configure --prefix=/usr \
    --enable-sdl-dlopen \
    --disable-arts \
    --disable-esd \
    --disable-nas \
    --enable-alsa \
    --enable-pulseaudio-shared \
    --enable-video-wayland \
    --enable-fcitx \
    --disable-rpath
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../SDL2-${pkgver}/COPYING.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
