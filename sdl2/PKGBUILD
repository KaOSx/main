
pkgname=sdl2
pkgver=2.30.9
pkgrel=1
pkgdesc="Library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64')
url="https://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxcursor' 'libxext' 'libxrender' 'libx11' 'libglvnd' 'pulseaudio'
         'alsa-lib' 'mesa' 'wayland-protocols')
makedepends=('cmake' 'libxrandr' 'libxinerama' 'libxkbcommon' 'fcitx' 'pipewire')
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz"
        "https://github.com/libsdl-org/SDL/commit/e2ade2bfc46d915cd306c63c830b81d800b2575f.diff")
md5sums=('57393a5e1a46dd19ae40968a301c70a3'
         '4a5a38720e477251ab7120f731c79146')

prepare() {
  cd SDL2-${pkgver}
  # wayland 1.20 needed
  #patch -p1 -i ${srcdir}/e2ade2bfc46d915cd306c63c830b81d800b2575f.diff
}

build() {
  cmake -B build -S SDL2-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSDL_STATIC=OFF \
    -DSDL_ARTS=OFF \
    -DSDL_ESD=OFF \
    -DSDL_NAS=OFF \
    -DSDL_ALSA=ON \
    -DSDL_HIDAPI=ON \
    -DSDL_PULSEAUDIO_SHARED=ON \
    -DSDL_WAYLAND_SHARED=ON \
    -DSDL_RPATH=OFF \
    -DSDL_CLOCK_GETTIME=ON \
    -DSDL_JACK_SHARED=ON \
    -DSDL_PIPEWIRE_SHARED=ON
  cmake --build build
}

package() {

  DESTDIR=${pkgdir} cmake --install build

  install -Dm644 SDL2-${pkgver}/LICENSE.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
