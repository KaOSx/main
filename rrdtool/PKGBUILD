
pkgname=rrdtool
pkgver=1.7.2
pkgrel=1
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="https://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango' 'ttf-dejavu' 'ruby' 'python2' 'tcl' 'lua' 'perl')
makedepends=('intltool' 'groff' 'python2-setuptools')
options=('!libtool' '!emptydirs' '!makeflags')
source=("https://oss.oetiker.ch/rrdtool/pub/rrdtool-${pkgver}.tar.gz")
md5sums=('638e3a0a1609d97e9f82e8832759dcd2')

build() {
  cd ${pkgname}-${pkgver}
  sed -e 's|$(RUBY) extconf.rb|& --vendor|' -i bindings/Makefile.in

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --disable-rpath \
    --enable-perl \
    --enable-perl-site-install \
    --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby \
    --enable-ruby-site-install \
    --enable-python \
    --enable-lua \
    --enable-lua-site-install \
    --enable-tcl \
    --disable-libwrap
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} includedir=/usr/include install
  
  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
