
pkgname=rrdtool
pkgver=1.5.6
pkgrel=1
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="http://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango' 'ttf-dejavu' 'ruby' 'python2' 'tcl' 'lua')
makedepends=('intltool' 'groff')
options=('!libtool' '!emptydirs' '!makeflags')
source=("http://oss.oetiker.ch/rrdtool/pub/rrdtool-${pkgver}.tar.gz")
md5sums=('c3703bca0472e43930e3ddaa848446fc')

build() {
  cd ${pkgname}-${pkgver}
  sed -e 's|$(RUBY) extconf.rb|& --vendor|' -i bindings/Makefile.in

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --disable-rpath \
    --enable-perl \
    --enable-perl-site-install \
    --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby \
    --enable-ruby-site-install \
    --enable-python \
    --enable-lua \
    --enable-lua-site-install \
    --enable-tcl \
    --disable-libwrap
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} includedir=/usr/include install
  
  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
