
pkgname=rrdtool
pkgver=1.9.0
pkgrel=1
pkgdesc="Data logging and graphing application"
arch=('x86_64')
url="https://www.rrdtool.org"
license=('GPL')
depends=('libpng' 'libxml2' 'pango' 'ttf-dejavu' 'ruby' 'tcl' 'lua' 'perl')
makedepends=('intltool' 'groff')
options=('!libtool' '!emptydirs' '!makeflags')
source=("https://github.com/oetiker/rrdtool-1.x/releases/download/v${pkgver}/rrdtool-${pkgver}.tar.gz")
md5sums=('bcbdb069e74fc866fb92deb6697c5392')

build() {
  cd ${pkgname}-${pkgver}
  #sed -e 's|$(RUBY) extconf.rb|& --vendor|' -i bindings/Makefile.in

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --disable-rpath \
    --enable-perl \
    --enable-perl-site-install \
    --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby \
    --enable-ruby-site-install \
    --disable-python \
    --enable-lua \
    --enable-tcl \
    --disable-libwrap
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} includedir=/usr/include install

  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
