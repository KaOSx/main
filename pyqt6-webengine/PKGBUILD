
pkgname=pyqt6-webengine
_pkgname=PyQt6_WebEngine
pkgver=6.5.0
pkgrel=1
pkgdesc="Set of Python3 bindings for the Qt Company's Qt6 WebEngine libraries"
arch=('x86_64')
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=('GPL')
depends=('qt6-webengine' 'pyqt6-python3')
makedepends=('pyqt-builder' 'python3-setuptools')
source=("https://pypi.python.org/packages/source/P/PyQt6-WebEngine/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('8ba9db56c4c181a2a2fab1673ca35e5b63dc69113f085027ddc43c710b6d6ee9')

build() {
  cd ${_pkgname}-${pkgver}

  sip-build \
    --api-dir /usr/share/qt6/qsci/api/python/ \
    --qmake /usr/lib/qt6/bin/qmake \
    --no-make
  cd build
  make
}

package(){
  cd ${_pkgname}-${pkgver}/build

  make DESTDIR=${pkgdir} INSTALL_ROOT=${pkgdir} install
}
