
pkgname=cairo
pkgver=1.17.6
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="https://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'sh' 'mesa'
         'libglvnd' 'lzo2')
makedepends=('librsvg' 'poppler' 'libspectre' 'meson' 'ninja')
options=('!libtool')
#source=("https://www.cairographics.org/snapshots/${pkgname}-${pkgver}.tar.xz")
source=("https://gitlab.freedesktop.org/cairo/cairo/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('51f2e2e5525665c0b25bea18826d2283a76801a9')

build() {
  mkdir -p build
  cd build

  meson ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -Dtee=enabled \
    -Dgl-backend=gl \
    -Dtests=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
