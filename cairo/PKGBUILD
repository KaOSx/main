
pkgname=cairo
pkgver=1.17.8
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="https://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'sh' 'mesa'
         'libglvnd' 'lzo2')
makedepends=('librsvg' 'poppler' 'libspectre' 'meson' 'ninja')
options=('!libtool')
source=("https://www.cairographics.org/snapshots/${pkgname}-${pkgver}.tar.xz")
#source=("https://gitlab.freedesktop.org/cairo/cairo/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('5b10c8892d1b58d70d3f0ba5b47863a061262fa56b9dc7944161f8c8b783bc64')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -Dtee=disabled \
    -Dtests=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
