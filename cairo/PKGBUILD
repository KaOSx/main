
pkgname=cairo
pkgver=1.14.6
pkgrel=3
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.6.17' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'sh' 'mesa'
         'libgl' 'lzo2')
makedepends=('librsvg' 'poppler-glib' 'libspectre')
options=('!libtool')
source=("http://cairographics.org/releases/${pkgname}-${pkgver}.tar.xz")
md5sums=('23a0b2f0235431d35238df1d3a517fdb')

build() {
  cd ${pkgname}-${pkgver}

#  autoreconf -vfi
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--enable-gl \
	--enable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--disable-xlib-xcb 

  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
}
