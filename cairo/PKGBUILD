
pkgname=cairo
pkgver=1.18.0
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="https://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman' 'glib2' 'sh' 'mesa'
         'libglvnd' 'lzo2')
makedepends=('librsvg' 'poppler' 'libspectre' 'meson' 'ninja')
options=('!libtool')
source=("https://www.cairographics.org/releases/${pkgname}-${pkgver}.tar.xz")
#source=("https://gitlab.freedesktop.org/cairo/cairo/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('243a0736b978a33dee29f9cca7521733b78a65b5418206fef7bd1c3d4cf10b64')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --sysconfdir=/etc \
    --localstatedir=/var \
    -Dtee=disabled \
    -Dtests=disabled
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}
