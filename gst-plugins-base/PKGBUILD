
pkgname=gst-plugins-base
pkgver=1.16.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
url="https://gstreamer.freedesktop.org/"
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango' 'opus' 'iso-codes')
makedepends=('pkgconfig' 'gobject-introspection' 'qt5-base' 'python3')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-base/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4093aa7b51e28fb24dfd603893fead8d1b7782f088b05ed0f22a21ef176fb5ae')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Base (KaOS)" \
    --with-package-origin="https://kaosx.us/" \
    PYTHON=/usr/bin/python3
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 3 failing elements/glimagesink, elements/multisocketsink & pipelines/gl-launch-lines
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  make -C ext DESTDIR=${pkgdir} install
}

