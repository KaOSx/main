
pkgname=gst-plugins-base
pkgver=1.16.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
url="https://gstreamer.freedesktop.org/"
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango' 'opus' 'iso-codes')
makedepends=('pkgconfig' 'gobject-introspection' 'qt5-base' 'python3')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-base/${pkgname}-${pkgver}.tar.xz")
sha256sums=('b13e73e2fe74a4166552f9577c3dcb24bed077021b9c7fa600d910ec6987816a')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Base (KaOS)" \
    --with-package-origin="https://kaosx.us/" 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 3 failing elements/glimagesink, elements/multisocketsink
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  make -C ext DESTDIR=${pkgdir} install
}

