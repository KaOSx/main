
pkgname=gst-plugins-base
pkgver=1.14.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
url="https://gstreamer.freedesktop.org/"
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango' 'opus' 'iso-codes')
makedepends=('pkgconfig' 'gobject-introspection' 'qt5-base')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-base/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a4b7e80ba869f599307449b17c9e00b5d1e94d3ba1d8a1a386b8770b2ef01c7c')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Base (KaOS)" \
    --with-package-origin="https://kaosx.us/" 
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # 2 failing elements/glimagesink, libs/gstglcontext
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  make -C ext DESTDIR=${pkgdir} install
}

