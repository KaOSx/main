
pkgname=gst-plugins-base
pkgver=1.8.2
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango')
makedepends=('pkgconfig' 'gobject-introspection')
options=('!libtool' '!emptydirs')
url="http://gstreamer.freedesktop.org/"
source=("http://gstreamer.freedesktop.org/src/gst-plugins-base/${pkgname}-${pkgver}.tar.xz")
md5sums=('f55254ca18b845a9a7d8fe671bc09c24')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer Base (KaOS)" \
    --with-package-origin="http://kaosx.us/" 
  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd ${pkgname}-${pkgver}
 
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  make -C ext DESTDIR=${pkgdir} install
}

