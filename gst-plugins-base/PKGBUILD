
pkgname=gst-plugins-base
pkgver=1.22.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
url="https://gstreamer.freedesktop.org/"
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango' 'opus' 'iso-codes')
makedepends=('pkgconfig' 'gobject-introspection' 'qt5-x11extras' 'qt5-tools'
             'python3' 'meson' 'ninja' 'graphene')
options=('!libtool' '!emptydirs')
source=("https://gstreamer.freedesktop.org/src/gst-plugins-base/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f53672294f3985d56355c8b1df8f6b49c8c8721106563e19f53be3507ff2229d')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release \
    --libexecdir=lib \
    -Dpackage-name="GStreamer (KaOS)" \
    -Dpackage-origin="https://kaosx.us/" \
    -Dgobject-cast-checks=disabled
  ninja
}

check() {
  cd build
  # 117/121 fail, elements_glimagesink             FAIL
  # 91/121 elements_textoverlay
  meson test
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
}


