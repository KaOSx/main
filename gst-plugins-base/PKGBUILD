
pkgname=gst-plugins-base
pkgver=1.2.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base Plugins"
arch=('x86_64')
license=('LGPL')
depends=("gstreamer>=${pkgver}" 'orc' 'libxv' 'alsa-lib' 'cdparanoia' 'libvisual'
         'libvorbis' 'libtheora' 'pango')
makedepends=('pkgconfig' 'gobject-introspection')
options=(!libtool !emptydirs)
url="http://gstreamer.freedesktop.org/"
source=("${url}/src/$pkgname/$pkgname-${pkgver}.tar.xz")
md5sums=('2e75fb03ed7e4d24d88601fcac4d57aa')

build() {
  cd $pkgname-$pkgver
  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  
  aclocal -I m4 -I common/m4
  autoconf
  automake --add-missing
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-experimental \
    --with-package-name="GStreamer (KaOS)" \
    --with-package-origin="http://kaosx.us/" 
  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

check() {
  cd $pkgname-$pkgver
 
  make check
}

package() {
  cd $pkgname-$pkgver
  
  make DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
}

