
pkgname=gpgme
pkgver=1.23.1
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=('x86_64')
url="https://www.gnupg.org/related_software/gpgme/"
license=('GPL')
depends=('libgpg-error' 'gnupg')
makedepends=('qt5-base')
provides=('gpgmepp')
replaces=('gpgmepp')
conflicts=('gpgmepp')
options=('!libtool' '!emptydirs')
source=("https://www.gnupg.org/ftp/gcrypt/gpgme/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('a0c316f7ab7d3bfb01a8753c3370dc906e5b61436021f3b54ff1483b513769bd')

build() {
  mkdir -p build
  cd build

  ../${pkgname}-${pkgver}/configure --prefix=/usr \
       --disable-static \
       --disable-gpgsm-test \
       --disable-fd-passing
  make
}

check() {
  cd build
  # no FAIL
  make check
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
