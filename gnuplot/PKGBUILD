
pkgname=gnuplot 
pkgver=5.2.6
pkgrel=2
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'pango' 'libjpeg-turbo' 'qt5-svg' 'gnutls') 
makedepends=('texinfo' 'texlive-core' 'texlive-latexextra' 'qt5-tools') 
install=gnuplot.install 
source=("https://downloads.sourceforge.net/sourceforge/gnuplot/${pkgname}-${pkgver}.tar.gz")
md5sums=('4be89e2a003af8c414239db5d370c441')

build() {
  cd ${pkgname}-${pkgver}

  # adjust default source location 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c
  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c
  # glib 2.23 adjustment
  #sed -i 's|isnan|std::isnan|' src/qtterminal/qt_conversion.cpp
  
  #CXXFLAGS="-std=c++11"

  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --datadir=/usr/share \
              --without-lua \
              --with-qt=qt5
  make pkglibexecdir=/usr/bin
}


package() {
  cd ${pkgname}-${pkgver}
  make pkglibexecdir=/usr/bin DESTDIR=${pkgdir} install 

  install -Dm644 Copyright ${pkgdir}/usr/share/licenses/${pkgname}/Copyright

  rm -f ${pkgdir}/usr/share/texmf-dist/ls-R
}
