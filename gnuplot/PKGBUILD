
pkgname=gnuplot 
pkgver=6.0.1
pkgrel=2
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'pango' 'libjpeg-turbo' 'qt6-svg' 'gnutls' 'qt6-5compat') 
makedepends=('texinfo' 'texlive-core' 'texlive-latexextra' 'qt6-tools') 
install=gnuplot.install 
source=("https://downloads.sourceforge.net/sourceforge/gnuplot/${pkgname}-${pkgver}.tar.gz")
md5sums=('744fde2362fb20db6cfc58de73f96e9c')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --datadir=/usr/share \
              --without-lua \
              --with-texdir=/usr/share/texmf/tex/latex/gnuplot
  make pkglibexecdir=/usr/bin
}


package() {
  cd ${pkgname}-${pkgver}
  make pkglibexecdir=/usr/bin DESTDIR=${pkgdir} install 

  install -Dm644 Copyright ${pkgdir}/usr/share/licenses/${pkgname}/Copyright

  rm -f ${pkgdir}/usr/share/texmf-dist/ls-R
}
