
pkgname=gnuplot 
pkgver=4.6.5
pkgrel=1
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'pango' 'libjpeg-turbo' 'qt' 'gnutls') 
makedepends=('texinfo' 'texlive-core' 'texlive-latexextra') 
install=gnuplot.install 
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('9a476b21f74bd99c876f1509d731a0f9')

build() {
  cd $srcdir/$pkgname-$pkgver

  # fix default source location; use the GDFONTPATH variable to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c
  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c

  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --datadir=/usr/share \
              --disable-wxwidgets \
              --without-lua \
              --enable-qt
  make pkglibexecdir=/usr/bin
}


package() {
  cd $srcdir/$pkgname-$pkgver
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install install-info

  install -Dm644 Copyright $pkgdir/usr/share/licenses/$pkgname/Copyright
  install -Dm644 lisp/dotemacs "$pkgdir/usr/share/emacs/site-lisp/dotemacs"

  rm -f $pkgdir/usr/share/texmf-dist/ls-R
}
