
pkgname=postfix
pkgver=3.8.0
pkgrel=1
pkgdesc='Secure, fast, easy to administer alternative to Sendmail'
url='https://www.postfix.org/'
arch=('x86_64')
license=('custom')
depends=('pcre2' 'libsasl' 'mariadb' 'postgresql-libs' 'sqlite3' 'libldap' 'db' 'icu' 'libnsl')
backup=('etc/postfix/'{access,aliases,canonical,generic,header_checks,main.cf,master.cf,relocated,transport,virtual})
install=${pkgname}.install
source=("https://mirror.lhsolutions.nl/postfix-release/official/${pkgname}-${pkgver}.tar.gz"
        'aliases.patch'
        'postfix.service'
        "https://gitweb.gentoo.org/repo/gentoo.git/plain/mail-mta/postfix/files/linux-6.patch")
md5sums=('73cce85d78a6bf0d5a0c326c23361ed1'
         '9cc2ee3c72e134c2da5a895abaa56f30'
         '24af169de13c34dac03d787c3e607ec5'
         '08967a35249243eb06e46b1868e2dae2')

build() {
  cd ${pkgname}-${pkgver}

  #sed -i -e 's|34].*)|345].*)|g' makedefs
  #patch -p1 -i ${srcdir}/linux-6.patch

	make makefiles DEBUG='' CCARGS=' \
		-DUSE_SASL_AUTH -DUSE_CYRUS_SASL -I/usr/include/sasl \
		-DHAS_LDAP \
		-DUSE_TLS \
		-DHAS_MYSQL -I/usr/include/mysql \
		-DHAS_PGSQL -I/usr/include/postgresql \
		-DHAS_SQLITE \
	' AUXLIBS=' \
		-lsasl2 \
		-lldap -llber \
		-lssl -lcrypto \
		-lmysqlclient -lz -lm \
		-lpq \
		-lsqlite3 -lpthread \
	' OPT="${CFLAGS} ${LDFLAGS}"

	make
}

package() {
	cd ${pkgname}-${pkgver}

	sh postfix-install -non-interactive \
		install_root=${pkgdir} \
		daemon_directory=/usr/lib/${pkgname} \
		sample_directory=/etc/${pkgname}/sample \
		manpage_directory=/usr/share/man \
		readme_directory=/usr/share/doc/${pkgname}

	install -Dm644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

	cd ${pkgdir}
	patch -p0 < ${srcdir}/aliases.patch
	sed 's/^\(\$manpage[^:]*\):/\1.gz:/' -i etc/${pkgname}/postfix-files
}
