
pkgname=postfix
pkgver=3.3.1
pkgrel=1
pkgdesc='Secure, fast, easy to administer alternative to Sendmail'
url='http://www.postfix.org/'
arch=('x86_64')
license=('custom')
depends=('pcre' 'libsasl' 'mariadb' 'postgresql-libs' 'sqlite3' 'libldap' 'db' 'icu')
backup=('etc/postfix/'{access,aliases,canonical,generic,header_checks,main.cf,master.cf,relocated,transport,virtual})
install=${pkgname}.install
source=("https://mirror.myf.cloud/mirror/p/postfix-release/official/${pkgname}-${pkgver}.tar.gz"
        'aliases.patch'
        'postfix.service')
md5sums=('4381c6492f415e4a69cf5099d4acea76'
         '9cc2ee3c72e134c2da5a895abaa56f30'
         '24af169de13c34dac03d787c3e607ec5')

build() {
  cd ${pkgname}-${pkgver}

	make makefiles DEBUG='' CCARGS=' \
		-DUSE_SASL_AUTH -DUSE_CYRUS_SASL -I/usr/include/sasl \
		-DHAS_LDAP \
		-DUSE_TLS \
		-DHAS_MYSQL -I/usr/include/mysql \
		-DHAS_PGSQL -I/usr/include/postgresql \
		-DHAS_SQLITE \
	' AUXLIBS=' \
		-lsasl2 \
		-lldap -llber \
		-lssl -lcrypto \
		-lmysqlclient -lz -lm \
		-lpq \
		-lsqlite3 -lpthread \
	' OPT="${CFLAGS} ${LDFLAGS}"

	make
}

package() {
	cd ${pkgname}-${pkgver}

	sh postfix-install -non-interactive \
		install_root=${pkgdir} \
		daemon_directory=/usr/lib/${pkgname} \
		sample_directory=/etc/${pkgname}/sample \
		manpage_directory=/usr/share/man \
		readme_directory=/usr/share/doc/${pkgname}

	install -Dm644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

	cd ${pkgdir}
	patch -p0 < ${srcdir}/aliases.patch
	sed 's/^\(\$manpage[^:]*\):/\1.gz:/' -i etc/${pkgname}/postfix-files
}
