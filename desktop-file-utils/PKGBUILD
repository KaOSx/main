
pkgname=desktop-file-utils
pkgver=0.27
pkgrel=1
pkgdesc="Command line utilities for working with desktop entries"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/desktop-file-utils"
license=('GPL')
depends=('glib2')
makedepends=('meson' 'ninja')
source=("https://www.freedesktop.org/software/desktop-file-utils/releases/desktop-file-utils-${pkgver}.tar.xz"
        'update-desktop-database.hook')
sha256sums=('a0817df39ce385b6621880407c56f1f298168c040c2032cedf88d5b76affe836'
            '501f1eb35d9fbbd61666f40302b0ce63425299edf023c075986a24dc3ef18575')

build() {
  mkdir -p build
  cd build

  meson setup ../${pkgname}-${pkgver} \
    --prefix=/usr \
    --buildtype=release 
  ninja
}

package() {
  cd build

  DESTDIR=${pkgdir} ninja install
  install -Dm644 ../update-desktop-database.hook ${pkgdir}/usr/share/libalpm/hooks/update-desktop-database.hook
}
