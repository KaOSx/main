
pkgname=libgsf
pkgver=1.14.50
_pkgver=1.14
pkgrel=1
arch=('x86_64')
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats"
url="https://gitlab.gnome.org/GNOME/libgsf/blob/master/README"
license=('GPL' 'LGPL')
depends=('libxml2' 'glib2' 'bzip2')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
source=("https://download.gnome.org/sources/libgsf/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6e6c20d0778339069d583c0d63759d297e817ea10d0d897ebbe965f16e2e8e52')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --disable-static \
    --enable-introspection
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 

  #All removes will strip gnome specific stuff
  rm -f ${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1
  rm -f ${pkgdir}/usr/lib/libgsf-gnome*
  rm -rf ${pkgdir}/usr/include/libgsf-1/gsf-gnome/
  rm -rf ${pkgdir}/etc/gconf
  rm -f ${pkgdir}/usr/lib/pkgconfig/libgsf-gnome-1.pc
  rm -f ${pkgdir}/usr/bin/gsf-office-thumbnailer
  rm -f ${pkgdir}/usr/lib/python*/site-packages/gsf/gnomemodule.so
}
