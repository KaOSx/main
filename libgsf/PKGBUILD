
pkgname=libgsf
pkgver=1.14.43
pkgrel=1
arch=('x86_64')
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats"
url="https://git.gnome.org/browse/libgsf/tree/README"
license=('GPL' 'LGPL')
depends=('libxml2' 'glib2' 'bzip2')
makedepends=('intltool' 'pkgconfig' 'python2' 'gobject-introspection')
options=('!libtool')
source=("https://ftp.gnome.org/pub/GNOME/sources/libgsf/1.14/${pkgname}-${pkgver}.tar.xz")
sha256sums=('12944e3d6d9a6e4071e89dbf58348e1e93544f5f9266dd1a107a28d8001cee16')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --disable-static \
    --enable-introspection
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install 
  
  #All removes will strip gnome specific stuff
  rm -f ${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1
  rm -f ${pkgdir}/usr/lib/libgsf-gnome*
  rm -rf ${pkgdir}/usr/include/libgsf-1/gsf-gnome/
  rm -rf ${pkgdir}/etc/gconf
  rm -f ${pkgdir}/usr/lib/pkgconfig/libgsf-gnome-1.pc
  rm -f ${pkgdir}/usr/bin/gsf-office-thumbnailer
  rm -f ${pkgdir}/usr/lib/python*/site-packages/gsf/gnomemodule.so
}
