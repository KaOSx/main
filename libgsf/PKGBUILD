#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=libgsf
pkgver=1.14.25
pkgrel=1
arch=('x86_64')
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats"
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=('libxml2' 'glib2' 'bzip2')
makedepends=('intltool' 'pkgconfig' 'python2' 'gobject-introspection')
options=('!libtool')
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/1.14/${pkgname}-${pkgver}.tar.xz")
md5sums=('a66a2c6fc327bc62bebe3f988c4f45f3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --mandir=/usr/share/man \
              --disable-static \
              --enable-introspection
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install 
  #All those removes will strip gnome specific stuff
  rm -f "${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1"
  rm -f "${pkgdir}/usr/lib/libgsf-gnome"*
  rm -rf "${pkgdir}/usr/include/libgsf-1/gsf-gnome/"
  rm -rf "${pkgdir}/etc/gconf"
  rm -f "${pkgdir}/usr/lib/pkgconfig/libgsf-gnome-1.pc"
  rm -f "${pkgdir}/usr/bin/gsf-office-thumbnailer"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/gsf/gnomemodule.so
}
