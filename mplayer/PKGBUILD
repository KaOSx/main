# check latest:svn info svn://svn.mplayerhq.hu/mplayer/trunk | grep Revision: | cut -d' ' -f2

pkgname=mplayer
pkgver=38666
pkgrel=1
pkgdesc="Multimedia player."
arch=('x86_64')
depends=('a52dec' 'aalib' 'alsa-lib' 'bzip2' 'enca' 'faad2'
         'fontconfig' 'freetype2' 'fribidi' 'gcc-libs' 'giflib' 'glibc' 'gsm' 'jack2' 'ladspa' 'lame'
         'libass' 'libbluray' 'libcaca' 'libcdio' 'libcdio-paranoia' 'libdca' 'libdv' 'libdvdnav' 'libglvnd' 
         'libjpeg-turbo' 'libmad' 'libmng' 'libmpeg2' 'libogg' 'libpng'
         'pulseaudio' 'libtheora' 'libvdpau' 'libvpx' 'libx11' 'libxext' 'libxinerama'
         'libxss' 'libxv' 'libxxf86vm' 'live-media' 'opus'
         'lzo2' 'mpg123' 'ncurses' 'openal' 'opencore-amr' 'orc' 'rtmpdump'  # not ready for 2.0 'openjpeg' and 1.29.9 'faac'
         'schroedinger' 'sdl' 'samba' 'speex' 'ttf-dejavu' 'x264' 'xvidcore' 'zlib' 'gnutls')
makedepends=('mesa' 'unzip' 'yasm' 'subversion' 'nasm')
license=('GPL2')
url="http://www.mplayerhq.hu/"
groups=('multimedia')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(${pkgname}-${pkgver}::"svn://svn.mplayerhq.hu/mplayer/trunk#revision=${pkgver}"
        'mplayer.desktop'
        'mplayer.png'
        "https://gitlab.archlinux.org/archlinux/packaging/packages/mplayer/-/raw/main/ffmpeg-7.patch")
md5sums=('SKIP'
         '62f44a58f072b2b1a3c3d3e4976d64b3'
         'd00874ccc644b7f43d6ef1c942fcef28'
         '96a22a47d168164fe928e925180e169d')

build() {
    cd ${pkgname}-${pkgver}
    patch -p0 -i ${srcdir}/ffmpeg-7.patch

    ./configure \
      --confdir=/etc/mplayer \
      --enable-runtime-cpudetection \
      --language=all \
      --enable-radio \
      --enable-radio-capture \
      --prefix=/usr \
      --disable-ffmpeg_a
    make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/

  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font

  # Desktop Integration.
  install -Dm644 ../mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
  install -Dm644 ../mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png
}
