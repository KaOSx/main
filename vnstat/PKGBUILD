
pkgname=vnstat
pkgver=1.15
pkgrel=1
pkgdesc="Console-based network traffic monitor for Linux and BSD that keeps
         a log of network traffic for the selected interface(s)"
arch=('x86_64')
url="https://github.com/vergoh/vnstat"
license=('GPL')
depends=('bash' 'gd')
backup=(etc/vnstat.conf)
install=vnstat.install
source=("http://humdi.net/vnstat/$pkgname-$pkgver.tar.gz")
md5sums=('351051ef3005e3ca99123eec07ac0a7d')

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc

  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm0755 examples/vnstat.cgi $pkgdir/usr/share/doc/vnstat/examples/vnstat.cgi
  install -Dm0644 examples/systemd/vnstat.service $pkgdir/usr/lib/systemd/system/vnstat.service
}