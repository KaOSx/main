
pkgname=clang
pkgbase="llvm-project"
pkgver=17.0.4
pkgrel=1
pkgdesc="C/C++/Objective-C compiler, which aims to deliver amazingly fast compiles, extremely useful error and warning messages and to provide a platform for building great source level tools."
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=("llvm=${pkgver}" 'gcc' 'compiler-rt')
makedepends=('python3' 'cmake')
provides=('clang-analyzer' 'clang-tools-extra')
replaces=('clang-analyzer' 'clang-tools-extra')
conflicts=('clang-analyzer' 'clang-tools-extra')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/clang-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/clang-tools-extra-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/cmake-${pkgver}.src.tar.xz"
        "https://src.fedoraproject.org/rpms/clang14/raw/rawhide/f/0003-PATCH-clang-Don-t-install-static-libraries.patch")
md5sums=('67eba128307f3fc6e2cf35a50ea68521'
         'e715a4ad675f61bef388e6d194103d56'
         '38ae9cc0950f277c8f88e570c4d18010'
         '2ba9b14ee03279853f1803071e7bb101')

prepare() {
    cd ${pkgname}-${pkgver}.src

    mv ${srcdir}/clang-tools-extra-${pkgver}.src tools/extra
    patch -p1 -i ${srcdir}/0003-PATCH-clang-Don-t-install-static-libraries.patch
}

build() {
    cmake -B build -S ${pkgname}-${pkgver}.src \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib/clang \
        -DLLVM_COMMON_CMAKE_UTILS="${srcdir}/cmake-${pkgver}.src" \
        -DCMAKE_MODULE_PATH="${srcdir}/cmake-${pkgver}.src" \
        -DCLANG_BUILT_STANDALONE=ON \
        -DCLANG_LINK_CLANG_DYLIB=ON \
        -DENABLE_LINKER_BUILD_ID=ON \
        -DCLANG_VENDOR=KaOS \
        -DLLVM_INCLUDE_TESTS=OFF
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    #sed -i 's|libexec|lib/clang|' ${pkgdir}/usr/bin/scan-build
    #sed -i 's|libexec|lib/clang|' ${pkgdir}/usr/lib/libscanbuild/analyze.py

    # Python scripts
    #python3 -m compileall ${pkgdir}/usr/lib/clang-analyzer
    #python3 -O -m compileall ${pkgdir}/usr/lib/clang-analyzer
    #python3 -OO -m compileall ${pkgdir}/usr/lib/clang-analyzer

    install -D -m644 ${pkgname}-${pkgver}.src/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

