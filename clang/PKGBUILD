
pkgname=clang
pkgver=15.0.3
pkgrel=1
pkgdesc="C/C++/Objective-C compiler, which aims to deliver amazingly fast compiles, extremely useful error and warning messages and to provide a platform for building great source level tools."
arch=('x86_64')
url="https://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=("llvm=${pkgver}" 'gcc' 'compiler-rt')
makedepends=('python3' 'cmake')
provides=('clang-analyzer' 'clang-tools-extra')
replaces=('clang-analyzer' 'clang-tools-extra')
conflicts=('clang-analyzer' 'clang-tools-extra')
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/clang-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/clang-tools-extra-${pkgver}.src.tar.xz"
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/cmake-${pkgver}.src.tar.xz"
        "https://src.fedoraproject.org/rpms/clang14/raw/rawhide/f/0003-PATCH-clang-Don-t-install-static-libraries.patch")
md5sums=('3a90430c26fc8b6dfa12b6724c12433f'
         '5f453fa4f1ca123cbf499bc4c657d559'
         'fc5d234a184dfc03dad241bfce566296'
         '2ba9b14ee03279853f1803071e7bb101')

prepare() {
    cd ${pkgname}-${pkgver}.src

    mv ${srcdir}/clang-tools-extra-${pkgver}.src tools/extra
    patch -p1 -i ${srcdir}/0003-PATCH-clang-Don-t-install-static-libraries.patch
}

build() {
    cmake -B build -S ${pkgname}-${pkgver}.src \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib/clang \
        -DCMAKE_MODULE_PATH="${srcdir}/cmake-${pkgver}.src/Modules" \
        -DCLANG_BUILT_STANDALONE=ON \
        -DCLANG_LINK_CLANG_DYLIB=ON \
        -DENABLE_LINKER_BUILD_ID=ON \
        -DCLANG_VENDOR=KaOS
    cmake --build build
}

package() {

    DESTDIR=${pkgdir} cmake --install build

    #sed -i 's|libexec|lib/clang|' ${pkgdir}/usr/bin/scan-build
    #sed -i 's|libexec|lib/clang|' ${pkgdir}/usr/lib/libscanbuild/analyze.py

    # Python scripts
    #python3 -m compileall ${pkgdir}/usr/lib/clang-analyzer
    #python3 -O -m compileall ${pkgdir}/usr/lib/clang-analyzer
    #python3 -OO -m compileall ${pkgdir}/usr/lib/clang-analyzer

    install -D -m644 ${pkgname}-${pkgver}.src/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

