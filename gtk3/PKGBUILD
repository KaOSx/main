
pkgbase=gtk3
pkgname=('gtk3' 'gtk-update-icon-cache')
pkgver=3.22.14
_pkgver=3.22
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
makedepends=('gobject-introspection' 'cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
             'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'gdk-pixbuf2' 
             'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'librsvg' 'wayland' 'wayland-protocols')
options=('!libtool' '!docs')
license=('LGPL')
source=("https://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-${pkgver}.tar.xz"
        'gtk-update-icon-cache.hook'
        'gtk-update-icon-cache.script')
md5sums=('2df75dc697d7bdf9b9e9c4da9b07151f'
         'c792ef5bb74ceba564b4d8ad306a6592'
         '025117f62dfb22e942a46784e238e470')

build() {
  cd gtk+-${pkgver}

 ./configure --prefix=/usr \
             --sysconfdir=/etc \
             --localstatedir=/var \
             --disable-schemas-compile \
             --enable-x11-backend \
             --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtk3() {
  pkgdesc="The GTK+ Toolkit (v3)"
  depends=('cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
          'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 
          'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'wayland' 'wayland-protocols')
  install="gtk3.install"
  
  cd gtk+-${pkgver}

  make DESTDIR=${pkgdir} install
  
  # avoid gtk2 conflict, used for both gtk2 & 3 this way
  rm $pkgdir/usr/bin/gtk-update-icon-cache
}

package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')

    cd gtk+-${pkgver}/gtk

    install -Dm755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
    
    install -Dm644 ../../gtk-update-icon-cache.hook ${pkgdir}/usr/share/libalpm/hooks/gtk-update-icon-cache.hook
    install -Dm755 ../../gtk-update-icon-cache.script ${pkgdir}/usr/share/libalpm/scripts/gtk-update-icon-cache
}
