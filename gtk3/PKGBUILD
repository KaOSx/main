#
# Platform Packages for Chakra, part of chakra-project.org
#
# maintainer abveritas@chakra-project.org

pkgname=gtk3
pkgver=3.6.4
pkgmin="${pkgver%.*}"
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3)"
arch=('x86_64')
url="http://www.gtk.org/"
install="gtk3.install"
depends=('cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor'
         'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'pango' 'shared-mime-info' 'colord' 'at-spi2-atk')
makedepends=('gobject-introspection')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgmin}/gtk+-${pkgver}.tar.xz"
        'settings.ini')
md5sums=('123d3dc6fc659539a308017934235b3e'
         '085ece008fa3a0b7a72e9fe3dda3631a')

build() {
  cd "${srcdir}/gtk+-${pkgver}"

 ./configure --prefix=/usr \
             --sysconfdir=/etc \
             --localstatedir=/var \
             --enable-gtk2-dependency \
             --disable-schemas-compile

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/settings.ini" \
                   "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
