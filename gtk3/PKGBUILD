
pkgbase=gtk3
pkgname=('gtk3' 'gtk-update-icon-cache')
pkgver=3.24.13
_pkgver=3.24
pkgrel=1
arch=('x86_64')
url="http://www.gtk.org/"
makedepends=('gobject-introspection' 'cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
             'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'gdk-pixbuf2' 
             'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'librsvg' 'wayland' 
             'wayland-protocols' 'iso-codes' 'sassc')
options=('!libtool' '!docs')
license=('LGPL')
source=("https://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-${pkgver}.tar.xz"
        'gtk-update-icon-cache.hook'
        'gtk-update-icon-cache.script')
sha256sums=('4c775c38cf1e3c534ef0ca52ca6c7a890fe169981af66141c713e054e68930a9'
            '496064a9dd6214bd58f689dd817dbdc4d7f17d42a8c9940a87018c3f829ce308'
            'bbe06e1b4e1ad5d61a4e703445a2bb93c6be918964d6dd76c0420c6667fa11eb')

build() {
  cd gtk+-${pkgver}
  # fix build fail with wayland https://bugzilla.gnome.org/show_bug.cgi?id=789630
  #patch -p1 -i ${srcdir}/wayland.diff
  sed -e "s|gdk_screen_get_dfeault|gdk_screen_get_default|" -i docs/tools/shooter.c

 ./configure --prefix=/usr \
             --sysconfdir=/etc \
             --localstatedir=/var \
             --disable-schemas-compile \
             --enable-x11-backend \
             --enable-wayland-backend

  make
}

package_gtk3() {
  pkgdesc="The GTK+ Toolkit (v3)"
  depends=('cairo' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 
          'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 
          'colord' 'at-spi2-atk' 'libepoxy' 'libxkbcommon' 'wayland' 
          'wayland-protocols' 'iso-codes')
  install="gtk3.install"
  
  cd gtk+-${pkgver}

  make DESTDIR=${pkgdir} install
  
  # avoid gtk2 conflict, used for both gtk2 & 3 this way
  rm $pkgdir/usr/bin/gtk-update-icon-cache
}

package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')

    cd gtk+-${pkgver}/gtk

    install -Dm755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
    
    install -Dm644 ../../gtk-update-icon-cache.hook ${pkgdir}/usr/share/libalpm/hooks/gtk-update-icon-cache.hook
    install -Dm755 ../../gtk-update-icon-cache.script ${pkgdir}/usr/share/libalpm/scripts/gtk-update-icon-cache
}
