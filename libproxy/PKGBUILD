
pkgname=libproxy
pkgver=0.4.15
pkgrel=7
pkgdesc="A library that provides automatic proxy configuration management"
arch=('x86_64')
url="http://libproxy.github.io/libproxy"
license=('LGPL')
depends=('gcc-libs' 'python3')
makedepends=('cmake' 'networkmanager' 'perl')
optdepends=('networkmanager: NetworkManager configuration module'
            'perl: Perl bindings')
source=("https://github.com/libproxy/libproxy/archive/${pkgver}.tar.gz")
options=('!libtool')
md5sums=('21ebe5b4ea2a04f5f468bf5d08c40d2c')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
  
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DPYTHON3_EXECUTABLE=/usr/bin/python3 \
    ..
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  
  make DESTDIR=${pkgdir} install
}
