
pkgname=libproxy
pkgver=0.4.13
pkgrel=2
pkgdesc="A library that provides automatic proxy configuration management"
arch=('x86_64')
url="http://libproxy.github.io/libproxy"
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake' 'networkmanager' 'python2' 'perl')
optdepends=('networkmanager: NetworkManager configuration module'
            'perl: Perl bindings'
            'python2: Python bindings')
source=("https://github.com/libproxy/libproxy/archive/${pkgver}.tar.gz")
options=('!libtool')
md5sums=('de293bb311f185a2ffa3492700a694c2')

build() {
  cd ${pkgname}-${pkgver}
  mkdir build
  
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    ..
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  
  make DESTDIR="${pkgdir}" install
}
